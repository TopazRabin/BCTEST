OBJECT Codeunit 14000378 E.D.I. Archive Management
{
  OBJECT-PROPERTIES
  {
    Date=11/11/13;
    Time=12:00:00 PM;
    Version List=SE0.60;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=Document %1 has been archived.';
      Text002@1003 : TextConst 'ENU=Restore %1 %2?';
      Text003@1002 : TextConst 'ENU=Send Document restored.';
      Text004@1001 : TextConst 'ENU="Restore %1 %2 Internal Doc. No. %3 Document No. %4 Version %5? "';
      Text005@1004 : TextConst 'ENU=Receive Document Hdr. %1 exist\Restore cancelled.';
      Text006@1005 : TextConst 'ENU=Receive Document %1 has been restored.';
      Text008@1007 : TextConst 'ENU=Send Document %1 Internal Doc. No. %2 Document No. %3 Version %4 exist.\Restore cancelled.';
      OrderStatusHdr@1240030003 : Record 14002400;
      OrderStatusLine@1240030002 : Record 14002401;
      OrderStatusEntry@1240030005 : Record 14002407;
      OrderStatusHdrArchive@1240030001 : Record 14002408;
      OrderStatusLineArchive@1240030000 : Record 14002409;
      OrderStatusEntryArchive@1240030004 : Record 14002410;
      SalesHeader@1240030016 : Record 36;
      SalesInvoiceHeader@1240030017 : Record 112;
      SalesCrMemoHeader@1240030026 : Record 114;
      EDIRecDocHdrInfo@1240030009 : Record 14002371;
      EDIRecDocLineInfo@1240030008 : Record 14002372;
      EDIChangedDocument@1240030030 : Record 14002373;
      EDIRecDocHdrInfoArchive@1240030007 : Record 14000379;
      EDIRecDocLineInfoArchive@1240030006 : Record 14000378;
      Text009@1240030010 : TextConst 'ENU=Archive %1 %2 Internal Doc. No. %3 Document No. %4 Version %5.';
      Text010@1240030011 : TextConst 'ENU=Documents with Data Errors cannot be archived.';
      Text011@1240030012 : TextConst 'ENU=Only Send Documents that have been sent may be archived.';
      Text012@1240030013 : TextConst 'ENU=Functional Acknowledgment required but not received. \ Archive request cancelled.';
      EDIChangedDocumentArchive@1240030028 : Record 14002379;
      SlsOrderFound@1240030014 : Boolean;
      SlsInvoiceFound@1240030015 : Boolean;
      Text013@1240030018 : TextConst 'ENU=Receive Document %1 not completely processed.\ Continue with archive?';
      Text014@1240030019 : TextConst 'ENU=%1 update for Receive Document %2 not processed. Continue with archive?';
      Text015@1240030021 : TextConst 'ENU=%1 not processed for Receive Document %2.\ Continue with archive?';
      Text017@1240030022 : TextConst 'ENU=Archive not performed.';
      SlsCrMemoFound@1240030024 : Boolean;
      PostedSlsCrMemoFound@1240030025 : Boolean;
      ArchiveDoc@1240030023 : Boolean;
      Text007@1006 : TextConst 'ENU=PO Change';
      Text016@1008 : TextConst 'ENU=PO Acknowledgment';
      Text018@1009 : TextConst 'ENU=Organizational Relationships';
      Text019@1010 : TextConst 'ENU=Sales Order';
      Text020@1011 : TextConst 'ENU=Purchase Order';
      Text021@1012 : TextConst 'ENU=Routing Request';
      Text022@1013 : TextConst 'ENU=EDI Send Document';
      Text023@1014 : TextConst 'ENU=EDI Receive Document';
      Text024@1240030020 : TextConst 'ENU=Restore Cancelled.';

    PROCEDURE ArchiveReceiveDocument@2(VAR EDIReceiveDocumentHdr@1004 : Record 14002358);
    BEGIN
      IF EDIReceiveDocumentHdr."Data Error" THEN
        ERROR(Text010);

      ArchiveDoc := TRUE;

      WITH EDIReceiveDocumentHdr DO BEGIN
        CASE "Navision Document" OF
          'I_SLSORD':
            BEGIN
              CASE "EDI Document No." OF
                '810','812':
                  BEGIN
                    SlsCrMemoFound := FALSE;
                    PostedSlsCrMemoFound := FALSE;

                    SalesHeader.RESET;
                    SalesHeader.SETCURRENTKEY("EDI Order","EDI Trade Partner","EDI Internal Doc. No.");
                    SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::"Credit Memo");
                    SalesHeader.SETRANGE("EDI Order",TRUE);
                    SalesHeader.SETRANGE("EDI Internal Doc. No.","Internal Doc. No.");
                    SlsCrMemoFound := SalesHeader.FIND('-');

                    SalesCrMemoHeader.RESET;
                    SalesCrMemoHeader.SETCURRENTKEY(
                      "EDI Order","EDI Cr. Memo Generated","EDI Trade Partner","EDI Internal Doc. No.");
                    SalesCrMemoHeader.SETRANGE("EDI Order",TRUE);
                    SalesCrMemoHeader.SETRANGE("EDI Cr. Memo Generated",TRUE);
                    SalesCrMemoHeader.SETRANGE("EDI Internal Doc. No.","Internal Doc. No.");
                    PostedSlsCrMemoFound := SalesCrMemoHeader.FIND('-');

                    IF SlsCrMemoFound OR PostedSlsCrMemoFound THEN
                      IF GUIALLOWED THEN BEGIN
                        IF CONFIRM(Text013,FALSE,"Internal Doc. No.") THEN
                          ArchiveDoc := TRUE
                        ELSE
                          ArchiveDoc := FALSE
                      END ELSE
                        ArchiveDoc := FALSE;
                  END;
                '850','862','ORDERS','DELJIT':
                  BEGIN
                    SlsOrderFound := FALSE;
                    SlsInvoiceFound := FALSE;

                    SalesHeader.RESET;
                    SalesHeader.SETCURRENTKEY("EDI Order","EDI Trade Partner","EDI Internal Doc. No.");
                    SalesHeader.SETRANGE("EDI Order",TRUE);
                    SalesHeader.SETRANGE("EDI Internal Doc. No.","Internal Doc. No.");
                    SlsOrderFound := SalesHeader.FIND('-');

                    SalesInvoiceHeader.RESET;
                    SalesInvoiceHeader.SETCURRENTKEY(
                      "EDI Order","EDI Invoice Generated","EDI Trade Partner","EDI Internal Doc. No.");
                    SalesInvoiceHeader.SETRANGE("EDI Order",TRUE);
                    SalesInvoiceHeader.SETRANGE("EDI Invoice Generated",TRUE);
                    SalesInvoiceHeader.SETRANGE("EDI Internal Doc. No.","Internal Doc. No.");
                    SlsInvoiceFound := SalesInvoiceHeader.FIND('-');

                    IF SlsOrderFound OR SlsInvoiceFound THEN
                      IF GUIALLOWED THEN BEGIN
                        IF CONFIRM(Text013,FALSE,"Internal Doc. No.") THEN
                          ArchiveDoc := TRUE
                        ELSE
                          ArchiveDoc := FALSE;
                      END ELSE
                        ArchiveDoc := FALSE;
                  END;
                '860':
                  BEGIN
                    IF "PO Change Updated Date" = 0D THEN
                      IF GUIALLOWED THEN BEGIN
                        IF CONFIRM(Text014,FALSE,Text007,"Internal Doc. No.") THEN
                          ArchiveDoc := TRUE
                        ELSE
                          ArchiveDoc := FALSE;
                       END ELSE
                         ArchiveDoc := FALSE;
                  END;
              END;
            END;
          'I_PURACK':
            BEGIN
              IF NOT "PO Ack. Processed" THEN
                IF GUIALLOWED THEN BEGIN
                  IF CONFIRM(Text015,FALSE,Text016,"Internal Doc. No.") THEN
                    ArchiveDoc := TRUE
                  ELSE
                    ArchiveDoc := FALSE;
                END ELSE
                  ArchiveDoc := FALSE;
            END;
          'I_SLSASN':
            BEGIN
              IF NOT "ASN Processed" THEN
                IF GUIALLOWED THEN BEGIN
                  IF CONFIRM(Text015,FALSE,'ASN',"Internal Doc. No.") THEN
                    ArchiveDoc := TRUE
                  ELSE
                    ArchiveDoc := FALSE;
                END ELSE
                  ArchiveDoc := FALSE;
            END;
          'I_SLSCUS':
            BEGIN
              IF NOT "Org. Relationships Updated" THEN
                IF GUIALLOWED THEN BEGIN
                  IF CONFIRM(Text014,FALSE,Text018,"Internal Doc. No.") THEN
                    ArchiveDoc := TRUE
                  ELSE
                    ArchiveDoc := FALSE;
                END ELSE
                  ArchiveDoc := FALSE;
            END;
          'U_SLSORD','U_SLSWSA':
            BEGIN
              IF NOT EDIReceiveDocumentHdr."Sales Order Updated" THEN
                IF GUIALLOWED THEN BEGIN
                  IF CONFIRM(Text014,FALSE,Text019,"Internal Doc. No.") THEN
                    ArchiveDoc := TRUE
                  ELSE
                    ArchiveDoc := FALSE;
                END ELSE
                  ArchiveDoc := FALSE;
            END;
          'I_PURINV','U_PURWSA':
            BEGIN
              IF NOT "Purchase Order Updated" THEN
                IF GUIALLOWED THEN BEGIN
                  IF CONFIRM(Text014,FALSE,Text020,"Internal Doc. No.") THEN
                    ArchiveDoc := TRUE
                  ELSE
                    ArchiveDoc := FALSE;
                END ELSE
                  ArchiveDoc := FALSE;
            END;
          'U_ROUTEREQ':
            BEGIN
              IF GUIALLOWED THEN BEGIN
                IF CONFIRM(Text014,FALSE,Text021,"Internal Doc. No.") THEN
                  ArchiveDoc := TRUE
                ELSE
                  ArchiveDoc := FALSE;
               END ELSE
                 ArchiveDoc := FALSE;
            END;
          'I_FUNCTACK','I_SLSGEN':
            ArchiveDoc := TRUE;
        END;
      END;

      IF ArchiveDoc THEN BEGIN
        StoreReceiveDocument(EDIReceiveDocumentHdr);
        IF GUIALLOWED THEN
          MESSAGE(Text001,EDIReceiveDocumentHdr."Internal Doc. No.")
      END ELSE
        IF GUIALLOWED THEN
          MESSAGE(Text017);
    END;

    PROCEDURE ArchiveSendDocument@10(VAR EDISendDocHdr@1000 : Record 14002369);
    BEGIN
      EDISendDocHdr.TESTFIELD("Data Error",FALSE);

      IF NOT EDISendDocHdr."Document Sent" THEN
        ERROR(Text011);

      IF (EDISendDocHdr."Funct. Ack. Required") AND
         (EDISendDocHdr."Funct. Group Ack. Status" =
           EDISendDocHdr."Funct. Group Ack. Status"::" ") AND
         (EDISendDocHdr."Tran. Set Funct. Ack. Status" =
           EDISendDocHdr."Tran. Set Funct. Ack. Status"::" ") THEN
        ERROR(Text012);

      IF GUIALLOWED THEN BEGIN
        IF CONFIRM(
          Text009,TRUE,Text022,EDISendDocHdr."No.",EDISendDocHdr."Internal Doc No.",
          EDISendDocHdr."Document No.",EDISendDocHdr.Version)
        THEN BEGIN
          StoreSendDocument(EDISendDocHdr);
          MESSAGE(Text001,EDISendDocHdr."No.");
        END;
      END;
    END;

    PROCEDURE StoreReceiveDocument@4(VAR EDIReceiveDocumentHdr@1000 : Record 14002358);
    VAR
      EDIReceiveDocumentField@1002 : Record 14002359;
      EDIRecDocHdrArchive@1004 : Record 14002375;
      EDIRecDocFieldArchive@1001 : Record 14002376;
    BEGIN
      EDIRecDocHdrArchive.INIT;
      EDIRecDocHdrArchive.TRANSFERFIELDS(EDIReceiveDocumentHdr);
      EDIRecDocHdrArchive."Archive Version No." := 1;
      EDIRecDocHdrArchive."Archived By" := USERID;
      EDIRecDocHdrArchive."Date Archived" := WORKDATE;
      EDIRecDocHdrArchive."Time Archived" := TIME;
      EDIRecDocHdrArchive.INSERT;

      EDIReceiveDocumentField.SETRANGE("Internal Doc. No.",EDIReceiveDocumentHdr."Internal Doc. No.");
      IF EDIReceiveDocumentField.FIND('-') THEN
        REPEAT
          EDIRecDocFieldArchive.INIT;
          EDIRecDocFieldArchive.TRANSFERFIELDS(EDIReceiveDocumentField);
          EDIRecDocFieldArchive."Archive Version No." := EDIRecDocHdrArchive."Archive Version No.";
          EDIRecDocFieldArchive.INSERT;
        UNTIL EDIReceiveDocumentField.NEXT = 0;

      StoreOrderStatus(EDIReceiveDocumentHdr."Internal Doc. No.");
      StoreOrderStatusEntry(EDIReceiveDocumentHdr."Internal Doc. No.");
      StoreChangedDocument(EDIReceiveDocumentHdr."Internal Doc. No.");
      StoreRecDocHdrInfo(EDIReceiveDocumentHdr."Internal Doc. No.");
      StoreRecDocLineInfo(EDIReceiveDocumentHdr."Internal Doc. No.");

      EDIReceiveDocumentHdr.DELETE(TRUE);
    END;

    PROCEDURE StoreSendDocument@12(VAR EDISendDocHdr@1001 : Record 14002369);
    VAR
      EDISendDocField@1005 : Record 14002370;
      EDISendDocHdrArchive@1004 : Record 14002377;
      EDISendDocFieldArchive@1003 : Record 14002378;
    BEGIN
      EDISendDocHdrArchive.INIT;
      EDISendDocHdrArchive.TRANSFERFIELDS(EDISendDocHdr);
      EDISendDocHdrArchive."Archive Version No." := 1;
      EDISendDocHdrArchive."Archived By" := USERID;
      EDISendDocHdrArchive."Date Archived" := WORKDATE;
      EDISendDocHdrArchive."Time Archived" := TIME;
      EDISendDocHdrArchive.INSERT;

      EDISendDocField.RESET;
      EDISendDocField.SETRANGE("No.",EDISendDocHdrArchive."No.");
      IF EDISendDocField.FIND('-') THEN
        REPEAT
          EDISendDocFieldArchive.INIT;
          EDISendDocFieldArchive.TRANSFERFIELDS(EDISendDocField);
          EDISendDocFieldArchive."Archive Version No." := EDISendDocHdrArchive."Archive Version No.";
          EDISendDocFieldArchive."Archived By" := USERID;
          EDISendDocFieldArchive."Date Archived" := WORKDATE;
          EDISendDocFieldArchive."Time Archived" := TIME;
          EDISendDocFieldArchive.INSERT;
        UNTIL EDISendDocField.NEXT = 0;

      EDISendDocHdr.DELETE(TRUE);
    END;

    PROCEDURE StoreOrderStatus@1000000001(DocNo@1000000004 : Code[10]);
    BEGIN
      OrderStatusHdr.RESET;
      OrderStatusHdr.SETRANGE("Internal Doc. No.",DocNo);
      IF OrderStatusHdr.FIND('-') THEN BEGIN
        REPEAT
          OrderStatusHdrArchive.TRANSFERFIELDS(OrderStatusHdr);
          OrderStatusHdrArchive."Date Archived" := WORKDATE;
          OrderStatusHdrArchive."Time Archived" := TIME;
          OrderStatusHdrArchive."Archived By" := USERID;
          OrderStatusHdrArchive."Archive Version No." := 1;
          OrderStatusHdrArchive.INSERT;
          OrderStatusLine.RESET;
          OrderStatusLine.SETRANGE("Internal Doc. No.",DocNo);
          OrderStatusLine.SETRANGE("Trade Partner Code",OrderStatusHdr."Trade Partner Code");
          OrderStatusLine.SETRANGE("Source Doc. Type",OrderStatusHdr."Source Doc. Type"::Order);
          OrderStatusLine.SETRANGE("Source Doc. No.",OrderStatusHdr."Source Doc. No.");
          OrderStatusLine.SETRANGE("Document Type",OrderStatusHdr."Document Type");
          IF OrderStatusLine.FIND('-') THEN
            REPEAT
              OrderStatusLineArchive.TRANSFERFIELDS(OrderStatusLine);
              OrderStatusLineArchive."Date Archived" := WORKDATE;
              OrderStatusLineArchive."Time Archived" := TIME;
              OrderStatusLineArchive."Archived By" := USERID;
              OrderStatusLineArchive."Archive Version No." := 1;
              OrderStatusLineArchive.INSERT;
            UNTIL OrderStatusLine.NEXT = 0;
        UNTIL OrderStatusHdr. NEXT =0;
      END;
    END;

    PROCEDURE StoreChangedDocument@1240030006(DocNo@1240030000 : Code[10]);
    BEGIN
      EDIChangedDocument.RESET;
      EDIChangedDocument.SETRANGE("Internal Doc. No.",DocNo);
      IF EDIChangedDocument.FIND('-') THEN BEGIN
        REPEAT
          EDIChangedDocumentArchive.TRANSFERFIELDS(EDIChangedDocument);
          EDIChangedDocumentArchive."Date Archived" := WORKDATE;
          EDIChangedDocumentArchive."Time Archived" := TIME;
          EDIChangedDocumentArchive."Archived By" := USERID;
          EDIChangedDocumentArchive."Archive Version No." := 1;
          EDIChangedDocumentArchive.INSERT;
        UNTIL EDIChangedDocument.NEXT =0;
      END;
    END;

    PROCEDURE StoreRecDocHdrInfo@1240030011(DocNo@1240030000 : Code[10]);
    VAR
      EDIRecDocHdrInfo@1240030001 : Record 14002371;
      EDIRecDocHdrInfoArchive@1240030002 : Record 14000379;
    BEGIN
      EDIRecDocHdrInfo.RESET;
      EDIRecDocHdrInfo.SETRANGE("Internal Doc. No.",DocNo);
      IF EDIRecDocHdrInfo.FIND('-') THEN
        REPEAT
          EDIRecDocHdrInfoArchive.TRANSFERFIELDS(EDIRecDocHdrInfo);
          EDIRecDocHdrInfoArchive."Date Archived" := WORKDATE;
          EDIRecDocHdrInfoArchive."Time Archived" := TIME;
          EDIRecDocHdrInfoArchive."Archived By" := USERID;
          EDIRecDocHdrInfoArchive."Archive Version No." := 1;
          EDIRecDocHdrInfoArchive.INSERT;
        UNTIL EDIRecDocHdrInfo.NEXT = 0;
    END;

    PROCEDURE StoreRecDocLineInfo@1240030012(DocNo@1240030000 : Code[10]);
    VAR
      EDIRecDocLineInfo@1240030001 : Record 14002372;
      EDIRecDocLineInfoArchive@1240030002 : Record 14000378;
    BEGIN
      EDIRecDocLineInfo.RESET;
      EDIRecDocLineInfo.SETRANGE("Internal Doc. No.",DocNo);
      IF EDIRecDocLineInfo.FIND('-') THEN
        REPEAT
          EDIRecDocLineInfoArchive.TRANSFERFIELDS(EDIRecDocLineInfo);
          EDIRecDocLineInfoArchive."Date Archived" := WORKDATE;
          EDIRecDocLineInfoArchive."Time Archived" := TIME;
          EDIRecDocLineInfoArchive."Archived By" := USERID;
          EDIRecDocLineInfoArchive."Archive Version No." := 1;
          EDIRecDocLineInfoArchive.INSERT;
        UNTIL EDIRecDocLineInfo.NEXT = 0;
    END;

    PROCEDURE StoreOrderStatusEntry@1240030001(DocNo@1240030000 : Code[10]);
    BEGIN
      OrderStatusEntry.RESET;
      OrderStatusEntry.SETRANGE("EDI Internal Doc. No.",DocNo);
      IF OrderStatusEntry.FIND('-') THEN BEGIN
       REPEAT
         IF OrderStatusEntry."Source Doc. No." <> '' THEN BEGIN
           OrderStatusEntryArchive.TRANSFERFIELDS(OrderStatusEntry);
           OrderStatusEntryArchive.INSERT;
         END;
       UNTIL OrderStatusEntry.NEXT = 0;
      END;
    END;

    PROCEDURE RetrieveReceiveDocument@1240030002(VAR EDIReceiveDocHdrArchive@1240030000 : Record 14002375);
    BEGIN
      IF GUIALLOWED THEN BEGIN
        IF CONFIRM(
          Text002,TRUE,Text023,EDIReceiveDocHdrArchive."Internal Doc. No.")
        THEN
          RestoreReceiveDocument(EDIReceiveDocHdrArchive,FALSE)
         ELSE
           ERROR(Text024);
      END ELSE
        ERROR(Text024);
    END;

    PROCEDURE RetrieveSendDocument@1240030003(VAR EDISendDocHdrArchive@1240030000 : Record 14002377);
    BEGIN
      IF GUIALLOWED THEN BEGIN
        IF CONFIRM(
          Text004,TRUE,Text022,EDISendDocHdrArchive."No.",EDISendDocHdrArchive."Internal Doc No.",
          EDISendDocHdrArchive."Document No.",EDISendDocHdrArchive.Version)
        THEN
          RestoreSendDocument(EDISendDocHdrArchive,FALSE)
        ELSE
          ERROR(Text024);;
      END ELSE
        ERROR(Text024);
    END;

    PROCEDURE RestoreReceiveDocument@1240030000(VAR EDIRecDocHdrArchive@1240030000 : Record 14002375;Batch@1240030004 : Boolean);
    VAR
      EDIRecDocHdr@1240030001 : Record 14002358;
      EDIRecDocField@1240030002 : Record 14002359;
      EDIRecDocFieldArchive@1240030003 : Record 14002376;
    BEGIN
      IF EDIRecDocHdr.GET(EDIRecDocHdrArchive."Internal Doc. No.") THEN
        ERROR(Text005,EDIRecDocHdrArchive."Internal Doc. No.");

      EDIRecDocHdr.INIT;
      EDIRecDocHdr.TRANSFERFIELDS(EDIRecDocHdrArchive);
      EDIRecDocHdr.INSERT(TRUE);
      EDIRecDocFieldArchive.RESET;
      EDIRecDocFieldArchive.SETRANGE("Internal Doc. No.",EDIRecDocHdrArchive."Internal Doc. No.");
        IF EDIRecDocFieldArchive.FIND('-') THEN
          REPEAT
            EDIRecDocField.INIT;
            EDIRecDocField.TRANSFERFIELDS(EDIRecDocFieldArchive);
            EDIRecDocField.INSERT(TRUE);
          UNTIL EDIRecDocFieldArchive.NEXT = 0;

      EDIChangedDocumentArchive.RESET;
      EDIChangedDocumentArchive.SETRANGE("Internal Doc. No.",EDIRecDocHdrArchive."Internal Doc. No.");
      IF EDIChangedDocumentArchive.FIND('-') THEN BEGIN
        REPEAT
          EDIChangedDocument.TRANSFERFIELDS(EDIChangedDocumentArchive);
          EDIChangedDocument.INSERT;
        UNTIL EDIChangedDocumentArchive.NEXT =0;
      END;

      OrderStatusHdrArchive.RESET;
      OrderStatusHdrArchive.SETRANGE("Internal Doc. No.",EDIRecDocHdrArchive."Internal Doc. No.");
      IF OrderStatusHdrArchive.FIND('-') THEN BEGIN
        REPEAT
          OrderStatusHdr.TRANSFERFIELDS(OrderStatusHdrArchive);
          OrderStatusHdr.INSERT;
          OrderStatusLineArchive.RESET;
          OrderStatusLineArchive.SETRANGE("Internal Doc. No.",EDIRecDocHdrArchive. "Internal Doc. No.");
          OrderStatusLineArchive.SETRANGE("Trade Partner Code",OrderStatusHdrArchive."Trade Partner Code");
          OrderStatusLineArchive.SETRANGE("Source Doc. Type",OrderStatusHdrArchive."Source Doc. Type"::Order);
          OrderStatusLineArchive.SETRANGE("Source Doc. No.",OrderStatusHdrArchive."Source Doc. No.");
          OrderStatusLineArchive.SETRANGE("Document Type",OrderStatusHdrArchive."Document Type");
          IF OrderStatusLineArchive.FIND('-') THEN
            REPEAT
              OrderStatusLine.TRANSFERFIELDS(OrderStatusLineArchive);
              OrderStatusLine.INSERT;
            UNTIL OrderStatusLineArchive.NEXT = 0;
        UNTIL OrderStatusHdrArchive. NEXT =0;
      END;

      OrderStatusEntryArchive.RESET;
      OrderStatusEntryArchive.SETRANGE("EDI Internal Doc. No.",EDIRecDocHdrArchive."Internal Doc. No.");
      IF OrderStatusEntryArchive.FIND('-') THEN BEGIN
       REPEAT
         IF OrderStatusEntry."Source Doc. No." <> '' THEN BEGIN
           OrderStatusEntry.TRANSFERFIELDS(OrderStatusEntryArchive);
           OrderStatusEntry.INSERT;
         END;
       UNTIL OrderStatusEntryArchive.NEXT = 0;
      END;

      EDIRecDocHdrInfoArchive.RESET;
      EDIRecDocHdrInfoArchive.SETRANGE("Internal Doc. No.",EDIRecDocHdrArchive."Internal Doc. No.");
      IF EDIRecDocHdrInfoArchive.FIND('-') THEN BEGIN
        REPEAT
          EDIRecDocHdrInfo.TRANSFERFIELDS(EDIRecDocHdrInfoArchive);
          EDIRecDocHdrInfo.INSERT;
        UNTIL EDIRecDocHdrInfoArchive.NEXT = 0;
      END;

      EDIRecDocLineInfoArchive.RESET;
      EDIRecDocLineInfoArchive.SETRANGE("Internal Doc. No.",EDIRecDocHdrArchive."Internal Doc. No.");
      IF EDIRecDocLineInfoArchive.FIND('-') THEN BEGIN
        REPEAT
          EDIRecDocLineInfo.TRANSFERFIELDS(EDIRecDocLineInfoArchive);
          EDIRecDocLineInfo.INSERT;
        UNTIL EDIRecDocLineInfoArchive.NEXT = 0;
      END;

      EDIRecDocHdrArchive.DELETE(TRUE);

      IF NOT Batch THEN
        IF GUIALLOWED THEN
          MESSAGE(Text006,EDIRecDocHdrArchive."Internal Doc. No.");
    END;

    PROCEDURE RestoreSendDocument@7(VAR EDISendDocumentHdrArchive@1000 : Record 14002377;Batch@1240030000 : Boolean);
    VAR
      EDISendDocumentHdr@1001 : Record 14002369;
      EDISendDocumentField@1004 : Record 14002370;
      EDISendDocumentFieldArchive@1003 : Record 14002378;
    BEGIN
      EDISendDocumentHdr.SETRANGE("No.",EDISendDocumentHdrArchive."No.");
      IF EDISendDocumentHdr.FIND('-') THEN
        ERROR(Text008,
              EDISendDocumentHdrArchive."No.",EDISendDocumentHdrArchive."Internal Doc No.",
              EDISendDocumentHdrArchive."Document No.",EDISendDocumentHdrArchive.Version);

      EDISendDocumentHdr.INIT;
      EDISendDocumentHdr.TRANSFERFIELDS(EDISendDocumentHdrArchive);
      EDISendDocumentHdr.INSERT(TRUE);
      EDISendDocumentFieldArchive.RESET;
      EDISendDocumentFieldArchive.SETRANGE("No.",EDISendDocumentHdrArchive."No.");
      IF EDISendDocumentFieldArchive.FIND('-') THEN
        REPEAT
          EDISendDocumentField.INIT;
          EDISendDocumentField.TRANSFERFIELDS(EDISendDocumentFieldArchive);
          EDISendDocumentField.INSERT(TRUE);
        UNTIL EDISendDocumentFieldArchive.NEXT = 0;

      EDISendDocumentHdrArchive.DELETE(TRUE);

      IF NOT Batch THEN
        MESSAGE(Text003);
    END;

    BEGIN
    END.
  }
}

