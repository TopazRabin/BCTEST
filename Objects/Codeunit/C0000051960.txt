OBJECT Codeunit 51960 Codeunit441EventSubscriber
{
  OBJECT-PROPERTIES
  {
    Date=07/01/20;
    Time=12:00:00 AM;
    Modified=Yes;
    Version List=TPZ000.00.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE Cu441_SetSalesBufferPrepaymentPct@1000000006(VAR SalesLine@1000000000 : Record 50006;Date@1000000002 : Date);
    VAR
      Cust@1000 : Record 18;
      SalesPrepaymentPct@1000000001 : Record 459;
    BEGIN
      WITH SalesPrepaymentPct DO BEGIN
        IF (SalesLine.Type <> SalesLine.Type::Item) OR (SalesLine."No." = '') OR
           (SalesLine."Document Type" <> SalesLine."Document Type"::Order)
        THEN
          EXIT;
        SETFILTER("Starting Date",'..%1',Date);
        SETFILTER("Ending Date",'%1|>=%2',0D,Date);
        SETRANGE("Item No.",SalesLine."No.");
        FOR "Sales Type" := "Sales Type"::Customer TO "Sales Type"::"All Customers" DO BEGIN
          SETRANGE("Sales Type","Sales Type");
          CASE "Sales Type" OF
            "Sales Type"::Customer:
              BEGIN
                SETRANGE("Sales Code",SalesLine."Bill-to Customer No.");
                IF Cu441_ApplySalesBufferPrepaymentPct(SalesLine,SalesPrepaymentPct) THEN
                  EXIT;
              END;
            "Sales Type"::"Customer Price Group":
              BEGIN
                //TPZEDI - added guarded GET
                IF Cust.GET(SalesLine."Bill-to Customer No.") THEN BEGIN
                  IF Cust."Customer Price Group" <> '' THEN
                    SETRANGE("Sales Code",Cust."Customer Price Group");
                  IF Cu441_ApplySalesBufferPrepaymentPct(SalesLine,SalesPrepaymentPct) THEN
                    EXIT;
                END;
                //TPZEDI
              END;
            "Sales Type"::"All Customers":
              BEGIN
                SETRANGE("Sales Code");
                IF Cu441_ApplySalesBufferPrepaymentPct(SalesLine,SalesPrepaymentPct) THEN
                  EXIT;
              END;
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE Cu441_ApplySalesBufferPrepaymentPct@1000000005(VAR SalesLine@1000 : Record 50006;VAR SalesPrepaymentPct@1001 : Record 459) : Boolean;
    BEGIN
      WITH SalesPrepaymentPct DO
        IF FINDLAST THEN BEGIN
          SalesLine."Prepayment %" := "Prepayment %";
          EXIT(TRUE);
        END;
    END;

    BEGIN
    END.
  }
}

