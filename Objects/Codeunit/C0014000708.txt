OBJECT Codeunit 14000708 E-Ship Survey
{
  OBJECT-PROPERTIES
  {
    Date=06/28/17;
    Time=12:00:00 PM;
    Version List=SE0.60.02;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            Window.OPEN(Text010);

            CompanyInfo.GET;

            CLEAR(Mail);
            Mail.AddBodyline(STRSUBSTNO(Text003,SERIALNUMBER,CompanyInfo.Name));
            Mail.AddBodyline(' ');

            FOR i := 0 TO 11 DO BEGIN
              ShippingAgent.SETRANGE("Shipper Type",i);
              IF ShippingAgent.FIND('-') THEN BEGIN
                Mail.AddBodyline(
                  STRSUBSTNO(
                    Text001,ShippingAgent."Shipper Type",
                    CalculateNoOfPackages(ShippingAgent."Shipper Type",1)));
                Mail.AddBodyline(
                  STRSUBSTNO(
                    Text002,ShippingAgent."Shipper Type",
                    CalculateNoOfPackages(ShippingAgent."Shipper Type",2)));
                Mail.AddBodyline(
                  STRSUBSTNO(
                    Text005,ShippingAgent."Shipper Type",
                    CalculateNoOfPackages(ShippingAgent."Shipper Type",3)));
                Mail.AddBodyline(
                  STRSUBSTNO(
                    Text006,ShippingAgent."Shipper Type",
                    CalculateNoOfPackages(ShippingAgent."Shipper Type",4)));
                Mail.AddBodyline(' ');
              END;
            END;

            TableInfo.RESET;
            TableInfo.SETRANGE("Company Name",COMPANYNAME);
            TableInfo.SETFILTER(
              "Table No.",
              '14000603|14000604|14000605|14000704|14000705|14000709|14000710|' +
              '14000822|14000823|14000825|14000826');
            IF TableInfo.FIND('-') THEN
              REPEAT
                Mail.AddBodyline(
                  STRSUBSTNO(
                    Text007,TableInfo."Table No.",TableInfo."Table Name",
                    TableInfo."No. of Records",TableInfo."Size (KB)"));
              UNTIL TableInfo.NEXT = 0;
            Mail.AddBodyline(' ');

            Object.RESET;
            Object.SETFILTER("Version List",'SE0*');
            IF Object.FIND('-') THEN BEGIN
              REPEAT
                Object.SETFILTER("Version List",'SE0*&>%1',Object."Version List");
              UNTIL NOT Object.FIND('-');

              Mail.AddBodyline(STRSUBSTNO(Text008,Object."Version List"));
            END;
            Mail.AddBodyline(' ');

            Object.RESET;
            Object.SETFILTER("Version List",'NAVW1*');
            IF Object.FIND('-') THEN BEGIN
              REPEAT
                Object.SETFILTER("Version List",'NAVW1*&>%1',Object."Version List");
              UNTIL NOT Object.FIND('-');

              Mail.AddBodyline(STRSUBSTNO(Text009,Object."Version List"));
            END;
            Mail.AddBodyline(' ');

            Window.CLOSE;

            Mail.NewMessage(
              'info@lanhamassoc.com','','',STRSUBSTNO(Text003,SERIALNUMBER,CompanyInfo.Name),'','',TRUE);
          END;

  }
  CODE
  {
    VAR
      CompanyInfo@1240030001 : Record 79;
      ShippingAgent@1240030002 : Record 291;
      TableInfo@1240030010 : Record 2000000028;
      Object@1240030014 : Record 2000000001;
      Mail@1240030000 : Codeunit 397;
      Text001@1240030003 : TextConst 'ENU=No. of %1 Packages last week: %2.';
      Text002@1240030004 : TextConst 'ENU=No. of %1 Packages last month: %2.';
      Text003@1240030005 : TextConst 'ENU=E-Ship Survey from %1 %2.';
      Text004@1240030006 : TextConst 'ENU=Wrong Date Rage Type';
      Window@1240030015 : Dialog;
      i@1240030007 : Integer;
      Text005@1240030008 : TextConst 'ENU=No. of %1 Packages last year: %2.';
      Text006@1240030009 : TextConst 'ENU=Total No. of %1 Packages: %2.';
      Text007@1240030011 : TextConst 'ENU=Table %1 %2 No. of Records %3, Size %4 Kb.';
      Text008@1240030012 : TextConst 'ENU=Highest SE Version List %1.';
      Text009@1240030013 : TextConst 'ENU=Highest NAVW1 Version List %1.';
      Text010@1240030016 : TextConst 'ENU=Calculating Statistics.';

    PROCEDURE CalculateNoOfPackages@1240030000(ShipperType@1240030004 : ' ,UPS,FEDEX,LTL,,Airborne,,,,,Generic';DateRangeType@1240030005 : ' ,Last Week,Last Month,Last Year,Total Count') : Integer;
    VAR
      ShippingAgent2@1240030000 : Record 291;
      Package@1240030001 : Record 14000701;
      PostedPackage@1240030002 : Record 14000704;
      TotalNo@1240030007 : Integer;
      FirstDate@1240030008 : Date;
      LastDate@1240030009 : Date;
    BEGIN
      ShippingAgent2.RESET;
      ShippingAgent2.SETRANGE("Shipper Type",ShipperType);
      IF NOT ShippingAgent2.FIND('-') THEN
        EXIT(0);

      CalculateFirstAndLastDate(DateRangeType,FirstDate,LastDate);

      TotalNo := 0;
      REPEAT
        Package.RESET;
        Package.SETCURRENTKEY("Shipping Agent Code");
        Package.SETRANGE("Shipping Agent Code",ShippingAgent2.Code);
        Package.SETRANGE("Packing Date",FirstDate,LastDate);
        Package.SETRANGE("Original Package No.",'');
        TotalNo := TotalNo + Package.COUNT;

        PostedPackage.RESET;
        PostedPackage.SETCURRENTKEY("Shipping Agent Code");
        PostedPackage.SETRANGE("Shipping Agent Code",ShippingAgent2.Code);
        PostedPackage.SETRANGE("Packing Date",FirstDate,LastDate);
        PostedPackage.SETRANGE("Original Package No.",'');
        TotalNo := TotalNo + PostedPackage.COUNT;
      UNTIL ShippingAgent2.NEXT = 0;

      EXIT(TotalNo);
    END;

    PROCEDURE CalculateFirstAndLastDate@1240030001(DateRangeType@1240030005 : ' ,Last Week,Last Month,Last Year,Total Count';VAR FirstDate@1240030004 : Date;VAR LastDate@1240030006 : Date);
    BEGIN
      CASE DateRangeType OF
        DateRangeType::"Last Week":
          BEGIN
            FirstDate := CALCDATE('-1W');
            FirstDate := DWY2DATE(1,DATE2DWY(FirstDate,2),DATE2DWY(FirstDate,3));
            LastDate := CALCDATE('+6D',FirstDate);
          END;
        DateRangeType::"Last Month":
          BEGIN
            FirstDate := CALCDATE('-1M');
            FirstDate := DMY2DATE(1,DATE2DMY(FirstDate,2),DATE2DMY(FirstDate,3));
            LastDate := CALCDATE('+1M-1D',FirstDate);
          END;
        DateRangeType::"Last Year":
          BEGIN
            FirstDate := CALCDATE('-1Y');
            FirstDate := DMY2DATE(1,1,DATE2DMY(FirstDate,3));
            LastDate := CALCDATE('+1Y-1D',FirstDate);
          END;
        DateRangeType::"Total Count":
          BEGIN
            FirstDate := 01010000D;
            LastDate := 12319999D;
          END;
        ELSE
          ERROR(Text004);
      END;
    END;

    BEGIN
    END.
  }
}

