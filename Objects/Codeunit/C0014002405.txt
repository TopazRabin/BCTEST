OBJECT Codeunit 14002405 E.D.I. Sales Cr. Memo Send
{
  OBJECT-PROPERTIES
  {
    Date=02/25/19;
    Time=12:00:00 PM;
    Version List=SE0.61;
  }
  PROPERTIES
  {
    TableNo=114;
    Permissions=TableData 114=rimd;
    OnRun=BEGIN
            IF NOT ExportSalesCrMemo(Rec) THEN
              ERROR(Text001,"No.");
          END;

  }
  CODE
  {
    VAR
      BilltoCustomer@1240030000 : Record 18;
      BillOfLading@1240030001 : Record 14000822;
      CompanyInformation@1240030002 : Record 79;
      EDISetup@1240030003 : Record 14002367;
      EDITemplate@1240030004 : Record 14002350;
      EDITradePartner@1240030005 : Record 14002360;
      EDIDocument@1240030006 : Record 14002353;
      EDISegment@1240030007 : Record 14002354;
      EDISegment2@1240030008 : Record 14002354;
      EDIElement@1240030009 : Record 14002355;
      EDISendDocHdr@1240030010 : Record 14002369;
      EDITotalFields@1240030011 : Record 14002361;
      EDICustCrossRef@1240030012 : Record 14002362;
      Item@1240030013 : Record 27;
      ItemUnitOfMeasure@1240030014 : Record 5404;
      PaymentTerms@1240030015 : Record 3;
      SelltoCustomer@1240030016 : Record 18;
      ShiptoAddress@1240030017 : Record 222;
      SalesCrMemoHeader@1240030018 : Record 114;
      SalesCrMemoLine@1240030019 : Record 115;
      SalespersonPurchaser@1240030021 : Record 13;
      SalesHeader@1240030022 : Record 36;
      SalesLine@1240030023 : Record 37;
      ShipmentMethod@1240030025 : Record 10;
      ShippingAgent@1240030026 : Record 291;
      EDIElementArray@1240030027 : ARRAY [100] OF Record 14002355;
      SalesCommentLine@1240030068 : Record 44;
      ItemCrossReference@1240030069 : Record 5717;
      EDITradePtnrItem@1240020000 : Record 14002364;
      BilltoCountry@1240020018 : Record 9;
      SelltoCountry@1240020017 : Record 9;
      EDISend@1240030028 : Codeunit 14002356;
      EDIFormat@1240030030 : Codeunit 14002351;
      EDIPackageMapIntegration@1240030065 : Codeunit 14002378;
      EDIGetReverseCrossRef@1240020002 : Codeunit 14000373;
      CreateEDIAlert@1240020008 : Codeunit 14000375;
      EDILoadDBFields@1240020010 : Codeunit 14002392;
      EDILoadEDIOutFields@1240020014 : Codeunit 14000381;
      TimeVariable@1240020011 : Time;
      DateTimeVariable@1240020012 : DateTime;
      BooleanVariable@1240030031 : Boolean;
      BypassSegment@1240030032 : Boolean;
      DateVariable@1240030033 : Date;
      DecimalVariable@1240030034 : Decimal;
      DocumentNo@1240030035 : Code[10];
      i@1240030036 : Integer;
      ILidx@1240030037 : Integer;
      InvoiceLineNoArray@1240030038 : ARRAY [500] OF Integer;
      IntegerVariable@1240030039 : Integer;
      InvoiceTax@1240030040 : Decimal;
      InvFreightAmt@1240030042 : Decimal;
      InvNetShipWeight@1240030043 : Decimal;
      InvGrossShipWeight@1240030044 : Decimal;
      InvPalletWeight@1240030045 : Decimal;
      InvTotalUnits@1240030046 : Decimal;
      InvTotalBaseUnits@1240030047 : Decimal;
      LineNumber@1240030048 : Integer;
      NewSegment@1240030049 : Boolean;
      Ok@1240030050 : Boolean;
      OutFldArray@1240030052 : ARRAY [100] OF Text[250];
      OutLenArray@1240030053 : ARRAY [100] OF Integer;
      OutErrorArray@1240030054 : ARRAY [100] OF Boolean;
      OutputOff@1240030055 : Boolean;
      ProcessingLines@1240030056 : Boolean;
      TotalInvDiscount@1240030062 : Decimal;
      TradePtnrFound@1240030063 : Boolean;
      TotalSegments@1240030064 : Integer;
      Text001@1001 : TextConst 'ENU=Credit Memo %1 not exported.';
      Text002@1002 : TextConst 'ENU=Customer %1 is not set up as an EDI Trading Partner.';
      Text003@1003 : TextConst 'ENU=Customer %1 is not set up with an Active EDI transaction %2.';
      Text004@1004 : TextConst 'ENU=EDI Document %1 for Trading Partner %2 has a mapping error.';
      Text005@1005 : TextConst 'ENU=Invoice line not found for Invoice # %1.';
      Text006@1006 : TextConst 'ENU=Unable to find EDI segment definition for Trading Partner %1.';
      Text007@1007 : TextConst 'ENU=LINE END Command found before LINE BEGIN.  Check Segment Definition.';
      Text008@1008 : TextConst 'ENU=Unable to find EDI Segment Elements for the following: %1 - %2 - %3 -%4';
      InvoiceTaxPercent@1240030066 : Decimal;
      InvDropshipAmt@1240030024 : Decimal;
      LoopFinished@1240030067 : Boolean;
      LoopCnt@1240020001 : Integer;
      RepeatCount@1240030051 : Integer;
      RecordNo@1240020009 : Integer;
      TotalInvLineDiscount@1240030070 : Decimal;
      ReverseCrossReference@1240020007 : Boolean;
      ReverseCrossRefArray@1240020006 : ARRAY [100] OF Text[40];
      ReverseCrossRefValue@1240020005 : Text[40];
      BypassSegment2@1240020004 : Boolean;
      OutError2@1240020003 : Boolean;
      RecRef@1240020013 : RecordRef;
      XMLDoc@1240020015 : Boolean;

    PROCEDURE ExportSalesCrMemo@18(VAR CurrentSalesCrMemoHdr@1240030001 : Record 114) ErrorCode@1240030000 : Boolean;
    VAR
      EDIXMLManagement@1240020002 : Codeunit 14099302;
      NodeOnly@1240020000 : Boolean;
      CreateNode@1240020001 : Boolean;
    BEGIN
      CLEAR(EDILoadEDIOutFields);
      SalesCrMemoHeader.COPY(CurrentSalesCrMemoHdr);
      SalesCrMemoHeader.CALCFIELDS(Amount,"Amount Including VAT");

      EDISetup.LOCKTABLE;
      EDISetup.GET;
      IF SalesCrMemoHeader."EDI Internal Doc. No." = '' THEN BEGIN
        EDISetup."Last Internal Doc. No." := INCSTR(EDISetup."Last Internal Doc. No.");
        SalesCrMemoHeader."EDI Internal Doc. No." := EDISetup."Last Internal Doc. No.";
        EDISetup.MODIFY;
      END;

      TradePtnrFound := FALSE;

      EDITradePartner.SETCURRENTKEY("Customer No.");
      EDITradePartner.SETRANGE("Customer No.",SalesCrMemoHeader."Bill-to Customer No.");
      IF EDITradePartner.FIND('-') THEN BEGIN
        TradePtnrFound := TRUE;
        EDICustCrossRef.INIT;
        EDICustCrossRef.RESET;
        EDICustCrossRef.SETRANGE("Trade Partner No.",EDITradePartner."No.");
        EDICustCrossRef.SETRANGE("Navision Ship To Code",SalesCrMemoHeader."Ship-to Code");
        Ok := EDICustCrossRef.FIND('-');
      END;

      IF NOT TradePtnrFound THEN BEGIN
        EDICustCrossRef.INIT;
        EDICustCrossRef.SETCURRENTKEY("Navision Sell To Code");
        EDICustCrossRef.SETRANGE(
          "Navision Sell To Code",SalesCrMemoHeader."Sell-to Customer No.");
        EDICustCrossRef.SETRANGE("Navision Ship To Code",SalesCrMemoHeader."Ship-to Code");
        IF EDICustCrossRef.FIND('-') THEN BEGIN
          EDITradePartner.GET(EDICustCrossRef."Trade Partner No.");
          TradePtnrFound := TRUE;
        END;
      END;
      IF NOT TradePtnrFound THEN
        ERROR(
          Text002,
          SalesCrMemoHeader."Sell-to Customer No.");

      DocumentNo := 'E_SLSCRM';

      EDIDocument.SETRANGE("Trade Partner No.",EDITradePartner."No.");
      EDIDocument.SETFILTER(
        Status,'%1|%2',EDIDocument.Status::Test,EDIDocument.Status::Production);
      EDIDocument.SETRANGE("Navision Document",DocumentNo);
      EDIDocument.SETRANGE(Type,EDIDocument.Type::Export);
      IF NOT EDIDocument.FIND('-') THEN
        ERROR(
          Text003,
          SalesCrMemoHeader."Bill-to Customer No.",DocumentNo);
      XMLDoc := EDIDocument."XML Document";

      IF EDIDocument."Packaged Map" THEN BEGIN
        IF EDIPackageMapIntegration.EDISalesCrMemoSend(SalesCrMemoHeader) THEN
        // IF EDISalesCrMemoSendPM.ExportSalesCrMemo(SalesCrMemoHeader) THEN
          EXIT(TRUE)
        ELSE
          EXIT(FALSE)
      END;

      EDIDocument.CALCFIELDS("Element Mapping Error","Conditional Mapping Error");
      IF EDIDocument."Element Mapping Error" OR EDIDocument."Conditional Mapping Error" THEN
        ERROR(
          Text004,
          EDIDocument."Navision Document",EDIDocument."Trade Partner No.");

      EDITemplate.GET(EDIDocument."EDI Template Code");

      EDISendDocHdr."Internal Doc No." := SalesCrMemoHeader."EDI Internal Doc. No.";
      EDISendDocHdr."Navision Document" := DocumentNo;
      EDISendDocHdr."Document No." := SalesCrMemoHeader."No.";
      EDISend.CreateSendHdr(EDITradePartner,EDIDocument,EDISendDocHdr);

      ReadHdrAssocTables;

      CLEAR(InvoiceLineNoArray);
      LineNumber := 0;
      InvFreightAmt := 0;
      InvNetShipWeight := 0;
      InvGrossShipWeight := 0;
      InvPalletWeight := 0;
      InvTotalUnits := 0;
      InvTotalBaseUnits := 0;
      TotalInvDiscount := 0;
      InvoiceTaxPercent := 0;
      TotalSegments := 0;
      InvDropshipAmt := 0;
      TotalInvLineDiscount := 0;
      SalesCrMemoHeader.CALCFIELDS("Amount Including VAT",Amount);
      InvoiceTax := SalesCrMemoHeader."Amount Including VAT" - SalesCrMemoHeader.Amount;
      InvoiceTaxPercent := InvoiceTax / SalesCrMemoHeader.Amount;

      SalesCrMemoLine.RESET;
      SalesCrMemoLine.SETRANGE("Document No.",SalesCrMemoHeader."No.");
      SalesCrMemoLine.SETFILTER("No.",'<>%1','');
      IF NOT EDIDocument."Export Zero Qty. Lines" THEN
        SalesCrMemoLine.SETFILTER(Quantity,'>0');
      ILidx := 0;
      IF SalesCrMemoLine.FIND('-') THEN
        REPEAT
          ILidx := ILidx + 1;
          InvoiceLineNoArray[ILidx] := SalesCrMemoLine."Line No.";
        UNTIL SalesCrMemoLine.NEXT = 0;
      IF NOT SalesCrMemoLine.FIND('-') THEN
        ERROR(Text005,SalesCrMemoHeader."No.");

      // Create Line Segments
      EDISegment.SETRANGE("Trade Partner No.",EDIDocument."Trade Partner No.");
      EDISegment.SETRANGE("Navision Document",EDIDocument."Navision Document");
      EDISegment.SETRANGE("EDI Document No.",EDIDocument."EDI Document No.");
      EDISegment.SETRANGE(Version,EDIDocument.Version);
      EDISegment.SETRANGE(Type,EDIDocument.Type);
      IF NOT EDISegment.FIND('-') THEN
        ERROR(
          Text006,
          EDIDocument."Trade Partner No.");
      REPEAT
        CASE EDISegment."Segment Type" OF
          EDISegment."Segment Type"::Command:
            BEGIN
              IF (EDISegment.Segment = 'LINE BEGIN') OR
                 (EDISegment."Node Name" = 'LINE BEGIN')
              THEN BEGIN
                EDISegment2.COPY(EDISegment);
                ProcessingLines := TRUE;
                OutputOff := FALSE;
                IF NOT EDIDocument."Export G/L Account Lines" AND
                  (SalesCrMemoLine.Type = EDITotalFields.Type::"Account (G/L)")
                THEN
                  OutputOff := TRUE;
                IF NOT EDIDocument."Export Resource Lines" AND
                  (SalesCrMemoLine.Type = EDITotalFields.Type::Resource)
                THEN
                  OutputOff := TRUE;
                IF NOT EDIDocument."Export Item Charge Lines" AND
                  (SalesCrMemoLine.Type = EDITotalFields.Type::"Fixed Asset")
                THEN
                  OutputOff := TRUE;
                 ReadLineAssocTables;
              END;
              IF (EDISegment.Segment = 'LINE END') OR
                 (EDISegment."Node Name" = 'LINE END')
              THEN BEGIN
                OutputOff := FALSE;
                IF NOT ProcessingLines THEN
                  ERROR(Text007);
                IF SalesCrMemoLine.NEXT <> 0 THEN BEGIN
                  IF NOT EDIDocument."Export G/L Account Lines" AND
                    (SalesCrMemoLine.Type = EDITotalFields.Type::"Account (G/L)")
                  THEN
                      OutputOff := TRUE;
                  IF NOT EDIDocument."Export Resource Lines" AND
                    (SalesCrMemoLine.Type = EDITotalFields.Type::Resource)
                  THEN
                      OutputOff := TRUE;
                  IF NOT EDIDocument."Export Item Charge Lines" AND
                    (SalesCrMemoLine.Type = EDITotalFields.Type::"Fixed Asset")
                  THEN
                      OutputOff := TRUE;
                  ReadLineAssocTables;
                  EDISegment.GET(
                    EDISegment2."Trade Partner No.",EDISegment2."Navision Document",
                    EDISegment2."EDI Document No.",EDISegment2.Version,
                    EDISegment2.Type,EDISegment2."Processing Sequence",EDISegment2.Segment);
                END;
              END;
            END;
          EDISegment."Segment Type"::Segment:
            BEGIN
              LoopFinished := TRUE;
              BypassSegment := FALSE;
              NodeOnly := FALSE;
              CreateNode := TRUE;
              EDIElement.SETRANGE("Trade Partner No.",EDISegment."Trade Partner No.");
              EDIElement.SETRANGE("Navision Document",EDISegment."Navision Document");
              EDIElement.SETRANGE("EDI Document No.",EDISegment."EDI Document No.");
              EDIElement.SETRANGE(Version,EDISegment.Version);
              EDIElement.SETRANGE(Type,EDISegment.Type);
              EDIElement.SETRANGE(Segment,EDISegment.Segment);
              IF NOT EDIElement.FIND('-') THEN BEGIN
                IF XMLDoc THEN BEGIN
                  EDISegment.CALCFIELDS("Has Attributes","Node Text");
                  IF EDISegment."Node Text" OR EDISegment."Has Attributes" THEN
                    ERROR(
                      Text008,
                        EDISegment."Trade Partner No.",EDISegment."Navision Document",
                        EDISegment.Version,EDISegment."Node Name")
                  ELSE
                    NodeOnly := TRUE;
                END ELSE
                  ERROR(
                    Text008,
                      EDISegment."Trade Partner No.",EDISegment."Navision Document",
                      EDISegment.Version,EDISegment.Segment);
              END ELSE
                IF XMLDoc AND EDIElement."Node Text" THEN
                  CreateNode := FALSE;
              IF (XMLDoc = FALSE) OR (XMLDoc AND NodeOnly = FALSE) THEN BEGIN
                TotalSegments := TotalSegments + 1;
                CLEAR(OutFldArray);
                CLEAR(OutLenArray);
                CLEAR(OutErrorArray);
                CLEAR(EDIElementArray);
                i := 1;
                LoopCnt := 1;
                REPEAT
                  ReverseCrossRefArray[i] := '';
                  ReverseCrossReference := EDIElement."Reverse Cross Reference";
                  IF ((EDIElement."Fixed Value" <> '') AND (XMLDoc = FALSE)) OR
                     ((EDIElement."XML Fixed Value" <> '') AND (XMLDoc))
                  THEN BEGIN
                    IF XMLDoc THEN
                      OutFldArray[i] := EDIElement."XML Fixed Value"
                    ELSE
                      OutFldArray[i] := EDIElement."Fixed Value";
                  END ELSE
                    CASE EDIElement.Origin OF
                      EDIElement.Origin::Navision:
                        BEGIN
                          LoadDBFields;
                          IF ReverseCrossReference THEN BEGIN
                            CLEAR(EDIGetReverseCrossRef);
                            ReverseCrossRefArray[i] :=
                              EDIGetReverseCrossRef.GetReverseCrossRef(
                                EDIElement,EDISendDocHdr,OutFldArray[i]);
                              EDIGetReverseCrossRef.FormatOutput(
                                EDISendDocHdr,EDIElement,OutError2,ReverseCrossRefArray,BypassSegment2,i);
                          END;
                        END;
                      EDIElement.Origin::"EDI In":
                        EDIFormat.LoadEDIInFields(
                          EDIElement,SalesCrMemoHeader."EDI Internal Doc. No.",SalesCrMemoLine."EDI Segment Group",
                          ProcessingLines,i,OutFldArray,BooleanVariable,DateVariable,DecimalVariable,IntegerVariable,
                          TimeVariable,DateTimeVariable);
                      EDIElement.Origin::"EDI Out":
                        LoadEDIOutFields;
                    END;
                  OutLenArray[i] := EDIElement.Length;
                  EDIFormat.FormatOutput(
                    EDITemplate,EDIElement,OutFldArray,OutErrorArray,i,BypassSegment,
                    BooleanVariable,DateVariable,DecimalVariable,IntegerVariable,TimeVariable,
                    DateTimeVariable);
                  EDIElementArray[i].COPY(EDIElement);
                  IF ReverseCrossReference THEN
                    OutErrorArray[i] := OutError2;
                  i := i + 1;
                UNTIL EDIElement.NEXT = 0;
              END;
              NewSegment := TRUE;
              i := 1;
              IF ReverseCrossReference THEN
                BypassSegment := BypassSegment2;
              CASE EDIElement.Origin OF
                EDIElement.Origin::Navision:
                  BEGIN
                    IF (LoopCnt = 1) AND (EDISegment."Repeat Segment") THEN BEGIN
                      RepeatSegment;
                      LoopCnt := LoopCnt + 1;
                    END;
                  END;
              END;
              IF (NOT BypassSegment) AND (NOT OutputOff) THEN BEGIN
                IF XMLDoc AND CreateNode THEN BEGIN
                  EDISend.CreateSendLine(
                    EDISendDocHdr,EDIElementArray[i],OutFldArray[i],OutLenArray[i],OutErrorArray[i],
                    NewSegment,ReverseCrossRefValue,EDIDocument,TRUE,EDISegment);
                  NewSegment := FALSE;
                END;
                IF (XMLDoc = FALSE) OR (XMLDoc AND NodeOnly = FALSE) THEN
                  REPEAT
                    ReverseCrossRefValue := ReverseCrossRefArray[i];
                    EDISend.CreateSendLine(
                      EDISendDocHdr,EDIElementArray[i],OutFldArray[i],OutLenArray[i],OutErrorArray[i],
                      NewSegment,ReverseCrossRefValue,EDIDocument,FALSE,EDISegment);
                    NewSegment := FALSE;
                    i := i + 1;
                  UNTIL OutLenArray[i] = 0;
              END ELSE
                TotalSegments := TotalSegments - 1;
            END;
        END;
        IF (EDISegment."Repeat Segment") AND (NOT LoopFinished) THEN BEGIN
          GetNextRecord;
          EDISegment.NEXT(-1);
        END;
      UNTIL EDISegment.NEXT = 0;

      IF EDISetup."Send Document Error" THEN BEGIN
        EDISendDocHdr.CALCFIELDS("Data Error");
        IF EDISendDocHdr."Data Error" THEN BEGIN
          CLEAR(CreateEDIAlert);
          CreateEDIAlert.CreateAlertSendDocError(EDISendDocHdr."No.",1,FALSE);
        END;
      END;

      SalesCrMemoHeader."EDI Cr. Memo Generated" := TRUE;
      SalesCrMemoHeader."EDI Cr. Memo Gen. Date" := TODAY;
      SalesCrMemoHeader.MODIFY;

      IF XMLDoc THEN
        EDIXMLManagement.ResponseIDSubstitution(EDISendDocHdr);

      EXIT(TRUE);
    END;

    PROCEDURE LoadEDIOutFields@16();
    BEGIN
      BooleanVariable := FALSE;
      DateVariable := 0D;
      IntegerVariable := 0;
      DecimalVariable := 0;
      TimeVariable := 0T;
      DateTimeVariable := 0DT;

      IF EDIElement.Origin = EDIElement.Origin::"EDI Out" THEN BEGIN
        CASE EDIElement."Field Name" OF
          ELSE BEGIN
            LoadEDIOutVariable;
            LoadEDIOutRecordRef;
            EDILoadEDIOutFields.SalesCrMemoSendEDIOut(
              EDIElement,OutFldArray,DecimalVariable,IntegerVariable,DateVariable,
              BooleanVariable,TimeVariable,DateTimeVariable,i);
          END;
        END;
      END;
    END;

    PROCEDURE UpdateLineTotals@28();
    BEGIN
      IF NOT OutputOff THEN BEGIN
        LineNumber := LineNumber + 1;
        InvTotalUnits := InvTotalUnits + SalesCrMemoLine.Quantity;
        InvTotalBaseUnits := InvTotalBaseUnits + SalesCrMemoLine."Quantity (Base)";
      END;

      IF EDITotalFields.GET(
        'Total Freight Amt.',SalesCrMemoLine.Type,SalesCrMemoLine."No.")
      THEN
        InvFreightAmt := InvFreightAmt + SalesCrMemoLine.Amount;

      IF EDITotalFields.GET(
        'Total Pallet Weight',SalesCrMemoLine.Type,SalesCrMemoLine."No.")
      THEN
        InvPalletWeight :=
          InvPalletWeight + (SalesCrMemoLine."Net Weight" * SalesCrMemoLine.Quantity);

      InvNetShipWeight := InvNetShipWeight + (SalesCrMemoLine."Net Weight" *
                                              SalesCrMemoLine.Quantity);
      InvGrossShipWeight := InvGrossShipWeight + (SalesCrMemoLine."Gross Weight" *
                                                  SalesCrMemoLine.Quantity);
      TotalInvDiscount := TotalInvDiscount + SalesCrMemoLine."Inv. Discount Amount";
      TotalInvLineDiscount := TotalInvLineDiscount + SalesCrMemoLine."Line Discount Amount";

      IF EDITotalFields.GET(
        'Total Dropship Amt.',SalesCrMemoLine.Type,SalesCrMemoLine."No.")
      THEN
        InvDropshipAmt := InvDropshipAmt + SalesCrMemoLine.Amount;
    END;

    PROCEDURE LoadDBFields@23();
    BEGIN
      CASE EDIElement."NAV Table No." OF
        3:
          BEGIN
            RecRef.OPEN(3);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(PaymentTerms);
          END;
        10:
          BEGIN
            RecRef.OPEN(10);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(ShipmentMethod);
          END;
        13:
          BEGIN
            RecRef.OPEN(10);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(SalespersonPurchaser);
          END;
        27:
          BEGIN
            RecRef.OPEN(27);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(Item);
          END;
        36:
          BEGIN
            RecRef.OPEN(36);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(SalesHeader);
          END;
        37:
          BEGIN
            RecRef.OPEN(37);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(SalesLine);
          END;
        44:
          BEGIN
            RecRef.OPEN(44);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(SalesCommentLine);
          END;
        79:
          BEGIN
            RecRef.OPEN(79);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(CompanyInformation);
          END;
        114:
          BEGIN
            RecRef.OPEN(114);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(SalesCrMemoHeader);
          END;
        115:
          BEGIN
            RecRef.OPEN(115);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(SalesCrMemoLine);
          END;
        222:
          BEGIN
            RecRef.OPEN(222);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(ShiptoAddress);
          END;
        291:
          BEGIN
            RecRef.OPEN(291);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(ShippingAgent);
          END;
        5404:
          BEGIN
            RecRef.OPEN(5404);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(ItemUnitOfMeasure);
          END;
        5717:
          BEGIN
            RecRef.OPEN(5717);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(ItemCrossReference);
          END;
        10018:
          BEGIN
            RecRef.OPEN(18);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(SelltoCustomer);
          END;
        10019:
          BEGIN
            RecRef.OPEN(18);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(BilltoCustomer);
          END;
          19009:
          BEGIN
            RecRef.OPEN(9);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(SelltoCountry);
          END;
        19010:
          BEGIN
            RecRef.OPEN(9);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(BilltoCountry);
          END;
        14000822:
          BEGIN
            RecRef.OPEN(14000822);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(BillOfLading);
          END;
        14002350:
          BEGIN
            RecRef.OPEN(14002350);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(EDITemplate);
          END;
        14002353:
          BEGIN
            RecRef.OPEN(14002353);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(EDIDocument);
          END;
        14002354:
          BEGIN
            RecRef.OPEN(14002354);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(EDISegment);
          END;
        14002360:
          BEGIN
            RecRef.OPEN(14002360);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(EDITradePartner);
          END;
        14002362:
          BEGIN
            RecRef.OPEN(14002362);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(EDICustCrossRef);
          END;
        14002364:
          BEGIN
            RecRef.OPEN(14002364);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(EDITradePtnrItem);
          END;
          14002369:
          BEGIN
            RecRef.OPEN(14002369);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(EDISendDocHdr);
          END;
        ELSE
          EXIT;
      END;
      EDILoadDBFields.LoadDBFields(
        EDIElement,OutFldArray,DecimalVariable,IntegerVariable,DateVariable,
        BooleanVariable,TimeVariable,DateTimeVariable,i,RecRef);
      RecRef.CLOSE;
    END;

    PROCEDURE ReadHdrAssocTables@26();
    BEGIN
      EDITemplate.GET(EDIDocument."EDI Template Code");

      CompanyInformation.GET;

      SalesCrMemoHeader.CALCFIELDS(Amount,"Amount Including VAT");

      PaymentTerms.INIT;
      IF SalesCrMemoHeader."Payment Terms Code" <> '' THEN
        PaymentTerms.GET(SalesCrMemoHeader."Payment Terms Code");

      SelltoCustomer.GET(SalesCrMemoHeader."Sell-to Customer No.");

      BilltoCustomer.GET(SalesCrMemoHeader."Bill-to Customer No.");

      ShiptoAddress.INIT;
      IF SalesCrMemoHeader."Ship-to Code" <> '' THEN
        ShiptoAddress.GET(
          SalesCrMemoHeader."Sell-to Customer No.",SalesCrMemoHeader."Ship-to Code");

      SalespersonPurchaser.INIT;
      IF SalesCrMemoHeader."Salesperson Code" <> '' THEN BEGIN
        SalespersonPurchaser.SETRANGE(Code,SalesCrMemoHeader."Salesperson Code");
        SalespersonPurchaser.FIND('-');
      END;

      ShipmentMethod.INIT;
      IF SalesCrMemoHeader."Shipment Method Code" <> '' THEN
        ShipmentMethod.GET(SalesCrMemoHeader."Shipment Method Code");

      SalesCommentLine.RESET;
      SalesCommentLine.SETRANGE("Document Type",SalesCommentLine."Document Type"::"Posted Credit Memo");
      SalesCommentLine.SETRANGE("No.",SalesCrMemoHeader."No.");
      IF NOT SalesCommentLine.FIND('-') THEN
        SalesCommentLine.INIT;

      CLEAR(SelltoCountry);
      IF SalesCrMemoHeader."Sell-to Country/Region Code" <> '' THEN
        SelltoCountry.GET(SalesCrMemoHeader."Sell-to Country/Region Code");

      CLEAR(BilltoCountry);
      IF SalesCrMemoHeader."Bill-to Country/Region Code" <> '' THEN
        BilltoCountry.GET(SalesCrMemoHeader."Bill-to Country/Region Code");

      //
      // Add any other tables associated with the sales cr. memo header here.
      //
    END;

    PROCEDURE ReadLineAssocTables@27();
    BEGIN
      UpdateLineTotals;

      // Get Line supporting data from other tables.
      IF SalesCrMemoLine.Type = SalesCrMemoLine.Type::Item THEN
          IF NOT Item.GET(SalesCrMemoLine."No.") THEN
            Item.INIT;

      IF SalesCrMemoLine.Type = SalesCrMemoLine.Type::Item THEN BEGIN
        IF NOT Item.GET(SalesCrMemoLine."No.") THEN
          Item.INIT;
        IF NOT ItemUnitOfMeasure.GET(
          SalesCrMemoLine."No.",SalesCrMemoLine."Unit of Measure Code")
        THEN
          ItemUnitOfMeasure.INIT;
      END;

      ItemCrossReference.RESET;
      ItemCrossReference.SETCURRENTKEY("Cross-Reference Type","Cross-Reference Type No.");
      ItemCrossReference.SETFILTER(
        "Cross-Reference Type",'<>%1',ItemCrossReference."Cross-Reference Type"::Vendor);
      ItemCrossReference.SETRANGE("Cross-Reference Type No.",SalesCrMemoHeader."Sell-to Customer No.");
      ItemCrossReference.SETRANGE("Item No.",SalesCrMemoLine."No.");
      ItemCrossReference.SETRANGE("Variant Code",SalesCrMemoLine."Variant Code");
      IF NOT ItemCrossReference.FIND('-') THEN BEGIN
        ItemCrossReference.SETRANGE("Cross-Reference Type No.",'');
        IF NOT ItemCrossReference.FIND('-') THEN
          ItemCrossReference.INIT;
      END;

      EDITradePtnrItem.RESET;
      EDITradePtnrItem.SETRANGE("Trade Partner No.",EDITradePartner."No.");
      EDITradePtnrItem.SETRANGE("Navision Item No.",SalesCrMemoLine."No.");
      EDITradePtnrItem.SETRANGE("Navision Variant Code",SalesCrMemoLine."Variant Code");
      IF NOT EDITradePtnrItem.FIND('-') THEN
        EDITradePtnrItem.SETRANGE("Navision Variant Code");
      IF NOT EDITradePtnrItem.FIND('-') THEN
        EDITradePtnrItem.INIT;

      //
      // Add any other tables associated with the sales credit memo line here.
      //
    END;

    PROCEDURE RoundValue@1240030001(DecimalValue@1240030004 : Decimal) RoundedValue : Decimal;
    VAR
      RoundingPrecision@1240030003 : Decimal;
      DecimalPlaces@1240030001 : Integer;
      Decimals@1240030000 : Integer;
      RoundingPrecisionString@1240030002 : Text[20];
    BEGIN
      IF EDIElement."NAV Data Type" = EDIElement."NAV Data Type"::Decimal THEN BEGIN
        Decimals := EDIElement.Decimals;
        DecimalPlaces := 0;
        IF Decimals > 0 THEN BEGIN
          RoundingPrecisionString  := '0.1';
          IF Decimals = 1 THEN
            DecimalPlaces := 1
          ELSE
            DecimalPlaces := Decimals - 1;
          WHILE DecimalPlaces > 0 DO BEGIN
            RoundingPrecisionString := INSSTR(RoundingPrecisionString,'0',3);
            DecimalPlaces := DecimalPlaces - 1;
          END;
        END ELSE
          RoundingPrecisionString := '1';
        EVALUATE(RoundingPrecision,RoundingPrecisionString);
        RoundedValue := ROUND(DecimalValue,RoundingPrecision);
      END;
    END;

    PROCEDURE RepeatSegment@1240030005() : Boolean;
    VAR
      SalesCommentLine2@1240020001 : Record 44;
      LastRecord@1240030002 : Boolean;
    BEGIN
      LoopFinished := TRUE;
      LastRecord := FALSE;
      RecordNo := 0;

      CASE EDIElement.Origin OF
        EDIElement.Origin::"EDI Out":
          BEGIN
          END;
        EDIElement.Origin::Navision:
          CASE EDIElement."NAV Table No." OF
            DATABASE::"Sales Comment Line":
              BEGIN
                RecordNo := 1;
                SalesCommentLine2.COPY(SalesCommentLine);
                IF EDISegment.Maximum <> 0 THEN BEGIN
                  IF NOT BypassSegment THEN
                    RepeatCount := RepeatCount + 1;
                  IF (SalesCommentLine2.NEXT = 0) OR (RepeatCount = EDISegment.Maximum) THEN
                    LastRecord := TRUE
                  ELSE
                    LoopFinished := FALSE;
                END ELSE BEGIN
                  IF SalesCommentLine2.NEXT = 0 THEN
                    LastRecord := TRUE
                  ELSE
                    LoopFinished := FALSE;
                END;
              END;

            // Add additional tables here

          END;
      END;

      IF LastRecord THEN BEGIN
        LoopFinished := TRUE;
        RecordNo := 0;
        RepeatCount := 0;
      END;
    END;

    PROCEDURE GetNextRecord@1240020002();
    BEGIN
      CASE RecordNo OF
        1:
          SalesCommentLine.NEXT;

          // Add additional tables here

      END;
    END;

    PROCEDURE LoadEDIOutRecordRef@1240020006();
    BEGIN
      RecRef.OPEN(3);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(PaymentTerms);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(10);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(ShipmentMethod);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(13);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(SalespersonPurchaser);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(18);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(SelltoCustomer);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'Sell-to Customer');
        RecRef.GETTABLE(BilltoCustomer);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'Bill-to Customer');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(27);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(Item);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(36);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(SalesHeader);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(37);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(SalesLine);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(44);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(SalesCommentLine);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(79);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(CompanyInformation);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(114);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(SalesCrMemoHeader);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(115);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(SalesCrMemoLine);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(222);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(ShiptoAddress);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(291);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(ShippingAgent);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(5404);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(ItemUnitOfMeasure);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(5717);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(ItemCrossReference);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14000822);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(BillOfLading);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14002350);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(EDITemplate);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14002353);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(EDIDocument);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14002354);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(EDISegment);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14002359);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(EDISendDocHdr);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14002360);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(EDITradePartner);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14002362);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(EDICustCrossRef);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14002364);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(EDITradePtnrItem);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;
    END;

    PROCEDURE LoadEDIOutVariable@1240020005();
    BEGIN
      EDILoadEDIOutFields.SetSalesCrMemoSendVariable(
        LineNumber,InvTotalUnits,InvTotalBaseUnits,InvNetShipWeight,InvGrossShipWeight,
        TotalSegments,InvFreightAmt,InvPalletWeight,TotalInvDiscount,TotalInvLineDiscount,
        InvDropshipAmt);
    END;

    BEGIN
    {
      // EDI Sales Credit Memo Export
    }
    END.
  }
}

