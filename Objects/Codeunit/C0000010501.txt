OBJECT Codeunit 10501 Upgrade IRS 1099 Form Boxes
{
  OBJECT-PROPERTIES
  {
    Date=12/20/18;
    Time=12:00:00 PM;
    Version List=NAVNA11.00.00.26893;
  }
  PROPERTIES
  {
    Permissions=TableData 23=rim,
                TableData 25=rim,
                TableData 38=rim,
                TableData 81=rim,
                TableData 122=rim,
                TableData 124=rim;
    OnRun=VAR
            IRS1099Management@1020003 : Codeunit 10500;
          BEGIN
            IF NOT IRS1099Management.UpgradeNeeded THEN
              EXIT;

            UpdateIRS1099FormBoxes;
          END;

  }
  CODE
  {
    VAR
      ConfirmIRS1099CodeUpdateQst@1020000 : TextConst '@@@="%1 - old code;%2 - new code";ENU=One or more entries have been posted with IRS 1099 code %1.\\Do you want to continue and update all the data associated with this vendor and the existing IRS 1099 code with the new code, %2?;ESM=Se han registrado una o m s entradas con el c¢digo IRS 1099 %1.\\¨Desea continuar y actualizar todos los datos asociados con este proveedor y el c¢digo IRS 1099 existente con el nuevo c¢digo, %2?;FRC=Une ou plusieurs ‚critures ont ‚t‚ report‚es avec le code IRSÿ1099 %1.\\Voulez-vous continuer et mettre … jour toutes les donn‚es associ‚es … ce fournisseur et remplacer le code IRSÿ1099 existant par le nouveau code, %2?;ENC=One or more entries have been posted with IRS 1099 code %1.\\Do you want to continue and update all the data associated with this vendor and the existing IRS 1099 code with the new code, %2?';

    LOCAL PROCEDURE UpdateIRS1099FormBoxes@1020006();
    BEGIN
      ShiftIRS1099('DIV-11');
      ShiftIRS1099('DIV-10');
      ShiftIRS1099('DIV-09');
      ShiftIRS1099('DIV-08');
      ShiftIRS1099('DIV-06');
      ShiftIRS1099('DIV-05');
      InsertIRS1099('DIV-05','Section 199A dividends',10.0);
    END;

    LOCAL PROCEDURE ShiftIRS1099@1020033(IRSCode@1020001 : Code[10]);
    VAR
      IRS1099FormBox@1020000 : Record 10010;
    BEGIN
      IF NOT IRS1099FormBox.GET(IRSCode) THEN
        EXIT;

      IRS1099FormBox.RENAME(INCSTR(IRSCode));
    END;

    LOCAL PROCEDURE InsertIRS1099@1020005(NewCode@1020001 : Code[10];NewDescription@1001 : Text[100];NewMinimum@1002 : Decimal);
    VAR
      IRS1099FormBox@1020000 : Record 10010;
    BEGIN
      IRS1099FormBox.INIT;
      IRS1099FormBox.Code := NewCode;
      IRS1099FormBox.Description := NewDescription;
      IRS1099FormBox."Minimum Reportable" := NewMinimum;
      IRS1099FormBox.INSERT;
    END;

    PROCEDURE UpdateIRSCodeInPostedData@1020001(VendNo@1020009 : Code[20];ExistingCode@1020007 : Code[10];NewCode@1020008 : Code[10]);
    VAR
      VendorLedgerEntry@1020001 : Record 25;
      PurchaseHeader@1020002 : Record 38;
      PurchInvHeader@1020003 : Record 122;
      PurchCrMemoHdr@1020004 : Record 124;
      GenJournalLineAccount@1020005 : Record 81;
      GenJournalLineBalAccount@1020006 : Record 81;
      Confirmed@1020000 : Boolean;
    BEGIN
      IF ExistingCode = '' THEN
        EXIT;

      IF ExistingCode <> NewCode THEN BEGIN
        VendorLedgerEntry.SETRANGE("Vendor No.",VendNo);
        VendorLedgerEntry.SETRANGE("IRS 1099 Code",ExistingCode);
        PurchaseHeader.SETRANGE("Pay-to Vendor No.",VendNo);
        PurchaseHeader.SETRANGE("IRS 1099 Code",ExistingCode);
        PurchInvHeader.SETRANGE("Pay-to Vendor No.",VendNo);
        PurchInvHeader.SETRANGE("IRS 1099 Code",ExistingCode);
        PurchCrMemoHdr.SETRANGE("Pay-to Vendor No.",VendNo);
        PurchCrMemoHdr.SETRANGE("IRS 1099 Code",ExistingCode);
        GenJournalLineAccount.SETRANGE("Account Type",GenJournalLineAccount."Account Type"::Vendor);
        GenJournalLineAccount.SETRANGE("Account No.",VendNo);
        GenJournalLineAccount.SETRANGE("IRS 1099 Code",ExistingCode);
        GenJournalLineBalAccount.SETRANGE("Bal. Account Type",GenJournalLineAccount."Bal. Account Type"::Vendor);
        GenJournalLineBalAccount.SETRANGE("Bal. Account No.",VendNo);
        GenJournalLineBalAccount.SETRANGE("IRS 1099 Code",ExistingCode);
        IF VendorLedgerEntry.ISEMPTY AND PurchaseHeader.ISEMPTY AND PurchInvHeader.ISEMPTY AND
           PurchCrMemoHdr.ISEMPTY AND GenJournalLineAccount.ISEMPTY AND GenJournalLineBalAccount.ISEMPTY
        THEN
          EXIT;
        IF GUIALLOWED THEN
          Confirmed := CONFIRM(STRSUBSTNO(ConfirmIRS1099CodeUpdateQst,ExistingCode,NewCode))
        ELSE
          Confirmed := TRUE;
        IF NOT Confirmed THEN
          EXIT;
        VendorLedgerEntry.MODIFYALL("IRS 1099 Code",NewCode);
        PurchaseHeader.MODIFYALL("IRS 1099 Code",NewCode);
        PurchInvHeader.MODIFYALL("IRS 1099 Code",NewCode);
        PurchCrMemoHdr.MODIFYALL("IRS 1099 Code",NewCode);
        GenJournalLineAccount.MODIFYALL("IRS 1099 Code",NewCode);
        GenJournalLineBalAccount.MODIFYALL("IRS 1099 Code",NewCode);
      END;
    END;

    BEGIN
    END.
  }
}

