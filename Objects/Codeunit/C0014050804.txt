OBJECT Codeunit 14050804 E.D.I. Return Shipment Send
{
  OBJECT-PROPERTIES
  {
    Date=08/07/17;
    Time=12:00:00 PM;
    Version List=SE0.60.14.01;
  }
  PROPERTIES
  {
    TableNo=6650;
    Permissions=TableData 6650=rimd;
    OnRun=BEGIN
            IF NOT ExportReturnShipment(Rec) THEN
              ERROR(Text001,"No.");
          END;

  }
  CODE
  {
    VAR
      BillOfLading@1240030001 : Record 14000822;
      CompanyInformation@1240030002 : Record 79;
      EDISetup@1240030003 : Record 14002367;
      EDITemplate@1240030004 : Record 14002350;
      EDITradePartner@1240030005 : Record 14002360;
      EDIDocument@1240030006 : Record 14002353;
      EDISegment@1240030007 : Record 14002354;
      EDISegment2@1240030008 : Record 14002354;
      EDIElement@1240030009 : Record 14002355;
      EDISendDocHdr@1240030010 : Record 14002369;
      EDITotalFields@1240030011 : Record 14002361;
      EDIVendorCrossRef@1240030012 : Record 14002366;
      Item@1240030013 : Record 27;
      ItemUnitOfMeasure@1240030014 : Record 5404;
      PaymentTerms@1240030015 : Record 3;
      BuyfromVendor@1240020001 : Record 23;
      PaytoVendor@1240030016 : Record 23;
      ShiptoAddress@1240030017 : Record 222;
      ReturnShipmentHdr@1240030018 : Record 6650;
      ReturnShipmentLine@1240030019 : Record 6651;
      SalespersonPurchaser@1240030021 : Record 13;
      PurchaseHeader@1240030022 : Record 38;
      ShipmentMethod@1240030025 : Record 10;
      ShippingAgent@1240030026 : Record 291;
      EDIElementArray@1240030027 : ARRAY [100] OF Record 14002355;
      PurchCommentLine@1240030068 : Record 43;
      ItemCrossReference@1240030069 : Record 5717;
      PostedPackage@1240020011 : Record 14000704;
      EDITradePtnrItem@1240020017 : Record 14002364;
      EDITradePtnrUOM@1240020018 : Record 14002365;
      BillOfLadingLine@1240020024 : Record 14000823;
      EDISend@1240030028 : Codeunit 14002356;
      EDIFormat@1240030030 : Codeunit 14002351;
      EDIGetReverseCrossRef@1240020000 : Codeunit 14000373;
      CreateEDIAlert@1240020005 : Codeunit 14000375;
      EDILoadDBFields@1240020019 : Codeunit 14002392;
      EDILoadEDIOutFields@1240020023 : Codeunit 14000381;
      RecRef@1240020022 : RecordRef;
      DateVariable@1240030033 : Date;
      DecimalVariable@1240030034 : Decimal;
      DocumentNo@1240030035 : Code[10];
      SegmentGroup@1240020002 : Integer;
      i@1240030036 : Integer;
      ILidx@1240030037 : Integer;
      ReturnLineNoArray@1240030038 : ARRAY [500] OF Integer;
      IntegerVariable@1240030039 : Integer;
      ReturnTax@1240030040 : Decimal;
      ReturnNetShipWeight@1240030043 : Decimal;
      ReturnGrossShipWeight@1240030044 : Decimal;
      ReturnPalletWeight@1240030045 : Decimal;
      ReturnTotalUnits@1240030046 : Decimal;
      ReturnTotalBaseUnits@1240030047 : Decimal;
      LineNumber@1240030048 : Integer;
      NewSegment@1240030049 : Boolean;
      GoodLine@1240020015 : Boolean;
      BooleanVariable@1240020014 : Boolean;
      BypassSegment@1240020003 : Boolean;
      OutLenArray@1240030053 : ARRAY [100] OF Integer;
      OutErrorArray@1240030054 : ARRAY [100] OF Boolean;
      OutputOff@1240030055 : Boolean;
      ProcessingLines@1240030056 : Boolean;
      PmtDiscPercent@1240030061 : Decimal;
      TradePtnrFound@1240030063 : Boolean;
      TotalSegments@1240030064 : Integer;
      Text001@1001 : TextConst 'ENU=Return Shipment %1 not exported.';
      Text002@1002 : TextConst 'ENU=Vendor %1 is not set up as an EDI Trading Partner.';
      Text003@1003 : TextConst 'ENU=Vendor %1 is not set up with an Active EDI transaction %2.';
      Text004@1004 : TextConst 'ENU=EDI Document %1 for Trading Partner %2 has a mapping error.';
      Text005@1005 : TextConst 'ENU=Return Shipment line not found for Return Shipmentt # %1.';
      Text006@1006 : TextConst 'ENU=Unable to find EDI segment definition for Trading Partner %1.';
      Text007@1007 : TextConst 'ENU=LINE END Command found before LINE BEGIN.  Check Segment Definition.';
      Text008@1008 : TextConst 'ENU=Unable to find EDI Segment Elements for the following: %1 - %2 - %3 -%4.';
      RepeatCount@1240020016 : Integer;
      LoopCnt@1240020012 : Integer;
      RecordNo@1240020004 : Integer;
      ReturnTaxPercent@1240030066 : Decimal;
      LoopFinished@1240030067 : Boolean;
      ReverseCrossReference@1240020008 : Boolean;
      BypassSegment2@1240020010 : Boolean;
      OutError2@1240020009 : Boolean;
      ReverseCrossRefArray@1240020007 : ARRAY [100] OF Text[40];
      ReverseCrossRefValue@1240020006 : Text[40];
      OutFldArray@1240020013 : ARRAY [100] OF Text[80];
      TimeVariable@1240020020 : Time;
      DateTimeVariable@1240020021 : DateTime;
      XMLDoc@1240020025 : Boolean;

    PROCEDURE ExportReturnShipment@18(VAR CurrentReturnShipmentHdr@1240030001 : Record 6650) ErrorCode@1240030000 : Boolean;
    VAR
      EDIXMLManagement@1240020002 : Codeunit 14099302;
      NodeOnly@1240020000 : Boolean;
      CreateNode@1240020001 : Boolean;
    BEGIN
      CLEAR(EDILoadEDIOutFields);
      ReturnShipmentHdr.COPY(CurrentReturnShipmentHdr);

      EDISetup.LOCKTABLE;
      EDISetup.GET;
      IF ReturnShipmentHdr."EDI Internal Doc. No." = '' THEN BEGIN
        EDISetup."Last Internal Doc. No." := INCSTR(EDISetup."Last Internal Doc. No.");
        ReturnShipmentHdr."EDI Internal Doc. No." := EDISetup."Last Internal Doc. No.";
        EDISetup.MODIFY;
      END;

      TradePtnrFound := FALSE;

      EDITradePartner.SETCURRENTKEY("Vendor No.");
      EDITradePartner.SETRANGE("Vendor No.",ReturnShipmentHdr."Buy-from Vendor No.");
      IF EDITradePartner.FIND('-') THEN
        TradePtnrFound := TRUE;

      IF NOT TradePtnrFound THEN BEGIN
        EDIVendorCrossRef.INIT;
        EDIVendorCrossRef.SETCURRENTKEY("Navision Buy-from Code");
        EDIVendorCrossRef.SETRANGE(
          "Navision Buy-from Code",ReturnShipmentHdr."Buy-from Vendor No.");
        IF EDIVendorCrossRef.FIND('-') THEN BEGIN
          EDITradePartner.GET(EDIVendorCrossRef."Trade Partner No.");
          TradePtnrFound := TRUE;
        END;
      END;

      IF NOT TradePtnrFound THEN
        ERROR(
          Text002,
          ReturnShipmentHdr."Buy-from Vendor No.");

      DocumentNo := 'E_PURRMA';

      EDIDocument.SETRANGE("Trade Partner No.",EDITradePartner."No.");
      EDIDocument.SETFILTER(
        Status,'%1|%2',EDIDocument.Status::Test,EDIDocument.Status::Production);
      EDIDocument.SETRANGE("Navision Document",DocumentNo);
      EDIDocument.SETRANGE(Type,EDIDocument.Type::Export);
      IF NOT EDIDocument.FIND('-') THEN
        ERROR(
          Text003,
          ReturnShipmentHdr."Buy-from Vendor No.",DocumentNo);
      XMLDoc := EDIDocument."XML Document";

      EDIDocument.CALCFIELDS("Element Mapping Error","Conditional Mapping Error");
      IF EDIDocument."Element Mapping Error" OR EDIDocument."Conditional Mapping Error" THEN
        ERROR(
          Text004,
          EDIDocument."Navision Document",EDIDocument."Trade Partner No.");

      EDITemplate.GET(EDIDocument."EDI Template Code");

      EDISendDocHdr."Internal Doc No." := ReturnShipmentHdr."EDI Internal Doc. No.";
      EDISendDocHdr."Navision Document" := DocumentNo;
      EDISendDocHdr."Document No." := ReturnShipmentHdr."No.";
      EDISend.CreateSendHdr(EDITradePartner,EDIDocument,EDISendDocHdr);

      ReadHdrAssocTables;

      CLEAR(ReturnLineNoArray);
      LineNumber := 0;
      ReturnNetShipWeight := 0;
      ReturnGrossShipWeight := 0;
      ReturnPalletWeight := 0;
      ReturnTotalUnits := 0;
      ReturnTotalBaseUnits := 0;
      ReturnTaxPercent := 0;
      TotalSegments := 0;

      IF NOT GetFirstReturnLine THEN
        ERROR(Text005,PurchaseHeader."No.");
      SegmentGroup := 0;
      IF GoodLine THEN BEGIN
        SegmentGroup := SegmentGroup + 1;
        ReturnShipmentLine."EDI Unit Cost" := SegmentGroup;
        ReturnShipmentLine.MODIFY;
      END;

      ReturnShipmentLine.RESET;
      ReturnShipmentLine.SETRANGE("Document No.",ReturnShipmentHdr."No.");
      ReturnShipmentLine.SETFILTER("No.",'<>%1','');
      IF NOT EDIDocument."Export Zero Qty. Lines" THEN
        ReturnShipmentLine.SETFILTER(Quantity,'>0');
      ILidx := 0;
      IF ReturnShipmentLine.FIND('-') THEN
        REPEAT
          ILidx := ILidx + 1;
          ReturnLineNoArray[ILidx] := ReturnShipmentLine."Line No.";
        UNTIL ReturnShipmentLine.NEXT = 0;
      IF NOT ReturnShipmentLine.FIND('-') THEN
        ERROR(Text005,ReturnShipmentHdr."No.");

      OutputOff := FALSE;
      IF NOT EDIDocument."Export G/L Account Lines"  AND
        (ReturnShipmentLine.Type = EDITotalFields.Type::"Account (G/L)")
      THEN
        OutputOff := TRUE;
      IF NOT EDIDocument."Export Resource Lines" AND
        (ReturnShipmentLine.Type = EDITotalFields.Type::Resource)
      THEN
        OutputOff := TRUE;
      ReadLineAssocTables;

      // Create Line Segments
      EDISegment.SETRANGE("Trade Partner No.",EDIDocument."Trade Partner No.");
      EDISegment.SETRANGE("Navision Document",EDIDocument."Navision Document");
      EDISegment.SETRANGE("EDI Document No.",EDIDocument."EDI Document No.");
      EDISegment.SETRANGE(Version,EDIDocument.Version);
      EDISegment.SETRANGE(Type,EDIDocument.Type);
      IF NOT EDISegment.FIND('-') THEN
        ERROR(
          Text006,
          EDIDocument."Trade Partner No.");
      REPEAT
        CASE EDISegment."Segment Type" OF
          EDISegment."Segment Type"::Command:
            BEGIN
              IF (EDISegment.Segment = 'LINE BEGIN') OR
                 (EDISegment."Node Name" = 'LINE BEGIN')
              THEN BEGIN
                EDISegment2.COPY(EDISegment);
                ProcessingLines := TRUE;
              END;
              IF( EDISegment.Segment = 'LINE END') OR
                (EDISegment."Node Name" = 'LINE END')
              THEN BEGIN
                OutputOff := FALSE;
                IF NOT ProcessingLines THEN
                  ERROR(Text007);
                IF GetNextReturnLine THEN BEGIN
                  IF GoodLine THEN BEGIN
                    SegmentGroup := SegmentGroup + 1;
                    ReturnShipmentLine."EDI Unit Cost" := SegmentGroup;
                    ReturnShipmentLine.MODIFY;
                  END;
                  IF NOT EDIDocument."Export G/L Account Lines" THEN
                    OutputOff := TRUE;
                  IF NOT EDIDocument."Export Resource Lines" THEN
                    OutputOff := TRUE;
                  IF NOT EDIDocument."Export Item Charge Lines" THEN
                    OutputOff := TRUE;
                  ReadLineAssocTables;
                  EDISegment.GET(
                    EDISegment2."Trade Partner No.",EDISegment2."Navision Document",
                    EDISegment2."EDI Document No.",EDISegment2.Version,
                    EDISegment2.Type,EDISegment2."Processing Sequence",EDISegment2.Segment);
                END;
              END;
            END;
          EDISegment."Segment Type"::Segment:
            BEGIN
              LoopFinished := TRUE;
              BypassSegment := FALSE;
              NodeOnly := FALSE;
              CreateNode := TRUE;
              EDIElement.SETRANGE("Trade Partner No.",EDISegment."Trade Partner No.");
              EDIElement.SETRANGE("Navision Document",EDISegment."Navision Document");
              EDIElement.SETRANGE("EDI Document No.",EDISegment."EDI Document No.");
              EDIElement.SETRANGE(Version,EDISegment.Version);
              EDIElement.SETRANGE(Type,EDISegment.Type);
              EDIElement.SETRANGE(Segment,EDISegment.Segment);
              EDIElement.SETRANGE(Type,EDIElement.Type::Export);
              IF NOT EDIElement.FIND('-') THEN BEGIN
                IF XMLDoc THEN BEGIN
                  EDISegment.CALCFIELDS("Has Attributes","Node Text");
                  IF EDISegment."Node Text" OR EDISegment."Has Attributes" THEN
                    ERROR(
                      Text008,
                        EDISegment."Trade Partner No.",EDISegment."Navision Document",
                        EDISegment.Version,EDISegment."Node Name")
                  ELSE
                    NodeOnly := TRUE;
                END ELSE
                  ERROR(
                    Text008,
                      EDISegment."Trade Partner No.",EDISegment."Navision Document",
                      EDISegment.Version,EDISegment.Segment);
              END ELSE
                IF XMLDoc AND EDIElement."Node Text" THEN
                  CreateNode := FALSE;
              IF (XMLDoc = FALSE) OR (XMLDoc AND NodeOnly = FALSE) THEN BEGIN
                TotalSegments := TotalSegments + 1;
                CLEAR(OutFldArray);
                CLEAR(OutLenArray);
                CLEAR(OutErrorArray);
                CLEAR(EDIElementArray);
                i := 1;
                LoopCnt := 1;
                REPEAT
                  ReverseCrossRefArray[i] := '';
                  ReverseCrossReference := EDIElement."Reverse Cross Reference";
                  IF ((EDIElement."Fixed Value" <> '') AND (XMLDoc = FALSE)) OR
                     ((EDIElement."XML Fixed Value" <> '') AND (XMLDoc))
                  THEN BEGIN
                    IF XMLDoc THEN
                      OutFldArray[i] := EDIElement."XML Fixed Value"
                    ELSE
                      OutFldArray[i] := EDIElement."Fixed Value";
                  END ELSE
                    CASE EDIElement.Origin OF
                      EDIElement.Origin::Navision:
                        BEGIN
                          LoadDBFields;
                          IF ReverseCrossReference THEN BEGIN
                            CLEAR(EDIGetReverseCrossRef);
                            ReverseCrossRefArray[i] :=
                              EDIGetReverseCrossRef.GetReverseCrossRef(
                                EDIElement,EDISendDocHdr,OutFldArray[i]);
                              EDIGetReverseCrossRef.FormatOutput(
                                EDISendDocHdr,EDIElement,OutError2,ReverseCrossRefArray,BypassSegment2,i);
                          END;
                        END;
                      EDIElement.Origin::"EDI In":
                        EDIFormat.LoadEDIInFields(
                          EDIElement,ReturnShipmentHdr."EDI Internal Doc. No.",
                          ReturnShipmentLine."EDI Unit Cost",ProcessingLines,i,OutFldArray,
                          BooleanVariable,DateVariable,DecimalVariable,IntegerVariable,TimeVariable,
                          DateTimeVariable);
                      EDIElement.Origin::"EDI Out":
                        LoadEDIOutFields;
                    END;
                  OutLenArray[i] := EDIElement.Length;
                  EDIFormat.FormatOutput(
                    EDITemplate,EDIElement,OutFldArray,OutErrorArray,i,BypassSegment,
                    BooleanVariable,DateVariable,DecimalVariable,IntegerVariable,TimeVariable,
                    DateTimeVariable);
                  IF ReverseCrossReference THEN
                    OutErrorArray[i] := OutError2;
                  EDIElementArray[i].COPY(EDIElement);
                  i := i + 1;
                UNTIL EDIElement.NEXT = 0;
              END;
              NewSegment := TRUE;
              i := 1;
              IF ReverseCrossReference THEN
                BypassSegment := BypassSegment2;
              IF (LoopCnt = 1) AND (EDISegment."Repeat Segment") THEN BEGIN
                RepeatSegment;
                LoopCnt := LoopCnt + 1;
              END;
              IF (NOT BypassSegment) AND (NOT OutputOff) THEN BEGIN
                IF XMLDoc AND CreateNode THEN BEGIN
                   EDISend.CreateSendLine(
                    EDISendDocHdr,EDIElementArray[i],OutFldArray[i],OutLenArray[i],OutErrorArray[i],
                    NewSegment,ReverseCrossRefValue,EDIDocument,TRUE,EDISegment);
                  NewSegment := FALSE;
                END;
                IF (XMLDoc = FALSE) OR (XMLDoc AND NodeOnly = FALSE) THEN
                  REPEAT
                    ReverseCrossRefValue := ReverseCrossRefArray[i];
                    EDISend.CreateSendLine(
                      EDISendDocHdr,EDIElementArray[i],OutFldArray[i],OutLenArray[i],OutErrorArray[i],
                      NewSegment,ReverseCrossRefValue,EDIDocument,FALSE,EDISegment);
                    NewSegment := FALSE;
                    i := i + 1;
                  UNTIL OutLenArray[i] = 0
              END ELSE
                TotalSegments := TotalSegments - 1;
            END;
        END;
        IF (EDISegment."Repeat Segment") AND (NOT LoopFinished) THEN BEGIN
          EDISegment.NEXT(-1);
          GetNextRecord;
        END;
      UNTIL EDISegment.NEXT = 0;

      IF EDISetup."Send Document Error" THEN BEGIN
        EDISendDocHdr.CALCFIELDS("Data Error");
        IF EDISendDocHdr."Data Error" THEN BEGIN
          CLEAR(CreateEDIAlert);
          CreateEDIAlert.CreateAlertSendDocError(EDISendDocHdr."No.",1,FALSE);
        END;
      END;

      ReturnShipmentHdr."EDI Return Shipment Generated" := TRUE;
      ReturnShipmentHdr."EDI Return Shipment Gen. Date" := TODAY;
      ReturnShipmentHdr.MODIFY;

      IF XMLDoc THEN
        EDIXMLManagement.ResponseIDSubstitution(EDISendDocHdr);

      EXIT(TRUE);
    END;

    PROCEDURE LoadEDIOutFields@16();
    BEGIN
      BooleanVariable := FALSE;
      DateVariable := 0D;
      IntegerVariable := 0;
      DecimalVariable := 0;
      TimeVariable := 0T;
      DateTimeVariable := 0DT;

      IF EDIElement.Origin = EDIElement.Origin::"EDI Out" THEN BEGIN
        CASE EDIElement."Field Name" OF
          ELSE BEGIN
            LoadEDIOutVariable;
            LoadEDIOutRecordRef;
            EDILoadEDIOutFields.ReturnShipmentSendEDIOut(
              EDIElement,OutFldArray,DecimalVariable,IntegerVariable,DateVariable,
              BooleanVariable,TimeVariable,DateTimeVariable,i);
          END;
        END;
      END;
    END;

    PROCEDURE UpdateLineTotals@28();
    BEGIN
      IF NOT OutputOff THEN BEGIN
        LineNumber := LineNumber + 1;
        ReturnTotalUnits := ReturnTotalUnits + ReturnShipmentLine.Quantity;
        ReturnTotalBaseUnits := ReturnTotalBaseUnits + ReturnShipmentLine."Quantity (Base)";
      END;

      ReturnNetShipWeight :=
        ReturnNetShipWeight + (ReturnShipmentLine."Net Weight" * ReturnShipmentLine.Quantity);
      ReturnGrossShipWeight :=
        ReturnGrossShipWeight + (ReturnShipmentLine."Gross Weight" * ReturnShipmentLine.Quantity);
    END;

    PROCEDURE LoadDBFields@23();
    BEGIN
      CASE EDIElement."NAV Table No." OF
        3:
          BEGIN
            RecRef.OPEN(3);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(PaymentTerms);
          END;
        10:
          BEGIN
            RecRef.OPEN(10);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(ShipmentMethod);
          END;
        27:
          BEGIN
            RecRef.OPEN(27);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(Item);
          END;
        43:
          BEGIN
            RecRef.OPEN(43);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(PurchCommentLine);
          END;
        79:
          BEGIN
            RecRef.OPEN(79);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(CompanyInformation);
          END;
        222:
          BEGIN
            RecRef.OPEN(222);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(ShiptoAddress);
          END;
        291:
          BEGIN
            RecRef.OPEN(291);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(ShippingAgent);
          END;
        5404:
          BEGIN
            RecRef.OPEN(5404);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(ItemUnitOfMeasure);
          END;
        5717:
          BEGIN
            RecRef.OPEN(5717);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(ItemCrossReference);
          END;
        6650:
          BEGIN
            RecRef.OPEN(6650);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(ReturnShipmentHdr);
          END;
        6651:
          BEGIN
            RecRef.OPEN(6651);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(ReturnShipmentLine);
          END;
        20023:
          BEGIN
            RecRef.OPEN(23);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(BuyfromVendor);
          END;
        20024:
          BEGIN
            RecRef.OPEN(23);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(PaytoVendor);
          END;
        14000822:
          BEGIN
            RecRef.OPEN(14000822);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(BillOfLading);
          END;
        14002350:
          BEGIN
            RecRef.OPEN(14002350);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(EDITemplate);
          END;
        14002353:
          BEGIN
            RecRef.OPEN(14002353);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(EDIDocument);
          END;
        14002354:
          BEGIN
            RecRef.OPEN(14002354);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(EDISegment);
          END;
        14002360:
          BEGIN
            RecRef.OPEN(14002360);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(EDITradePartner);
          END;
          14002369:
          BEGIN
            RecRef.OPEN(14002369);
            IF RecRef.FIND('-') THEN
              RecRef.GETTABLE(EDISendDocHdr);
          END;
        ELSE
          EXIT;
      END;
      EDILoadDBFields.LoadDBFields(
        EDIElement,OutFldArray,DecimalVariable,IntegerVariable,DateVariable,
        BooleanVariable,TimeVariable,DateTimeVariable,i,RecRef);
      RecRef.CLOSE;
    END;

    PROCEDURE ReadHdrAssocTables@26();
    BEGIN
      EDITemplate.GET(EDIDocument."EDI Template Code");

      CompanyInformation.GET;

      PaymentTerms.INIT;
      IF ReturnShipmentHdr."Payment Terms Code" <> '' THEN BEGIN
        PaymentTerms.GET(ReturnShipmentHdr."Payment Terms Code");
        PmtDiscPercent := PaymentTerms."Discount %";
      END ELSE BEGIN
        PmtDiscPercent := 0;
      END;

      BuyfromVendor.GET(ReturnShipmentHdr."Buy-from Vendor No.");

      PaytoVendor.GET(ReturnShipmentHdr."Pay-to Vendor No.");

      ShiptoAddress.INIT;
      IF ReturnShipmentHdr."Ship-to Code" <> '' THEN
        ShiptoAddress.GET(
          ReturnShipmentHdr."Sell-to Customer No.",ReturnShipmentHdr."Ship-to Code");

      SalespersonPurchaser.INIT;
      IF ReturnShipmentHdr."Purchaser Code" <> '' THEN BEGIN
        SalespersonPurchaser.SETRANGE(Code,ReturnShipmentHdr."Purchaser Code");
        SalespersonPurchaser.FIND('-');
      END;

      ShipmentMethod.INIT;
      IF ReturnShipmentHdr."Shipment Method Code" <> '' THEN
        ShipmentMethod.GET(ReturnShipmentHdr."Shipment Method Code");

      PurchCommentLine.RESET;
      PurchCommentLine.SETRANGE(
        "Document Type",PurchCommentLine."Document Type"::"Posted Return Shipment");
      PurchCommentLine.SETRANGE("No.",ReturnShipmentHdr."No.");
      IF NOT PurchCommentLine.FIND('-') THEN
        PurchCommentLine.INIT;


      PostedPackage.RESET;
      PostedPackage.SETCURRENTKEY(
        "Source Type","Source Subtype","Posted Source ID","Bill of Lading No.");
      PostedPackage.SETRANGE("Source Type",38);
      PostedPackage.SETRANGE("Posted Source ID",ReturnShipmentHdr."No.");
      IF NOT PostedPackage.FIND('-') THEN
        CLEAR(PostedPackage);

      BillOfLadingLine.RESET;
      BillOfLadingLine.SETRANGE("Source Type",38);
      BillOfLadingLine.SETRANGE("Posted Source ID",ReturnShipmentHdr."No.");
      IF BillOfLadingLine.FIND('-') THEN
        BillOfLading.GET(BillOfLadingLine."Bill of Lading No.")
      ELSE
        CLEAR(BillOfLading);

      //
      // Add any other tables associated with the Return Shipment Header here.
      //
    END;

    PROCEDURE ReadLineAssocTables@27();
    BEGIN
      UpdateLineTotals;

      // Get Line supporting data from other tables.
      IF ReturnShipmentLine.Type = ReturnShipmentLine.Type::Item THEN BEGIN
        IF NOT Item.GET(ReturnShipmentLine."No.") THEN
          Item.INIT;
        IF NOT ItemUnitOfMeasure.GET(
          ReturnShipmentLine."No.",ReturnShipmentLine."Unit of Measure Code")
        THEN
          ItemUnitOfMeasure.INIT;
      END;

      ItemCrossReference.RESET;
      ItemCrossReference.SETCURRENTKEY("Cross-Reference Type","Cross-Reference Type No.");
      ItemCrossReference.SETFILTER(
        "Cross-Reference Type",'<>%1',ItemCrossReference."Cross-Reference Type"::Vendor);
      ItemCrossReference.SETRANGE("Cross-Reference Type No.",ReturnShipmentHdr."Buy-from Vendor No.");
      ItemCrossReference.SETRANGE("Item No.",ReturnShipmentLine."No.");
      ItemCrossReference.SETRANGE("Variant Code",ReturnShipmentLine."Variant Code");
      IF NOT ItemCrossReference.FIND('-') THEN BEGIN
        ItemCrossReference.SETRANGE("Cross-Reference Type No.",'');
        IF NOT ItemCrossReference.FIND('-') THEN
          ItemCrossReference.INIT;
      END;

      EDITradePtnrItem.RESET;
      EDITradePtnrItem.SETRANGE("Trade Partner No.",EDITradePartner."No.");
      EDITradePtnrItem.SETRANGE("Navision Item No.",ReturnShipmentLine."No.");
      EDITradePtnrItem.SETRANGE("Navision Variant Code",ReturnShipmentLine."Variant Code");
      IF NOT EDITradePtnrItem.FIND('-') THEN
        EDITradePtnrItem.SETRANGE("Navision Variant Code");
      IF NOT EDITradePtnrItem.FIND('-') THEN
        CLEAR(EDITradePtnrItem);

      EDITradePtnrUOM.RESET;
      EDITradePtnrUOM.SETRANGE("Trade Partner No.",EDITradePartner."No.");
      EDITradePtnrUOM.SETRANGE("Navision Item No.",ReturnShipmentLine."No.");
      EDITradePtnrUOM.SETRANGE("Navision Variant Code",ReturnShipmentLine."Variant Code");
      EDITradePtnrUOM.SETRANGE("Order Unit of Measure",ReturnShipmentLine."Unit of Measure Code");
      IF NOT EDITradePtnrUOM.FIND('-') THEN BEGIN
        EDITradePtnrUOM.SETFILTER("Navision Item No.",'=%1','');
        EDITradePtnrUOM.SETFILTER("Navision Variant Code",'=%1','');
        IF NOT EDITradePtnrUOM.FIND('-') THEN
          EDITradePtnrUOM.INIT
      END;

      //
      // Add any other tables associated with the Return Shipment Line here.
      //
    END;

    PROCEDURE RepeatSegment@1240030005() : Boolean;
    VAR
      PurchCommentLine2@1240020000 : Record 43;
      PostedPackage2@1240020001 : Record 14000704;
      EDIElement2@1240020003 : Record 14002355;
      LastRecord@1240030002 : Boolean;
      Found@1240020002 : Boolean;
    BEGIN
      LoopFinished := TRUE;
      LastRecord := FALSE;
      Found := FALSE;
      RecordNo := 0;

      EDIElement2.SETRANGE("Trade Partner No.",EDISegment."Trade Partner No.");
      EDIElement2.SETRANGE("Navision Document",EDISegment."Navision Document");
      EDIElement2.SETRANGE("EDI Document No.",EDISegment."EDI Document No.");
      EDIElement2.SETRANGE(Version,EDISegment.Version);
      EDIElement2.SETRANGE(Type,EDISegment.Type);
      EDIElement2.SETRANGE(Segment,EDISegment.Segment);
      IF EDIElement2.FIND('-') THEN
        REPEAT
          CASE EDIElement2.Origin OF
            EDIElement2.Origin::"EDI Out":
              BEGIN
              END;
            EDIElement2.Origin::Navision:
              CASE EDIElement2."NAV Table No." OF
                DATABASE::"Purch. Comment Line":
                  BEGIN
                    Found := TRUE;
                    RecordNo := 1;
                    PurchCommentLine2.COPY(PurchCommentLine);
                    IF EDISegment.Maximum <> 0 THEN BEGIN
                      IF NOT BypassSegment THEN
                        RepeatCount := RepeatCount + 1;
                      IF (PurchCommentLine2.NEXT = 0) OR (RepeatCount = EDISegment.Maximum) THEN
                        LastRecord := TRUE
                      ELSE
                        LoopFinished := FALSE;
                    END ELSE BEGIN
                      IF PurchCommentLine2.NEXT = 0 THEN
                        LastRecord := TRUE
                      ELSE
                        LoopFinished := FALSE;
                    END;
                  END;
                DATABASE::"Posted Package":
                  BEGIN
                    Found := TRUE;
                    RecordNo := 2;
                    PostedPackage2.COPY(PostedPackage);
                    IF EDISegment.Maximum <> 0 THEN BEGIN
                      IF NOT BypassSegment THEN
                        RepeatCount := RepeatCount + 1;
                      IF (PostedPackage2.NEXT = 0) OR (RepeatCount = EDISegment.Maximum) THEN
                        LastRecord := TRUE
                      ELSE
                        LoopFinished := FALSE;
                    END ELSE BEGIN
                      IF PostedPackage2.NEXT = 0 THEN
                        LastRecord := TRUE
                     ELSE
                       LoopFinished := FALSE;
                    END;
                  END;

                // Add additional tables here

              END;
          END;
        UNTIL (EDIElement2.NEXT = 0) OR (Found = TRUE);
      IF LastRecord THEN BEGIN
        LoopFinished := TRUE;
        RecordNo := 0;
        RepeatCount := 0;
      END;
    END;

    PROCEDURE GetNextRecord@1240020002();
    BEGIN
      CASE RecordNo OF
        1:
          PurchCommentLine.NEXT;
        2:
          PostedPackage.NEXT;

        // Add additional tables here

      END;
    END;

    PROCEDURE GetFirstReturnLine@1240030000() : Boolean;
    BEGIN
      ReturnShipmentLine.RESET;
      ReturnShipmentLine.SETRANGE("Document No.",ReturnShipmentHdr."No.");
      ReturnShipmentLine.SETFILTER("No.",'<>%1','');
      IF NOT EDIDocument."Export Zero Qty. Lines" THEN
        ReturnShipmentLine.SETFILTER(Quantity,'<>%1',0);
      IF ReturnShipmentLine.FIND('-') THEN
        REPEAT
          GoodLine := TRUE;
          CASE ReturnShipmentLine.Type OF
            ReturnShipmentLine.Type::"G/L Account":
              IF EDIDocument."Export G/L Account Lines" THEN BEGIN
                GoodLine := FALSE;
                EXIT(TRUE);
              END;
            ReturnShipmentLine.Type::Item:
              EXIT(TRUE);
            ReturnShipmentLine.Type::"Charge (Item)":
              IF EDIDocument."Export Item Charge Lines" THEN BEGIN
                GoodLine := FALSE;
                EXIT(TRUE);
             END;
          END;
        UNTIL ReturnShipmentLine.NEXT = 0;
      EXIT(FALSE);
    END;

    PROCEDURE GetNextReturnLine@1240020000() : Boolean;
    BEGIN
      IF ReturnShipmentLine.NEXT = 0 THEN
        EXIT(FALSE);
      REPEAT
        GoodLine := TRUE;
        CASE ReturnShipmentLine.Type OF
          ReturnShipmentLine.Type::"G/L Account":
            IF EDIDocument."Export G/L Account Lines" THEN BEGIN
              GoodLine := FALSE;
              EXIT(TRUE);
           END;
          ReturnShipmentLine.Type::Item:
            EXIT(TRUE);
          ReturnShipmentLine.Type::"Charge (Item)":
            IF EDIDocument."Export Item Charge Lines" THEN BEGIN
              GoodLine := FALSE;
              EXIT(TRUE);
            END;
        END;
      UNTIL ReturnShipmentLine.NEXT = 0;
      EXIT(FALSE);
    END;

    PROCEDURE LoadEDIOutRecordRef@1240020006();
    BEGIN
      RecRef.OPEN(3);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(PaymentTerms);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(10);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(ShipmentMethod);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(23);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(BuyfromVendor);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'Buy-from Vendor');
        RecRef.GETTABLE(PaytoVendor);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'Pay-to Vendor');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(27);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(Item);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(43);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(PurchCommentLine);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(79);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(CompanyInformation);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(222);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(ShiptoAddress);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(291);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(ShippingAgent);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(5404);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(ItemUnitOfMeasure);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(5717);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(ItemCrossReference);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(6650);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(ReturnShipmentHdr);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(6651);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(ReturnShipmentLine);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14000704);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(PostedPackage);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14000822);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(BillOfLading);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14002350);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(EDITemplate);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14002353);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(EDIDocument);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14002354);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(EDISegment);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14002359);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(EDISendDocHdr);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14002360);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(EDITradePartner);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14002364);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(EDITradePtnrItem);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;

      RecRef.OPEN(14002365);
      IF RecRef.FIND('-') THEN BEGIN
        RecRef.GETTABLE(EDITradePtnrUOM);
        EDILoadEDIOutFields.SetRecordVariable(RecRef,'');
      END;
      RecRef.CLOSE;
    END;

    PROCEDURE LoadEDIOutVariable@1240020005();
    BEGIN
      EDILoadEDIOutFields.SetReturnReceiptSendVariable(
        LineNumber,ReturnTotalUnits,ReturnTotalBaseUnits,ReturnNetShipWeight,ReturnGrossShipWeight,
        TotalSegments);
    END;

    BEGIN
    END.
  }
}

