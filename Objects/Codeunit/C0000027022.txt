OBJECT Codeunit 27022 DIOT Subscribers
{
  OBJECT-PROPERTIES
  {
    Date=10/26/19;
    Time=12:00:00 PM;
    Version List=NAVNA11.00.00.37606;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      DIOTDataManagement@1310000 : Codeunit 27021;
      LeaseAndRentErr@1310001 : TextConst 'ENU=Non-MX Vendors cannot have Type of Operation equal to Lease and Rent.;ESM=Los proveedores que no son MX no pueden tener el tipo de operaci¢n igual que alquiler y alquiler.;FRC=Les fournisseurs non mexicains ne peuvent pas avoir Location et loyer comme Type d''op‚ration.;ENC=Non-MX Vendors cannot have Type of Operation equal to Lease and Rent.';
      WHTMoreThanVATErr@1310002 : TextConst '@@@="%1=Field name, %2=Another field name.";ENU=%1 can not have higher value than %2.;ESM=%1 no puede tener un valor mayor que %2.;FRC=%1 ne peut pas avoir une valeur sup‚rieure … %2.;ENC=%1 can not have higher value than %2.';

    [EventSubscriber(Table,23,OnAfterModifyEvent,"",Skip,Skip)]
    LOCAL PROCEDURE CheckTypeOfOperationAndCountryCodeOnAfterModify@1310000(VAR Rec@1310000 : Record 23);
    BEGIN
      IF Rec.ISTEMPORARY THEN
        EXIT;
      CheckCountryCodeDIOTOperationType(Rec);
    END;

    [EventSubscriber(Table,23,OnAfterInsertEvent,"",Skip,Skip)]
    LOCAL PROCEDURE CheckTypeOfOperationAndCountryCodeOnAfterInsert@1310002(VAR Rec@1310000 : Record 23);
    BEGIN
      IF Rec.ISTEMPORARY THEN
        EXIT;
      CheckCountryCodeDIOTOperationType(Rec);
    END;

    [EventSubscriber(Table,325,OnAfterModifyEvent,"",Skip,Skip)]
    LOCAL PROCEDURE CheckWHTandVATOnModify@1310003(VAR Rec@1310000 : Record 325);
    BEGIN
      IF Rec.ISTEMPORARY THEN
        EXIT;
      CheckWHTIsNotMoreThanVAT(Rec);
    END;

    [EventSubscriber(Table,325,OnAfterInsertEvent,"",Skip,Skip)]
    LOCAL PROCEDURE CheckWHTandVATOnInsert@1310004(VAR Rec@1310000 : Record 325);
    BEGIN
      IF Rec.ISTEMPORARY THEN
        EXIT;
      CheckWHTIsNotMoreThanVAT(Rec);
    END;

    [EventSubscriber(Codeunit,90,OnBeforePostInvPostBuffer,"",Skip,Skip)]
    LOCAL PROCEDURE TransferDIOTTypeOfOperationOnBeforePostInvPostBuffer@1310005(VAR GenJnlLine@1310000 : Record 81;VAR PurchHeader@1310001 : Record 38);
    BEGIN
      GenJnlLine."DIOT-Type of Operation" := PurchHeader."DIOT-Type of Operation";
    END;

    [EventSubscriber(Table,23,OnAfterInsertEvent,"",Skip,Skip)]
    LOCAL PROCEDURE ModifyDIOTTypeOfOperationOnInsertVendor@1310008(VAR Rec@1310000 : Record 23;RunTrigger@1310001 : Boolean);
    VAR
      PurchasesPayablesSetup@1310002 : Record 312;
    BEGIN
      IF Rec.ISTEMPORARY THEN
        EXIT;
      IF NOT PurchasesPayablesSetup.GET THEN
        EXIT;
      IF PurchasesPayablesSetup."DIOT Default Vendor Type" = PurchasesPayablesSetup."DIOT Default Vendor Type"::" " THEN
        EXIT;
      IF (NOT DIOTDataManagement.IsCountryCodeMXorBlank(Rec."Country/Region Code")) AND
         (PurchasesPayablesSetup."DIOT Default Vendor Type" = PurchasesPayablesSetup."DIOT Default Vendor Type"::"Lease and Rent")
      THEN
        EXIT;
      Rec.VALIDATE("DIOT-Type of Operation",PurchasesPayablesSetup."DIOT Default Vendor Type");
      Rec.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE CheckCountryCodeDIOTOperationType@1310009(Vendor@1310000 : Record 23);
    BEGIN
      IF (NOT DIOTDataManagement.IsCountryCodeMXorBlank(Vendor."Country/Region Code")) AND
         (Vendor."DIOT-Type of Operation" = Vendor."DIOT-Type of Operation"::"Lease and Rent")
      THEN
        ERROR(LeaseAndRentErr);
    END;

    LOCAL PROCEDURE CheckWHTIsNotMoreThanVAT@1310010(VATPostingSetup@1310000 : Record 325);
    BEGIN
      WITH VATPostingSetup DO
        IF "DIOT-WHT %" > "VAT %" THEN
          ERROR(STRSUBSTNO(WHTMoreThanVATErr,FIELDCAPTION("DIOT-WHT %"),FIELDCAPTION("VAT %")));
    END;

    BEGIN
    END.
  }
}

