OBJECT Codeunit 14000607 Over Receive Management
{
  OBJECT-PROPERTIES
  {
    Date=06/28/17;
    Time=12:00:00 PM;
    Version List=SE0.55.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ReceiveMgt@1240030000 : Codeunit 14000602;
      Text002@1240030003 : TextConst 'ENU=Illegal Source Type %1.';

    PROCEDURE SummarizeTotals@1240030001(VAR ReceiveControl@1240030000 : Record 14000611;VAR FastReceiveLine@1240030003 : Record 14000609;ReceiveSetup@1240030007 : Record 14000607;ReceiveStation@1240030006 : Record 14000608;VAR NoOfOrdersWithItem@1240030004 : Integer;VAR OtherDocumentExists@1240030005 : Boolean);
    VAR
      TotalFastReceiveLine@1240030002 : Record 14000609;
      FastReceiveLineTmp@1240030001 : TEMPORARY Record 14000609;
    BEGIN
      CLEAR(ReceiveMgt);
      ReceiveMgt.Initialize(ReceiveStation,ReceiveSetup);
      ReceiveMgt.UpdateFastReceiveLines(
        ReceiveControl,FastReceiveLineTmp,FALSE,TRUE,TRUE,
        ReceiveControl."Input Type",ReceiveControl."Input No.",ReceiveControl."Input Variant Code");

      FastReceiveLineTmp.RESET;
      FastReceiveLineTmp.FIND('-');
      TotalFastReceiveLine := FastReceiveLineTmp;
      NoOfOrdersWithItem := 0;
      REPEAT
        NoOfOrdersWithItem := NoOfOrdersWithItem + 1;
        IF NoOfOrdersWithItem > 1 THEN BEGIN
          TotalFastReceiveLine."Quantity (Base)" :=
            TotalFastReceiveLine."Quantity (Base)" + FastReceiveLineTmp."Quantity (Base)";
          TotalFastReceiveLine."Qty. on Order (Base)" :=
            TotalFastReceiveLine."Qty. on Order (Base)" + FastReceiveLineTmp."Qty. on Order (Base)";
          TotalFastReceiveLine."Outstanding Qty. (Base)" :=
            TotalFastReceiveLine."Outstanding Qty. (Base)" +
            FastReceiveLineTmp."Outstanding Qty. (Base)";
          TotalFastReceiveLine."Qty. to Receive (Base)" :=
            TotalFastReceiveLine."Qty. to Receive (Base)" + FastReceiveLineTmp."Qty. to Receive (Base)";
          TotalFastReceiveLine."Rem. Qty. to Receive (Base)" :=
            TotalFastReceiveLine."Rem. Qty. to Receive (Base)" +
            FastReceiveLineTmp."Rem. Qty. to Receive (Base)";
          TotalFastReceiveLine."Qty. Received (Base)" :=
            TotalFastReceiveLine."Qty. Received (Base)" + FastReceiveLineTmp."Qty. Received (Base)";
          TotalFastReceiveLine."Qty. Received this Rec. (Base)" :=
            TotalFastReceiveLine."Qty. Received this Rec. (Base)" +
            FastReceiveLineTmp."Qty. Received this Rec. (Base)";

          IF TotalFastReceiveLine."Unit of Measure Code" = FastReceiveLineTmp."Unit of Measure Code"
          THEN BEGIN
            TotalFastReceiveLine.Quantity :=
              TotalFastReceiveLine.Quantity + FastReceiveLineTmp.Quantity;
            TotalFastReceiveLine."Qty. on Order" :=
              TotalFastReceiveLine."Qty. on Order" + FastReceiveLineTmp."Qty. on Order";
            TotalFastReceiveLine."Outstanding Quantity" :=
              TotalFastReceiveLine."Outstanding Quantity" + FastReceiveLineTmp."Outstanding Quantity";
            TotalFastReceiveLine."Qty. to Receive" :=
              TotalFastReceiveLine."Qty. to Receive" + FastReceiveLineTmp."Qty. to Receive";
            TotalFastReceiveLine."Remaining Qty. to Receive" :=
              TotalFastReceiveLine."Remaining Qty. to Receive" +
              FastReceiveLineTmp."Remaining Qty. to Receive";
            TotalFastReceiveLine."Qty. Received" :=
              TotalFastReceiveLine."Qty. Received" + FastReceiveLineTmp."Qty. Received";
            TotalFastReceiveLine."Qty. Received this Receive" :=
              TotalFastReceiveLine."Qty. Received this Receive" +
              FastReceiveLineTmp."Qty. Received this Receive";
          END ELSE BEGIN
            TotalFastReceiveLine.Quantity :=
              TotalFastReceiveLine.Quantity +
              ROUND(
                FastReceiveLineTmp."Quantity (Base)" / TotalFastReceiveLine."Qty. per Unit of Measure",
                0.00001);
            TotalFastReceiveLine."Qty. on Order" :=
              TotalFastReceiveLine."Qty. on Order" +
              ROUND(
                FastReceiveLineTmp."Qty. on Order (Base)" /
                TotalFastReceiveLine."Qty. per Unit of Measure",
                0.00001);
            TotalFastReceiveLine."Outstanding Quantity" :=
              TotalFastReceiveLine."Outstanding Quantity" +
              ROUND(
                FastReceiveLineTmp."Outstanding Qty. (Base)" /
                TotalFastReceiveLine."Qty. per Unit of Measure",
                0.0001);
            TotalFastReceiveLine."Qty. to Receive" :=
              TotalFastReceiveLine."Qty. to Receive" +
              ROUND(
                FastReceiveLineTmp."Qty. to Receive (Base)" /
                TotalFastReceiveLine."Qty. per Unit of Measure",
                0.0001);
            TotalFastReceiveLine."Remaining Qty. to Receive" :=
              TotalFastReceiveLine."Remaining Qty. to Receive" +
              ROUND(
                FastReceiveLineTmp."Rem. Qty. to Receive (Base)" /
                TotalFastReceiveLine."Qty. per Unit of Measure",
                0.0001);
            TotalFastReceiveLine."Qty. Received" :=
              TotalFastReceiveLine."Qty. Received" +
              ROUND(
                FastReceiveLineTmp."Qty. Received (Base)" /
                TotalFastReceiveLine."Qty. per Unit of Measure",
                0.0001);
            TotalFastReceiveLine."Qty. Received this Receive" :=
              TotalFastReceiveLine."Qty. Received this Receive" +
              ROUND(
                FastReceiveLineTmp."Qty. Received this Rec. (Base)" /
                TotalFastReceiveLine."Qty. per Unit of Measure",
                0.0001);
          END;
        END;
      UNTIL FastReceiveLineTmp.NEXT = 0;

      CalcShowOtherLines(ReceiveControl,ReceiveSetup,ReceiveStation,FALSE,OtherDocumentExists);
    END;

    PROCEDURE LookupLines@1240030002(VAR ReceiveControl@1240030002 : Record 14000611;ReceiveSetup@1240030004 : Record 14000607;ReceiveStation@1240030003 : Record 14000608);
    VAR
      SalesLine@1240030001 : Record 37;
      PurchLine@1240030000 : Record 39;
    BEGIN
      CASE ReceiveControl."Source Type" OF
        DATABASE::"Sales Header":
          BEGIN
            SalesLine.RESET;
            SalesLine.SETRANGE("Document Type",ReceiveControl."Source Subtype");
            IF ReceiveControl."Multi Document Receive" THEN
              SalesLine.SETFILTER("Document No.",ReceiveControl."Multi Document No.")
            ELSE
              SalesLine.SETRANGE("Document No.",ReceiveControl."Source ID");
            SalesLine.SETRANGE(Type,ReceiveControl."Input Type");
            SalesLine.SETRANGE("No.",ReceiveControl."Input No.");
            SalesLine.SETRANGE("Variant Code",ReceiveControl."Input Variant Code");
            IF ReceiveSetup."Location Receiving" THEN
              SalesLine.SETRANGE("Location Code",ReceiveStation."Location Code");
            PAGE.RUNMODAL(0,SalesLine);
          END;
        DATABASE::"Purchase Header":
          BEGIN
            PurchLine.RESET;
            PurchLine.SETRANGE("Document Type",ReceiveControl."Source Subtype");
            IF ReceiveControl."Multi Document Receive" THEN
              PurchLine.SETFILTER("Document No.",ReceiveControl."Multi Document No.")
            ELSE
              PurchLine.SETRANGE("Document No.",ReceiveControl."Source ID");
            PurchLine.SETRANGE(Type,ReceiveControl."Input Type");
            PurchLine.SETRANGE("No.",ReceiveControl."Input No.");
            PurchLine.SETRANGE("Variant Code",ReceiveControl."Input Variant Code");
            IF ReceiveSetup."Location Receiving" THEN
              PurchLine.SETRANGE("Location Code",ReceiveStation."Location Code");
            PAGE.RUNMODAL(0,PurchLine);
          END;
        ELSE
          ERROR(Text002,ReceiveControl.FormatSource2);
      END;
    END;

    PROCEDURE CalcShowOtherLines@1240030005(VAR ReceiveControl@1240030003 : Record 14000611;ReceiveSetup@1240030006 : Record 14000607;ReceiveStation@1240030005 : Record 14000608;ShowForm@1240030001 : Boolean;VAR OtherDocumentExists@1240030004 : Boolean);
    VAR
      SalesLine@1240030002 : Record 37;
      PurchLine@1240030000 : Record 39;
    BEGIN
      CASE ReceiveControl."Source Type" OF
        DATABASE::"Sales Header":
          BEGIN
            SalesLine.RESET;
            SalesLine.SETCURRENTKEY(
              "Document Type",Type,"No.","Variant Code","Drop Shipment");
            SalesLine.SETRANGE("Document Type",ReceiveControl."Source Subtype");
            IF ReceiveControl."Multi Document Receive" THEN
              SalesLine.SETFILTER("Document No.",ReceiveControl.ReverseMultiDocNoFilter)
            ELSE
              SalesLine.SETFILTER("Document No.",'<>%1',ReceiveControl."Source ID");
            SalesLine.SETRANGE(Type,ReceiveControl."Input Type");
            SalesLine.SETRANGE("No.",ReceiveControl."Input No.");
            SalesLine.SETRANGE("Variant Code",ReceiveControl."Input Variant Code");
            SalesLine.SETRANGE("Drop Shipment",FALSE);
            IF ReceiveSetup."Location Receiving" THEN
              SalesLine.SETRANGE("Location Code",ReceiveStation."Location Code");
            SalesLine.SETRANGE("Sell-to Customer No.",ReceiveControl."Ship-from No.");
            SalesLine.SETFILTER("Outstanding Qty. (Base)",'<>0');
            IF ShowForm THEN
              PAGE.RUNMODAL(0,SalesLine)
            ELSE
              OtherDocumentExists := SalesLine.FIND('-');
          END;
        DATABASE::"Purchase Header":
          BEGIN
            PurchLine.RESET;
            PurchLine.SETCURRENTKEY(
              "Document Type",Type,"No.","Variant Code","Drop Shipment","Location Code");
            PurchLine.SETRANGE("Document Type",ReceiveControl."Source Subtype");
            IF ReceiveControl."Multi Document Receive" THEN
              PurchLine.SETFILTER("Document No.",ReceiveControl.ReverseMultiDocNoFilter)
            ELSE
              PurchLine.SETFILTER("Document No.",'<>%1',ReceiveControl."Source ID");
            PurchLine.SETRANGE(Type,ReceiveControl."Input Type");
            PurchLine.SETRANGE("No.",ReceiveControl."Input No.");
            PurchLine.SETRANGE("Variant Code",ReceiveControl."Input Variant Code");
            PurchLine.SETRANGE("Drop Shipment",FALSE);
            IF ReceiveSetup."Location Receiving" THEN
              PurchLine.SETRANGE("Location Code",ReceiveStation."Location Code");
            PurchLine.SETRANGE("Buy-from Vendor No.",ReceiveControl."Ship-from No.");
            PurchLine.SETFILTER("Outstanding Qty. (Base)",'<>0');
            IF ShowForm THEN
              PAGE.RUNMODAL(0,PurchLine)
            ELSE
              OtherDocumentExists := PurchLine.FIND('-');
          END;
        ELSE
          ERROR(Text002,ReceiveControl.FormatSource2);
      END;
    END;

    BEGIN
    END.
  }
}

