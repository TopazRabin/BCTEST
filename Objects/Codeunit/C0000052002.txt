OBJECT Codeunit 52002 Codeunit5778EventSubscriber
{
  OBJECT-PROPERTIES
  {
    Date=04/07/21;
    Time=[ 6:14:56 AM];
    Modified=Yes;
    Version List=;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE QtyAvailtoPick@1000000000(ItemNo@1000000002 : Code[20];Loc@1000000003 : Code[10]) PickQtyAvail : Decimal;
    VAR
      LocQtyAvailTake@1000000000 : Decimal;
      BinContent@1000000001 : Record 7302;
    BEGIN
      BinContent.RESET;
      BinContent.SETCURRENTKEY("Location Code","Item No.");
      IF Loc <> '' THEN
        BinContent.SETRANGE("Location Code",Loc);
      BinContent.SETRANGE("Item No.",ItemNo);

      //<TPZ1675>
      //BinContent.SETFILTER("Bin Type Code",'%1','PICKPUT');
      BinContent.SETFILTER("Bin Type Code",'%1|%2','PICKPUT', 'PICK');
      //</TPZ1675>

      BinContent.SETFILTER("Block Movement",'<>%1&<>%2',BinContent."Block Movement"::Outbound,BinContent."Block Movement"::All);

      //<TPZ1675>
      BinContent.SETFILTER("Bin Code", '<>%1&<>%2&<>%3&<>%4', 'RTV','MRB','QA','QC');
      //</TPZ1675>

      LocQtyAvailTake := 0;
      IF BinContent.FINDSET THEN
        REPEAT
          BinContent.CALCFIELDS(Quantity,"Pick Qty.","Neg. Adjmt. Qty.");
          LocQtyAvailTake := LocQtyAvailTake + BinContent.Quantity - BinContent."Pick Qty." - BinContent."Neg. Adjmt. Qty.";
        UNTIL BinContent.NEXT = 0;
      PickQtyAvail := LocQtyAvailTake;
    END;

    PROCEDURE QtyInReceive@1000000008(ItemNo@1000000000 : Code[20];LocationCode@1000000001 : Code[10]) : Decimal;
    VAR
      BinContent@1000000002 : Record 7302;
      LocQtyInReceive@1000000003 : Decimal;
    BEGIN
      // <TOP3906>
      BinContent.RESET;
      BinContent.SETCURRENTKEY("Location Code","Item No.");
      BinContent.SETRANGE("Location Code",LocationCode);
      BinContent.SETRANGE("Item No.",ItemNo);
      BinContent.SETRANGE("Bin Type Code",'RECEIVE');
      IF BinContent.FIND('-') THEN
        REPEAT
          BinContent.CALCFIELDS(Quantity);
          LocQtyInReceive := LocQtyInReceive + BinContent.Quantity;
        UNTIL BinContent.NEXT = 0;
      EXIT(LocQtyInReceive);
      // </TOP3906>
    END;

    BEGIN
    END.
  }
}

