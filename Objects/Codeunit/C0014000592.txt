OBJECT Codeunit 14000592 Create Multiple Sug. Orders
{
  OBJECT-PROPERTIES
  {
    Date=01/01/21;
    Time=12:00:00 PM;
    Version List=UBP3.03.02;
  }
  PROPERTIES
  {
    OnRun=VAR
            DocNumber@1240020000 : Code[20];
          BEGIN
            InvMgt.CreateLogEntry('',Text001,UBPLogEntryNo);
            COMMIT;

            VendorBuyingCalendar.RESET;
            VendorBuyingCalendar.SETCURRENTKEY("Vendor No.","Procurement Group Code");
            VendorBuyingCalendar.SETFILTER("Vendor No.",'<>%1','');
            VendorBuyingCalendar.SETFILTER("Location Code",'<>%1','');
            VendorBuyingCalendar.SETRANGE("Line Buy Vendor",TRUE);
            IF VendorBuyingCalendar.FINDSET THEN
              REPEAT
                IF VendorBuyingCalendar."Procurement Group Code" = '' THEN BEGIN
                  IF VendorBuyingCalendar."Division Code" <> '' THEN
                    DocNumber := VendorBuyingCalendar."Location Code" + VendorBuyingCalendar."Division Code"
                  ELSE
                    DocNumber := VendorBuyingCalendar."Location Code";
                  IF SugOrderHeader.GET(VendorBuyingCalendar."Vendor No.", DocNumber) THEN
                    SugOrderHeader.DELETE(TRUE);
                END ELSE BEGIN
                  DocNumber := VendorBuyingCalendar."Procurement Group Code";
                  IF (PrevVendBuyingCalendar."Vendor No." <> VendorBuyingCalendar."Vendor No.") OR
                     (PrevVendBuyingCalendar."Procurement Group Code" <> VendorBuyingCalendar."Procurement Group Code") THEN
                    IF SugOrderHeader.GET(VendorBuyingCalendar."Vendor No.", DocNumber) THEN
                      SugOrderHeader.DELETE(TRUE);
                END;
                PrevVendBuyingCalendar.COPY(VendorBuyingCalendar);

                VendorBuyingCalendar2.GET(VendorBuyingCalendar."Vendor No.",VendorBuyingCalendar."Location Code", VendorBuyingCalendar."Division Code");
                VendorBuyingCalendar2."Order Create  Error" := TRUE;
                VendorBuyingCalendar2."Order Create Date" := CURRENTDATETIME;
                VendorBuyingCalendar2.MODIFY(FALSE);
                COMMIT;
                CLEAR(CreateSuggestedOrder);
                IF CreateSuggestedOrder.RUN(VendorBuyingCalendar) THEN BEGIN
                  VendorBuyingCalendar2."Doc. No. Filter" := DocNumber;
                  VendorBuyingCalendar2."Order Create Date" := CURRENTDATETIME;
                  VendorBuyingCalendar2."Order Create  Error" := FALSE;
                  VendorBuyingCalendar2.MODIFY(TRUE);
                END;
              UNTIL VendorBuyingCalendar.NEXT = 0;

            InvMgt.CloseLogEntry(UBPLogEntryNo,'');
          END;

  }
  CODE
  {
    VAR
      SugOrderHeader@1240020002 : Record 14000565;
      PrevVendBuyingCalendar@1240020003 : Record 14000563;
      VendorBuyingCalendar@1240020000 : Record 14000563;
      VendorBuyingCalendar2@1240020001 : Record 14000563;
      CreateSuggestedOrder@1240020004 : Codeunit 14000559;
      InvMgt@1240020005 : Codeunit 14000553;
      UBPLogEntryNo@1240020006 : Integer;
      Text001@1240020007 : TextConst 'ENU=Create All Suggested Orders';

    BEGIN
    END.
  }
}

