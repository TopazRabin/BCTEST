OBJECT Codeunit 50028 Populate NAV Table Name
{
  OBJECT-PROPERTIES
  {
    Date=07/01/20;
    Time=12:00:00 AM;
    Modified=Yes;
    Version List=TPZ000.00.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            Window@1000000000 : Dialog;
            Counter@1000000001 : Integer;
            CaptionIndex@1000000002 : Integer;
            CaptionText@1000000003 : Text;
            NAVFieldOptions@1000000004 : Record 51480;
            OptionString@1000000005 : Text;
            CommaIndex@1000000006 : Integer;
          BEGIN
            Window.OPEN('#1###### OF #2######');
            Window.UPDATE(2, Field.COUNT);
            NAVFieldName.DELETEALL;
            NAVFieldOptions.DELETEALL;
            IF Field.FINDSET THEN
            REPEAT
              Counter += 1;
              Window.UPDATE(1, Counter);

              NAVFieldName.INIT;
              NAVFieldName.TableNo := Field.TableNo;
              NAVFieldName."No." := Field."No.";
              NAVFieldName.TableName := Field.TableName;
              NAVFieldName.FieldName := Field.FieldName;
              NAVFieldName.Type := FORMAT(Field.Type);
              NAVFieldName.Len := Field.Len;
              NAVFieldName.Class := FORMAT(Field.Class);
              NAVFieldName.Enabled := Field.Enabled;
              NAVFieldName."Type Name" := Field."Type Name";
              NAVFieldName."Field Caption" := Field."Field Caption";

              NAVFieldName.INSERT;

              IF NAVFieldName."Type Name" = 'Option' THEN BEGIN
                CaptionIndex := 0;
                OptionString := Field.OptionString;
                REPEAT
                  CommaIndex := STRPOS(OptionString, ',');
                  NAVFieldOptions.TableNo := NAVFieldName.TableNo;
                  NAVFieldOptions."No." := NAVFieldName."No.";
                  NAVFieldOptions."Option No." := CaptionIndex;
                  IF CommaIndex <> 0 THEN
                    NAVFieldOptions."Option Caption" := COPYSTR(OptionString, 1, CommaIndex-1)
                  ELSE
                    NAVFieldOptions."Option Caption" := OptionString;

                  OptionString := DELSTR(OptionString, 1, CommaIndex);
                  NAVFieldOptions.INSERT;
                  CaptionIndex += 1;
                UNTIL CommaIndex <= 0;
              END;
            UNTIL Field.NEXT = 0;
            MESSAGE('DONE');
          END;

  }
  CODE
  {
    VAR
      NAVFieldName@1000000000 : Record 50038;
      Field@1000000001 : Record 2000000041;

    BEGIN
    END.
  }
}

