OBJECT Codeunit 50071 Table222EventSubscribers
{
  OBJECT-PROPERTIES
  {
    Date=11/18/20;
    Time=[ 6:47:49 AM];
    Modified=Yes;
    Version List=TPZ000.00.00,TPZ2981;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [EventSubscriber(Table,222,OnBeforeInsertEvent)]
    LOCAL PROCEDURE Tb222_OnInsert@1000000000(VAR Rec@1000000000 : Record 222;RunTrigger@1000000001 : Boolean);
    BEGIN
      IF NOT RunTrigger THEN
        EXIT;
      WITH Rec DO BEGIN
        // <TPZ208>
        "Date Created" := TODAY;
        "Created by User ID" := USERID;
        "Last Date Modified" := TODAY;
        "Last Modified by User ID" := USERID;
        // </TPZ208>
      END;
    END;

    [EventSubscriber(Table,222,OnBeforeModifyEvent)]
    LOCAL PROCEDURE Tb222_OnModify@1000000001(VAR Rec@1000000000 : Record 222;VAR xRec@1000000001 : Record 222;RunTrigger@1000000002 : Boolean);
    BEGIN
      IF NOT RunTrigger THEN
        EXIT;
      WITH Rec DO BEGIN
        // <TPZ208>
        "Last Modified by User ID" := USERID;
        // </TPZ208>
      END;
    END;

    [EventSubscriber(Table,222,OnAfterDeleteEvent)]
    LOCAL PROCEDURE Tb222_OnDelete@1000000002(VAR Rec@1000000000 : Record 222;RunTrigger@1000000001 : Boolean);
    VAR
      ShipToAddrDivision@1000000002 : Record 51002;
    BEGIN
      IF NOT RunTrigger THEN
        EXIT;
      WITH Rec DO BEGIN
        // <TPZ717>
        ShipToAddrDivision.SETRANGE("Customer No.","Customer No.");
        ShipToAddrDivision.SETRANGE("Ship-to Code",Code);
        ShipToAddrDivision.DELETEALL;
        // </TPZ717>
      END;
    END;

    [EventSubscriber(Table,222,OnAfterValidateEvent,City)]
    LOCAL PROCEDURE Tb222_City_OnValidate@1000000003(VAR Rec@1000000000 : Record 222;VAR xRec@1000000001 : Record 222;CurrFieldNo@1000000002 : Integer);
    VAR
      PostCode@1000000003 : Record 225;
    BEGIN
      WITH Rec DO BEGIN
        // <TPZ416>
        VALIDATE(County);
        // </TPZ416>
        // <TPZ417>
        IF "Country/Region Code" <> 'US' THEN
          EXIT;
        IF PostCode.GET("Post Code",City) THEN
          District := PostCode.District
        ELSE
          District := '';
        // </TPZ417>
      END;
    END;

    [EventSubscriber(Table,222,OnAfterValidateEvent,"Post Code")]
    LOCAL PROCEDURE Tb222_PostCode_OnValidate@1000000004(VAR Rec@1000000000 : Record 222;VAR xRec@1000000001 : Record 222;CurrFieldNo@1000000002 : Integer);
    VAR
      PostCode@1000000003 : Record 225;
    BEGIN
      WITH Rec DO BEGIN
        // <TPZ416>
        VALIDATE(County);
        // </TPZ416>
        // <TPZ417>
        IF "Country/Region Code" <> 'US' THEN
          EXIT;
        IF PostCode.GET("Post Code",City) THEN
          District := PostCode.District
        ELSE
          District := '';
        // </TPZ417>
      END;
    END;

    [EventSubscriber(Table,222,OnAfterValidateEvent,County)]
    LOCAL PROCEDURE Tb222_County_OnValidate@1000000005(VAR Rec@1000000000 : Record 222;VAR xRec@1000000001 : Record 222;CurrFieldNo@1000000002 : Integer);
    VAR
      CountyRec@1000000003 : Record 51128;
    BEGIN
      WITH Rec DO BEGIN
        // <TPZ416>
        IF "Country/Region Code" <> 'US' THEN
          EXIT;
        IF CountyRec.GET("Country/Region Code",County) THEN BEGIN
          "Region Code" := CountyRec."Region Code";
          "Territory Code" := CountyRec."Territory Code";
        END ELSE BEGIN
          "Region Code" := '';
          "Territory Code" := '';
        END;
        // </TPZ416>
      END;
    END;

    [EventSubscriber(Table,222,OnBeforeValidateEvent,Name)]
    LOCAL PROCEDURE Tb222_Name_OnValidate@1000000006(VAR Rec@1000000000 : Record 222;VAR xRec@1000000001 : Record 222;CurrFieldNo@1000000002 : Integer);
    BEGIN
      //TPZ2981
      IF STRLEN(Rec.Name) > 35 THEN
        ERROR('More than 35 Char not allowed');
    END;

    BEGIN
    {
      PSHUKLA
      001 TPZ2981 VAH 11182020
    }
    END.
  }
}

