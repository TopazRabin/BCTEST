OBJECT Codeunit 51706 CommJnlManagement
{
  OBJECT-PROPERTIES
  {
    Date=07/01/20;
    Time=12:00:00 AM;
    Modified=Yes;
    Version List=TPZ000.00.00;
  }
  PROPERTIES
  {
    Permissions=TableData 80=imd,
                TableData 232=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Commission journal;ESM=Diario %1;FRC=Journal %1;ENC=%1 journal';
      Text004@1004 : TextConst 'ENU=DEFAULT;ESM=GENERICO;FRC=DêFAUT;ENC=DEFAULT';
      Text005@1005 : TextConst 'ENU=Default Journal;ESM=Diario genÇrico;FRC=Journal par dÇfaut;ENC=Default Journal';
      LastGenJnlLine@1006 : Record 81;
      OpenFromBatch@1007 : Boolean;
      USText000@1020000 : TextConst 'ENU=Deposit Document;ESM=Doc. dep¢sito;FRC=Document de dÇpìt;ENC=Deposit Document';

    PROCEDURE TemplateSelection@1(PageID@1005 : Integer;VAR CommJnlLine@1004 : Record 51705;VAR JnlSelected@1003 : Boolean);
    VAR
      CommJnlTemplate@1002 : Record 51703;
    BEGIN
      JnlSelected := TRUE;

      CommJnlTemplate.RESET;
      CommJnlTemplate.SETRANGE("Page ID",PageID);

      CASE CommJnlTemplate.COUNT OF
        0:
          BEGIN
            CommJnlTemplate.INIT;
            CommJnlTemplate.Name := FORMAT(MAXSTRLEN(CommJnlTemplate.Name));
            CommJnlTemplate.Description := STRSUBSTNO(Text001);
            CommJnlTemplate.INSERT;
            COMMIT;
          END;
        1:
          CommJnlTemplate.FINDFIRST;
        ELSE
          JnlSelected := PAGE.RUNMODAL(0,CommJnlTemplate) = ACTION::LookupOK;
      END;
      IF JnlSelected THEN BEGIN
        CommJnlLine.FILTERGROUP := 2;
        CommJnlLine.SETRANGE("Journal Template Name",CommJnlTemplate.Name);
        CommJnlLine.FILTERGROUP := 0;
        IF OpenFromBatch THEN BEGIN
          CommJnlLine."Journal Template Name" := '';
          PAGE.RUN(CommJnlTemplate."Page ID",CommJnlLine);
        END;
      END;
    END;

    PROCEDURE TemplateSelectionFromBatch@9(VAR CommJnlBatch@1000 : Record 51704);
    VAR
      CommJnlLine@1002 : Record 51705;
      CommJnlTemplate@1003 : Record 51703;
    BEGIN
      OpenFromBatch := TRUE;
      CommJnlTemplate.GET(CommJnlBatch."Journal Template Name");
      CommJnlTemplate.TESTFIELD("Page ID");
      CommJnlBatch.TESTFIELD(Name);

      CommJnlLine.FILTERGROUP := 2;
      CommJnlLine.SETRANGE("Journal Template Name",CommJnlTemplate.Name);
      CommJnlLine.FILTERGROUP := 0;

      CommJnlLine."Journal Template Name" := '';
      CommJnlLine."Journal Batch Name" := CommJnlBatch.Name;
      PAGE.RUN(CommJnlTemplate."Page ID",CommJnlLine);
    END;

    PROCEDURE OpenJnl@2(VAR CurrentJnlBatchName@1000 : Code[10];VAR CommJnlLine@1001 : Record 51705);
    BEGIN
      CheckTemplateName(CommJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
      CommJnlLine.FILTERGROUP := 2;
      CommJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      CommJnlLine.FILTERGROUP := 0;
    END;

    PROCEDURE OpenJnlBatch@10(VAR CommJnlBatch@1000 : Record 51704);
    VAR
      CommJnlTemplate@1002 : Record 51703;
      CommJnlLine@1004 : Record 51705;
      JnlSelected@1003 : Boolean;
    BEGIN
      IF CommJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
        EXIT;
      CommJnlBatch.FILTERGROUP(2);
      IF CommJnlBatch.GETFILTER("Journal Template Name") <> '' THEN BEGIN
        CommJnlBatch.FILTERGROUP(0);
        EXIT;
      END;
      CommJnlBatch.FILTERGROUP(0);

      IF NOT CommJnlBatch.FINDFIRST THEN BEGIN
        TemplateSelection(0,CommJnlLine,JnlSelected);
        IF CommJnlTemplate.FINDFIRST THEN
          CheckTemplateName(CommJnlTemplate.Name,CommJnlBatch.Name);
      END;

      CommJnlBatch.FINDFIRST;
      JnlSelected := TRUE;
      IF CommJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
        CommJnlTemplate.SETRANGE(Name,CommJnlBatch.GETFILTER("Journal Template Name"));
      CASE CommJnlTemplate.COUNT OF
        1:
          CommJnlTemplate.FINDFIRST;
        ELSE
          JnlSelected := PAGE.RUNMODAL(0,CommJnlTemplate) = ACTION::LookupOK;
      END;
      IF NOT JnlSelected THEN
        ERROR('');

      CommJnlBatch.FILTERGROUP(0);
      CommJnlBatch.SETRANGE("Journal Template Name",CommJnlTemplate.Name);
      CommJnlBatch.FILTERGROUP(2);
    END;

    PROCEDURE CheckTemplateName@3(CurrentJnlTemplateName@1000 : Code[10];VAR CurrentJnlBatchName@1001 : Code[10]);
    VAR
      CommJnlBatch@1002 : Record 51704;
    BEGIN
      CommJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
      IF NOT CommJnlBatch.GET(CurrentJnlTemplateName,CurrentJnlBatchName) THEN BEGIN
        IF NOT CommJnlBatch.FINDFIRST THEN BEGIN
          CommJnlBatch.INIT;
          CommJnlBatch."Journal Template Name" := CurrentJnlTemplateName;
          CommJnlBatch.SetupNewBatch;
          CommJnlBatch.Name := Text004;
          CommJnlBatch.Description := Text005;
          CommJnlBatch.INSERT(TRUE);
          COMMIT;
        END;
        CurrentJnlBatchName := CommJnlBatch.Name
      END;
    END;

    PROCEDURE CheckName@4(CurrentJnlBatchName@1000 : Code[10];VAR CommJnlLine@1001 : Record 51705);
    VAR
      CommJnlBatch@1002 : Record 51704;
    BEGIN
      CommJnlBatch.GET(CommJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
    END;

    PROCEDURE SetName@5(CurrentJnlBatchName@1000 : Code[10];VAR CommJnlLine@1001 : Record 51705);
    BEGIN
      CommJnlLine.FILTERGROUP := 2;
      CommJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      CommJnlLine.FILTERGROUP := 0;
      IF CommJnlLine.FIND('-') THEN;
    END;

    PROCEDURE LookupName@6(VAR CurrentJnlBatchName@1000 : Code[10];VAR CommJnlLine@1001 : Record 51705);
    VAR
      CommJnlBatch@1002 : Record 51704;
    BEGIN
      COMMIT;
      CommJnlBatch."Journal Template Name" := CommJnlLine.GETRANGEMAX("Journal Template Name");
      CommJnlBatch.Name := CommJnlLine.GETRANGEMAX("Journal Batch Name");
      CommJnlBatch.FILTERGROUP(2);
      CommJnlBatch.SETRANGE("Journal Template Name",CommJnlBatch."Journal Template Name");
      CommJnlBatch.FILTERGROUP(0);
      IF PAGE.RUNMODAL(0,CommJnlBatch) = ACTION::LookupOK THEN BEGIN
        CurrentJnlBatchName := CommJnlBatch.Name;
        SetName(CurrentJnlBatchName,CommJnlLine);
      END;
    END;

    BEGIN
    {
      2015-05-11 TPZ92 TAKHMETO
        Codeunit has been created
    }
    END.
  }
}

