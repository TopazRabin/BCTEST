OBJECT Codeunit 14002582 RF Bin Count Line
{
  OBJECT-PROPERTIES
  {
    Date=01/22/18;
    Time=12:00:00 PM;
    Version List=RF1.50.11;
  }
  PROPERTIES
  {
    TableNo=7700;
    OnRun=VAR
            MiniformMgmt@1000 : Codeunit 7702;
          BEGIN
            MiniformMgmt.Initialize(
              MiniformHeader,Rec,DOMxmlin,ReturnedNode,
              RootNode,XMLDOMMgmt,ADCSCommunication,LoginID,
              CurrentCode,StackCode,WhseEmpId,LocationFilter);

            IF Code <> CurrentCode THEN
              PrepareData
            ELSE
              ProcessSelection;

            CLEAR(DOMxmlin);
          END;

  }
  CODE
  {
    VAR
      MiniformHeader@1000 : Record 7700;
      XMLDOMMgmt@1003 : Codeunit 6224;
      ADCSCommunication@1004 : Codeunit 7701;
      ADCSNASStartup@1005 : Codeunit 7700;
      DOMxmlin@1240020002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      ReturnedNode@1240020001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      RootNode@1240020000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      Text000@1240020003 : TextConst 'ENU=Function not found';
      Text004@1240030000 : TextConst 'ENU=Invalid %1.';
      Text006@1010 : TextConst 'ENU=No input Node found';
      TextValue@1011 : Text[250];
      WhseEmpId@1240020004 : Text[250];
      LoginID@1012 : Text[250];
      LocationFilter@1013 : Text[250];
      CurrentCode@1015 : Text[250];
      PreviousCode@1016 : Text[250];
      StackCode@1017 : Text[250];
      Remark@1018 : Text[250];
      ActiveInputField@1019 : Integer;
      Text009@1020 : TextConst 'ENU=No Documents found';
      RecRef@1021 : RecordRef;
      Text010@1240020005 : TextConst 'ENU=Count failed to generate';
      Text011@1240020006 : TextConst 'ENU=Batch not found';
      Text012@1240020007 : TextConst 'ENU=No matching Miniform found';
      Text013@1240020008 : TextConst 'ENU=No Call Miniform found';
      Text014@1240020009 : TextConst 'ENU=No Empty Bin line found';

    LOCAL PROCEDURE ProcessSelection@8();
    VAR
      BinCount@1000 : Record 14002580;
      BinCountLine@1240020001 : Record 14002581;
      BinCountLine2@1240020007 : Record 14002581;
      MiniformLine@1240020002 : Record 7701;
      FuncGroup@1001 : Record 7702;
      RFEmployeeMenuFilter@1240020008 : Record 14002806;
      Bin@1002 : Record 7354;
      BinCountMgt@1240020005 : Codeunit 14002580;
      ReturnedNode2@1240020000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      TableNo@1004 : Integer;
      RecordId@1005 : RecordID;
      FldNo@1240020006 : Integer;
      FieldID@1240020004 : Text[250];
      Command@1240020003 : Code[10];
    BEGIN
      IF XMLDOMMgmt.FindNode(RootNode,'Header/Input',ReturnedNode) THEN
        TextValue := ReturnedNode.InnerText
      ELSE
        ERROR(Text006);

      EVALUATE(TableNo,ADCSCommunication.GetNodeAttribute(ReturnedNode,'TableNo'));
      RecRef.OPEN(TableNo);
      EVALUATE(RecordId,ADCSCommunication.GetNodeAttribute(ReturnedNode,'RecordID'));
      IF RecRef.GET(RecordId) THEN BEGIN
        RecRef.SETTABLE(BinCountLine);
        BinCountLine.RESET;
        BinCountLine.SETFILTER("Location Code",LocationFilter);
        BinCountLine.SETRANGE("Bin Count No.",BinCountLine."Bin Count No.");
        BinCountLine.SETFILTER("Assigned User ID",'%1|%2','',WhseEmpId);
        BinCountLine.SETRANGE("Count Performed",FALSE);
        RecRef.GETTABLE(BinCountLine);
        ADCSCommunication.SetRecRef(RecRef);
      END ELSE BEGIN
        ADCSCommunication.RunPreviousMiniform(DOMxmlin);
        EXIT;
      END;

      FuncGroup.KeyDef := ADCSCommunication.GetFunctionKey(MiniformHeader.Code,TextValue);
      ActiveInputField := 1;

      CASE FuncGroup.KeyDef OF
        FuncGroup.KeyDef::Esc:
          ADCSCommunication.RunPreviousMiniform(DOMxmlin);
        FuncGroup.KeyDef::First:
          ADCSCommunication.FindRecRef(0,MiniformHeader."No. of Records in List");
        FuncGroup.KeyDef::LnDn:
          IF NOT ADCSCommunication.FindRecRef(1,MiniformHeader."No. of Records in List") THEN
            Remark := Text009;
        FuncGroup.KeyDef::LnUp:
          ADCSCommunication.FindRecRef(2,MiniformHeader."No. of Records in List");
        FuncGroup.KeyDef::Last:
          ADCSCommunication.FindRecRef(3,MiniformHeader."No. of Records in List");
        FuncGroup.KeyDef::PgDn:
          IF NOT ADCSCommunication.FindRecRef(4,MiniformHeader."No. of Records in List") THEN
            Remark := Text009;
        FuncGroup.KeyDef::PgUp:
          ADCSCommunication.FindRecRef(5,MiniformHeader."No. of Records in List");
        FuncGroup.KeyDef::Input:
          BEGIN
            IF BinCountLine."Count Initiated" THEN BEGIN
              OpenExistingBinCount(BinCountLine);
            END ELSE BEGIN
              IF BinCountLine."Empty Bin" THEN BEGIN
                Bin.RESET;
                IF Bin.GET(LocationFilter,BinCountLine."Bin Code") THEN
                  IF Bin.Empty THEN BEGIN
                    IF NOT LaunchEmptyBinCount(BinCountLine) THEN
                      EXIT;
                  END ELSE BEGIN
                    BinCountMgt.ToggleEmptyBin(BinCountLine,FALSE);
                    BinCountLine2.RESET;
                    IF BinCountLine2.GET(
                      BinCountLine."Bin Count No.",BinCountLine."Bin Code",
                      BinCountLine."Zone Code",BinCountLine."Location Code")
                    THEN BEGIN
                      IF NOT GenerateWhseBinCount(BinCountLine2) THEN
                        ERROR(Text010)
                      ELSE
                        EXIT;
                    END ELSE
                      ERROR(Text010);
                  END;
              END ELSE BEGIN
                Bin.RESET;
                IF Bin.GET(LocationFilter,BinCountLine."Bin Code") THEN
                  IF Bin.Empty THEN BEGIN
                    BinCountMgt.ToggleEmptyBin(BinCountLine,TRUE);
                    BinCountLine2.RESET;
                    IF BinCountLine2.GET(
                      BinCountLine."Bin Count No.",BinCountLine."Bin Code",
                      BinCountLine."Zone Code",BinCountLine."Location Code")
                    THEN BEGIN
                      IF NOT LaunchEmptyBinCount(BinCountLine2) THEN
                        ERROR(Text010);
                    END;
                  END ELSE BEGIN
                    IF NOT GenerateWhseBinCount(BinCountLine) THEN
                      ERROR(Text010)
                    ELSE
                      EXIT;
                  END;
              END;
            END;
          END;
        ELSE
          ERROR(Text000);
      END;

      IF NOT (FuncGroup.KeyDef IN [FuncGroup.KeyDef::Esc,FuncGroup.KeyDef::Input]) THEN
        SendForm(ActiveInputField);
    END;

    LOCAL PROCEDURE PrepareData@4();
    VAR
      BinCount@1240020002 : Record 14002580;
      BinCountLine@1240020001 : Record 14002581;
      BinCountLine2@1240020003 : Record 14002581;
      RFEmployeeSetup@1240030000 : Record 14002802;
      RFMgt@1240030002 : Codeunit 14002801;
      ReturnedNode2@1240020000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      TableNo@1002 : Integer;
      RecordId@1003 : RecordID;
      ForwardTableNo@1240020008 : Integer;
      ForwardTable@1240020007 : Boolean;
      ForwardRecordLine@1240020006 : Boolean;
      ForwardRecordId@1240020005 : RecordID;
      RecRef2@1240020004 : RecordRef;
    BEGIN
      XMLDOMMgmt.FindNode(RootNode,'Header/Input',ReturnedNode);

      XMLDOMMgmt.FindNode(RootNode,'ExtraInfo',ReturnedNode2);
      ForwardTable := EVALUATE(ForwardTableNo,ADCSCommunication.GetNodeAttribute(ReturnedNode2,'ForwardTable'));
      IF ForwardTable THEN
        EVALUATE(TableNo,ADCSCommunication.GetNodeAttribute(ReturnedNode2,'ForwardTable'))
      ELSE
        EVALUATE(TableNo,ADCSCommunication.GetNodeAttribute(ReturnedNode,'TableNo'));

      RecRef.OPEN(TableNo);
      IF ForwardTable THEN
        EVALUATE(RecordId,ADCSCommunication.GetNodeAttribute(ReturnedNode2,'ForwardRecord'))
      ELSE
        EVALUATE(RecordId,ADCSCommunication.GetNodeAttribute(ReturnedNode,'RecordID'));

      ForwardRecordLine :=
        EVALUATE(ForwardRecordId,ADCSCommunication.GetNodeAttribute(ReturnedNode2,'ForwardRecordLine'));
      IF ForwardRecordLine THEN
        IF RecRef2.GET(ForwardRecordId) THEN BEGIN
          RecRef2.SETTABLE(BinCountLine);
          BinCountLine2.RESET;
          BinCountLine2.SETFILTER("Location Code",BinCountLine."Location Code");
          BinCountLine2.SETRANGE("Bin Count No.",BinCountLine."Bin Count No.");
          BinCountLine2.SETFILTER("Assigned User ID",'%1|%2','',WhseEmpId);
          BinCountLine2.SETRANGE("Count Performed",FALSE);
          IF BinCountLine2.FIND('-') THEN BEGIN
            RecRef.GETTABLE(BinCountLine2);
            ADCSCommunication.SetRecRef(RecRef);
            ActiveInputField := 1;
            SendForm(ActiveInputField);
            EXIT;
          END ELSE BEGIN
            ADCSCommunication.RunPreviousMiniform(DOMxmlin);
            EXIT;
          END;
        END;

      IF RecRef.GET(RecordId) THEN BEGIN
        RecRef.SETTABLE(BinCount);
        BinCountLine.RESET;
        BinCountLine.SETFILTER("Location Code",LocationFilter);
        BinCountLine.SETRANGE("Bin Count No.",BinCount."Bin Count No.");
        BinCountLine.SETFILTER("Assigned User ID",'%1|%2','',WhseEmpId);
        BinCountLine.SETRANGE("Count Performed",FALSE);
        IF BinCountLine.FIND('-') THEN;
        RecRef.GETTABLE(BinCountLine);
        ADCSCommunication.SetRecRef(RecRef);
        ActiveInputField := 1;
        SendForm(ActiveInputField);
      END ELSE BEGIN
        ADCSCommunication.RunPreviousMiniform(DOMxmlin);
        EXIT;
      END;
    END;

    LOCAL PROCEDURE SendForm@1(InputField@1000 : Integer);
    BEGIN
      ADCSCommunication.EncodeMiniForm(MiniformHeader,StackCode,DOMxmlin,InputField,Remark,LoginID);
      ADCSCommunication.GetReturnXML(DOMxmlin);
      ADCSNASStartup.SendXMLReply(DOMxmlin);
    END;

    PROCEDURE GenerateWhseBinCount@1240020013(BinCountLine@1240030000 : Record 14002581) : Boolean;
    VAR
      MiniformHeader2@1240020005 : Record 7700;
      BinCount@1240020008 : Record 14002580;
      BinCountLine2@1240020011 : Record 14002581;
      BinCountTemplate@1240020012 : Record 14002582;
      BinContent@1240030001 : Record 7302;
      WhseJournalBatch@1240020001 : Record 7310;
      WhseJournalBatch2@1240020000 : Record 7310;
      RFEmployeeSetup@1240020010 : Record 14002802;
      RFDynamicMgt@1240030002 : Codeunit 14002882;
      BatchName@1240020004 : Text[30];
      ForwardRecID@1240020003 : RecordID;
      ForwardTableNo@1240020002 : Integer;
      RemarkMessage@1240020007 : Text[250];
      BinCountExists@1240020009 : Boolean;
      EmptyBin@1240020006 : Boolean;
    BEGIN
      RFEmployeeSetup.GetRFEmployeeSetup(WhseEmpId);

      IF NOT BinCountLine."Empty Bin" THEN BEGIN
        BinCount.RESET;
        IF BinCount.GET(BinCountLine."Bin Count No.") THEN
          IF BinCountTemplate.GET(BinCount."Bin Count Template Code") THEN;

        BinContent.RESET;
        BinContent.SETRANGE("Location Code",BinCountLine."Location Code");
        BinContent.SETRANGE("Bin Code",BinCountLine."Bin Code");
        BinContent.SETRANGE("Zone Code",BinCountLine."Zone Code");
        IF BinCountTemplate."Item Filter" <> '' THEN
          BinContent.SETFILTER("Item No.",BinCountTemplate."Item Filter");
        IF BinCountTemplate."Variant Filter" <> '' THEN
          BinContent.SETFILTER("Variant Code",BinCountTemplate."Variant Filter");
        IF BinContent.FINDSET(FALSE) THEN
          RFDynamicMgt.CreateBinCountWhsePhysicalInventory(
            BinContent,RFEmployeeSetup,WhseJournalBatch,BinCountLine."Location Code",
            BinCountLine."Bin Code",BinCountLine."Bin Count No.",WhseEmpId,RemarkMessage);

        WhseJournalBatch2.RESET;
        IF WhseJournalBatch2.GET(
          WhseJournalBatch."Journal Template Name",WhseJournalBatch.Name,WhseJournalBatch."Location Code")
        THEN BEGIN
          BinCountLine2.RESET;
          IF BinCountLine2.GET(
              BinCountLine."Bin Count No.",BinCountLine."Bin Code",
              BinCountLine."Zone Code",BinCountLine."Location Code")
          THEN BEGIN
            BinCountLine2."Batch No." := WhseJournalBatch2.Name;
            BinCountLine2.MODIFY;
          END;

          ForwardTableNo := DATABASE::"Warehouse Journal Batch";
          ForwardRecID := WhseJournalBatch2.RECORDID;
          BinCountExists := TRUE;
          ADCSCommunication.SetExtraValue('BinCountExists',FORMAT(BinCountExists));
          ADCSCommunication.SetExtraValue('ForwardName',WhseJournalBatch2.Name);
          ADCSCommunication.SetExtraValue('ForwardTable',FORMAT(ForwardTableNo));
          ADCSCommunication.SetExtraValue('ForwardRecord',FORMAT(ForwardRecID));
          ADCSCommunication.SetExtraValue('FromTable',FORMAT(DATABASE::"Bin Count Line"));
          ADCSCommunication.SetExtraValue('FromRecord',FORMAT(BinCountLine.RECORDID));
          ADCSCommunication.SetExtraValue('FromRecordLine',FORMAT(BinCountLine.RECORDID));
          ADCSCommunication.SetExtraValue('FromMiniformCode',MiniformHeader.Code);
          SendForm(ActiveInputField);

          MiniformHeader2.GET(MiniformHeader."Next Miniform");
          MiniformHeader2.SaveXMLin(DOMxmlin);
          CODEUNIT.RUN(MiniformHeader2."Handling Codeunit",MiniformHeader2);
          EXIT(TRUE);
        END;
      END;

      EXIT(FALSE);
    END;

    PROCEDURE OpenExistingBinCount@1240020000(BinCountLine@1240020000 : Record 14002581);
    VAR
      MiniformHeader2@1240020013 : Record 7700;
      BinCount@1240020014 : Record 14002580;
      BinContent@1240020011 : Record 7302;
      WhseJournalBatch@1240020009 : Record 7310;
      WhseJournalBatch2@1240020008 : Record 7310;
      RFEmployeeSetup@1240020007 : Record 14002802;
      RFDynamicMgt@1240020006 : Codeunit 14002882;
      BatchName@1240020005 : Text[30];
      ForwardRecID@1240020004 : RecordID;
      ForwardTableNo@1240020003 : Integer;
      RemarkMessage@1240020002 : Text[250];
      BinCountExists@1240020001 : Boolean;
    BEGIN
      BinCount.GET(BinCountLine."Bin Count No.");

      WhseJournalBatch2.RESET;
      IF WhseJournalBatch2.GET(
        BinCount."Journal Template Name",BinCountLine."Batch No.",BinCount."Location Code")
      THEN BEGIN
        ForwardTableNo := DATABASE::"Warehouse Journal Batch";
        ForwardRecID := WhseJournalBatch2.RECORDID;
        BinCountExists := TRUE;
        ADCSCommunication.SetExtraValue('BinCountExists',FORMAT(BinCountExists));
        ADCSCommunication.SetExtraValue('ForwardName',WhseJournalBatch2.Name);
        ADCSCommunication.SetExtraValue('ForwardTable',FORMAT(ForwardTableNo));
        ADCSCommunication.SetExtraValue('ForwardRecord',FORMAT(ForwardRecID));
        ADCSCommunication.SetExtraValue('FromTable',FORMAT(DATABASE::"Bin Count Line"));
        ADCSCommunication.SetExtraValue('FromRecord',FORMAT(BinCountLine.RECORDID));
        ADCSCommunication.SetExtraValue('FromRecordLine',FORMAT(BinCountLine.RECORDID));
        ADCSCommunication.SetExtraValue('FromMiniformCode',MiniformHeader.Code);
        SendForm(ActiveInputField);

        MiniformHeader2.GET(MiniformHeader."Next Miniform");
        MiniformHeader2.SaveXMLin(DOMxmlin);
        CODEUNIT.RUN(MiniformHeader2."Handling Codeunit",MiniformHeader2);
        EXIT;
      END ELSE
        ERROR(Text011);
    END;

    PROCEDURE LaunchEmptyBinCount@1240020001(BinCountLine@1240020000 : Record 14002581) : Boolean;
    VAR
      MiniformHeader2@1240020013 : Record 7700;
      MiniformLine@1240020015 : Record 7701;
      BinCountLine2@1240020012 : Record 14002581;
      RFEmployeeSetup@1240020008 : Record 14002802;
      RFDynamicMgt@1240020007 : Codeunit 14002882;
      BinCountMgt@1240020009 : Codeunit 14002580;
      ForwardRecID@1240020005 : RecordID;
      ForwardTableNo@1240020004 : Integer;
      BinCountExists@1240020002 : Boolean;
      ForwardMiniformCode@1240020014 : Code[20];
    BEGIN
      RFEmployeeSetup.GetRFEmployeeSetup(WhseEmpId);

      IF NOT BinCountLine."Empty Bin" THEN
        EXIT(FALSE);

      BinCountMgt.ToggleAssignedUserID(BinCountLine,TRUE,WhseEmpId);

      ForwardTableNo := DATABASE::"Bin Count Line";
      ForwardRecID := BinCountLine.RECORDID;
      BinCountExists := TRUE;
      ForwardMiniformCode := MiniformHeader.Code;
      ADCSCommunication.SetExtraValue('BinCountExists',FORMAT(BinCountExists));
      ADCSCommunication.SetExtraValue('ForwardTable',FORMAT(ForwardTableNo));
      ADCSCommunication.SetExtraValue('ForwardRecord',FORMAT(ForwardRecID));
      ADCSCommunication.SetExtraValue('ForwardMiniformCode',ForwardMiniformCode);
      ADCSCommunication.SetExtraValue('FromTable',FORMAT(DATABASE::"Bin Count Line"));
      ADCSCommunication.SetExtraValue('FromRecord',FORMAT(BinCountLine.RECORDID));
      ADCSCommunication.SetExtraValue('FromRecordLine',FORMAT(BinCountLine.RECORDID));
      ADCSCommunication.SetExtraValue('FromMiniformCode',MiniformHeader.Code);
      SendForm(ActiveInputField);

      MiniformLine.RESET;
      MiniformLine.SETRANGE("Miniform Code",MiniformHeader.Code);
      MiniformLine.SETRANGE("Table No.",DATABASE::"Bin Count Line");
      MiniformLine.SETRANGE("Field No.",BinCountLine.FIELDNO("Empty Bin"));
      IF MiniformLine.FIND('-') THEN BEGIN
        IF MiniformLine."Call Miniform" <> '' THEN BEGIN
          IF MiniformHeader2.GET(MiniformLine."Call Miniform") THEN BEGIN
            MiniformHeader2.SaveXMLin(DOMxmlin);
            CODEUNIT.RUN(MiniformHeader2."Handling Codeunit",MiniformHeader2);
            EXIT(TRUE);
          END ELSE
            ERROR(Text012);
        END ELSE
          ERROR(Text013);
      END ELSE
        ERROR(Text014);

      EXIT(FALSE);
    END;

    EVENT DOMxmlin@1240020002::NodeInserting@93(sender@1240020001 : Variant;e@1240020000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT DOMxmlin@1240020002::NodeInserted@94(sender@1240020001 : Variant;e@1240020000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT DOMxmlin@1240020002::NodeRemoving@95(sender@1240020001 : Variant;e@1240020000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT DOMxmlin@1240020002::NodeRemoved@96(sender@1240020001 : Variant;e@1240020000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT DOMxmlin@1240020002::NodeChanging@97(sender@1240020001 : Variant;e@1240020000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT DOMxmlin@1240020002::NodeChanged@98(sender@1240020001 : Variant;e@1240020000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    BEGIN
    END.
  }
}

