OBJECT Codeunit 14002352 E.D.I. Output Envelope
{
  OBJECT-PROPERTIES
  {
    Date=10/23/17;
    Time=12:00:00 PM;
    Version List=SE0.60.16,TPZEDI;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Char@1240030000 : Char;
      GSCtrlNo@1240030001 : Text[30];
      ISACtrlNo@1240030002 : Text[30];
      OutVersion@1240030003 : Text[12];
      SndToTPID@1240030004 : Text[15];
      SndFmTPID@1240030005 : Text[15];
      STCtrlNo@1240030006 : Text[30];
      Text001@1001 : TextConst 'ENU=You must specify a companent delimiter.';
      Text002@1002 : TextConst 'ENU=You must specify an element delimiter.';
      Text003@1003 : TextConst 'ENU=You must specify a segment delimiter.';
      Text004@1004 : TextConst 'ENU=You must specify an element delimiter in template %1';
      GlobalTopazDivCode@1000000000 : Code[20];

    PROCEDURE CreateISASegment@3(VAR EDITemplate@1240030000 : Record 14002350;VAR EDITradePartner@1240030001 : Record 14002360;VAR EDIDocument@1240030002 : Record 14002353;VAR OutLineArray@1240030003 : ARRAY [5] OF Text[200]);
    VAR
      FunctAckReq@1240030004 : Code[1];
      TradePtnrIDQualifier@1240030005 : Code[2];
      InterchangeVersionOverride@1240030010 : Code[5];
      AuthorizationInfoQualifier@1240030013 : Code[2];
      AuthorizationInformation@1240030012 : Code[10];
      SecurityInfoQualifier@1240030011 : Code[2];
      SecurityInformation@1240030014 : Code[10];
      TradePartnerNo@1240030006 : Text[20];
      Delim@1240030007 : Char;
      SegDelim@1240030008 : Char;
      CompDelim@1240030009 : Char;
      EDIStandard@1240020000 : Char;
    BEGIN
      CLEAR(OutLineArray);

      IF EDIDocument."ISA Funct. Ack. Requested" = '' THEN
        FunctAckReq := '0'
      ELSE
        FunctAckReq := EDIDocument."ISA Funct. Ack. Requested";

      IF EDIDocument."ISA T.P. ID Qualifier Override" <> '' THEN
        TradePtnrIDQualifier := EDIDocument."ISA T.P. ID Qualifier Override"
      ELSE
        TradePtnrIDQualifier := EDITradePartner."T.P. ID Qualifier";

      IF EDIDocument."ISA Trade Partner No. Override" <> '' THEN
        TradePartnerNo := EDIDocument."ISA Trade Partner No. Override"
      ELSE
        TradePartnerNo := EDITradePartner."No.";

      IF EDIDocument."Interchange Version Override" <> '' THEN
        InterchangeVersionOverride := EDIDocument."Interchange Version Override"
      ELSE
        InterchangeVersionOverride := COPYSTR(EDITradePartner."Interchange Control Version",1,5);

      IF EDIDocument."ISA Auth. Info. Qual. Override" <> '' THEN
        AuthorizationInfoQualifier := EDIDocument."ISA Auth. Info. Qual. Override"
      ELSE
        AuthorizationInfoQualifier := '00';

      IF EDIDocument."Security Info. Qual. Override" <> '' THEN
        SecurityInfoQualifier := EDIDocument."Security Info. Qual. Override"
      ELSE
        SecurityInfoQualifier := '00';

      IF EDIDocument."ISA Auth. Information Override" <> '' THEN
        AuthorizationInformation := EDIDocument."ISA Auth. Information Override"
      ELSE
        AuthorizationInformation := PADSTR(AuthorizationInformation,10,' ');

      IF EDIDocument."ISA Security Info. Override" <> '' THEN
        SecurityInformation := EDIDocument."ISA Security Info. Override"
      ELSE
        SecurityInformation := PADSTR(SecurityInformation,10,' ');

      IF EDITemplate."Interface File Type" =
        EDITemplate."Interface File Type"::"X12 Delimited"
      THEN BEGIN
        IF EDIDocument."Decimal EDI Standard" <> 0 THEN
          EDIStandard := EDIDocument."Decimal EDI Standard"
        ELSE BEGIN
          IF COPYSTR(EDIDocument."EDI Standard",1,1) = '' THEN
            EDIStandard := 0
          ELSE
            EVALUATE(EDIStandard,COPYSTR(EDIDocument."EDI Standard",1,1));
        END;
      END;

      GetDelim(EDITemplate,Delim,SegDelim,CompDelim);

      CASE EDITemplate."Interface File Type" OF
        EDITemplate."Interface File Type"::Harbinger:
          BEGIN
            EDITemplate."Export ISA Control No." := INCSTR(EDITemplate."Export ISA Control No.");
            OutLineArray[1] := 'ISA' + FORMAT(Delim) +
              'AuthorizationInfoQualifier' + FORMAT(Delim) +
              '          ' + FORMAT(Delim) +
              '00' + FORMAT(Delim) +
              '          ' + FORMAT(Delim) +
              EDITemplate."My Trading Partner Qualifier" + FORMAT(Delim) +
              COPYSTR(PADSTR(EDITemplate."My Trading Partner No.",15,' '),1,15) + FORMAT(Delim) +
              TradePtnrIDQualifier + FORMAT(Delim) +
              COPYSTR(PADSTR(TradePartnerNo,15,' '),1,15) + FORMAT(Delim) +
              FORMAT(TODAY,6,'<year,2><Month,2><Day,2>') + FORMAT(Delim) +
              FORMAT(TIME,4,'<Hours24,2><filler character,0><Minutes,2>') + FORMAT(Delim) +
              COPYSTR(EDIDocument."EDI Standard",1,1) + FORMAT(Delim) +
              InterchangeVersionOverride + FORMAT(Delim) +
              COPYSTR(EDITemplate."Export ISA Control No.",1,9) + FORMAT(Delim) +
              FunctAckReq + FORMAT(Delim) +
              COPYSTR(FORMAT(EDIDocument.Status),1,1) + FORMAT(Delim) +
              EDITradePartner."Component Separator" + FORMAT(SegDelim);
          END;
        EDITemplate."Interface File Type"::"X12 Delimited":
          BEGIN
            EDITemplate."Export ISA Control No." := INCSTR(EDITemplate."Export ISA Control No.");
            OutLineArray[1] := 'ISA' + FORMAT(Delim) +
              AuthorizationInfoQualifier + FORMAT(Delim) +
              COPYSTR(PADSTR(AuthorizationInformation,10,' '),1,10) + FORMAT(Delim) +
              SecurityInfoQualifier + FORMAT(Delim) +
              COPYSTR(PADSTR(SecurityInformation,10,' '),1,10) + FORMAT(Delim) +
              EDITemplate."My Trading Partner Qualifier" + FORMAT(Delim) +
              COPYSTR(PADSTR(EDITemplate."My Trading Partner No.",15,' '),1,15) + FORMAT(Delim) +
              TradePtnrIDQualifier + FORMAT(Delim) +
              COPYSTR(PADSTR(TradePartnerNo,15,' '),1,15) + FORMAT(Delim) +
              FORMAT(TODAY,6,'<year,2><Month,2><Day,2>') + FORMAT(Delim) +
              FORMAT(TIME,4,'<Hours24,2><filler character,0><Minutes,2>') + FORMAT(Delim);
              IF EDIStandard > 0 THEN
               OutLineArray[1] := OutLineArray[1] + FORMAT(EDIStandard);
              OutLineArray[1] := OutLineArray[1] + FORMAT(Delim) +
              InterchangeVersionOverride + FORMAT(Delim) +
              COPYSTR(EDITemplate."Export ISA Control No.",1,9) + FORMAT(Delim) +
              FunctAckReq + FORMAT(Delim) +
              COPYSTR(FORMAT(EDIDocument.Status),1,1) + FORMAT(Delim) +
              EDITradePartner."Component Separator";
              IF SegDelim > 0 THEN
                OutLineArray[1] :=  OutLineArray[1] + FORMAT(SegDelim);
          END;
        EDITemplate."Interface File Type"::Sterling:
          BEGIN
            TradePartnerNo := DELCHR(TradePartnerNo,'<',' ');
            TradePartnerNo := PADSTR(TradePartnerNo,15,' ');
            OutVersion := COPYSTR(EDIDocument.Version,1,12);
            OutVersion := DELCHR(OutVersion,'<',' ');
            OutVersion := PADSTR(OutVersion,12,' ');
            OutLineArray[1] := COPYSTR(EDIDocument."EDI Document No.",1,3) +   // 3
              '   ' +                                         // 3
              '00000000000' +                                 // 11
              '                      ' +                      // 22
              '  ' +                                          // 2
              '               ' +                             // 15
              COPYSTR(TradePartnerNo,1,15) +                  // 15
              '               ' +                             // 15
              '         ' +                                   // 9
              '         ' +                                   // 9
              '         ' +                                   // 9
              OutVersion +                                    // 12
              '               ' +                             // 15
              '  ' +                                          // 2
              COPYSTR(EDIDocument."EDI Standard",1,1) +        // 1
              '                                           ' + // 43
              ' ' +                                           // 1
              ' ' +                                           // 1
              COPYSTR(FORMAT(EDIDocument.Status),1,1);        // 1
          END;
        EDITemplate."Interface File Type"::"GE PC EDI":
          BEGIN
            SndToTPID := '';
            SndToTPID := COPYSTR(EDITradePartner."No.",1,15);
            SndToTPID := DELCHR(SndToTPID,'<',' ');
            SndToTPID := PADSTR(SndToTPID,15,' ');

            SndFmTPID := '';
            SndFmTPID := COPYSTR(EDITemplate."My Trading Partner No.",1,15);
            SndFmTPID := DELCHR(SndFmTPID,'<',' ');
            SndFmTPID := PADSTR(SndFmTPID,15,' ');

            ISACtrlNo := '';
            ISACtrlNo := EDITemplate."Export ISA Control No.";
            ISACtrlNo := DELCHR(ISACtrlNo,'<',' ');
            ISACtrlNo := PADSTR(ISACtrlNo,9,' ');

            STCtrlNo := '';
            STCtrlNo := EDITemplate."Export ST Control No.";
            STCtrlNo := DELCHR(STCtrlNo,'<',' ');
            STCtrlNo := PADSTR(STCtrlNo,9,' ');

            GSCtrlNo := '';
            GSCtrlNo := EDITemplate."Export GS Control No.";
            GSCtrlNo := DELCHR(GSCtrlNo,'<',' ');
            GSCtrlNo := PADSTR(GSCtrlNo,9,' ');

            Char := 7;
            OutLineArray[1] := '*GE' +                        // 3
              COPYSTR(EDIDocument."EDI Document No.",1,6) +   // 6
              '03' +                                          // 2
              'ISA' +                                         // 3
              'U   ' +                                        // 4
              '00300' +                                       // 5
              FORMAT(Char) +                                  // 1
              '>   ' +                                        // 4
              COPYSTR(SndFmTPID,1,15) +                       // 15
              '                    ' +                        // 20
              'ZZ  ' +                                        // 4
              COPYSTR(SndToTPID,1,15) +                       // 15
              '                    ' +                        // 20
              '12  ' +                                        // 4
              FORMAT(TODAY,6,'<year,2><Month,2><Day,2>') +    // 6
              FORMAT(TIME,4,'<hours24,2><minutes,2>') +       // 4
              COPYSTR(ISACtrlNo,1,9) +                        // 9
              '     ' +                                       // 5
              '              ' +                              // 14
              '00' +                                          // 2
              '              ' +                              // 14
              '00' +                                          // 2
              ' ' +                                           // 1
              '0' +                                           // 1
              '                                   ' +         // 35
              COPYSTR(FORMAT(EDIDocument.Status),1,1);        // 1
            OutLineArray[2] := COPYSTR(SndFmTPID,1,15) +      // 15
              '                    ' +                        // 20
              '    ' +                                        // 4
              COPYSTR(SndToTPID,1,15) +                       // 15
              '                    ' +                        // 20
              '    ' +                                        // 4
              FORMAT(TODAY,6,'<year,2><Month,2><Day,2>') +    // 6
              FORMAT(TIME,4,'<hours24,2><minutes,2>') +       // 4
              COPYSTR(STCtrlNo,1,9) +                         // 9
              '     ' +                                       // 5
              COPYSTR(EDIDocument."EDI Standard",1,1) +       // 1
              ' ' +                                           // 1
              COPYSTR(EDIDocument.Version,1,10) +             // 10
              '  ' +                                          // 2
              '              ' +                              // 14
              COPYSTR(GSCtrlNo,1,9) +                         // 9
              '     ' +                                       // 5
              '                                                    '; // 52
          END;
      END;
    END;

    PROCEDURE CreateGSSegment@4(VAR EDITemplate@1240030000 : Record 14002350;VAR EDITradePartner@1240030001 : Record 14002360;VAR EDIDocument@1240030002 : Record 14002353;VAR OutLineArray@1240030003 : ARRAY [5] OF Text[200]);
    VAR
      TradePartnerNo@1240030004 : Code[20];
      Delim@1240030005 : Char;
      FormattedDate@1240030007 : Text[12];
      SegDelim@1240030006 : Char;
      CompDelim@1240030008 : Char;
      GSResponsibleAgencyCode@1240030009 : Code[1];
      UNBApplicationReference@1240030010 : Code[14];
      GSTradePtnrNo@1240030011 : Code[20];
      SegDelimPos@1240020000 : Integer;
      NewPosition@1240020002 : Integer;
      TrailingDelimiter@1240020003 : Code[20];
      Finished@1240020004 : Boolean;
      UNBTradePartnerNo@1240020001 : Code[20];
    BEGIN
      CLEAR(OutLineArray);

      IF EDIDocument."GS Trade Partner No. Override" <> '' THEN
        TradePartnerNo := EDIDocument."GS Trade Partner No. Override"
      ELSE
        TradePartnerNo := EDITradePartner."No.";

      IF EDIDocument."GS Resp. Agency Code Override" <> '' THEN
        GSResponsibleAgencyCode := EDIDocument."GS Resp. Agency Code Override"
      ELSE
        GSResponsibleAgencyCode := 'X';

      IF EDIDocument."Blank UNB Application Ref." THEN
        UNBApplicationReference := ''
      ELSE
        IF EDIDocument."UNB Application Ref. Override" <> '' THEN
          UNBApplicationReference := EDIDocument."UNB Application Ref. Override"
        ELSE
          UNBApplicationReference := EDIDocument."EDI Document No.";


      IF EDITemplate."My GS Trade Ptnr. Override" <> '' THEN BEGIN //TPZEDI
         //<TPZEDI>
        CASE GlobalTopazDivCode OF
          'E':
            GSTradePtnrNo := COPYSTR(EDITemplate."My Trading Partner No.",1,15);
          //<TPZ2561>
          'P':
            GSTradePtnrNo := COPYSTR(EDITemplate."My GS Trade Ptnr. Ovrrd TPZ P",1,15); //utk
          //</TPZ2561>
          'L':
            GSTradePtnrNo := COPYSTR(EDITemplate."My GS Trade Ptnr. Override",1,15);
          'I':
            GSTradePtnrNo := COPYSTR(EDITemplate."My GS Trade Ptnr. Override TPZ",1,15);
        END;
        IF GlobalTopazDivCode = '' THEN
          GSTradePtnrNo := COPYSTR(EDITemplate."My Trading Partner No.",1,15);
        //</TPZEDI>
      END ELSE //TPZEDI
        GSTradePtnrNo := COPYSTR(EDITemplate."My Trading Partner No.",1,15);

      IF EDIDocument."UNB Recipient ID Override" <> '' THEN
        UNBTradePartnerNo := EDIDocument."UNB Recipient ID Override"
      ELSE
        UNBTradePartnerNo := EDIDocument."Trade Partner No.";

      GetDelim(EDITemplate,Delim,SegDelim,CompDelim);

      CASE EDITemplate."Interface File Type" OF
        EDITemplate."Interface File Type"::Harbinger:
          BEGIN
            EDITemplate."Export GS Control No." := INCSTR(EDITemplate."Export GS Control No.");
            OutLineArray[1] :=
              'GS' + FORMAT(Delim) +
              EDIDocument."GS Funct. Identifier Code" + FORMAT(Delim) +
              GSTradePtnrNo + FORMAT(Delim) +
              COPYSTR(TradePartnerNo,1,15) + FORMAT(Delim) +
              FORMAT(TODAY,8,'<year4,4><Month,2><Day,2>') + FORMAT(Delim) +
              FORMAT(TIME,4,'<Hours24,2><filler character,0><Minutes,2>') + FORMAT(Delim) +
              COPYSTR(EDITemplate."Export GS Control No.",1,9) + FORMAT(Delim) +
              GSResponsibleAgencyCode + FORMAT(Delim) +
              EDIDocument.Version + FORMAT(SegDelim);
          END;
        EDITemplate."Interface File Type"::"X12 Delimited":
          BEGIN
            EDITemplate."Export GS Control No." := INCSTR(EDITemplate."Export GS Control No.");
            IF EDIDocument."Pre. 4010 Date Format" THEN
              FormattedDate := FORMAT(TODAY,6,'<year,2><Month,2><Day,2>')
            ELSE
              FormattedDate := FORMAT(TODAY,8,'<year4,4><Month,2><Day,2>');
            OutLineArray[1] :=
              'GS' + FORMAT(Delim) +
              EDIDocument."GS Funct. Identifier Code" + FORMAT(Delim) +
              GSTradePtnrNo + FORMAT(Delim) +
              COPYSTR(TradePartnerNo,1,15) + FORMAT(Delim) +
              FormattedDate + FORMAT(Delim) +
              FORMAT(TIME,4,'<Hours24,2><filler character,0><Minutes,2>') + FORMAT(Delim) +
              COPYSTR(EDITemplate."Export GS Control No.",1,9) + FORMAT(Delim) +
              GSResponsibleAgencyCode + FORMAT(Delim) +
              EDIDocument.Version;
              IF SegDelim > 0 THEN
                OutLineArray[1] :=  OutLineArray[1] + FORMAT(SegDelim);
          END;
        EDITemplate."Interface File Type"::"EDIFACT Delimited":
          BEGIN
            EDITemplate."Export GS Control No." := INCSTR(EDITemplate."Export GS Control No.");
            OutLineArray[1] :=
              'UNB' + FORMAT(Delim) +
              EDIDocument."UNB Syntax Identifier" + FORMAT(CompDelim) +
              EDIDocument."UNB Version Number" + FORMAT(Delim);
            IF EDIDocument."UNB Sender Qualifier" <> '' THEN
              OutLineArray[1] :=
                OutLineArray[1] +
                EDITemplate."My Trading Partner No." + FORMAT(CompDelim) +
                EDIDocument."UNB Sender Qualifier" + FORMAT(Delim)
            ELSE
              OutLineArray[1] :=
                OutLineArray[1] +EDITemplate."My Trading Partner No." + FORMAT(Delim);
            IF EDIDocument."UNB Recipient Qualifier" <> '' THEN
              OutLineArray[1] :=
                OutLineArray[1] +
                UNBTradePartnerNo + FORMAT(CompDelim) +
                EDIDocument."UNB Recipient Qualifier" + FORMAT(Delim)
            ELSE
              OutLineArray[1] :=
                OutLineArray[1] + UNBTradePartnerNo + FORMAT(Delim);
            OutLineArray[1] :=
               OutLineArray[1] +
               FORMAT(TODAY,6,'<year,2><Month,2><Day,2>') + FORMAT(CompDelim) +
               FORMAT(TIME,4,'<Hours24,2><filler character,0><Minutes,2>') + FORMAT(Delim) +
                COPYSTR(EDITemplate."Export GS Control No.",1,14) + FORMAT(Delim);
            IF EDIDocument."UNB Recipient's Reference" <> '' THEN BEGIN
              OutLineArray[1] :=
                OutLineArray[1] +
                EDIDocument."UNB Recipient's Reference" + FORMAT(CompDelim) +
                EDIDocument."UNB Recipient's Ref. Qualifier" + FORMAT(Delim);
            END ELSE
              IF EDIDocument."Bypass UNB Rcpt. Ref. Element" = FALSE THEN
                OutLineArray[1] :=
                  OutLineArray[1] + EDIDocument."UNB Recipient's Reference" + FORMAT(Delim);
            IF EDIDocument."Bypass UNB App. Ref. Element" = FALSE THEN
              OutLineArray[1] :=
                OutLineArray[1] +
                UNBApplicationReference + FORMAT(Delim);
            OutLineArray[1] :=
              OutLineArray[1] +
              EDIDocument."UNB Processing Priority Code" + FORMAT(Delim) +
              EDIDocument."UNB Acknowledgement Request" + FORMAT(Delim) +
              EDIDocument."UNB Comm. Agreement ID" + FORMAT(Delim) +
              EDIDocument."UNB Test Indicator";

              SegDelimPos := STRLEN(OutLineArray[1]);
              NewPosition := 0;
              Finished := FALSE;
              WHILE Finished = FALSE DO BEGIN
                TrailingDelimiter := COPYSTR(OutLineArray[1],(SegDelimPos - NewPosition),1);
                IF STRPOS(TrailingDelimiter,'+') > 0 THEN BEGIN
                  NewPosition := NewPosition + 1;
                  OutLineArray[1] := COPYSTR(OutLineArray[1],1,(SegDelimPos - NewPosition));
                END ELSE
                 Finished := TRUE
              END;

              OutLineArray[1] := OutLineArray[1] + FORMAT(SegDelim);
          END;
      END;
    END;

    PROCEDURE CreateSTSegment@5(VAR EDITemplate@1240030000 : Record 14002350;VAR EDITradePartner@1240030001 : Record 14002360;VAR EDIDocument@1240030002 : Record 14002353;VAR OutLineArray@1240030003 : ARRAY [5] OF Text[200]);
    VAR
      Delim@1240030004 : Char;
      SegDelim@1240030005 : Char;
      CompDelim@1240030006 : Char;
    BEGIN
      CLEAR(OutLineArray);

      GetDelim(EDITemplate,Delim,SegDelim,CompDelim);

      CASE EDITemplate."Interface File Type" OF
        EDITemplate."Interface File Type"::Harbinger:
          BEGIN
            EDITemplate."Export ST Control No." := INCSTR(EDITemplate."Export ST Control No.");
            OutLineArray[1] := 'ST' + FORMAT(Delim) +
              COPYSTR(EDIDocument."EDI Document No.",1,3) + FORMAT(Delim) +
              COPYSTR(EDITemplate."Export ST Control No.",1,9) + FORMAT(SegDelim);
          END;
        EDITemplate."Interface File Type"::"X12 Delimited":
          BEGIN
            EDITemplate."Export ST Control No." := INCSTR(EDITemplate."Export ST Control No.");
            OutLineArray[1] := 'ST' + FORMAT(Delim) +
              COPYSTR(EDIDocument."EDI Document No.",1,3) + FORMAT(Delim) +
              COPYSTR(EDITemplate."Export ST Control No.",1,9);
              IF SegDelim > 0 THEN
                OutLineArray[1] :=  OutLineArray[1] + FORMAT(SegDelim);
          END;
        EDITemplate."Interface File Type"::"EDIFACT Delimited":
          BEGIN
            EDITemplate."Export ST Control No." := INCSTR(EDITemplate."Export ST Control No.");
            IF STRLEN(EDIDocument."UNH Assoc. Assigned Code") = 0 THEN
              BEGIN
                OutLineArray[1] :=
                  'UNH' + FORMAT(Delim) +
                  COPYSTR(EDITemplate."Export ST Control No.",1,14) + FORMAT(Delim) +
                  EDIDocument."EDI Document No." + FORMAT(CompDelim) +
                  EDIDocument."UNH Message Version Number" + FORMAT(CompDelim) +
                  EDIDocument."UNH Message Release Number" + FORMAT(CompDelim) +
                  EDIDocument."UNH Controlling Agency" + FORMAT(SegDelim);
              END ELSE BEGIN
                OutLineArray[1] :=
                  'UNH' + FORMAT(Delim) +
                COPYSTR(EDITemplate."Export ST Control No.",1,14) + FORMAT(Delim) +
                EDIDocument."EDI Document No." + FORMAT(CompDelim) +
                EDIDocument."UNH Message Version Number" + FORMAT(CompDelim) +
                EDIDocument."UNH Message Release Number" + FORMAT(CompDelim) +
                EDIDocument."UNH Controlling Agency" + FORMAT(CompDelim) +
                EDIDocument."UNH Assoc. Assigned Code" + FORMAT(SegDelim);
              END;
          END;
      END;
    END;

    PROCEDURE CreateSESegment@6(VAR EDITemplate@1240030000 : Record 14002350;VAR EDITradePartner@1240030001 : Record 14002360;VAR EDIDocument@1240030002 : Record 14002353;VAR OutLineArray@1240030003 : ARRAY [5] OF Text[200];VAR TotalISASegments@1240030004 : Integer;VAR TotalGSSegments@1240030005 : Integer;VAR TotalSTSegments@1240030006 : Integer;VAR TotalSegments@1240030007 : Integer);
    VAR
      Delim@1240030008 : Char;
      SegDelim@1240030009 : Char;
      CompDelim@1240030010 : Char;
    BEGIN
      CLEAR(OutLineArray);

      GetDelim(EDITemplate,Delim,SegDelim,CompDelim);

      CASE EDITemplate."Interface File Type" OF
        EDITemplate."Interface File Type"::Harbinger,EDITemplate."Interface File Type"::"X12 Delimited":
          BEGIN
            OutLineArray[1] := 'SE' + FORMAT(Delim) +
              FORMAT(TotalSegments + 1) + FORMAT(Delim) +
              COPYSTR(EDITemplate."Export ST Control No.",1,9);
              IF SegDelim > 0 THEN
                OutLineArray[1] :=  OutLineArray[1] + FORMAT(SegDelim);
          END;
        EDITemplate."Interface File Type"::"EDIFACT Delimited":
          BEGIN
            OutLineArray[1] :=
              'UNT' + FORMAT(Delim) +
              FORMAT(TotalSegments + 1) + FORMAT(Delim) +
              COPYSTR(EDITemplate."Export ST Control No.",1,14) + FORMAT(SegDelim);
          END;
      END;
    END;

    PROCEDURE CreateGESegment@7(VAR EDITemplate@1240030000 : Record 14002350;VAR EDITradePartner@1240030001 : Record 14002360;VAR EDIDocument@1240030002 : Record 14002353;VAR OutLineArray@1240030003 : ARRAY [5] OF Text[200];VAR TotalISASegments@1240030004 : Integer;VAR TotalGSSegments@1240030005 : Integer;VAR TotalSTSegments@1240030006 : Integer;VAR TotalSegments@1240030007 : Integer);
    VAR
      Delim@1240030008 : Char;
      SegDelim@1240030009 : Char;
      CompDelim@1240030010 : Char;
    BEGIN
      CLEAR(OutLineArray);

      GetDelim(EDITemplate,Delim,SegDelim,CompDelim);

      CASE EDITemplate."Interface File Type" OF
        EDITemplate."Interface File Type"::Harbinger,EDITemplate."Interface File Type"::"X12 Delimited":
          BEGIN
            OutLineArray[1] := 'GE' +
              FORMAT(Delim) +
              FORMAT(TotalSTSegments) +
              FORMAT(Delim) +
              COPYSTR(EDITemplate."Export GS Control No.",1,9);
              IF SegDelim > 0 THEN
                OutLineArray[1] :=  OutLineArray[1] + FORMAT(SegDelim);
          END;
        EDITemplate."Interface File Type"::"EDIFACT Delimited":
          BEGIN
            OutLineArray[1] :=
              'UNZ' + FORMAT(Delim) +
              FORMAT(TotalSTSegments) + FORMAT(Delim) +
              COPYSTR(EDITemplate."Export GS Control No.",1,14) + FORMAT(SegDelim);
          END;
      END;
    END;

    PROCEDURE CreateIEASegment@8(VAR EDITemplate@1240030000 : Record 14002350;VAR EDITradePartner@1240030001 : Record 14002360;VAR EDIDocument@1240030002 : Record 14002353;VAR OutLineArray@1240030003 : ARRAY [5] OF Text[200];VAR TotalISASegments@1240030004 : Integer;VAR TotalGSSegments@1240030005 : Integer;VAR TotalSTSegments@1240030006 : Integer;VAR TotalSegments@1240030007 : Integer);
    VAR
      Delim@1240030008 : Char;
      SegDelim@1240030009 : Char;
      CompDelim@1240030010 : Char;
    BEGIN
      CLEAR(OutLineArray);

      GetDelim(EDITemplate,Delim,SegDelim,CompDelim);

      IF (EDITemplate."Interface File Type" = EDITemplate."Interface File Type"::Harbinger) OR
         (EDITemplate."Interface File Type" = EDITemplate."Interface File Type"::"X12 Delimited")
      THEN BEGIN
        OutLineArray[1] := 'IEA' + FORMAT(Delim) +
          FORMAT(TotalGSSegments) + FORMAT(Delim) +
          COPYSTR(EDITemplate."Export ISA Control No.",1,9);
          IF SegDelim > 0 THEN
            OutLineArray[1] :=  OutLineArray[1] + FORMAT(SegDelim);
      END;
    END;

    PROCEDURE GetDelim@10(EDITemplate@1240030000 : Record 14002350;VAR Delim@1240030001 : Char;VAR SegDelim@1240030002 : Char;VAR CompDelim@1240030003 : Char);
    BEGIN
      IF EDITemplate."Interface File Type" = EDITemplate."Interface File Type"::"EDIFACT Delimited" THEN BEGIN
        IF EDITemplate."Component Delimeter" <> '' THEN
          CompDelim := EDITemplate."Component Delimeter"[1]
        ELSE
          CompDelim := EDITemplate."Dec. Component Delimeter";
        IF EDITemplate."Element Delimeter" <> '' THEN
          Delim := EDITemplate."Element Delimeter"[1]
        ELSE
          Delim := EDITemplate."Dec. Element Delimeter";
        IF EDITemplate."Segment Terminator" <> '' THEN
          SegDelim := EDITemplate."Segment Terminator"[1]
        ELSE
          SegDelim := EDITemplate."Dec. Segment Terminator";
        IF CompDelim = 0 THEN
          ERROR(Text001);
        IF Delim = 0 THEN
          ERROR(Text002);
        IF SegDelim = 0 THEN
          ERROR(Text003);
        EXIT;
      END;

      IF EDITemplate."File Format" = EDITemplate."File Format"::Delimited THEN BEGIN
        IF (EDITemplate."Element Delimeter" = '') AND
           (EDITemplate."Dec. Element Delimeter" = 0) THEN
          ERROR(Text004,EDITemplate.Code);
        IF EDITemplate."Segment Terminator" <> '' THEN
          SegDelim := EDITemplate."Segment Terminator"[1]
        ELSE
          SegDelim := EDITemplate."Dec. Segment Terminator";
        IF EDITemplate."Element Delimeter" <> '' THEN
          Delim := EDITemplate."Element Delimeter"[1]
        ELSE
          Delim := EDITemplate."Dec. Element Delimeter";
      END;
    END;

    PROCEDURE SetTopazDivision@1000000000(pDivCode@1000000000 : Code[20]);
    BEGIN
      //TPZEDI
      GlobalTopazDivCode := pDivCode;
    END;

    BEGIN
    {
      2019-05-01 UCHOUHAN TPZ2561
        Added Code for Division 'P'.
    }
    END.
  }
}

