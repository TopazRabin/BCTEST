OBJECT Codeunit 415 Release Purchase Document
{
  OBJECT-PROPERTIES
  {
    Date=02/25/21;
    Time=10:18:27 AM;
    Modified=Yes;
    Version List=NAVW111.00.00.44345,NAVNA11.00.00.44345;
  }
  PROPERTIES
  {
    TableNo=38;
    Permissions=TableData 38=rm;
    OnRun=BEGIN
            PurchaseHeader.COPY(Rec);
            Code;
            Rec := PurchaseHeader;
          END;

  }
  CODE
  {
    VAR
      Text001@1010 : TextConst 'ENU=There is nothing to release for the document of type %1 with the number %2.;ESM=No hay nada que lanzar para el documento de tipo %1 con el n�mero %2.;FRC=Il n''y a rien � lib�rer pour le document de type %1 portant le num�ro %2.;ENC=There is nothing to release for the document of type %1 with the number %2.';
      PurchSetup@1002 : Record 312;
      InvtSetup@1000 : Record 313;
      PurchaseHeader@1007 : Record 38;
      WhsePurchRelease@1004 : Codeunit 5772;
      Text002@1005 : TextConst 'ENU=This document can only be released when the approval process is complete.;ESM=Este documento s�lo se puede lanzar una vez completado el proceso de aprobaci�n.;FRC=Ce document ne peut �tre lanc� qu''� la fin du processus d''approbation.;ENC=This document can only be released when the approval process is complete.';
      Text003@1006 : TextConst 'ENU=The approval process must be canceled or completed to reopen this document.;ESM=El proceso de aprobaci�n se debe cancelar o completar para volver a abrir este documento.;FRC=On doit annuler ou terminer le processus d''approbation avant de rouvrir ce document.;ENC=The approval process must be cancelled or completed to reopen this document.';
      Text005@1001 : TextConst 'ENU=There are unpaid prepayment invoices that are related to the document of type %1 with the number %2.;ESM=Hay facturas anticipo sin pagar relacionadas con el documento de tipo %1 con el n�mero %2.;FRC=Il existe des factures de paiement anticip� impay�es li�es au document de type %1 portant le num�ro %2.;ENC=There are unpaid prepayment invoices that are related to the document of type %1 with the number %2.';
      UnpostedPrepaymentAmountsErr@1003 : TextConst '@@@="%1 - Document Type; %2 - Document No.";ENU=There are unposted prepayment amounts on the document of type %1 with the number %2.;ESM=Hay importes de anticipo sin registrar en el documento de tipo %1 con el n�mero %2.;FRC=Il existe des montants paiement anticip� non report�s sur le document de type %1 portant le num�ro %2.;ENC=There are unposted prepayment amounts on the document of type %1 with the number %2.';
      PreviewMode@1008 : Boolean;
      SkipCheckReleaseRestrictions@1009 : Boolean;
      "==TPZ==="@1000000000 : Integer;
      AFPDocSynch@1000000007 : Codeunit 14000572;
      InvMgtSetup@1000000006 : Record 14000551;
      NameAndAddressMgt@1000000002 : Codeunit 14000709;
      EMailMgt@1000000005 : Codeunit 14000903;
      RunFromEship@1000000001 : Boolean;
      "***ABCSI Globals***"@1000000004 : Integer;
      ArchiveManagement@1000000003 : Codeunit 5063;

    LOCAL PROCEDURE Code@11() LinesWereModified : Boolean;
    VAR
      PurchLine@1006 : Record 39;
      PrepaymentMgt@1003 : Codeunit 441;
      NotOnlyDropShipment@1002 : Boolean;
      PostingDate@1001 : Date;
      PrintPostedDocuments@1000 : Boolean;
      UBPEvents@1240020000 : Codeunit 14000599;
    BEGIN
      WITH PurchaseHeader DO BEGIN
        IF Status = Status::Released THEN
          EXIT;

        OnBeforeReleasePurchaseDoc(PurchaseHeader,PreviewMode);
        IF NOT (PreviewMode OR SkipCheckReleaseRestrictions) THEN
          CheckPurchaseReleaseRestrictions;

        TESTFIELD("Buy-from Vendor No.");

        PurchLine.SETRANGE("Document Type","Document Type");
        PurchLine.SETRANGE("Document No.","No.");
        PurchLine.SETFILTER(Type,'>0');
        PurchLine.SETFILTER(Quantity,'<>0');
        IF NOT PurchLine.FIND('-') THEN
          ERROR(Text001,"Document Type","No.");
        InvtSetup.GET;
        IF InvtSetup."Location Mandatory" THEN BEGIN
          PurchLine.SETRANGE(Type,PurchLine.Type::Item);
          IF PurchLine.FIND('-') THEN
            REPEAT
              IF NOT PurchLine.IsServiceItem THEN
                PurchLine.TESTFIELD("Location Code");
            UNTIL PurchLine.NEXT = 0;
          PurchLine.SETFILTER(Type,'>0');
        END;
        PurchLine.SETRANGE("Drop Shipment",FALSE);
        NotOnlyDropShipment := PurchLine.FIND('-');
        PurchLine.RESET;

        PurchSetup.GET;
        IF PurchSetup."Calc. Inv. Discount" THEN BEGIN
          PostingDate := "Posting Date";
          PrintPostedDocuments := "Print Posted Documents";
          CODEUNIT.RUN(CODEUNIT::"Purch.-Calc.Discount",PurchLine);
          LinesWereModified := TRUE;
          GET("Document Type","No.");
          "Print Posted Documents" := PrintPostedDocuments;
          IF PostingDate <> "Posting Date" THEN
            VALIDATE("Posting Date",PostingDate);
        END;

        IF PrepaymentMgt.TestPurchasePrepayment(PurchaseHeader) AND ("Document Type" = "Document Type"::Order) THEN BEGIN
          Status := Status::"Pending Prepayment";
          MODIFY(TRUE);
          EXIT;
        END;
        Status := Status::Released;

        LinesWereModified := LinesWereModified OR CalcAndUpdateVATOnLines(PurchaseHeader,PurchLine);
        // >> Shipping
      //IF PurchSetup."Enable Shipping" AND NOT RunFromEship THEN //3073 code commented
        //NameAndAddressMgt.CheckNameAddressPurchaseHeader(PurchaseHeader,"E-Ship Agent Code"); //3073 code commented
      // << Shipping

        MODIFY(TRUE);

      //TOP160 KT ABCSI Purchase Order Fields 03312015
      {PurchSetup.GET;
      IF PurchSetup."Archive Quotes and Orders" THEN
        ArchiveManagement.ArchPurchDocumentNoConfirm(PurchaseHeader);} //NAVEVENT
      //TOP160 KT ABCSI Purchase Order Fields 03312015

        IF NotOnlyDropShipment THEN
          IF "Document Type" IN ["Document Type"::Order,"Document Type"::"Return Order"] THEN
            WhsePurchRelease.Release(PurchaseHeader);


        // >> Shipping
        //IF PurchSetup."Enable E-Mail" AND NOT RunFromEship THEN //3073 code commented
          //EMailMgt.SendPurchaseConfirmation(PurchaseHeader,FALSE,FALSE); //3073 code commented
        // << Shipping

        OnAfterReleasePurchaseDoc(PurchaseHeader,PreviewMode,LinesWereModified);
        // UBP >>
      //  UBPEvents.Codeunit415_OnAfterReleasePurchaseDoc(PurchaseHeader);
        // UBP <<

      END;
    END;

    [External]
    PROCEDURE Reopen@1(VAR PurchHeader@1000 : Record 38);
    BEGIN
      OnBeforeReopenPurchaseDoc(PurchHeader);

      WITH PurchHeader DO BEGIN
        IF Status = Status::Open THEN
          EXIT;
        IF "Document Type" IN ["Document Type"::Order,"Document Type"::"Return Order"] THEN
          WhsePurchRelease.Reopen(PurchHeader);
        Status := Status::Open;

        MODIFY(TRUE);
      END;

      OnAfterReopenPurchaseDoc(PurchHeader);
    END;

    [External]
    PROCEDURE PerformManualRelease@2(VAR PurchHeader@1002 : Record 38);
    VAR
      PrepaymentMgt@1001 : Codeunit 441;
    BEGIN
      IF PrepaymentMgt.TestPurchasePrepayment(PurchHeader) THEN
        ERROR(UnpostedPrepaymentAmountsErr,PurchHeader."Document Type",PurchHeader."No.");

      PerformManualCheckAndRelease(PurchHeader);
    END;

    [External]
    PROCEDURE PerformManualCheckAndRelease@13(VAR PurchHeader@1002 : Record 38);
    VAR
      PrepaymentMgt@1001 : Codeunit 441;
      ApprovalsMgmt@1000 : Codeunit 1535;
    BEGIN
      WITH PurchHeader DO
        IF ("Document Type" = "Document Type"::Order) AND PrepaymentMgt.TestPurchasePayment(PurchHeader) THEN BEGIN
          IF Status <> Status::"Pending Prepayment" THEN BEGIN
            Status := Status::"Pending Prepayment";
            MODIFY;
            COMMIT;
          END;
          ERROR(STRSUBSTNO(Text005,"Document Type","No."));
        END;

      IF ApprovalsMgmt.IsPurchaseHeaderPendingApproval(PurchHeader) THEN
        ERROR(Text002);

      CODEUNIT.RUN(CODEUNIT::"Release Purchase Document",PurchHeader);
    END;

    [External]
    PROCEDURE PerformManualReopen@3(VAR PurchHeader@1002 : Record 38);
    BEGIN
      IF PurchHeader.Status = PurchHeader.Status::"Pending Approval" THEN
        ERROR(Text003);

      Reopen(PurchHeader);
    END;

    [External]
    PROCEDURE ReleasePurchaseHeader@8(VAR PurchHdr@1000 : Record 38;Preview@1001 : Boolean) LinesWereModified : Boolean;
    BEGIN
      PreviewMode := Preview;
      PurchaseHeader.COPY(PurchHdr);
      LinesWereModified := Code;
      PurchHdr := PurchaseHeader;
    END;

    PROCEDURE CalcAndUpdateVATOnLines@9(VAR PurchaseHeader@1003 : Record 38;VAR PurchLine@1002 : Record 39) LinesWereModified : Boolean;
    VAR
      TempVATAmountLine0@1001 : TEMPORARY Record 290;
      TempVATAmountLine1@1000 : TEMPORARY Record 290;
    BEGIN
      PurchLine.SetPurchHeader(PurchaseHeader);
      IF PurchaseHeader."Tax Area Code" = '' THEN BEGIN  // VAT
        PurchLine.CalcVATAmountLines(0,PurchaseHeader,PurchLine,TempVATAmountLine0);
        PurchLine.CalcVATAmountLines(1,PurchaseHeader,PurchLine,TempVATAmountLine1);
        LinesWereModified :=
          PurchLine.UpdateVATOnLines(0,PurchaseHeader,PurchLine,TempVATAmountLine0) OR
          PurchLine.UpdateVATOnLines(1,PurchaseHeader,PurchLine,TempVATAmountLine1);
      END ELSE BEGIN
        PurchLine.CalcSalesTaxLines(PurchaseHeader,PurchLine);
        LinesWereModified := TRUE;
      END;
    END;

    PROCEDURE SetSkipCheckReleaseRestrictions@17();
    BEGIN
      SkipCheckReleaseRestrictions := TRUE;
    END;

    [Integration]
    [External]
    LOCAL PROCEDURE OnBeforeReleasePurchaseDoc@5(VAR PurchaseHeader@1000 : Record 38;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    [External]
    LOCAL PROCEDURE OnAfterReleasePurchaseDoc@4(VAR PurchaseHeader@1000 : Record 38;PreviewMode@1001 : Boolean;LinesWereModified@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    [External]
    LOCAL PROCEDURE OnBeforeReopenPurchaseDoc@6(VAR PurchaseHeader@1000 : Record 38);
    BEGIN
    END;

    [Integration]
    [External]
    LOCAL PROCEDURE OnAfterReopenPurchaseDoc@7(VAR PurchaseHeader@1000 : Record 38);
    BEGIN
    END;

    BEGIN
    {
      TOP160 KT ABCSI Purchase Order Fields 03312015
        - Added code to Archive Purchase Documents on OnRun function
    }
    END.
  }
}

