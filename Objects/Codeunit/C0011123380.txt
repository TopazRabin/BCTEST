OBJECT Codeunit 11123380 SC - Upgrade Management
{
  OBJECT-PROPERTIES
  {
    Date=04/18/17;
    Time=[ 1:00:00 PM];
    Version List=SCW19.2.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            InvokeUpgrade;
          END;

  }
  CODE
  {
    VAR
      Window@11123302 : Dialog;
      Text11123304@11123305 : TextConst 'ENU=Sana Commerce - Updating ...\\';
      Text11123305@11123304 : TextConst 'ENU=Processing        #2####################\';
      Text11123306@11123303 : TextConst 'ENU=Progress          @3@@@@@@@@@@@@@@@@@@@@\';

    PROCEDURE InvokeUpgrade@11123309();
    BEGIN
      IF NOT UpgradeRequired THEN
        EXIT;

      RunStatusIndicator;
      SetDefaultOperations;
      UpgradeWebshopFilter;
      UpgradeSetup;
      UpgradeWebshop;
    END;

    PROCEDURE GetNewSanaVersion@11123305() : Text[20];
    BEGIN
      EXIT('SC 9.2.0');
    END;

    PROCEDURE GetOldSanaVersion@11123306() : Text[20];
    VAR
      Setup@11123302 : Record 11123305;
      Context@11123303 : Codeunit 11123305;
      ChangeMgt@11123305 : Codeunit 11123324;
      SanaVersion@11123304 : Text[20];
    BEGIN
      SanaVersion := Context.GetSanaVersion;

      IF SanaVersion = '' THEN
        SanaVersion := ChangeMgt.GetSanaVersion;

      EXIT(SanaVersion);
    END;

    PROCEDURE UpgradeRequired@11123307() : Boolean;
    BEGIN
      EXIT(GetNewSanaVersion <> GetOldSanaVersion);
    END;

    PROCEDURE UpgradeSetup@11123303();
    VAR
      Setup@11123302 : Record 11123305;
    BEGIN
      IF NOT Setup.GET THEN BEGIN
        Setup.INIT;
        Setup.INSERT;
      END;

      Setup."Obsolete Field 100" := 0;
      Setup.VALIDATE("Sana Version",GetNewSanaVersion);
      Setup.MODIFY(TRUE);
    END;

    PROCEDURE UpgradeWebshopFilter@11123302();
    VAR
      CatalogFilter@11123302 : Record 11123308;
      WebshopFilter@11123303 : Record 11123309;
      Item@11123304 : Record 27;
    BEGIN
      IF CatalogFilter.FINDSET THEN
        REPEAT
          WebshopFilter.INIT;
          WebshopFilter.Code := CatalogFilter.Code;
          WebshopFilter."Table No." := DATABASE::Item;
          WebshopFilter."Field No." := CatalogFilter."Item Field No.";
          WebshopFilter."Table Name" := Item.TABLENAME;
          WebshopFilter."Field Name" := CatalogFilter."Item Field Name";
          WebshopFilter.Filter := CatalogFilter.Filter;
          IF WebshopFilter.INSERT THEN;
        UNTIL CatalogFilter.NEXT = 0;

      CatalogFilter.DELETEALL;
    END;

    PROCEDURE SetDefaultOperations@11123304();
    VAR
      Operation@11123302 : Record 11123302;
    BEGIN
      Operation.SetDefaults;
    END;

    PROCEDURE RunStatusIndicator@11123308();
    BEGIN
      IF NOT GUIALLOWED THEN
        EXIT;

      Window.OPEN(Text11123304 + Text11123305 + Text11123306);
    END;

    PROCEDURE UpdateStatusIndicator@11123321(LineDiscription@11123302 : Text[30];LineCount@11123303 : Integer;LineIndicator@11123304 : Integer);
    BEGIN
      IF NOT GUIALLOWED THEN
        EXIT;

      Window.UPDATE(2,LineDiscription);
      Window.UPDATE(3,ROUND(LineIndicator / LineCount * 10000,1));
    END;

    PROCEDURE UpgradeWebshop@11123310();
    VAR
      Webshop@11123302 : Record 11123313;
    BEGIN
      Webshop.SETRANGE("Obsolete Field 23",TRUE);
      Webshop.MODIFYALL("Obsolete Field 23",FALSE);
    END;

    BEGIN
    END.
  }
}

