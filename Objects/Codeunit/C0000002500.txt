OBJECT Codeunit 2500 NavExtensionInstallationMgmt
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    Permissions=TableData 2000000153=rimd,
                TableData 2000000160=rimd;
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      InstalledExtensionTable@1000 : Record 2000000153;
      Installer@1001 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=11.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.Apps.NavAppALInstaller";
      InstallerHasBeenCreated@1002 : Boolean;
      InstalledTxt@1004 : TextConst 'ENU=Installed;ESM=Instalado;FRC=Install‚;ENC=Installed';
      NotInstalledTxt@1003 : TextConst 'ENU=Not Installed;ESM=No instalado;FRC=Non install‚.;ENC=Not Installed';
      FullVersionStringTxt@1007 : TextConst '@@@="%1=Version Major, %2=Version Minor, %3=Version build, %4=Version revision";ENU=%1.%2.%3.%4;ESM=%1.%2.%3.%4;FRC=%1.%2.%3.%4;ENC=%1.%2.%3.%4';
      NoRevisionVersionStringTxt@1006 : TextConst '@@@="%1=Version Major, %2=Version Minor, %3=Version build";ENU=%1.%2.%3;ESM=%1.%2.%3;FRC=%1.%2.%3;ENC=%1.%2.%3';
      NoBuildVersionStringTxt@1005 : TextConst '@@@="%1=Version Major, %2=Version Minor";ENU=%1.%2;ESM=%1.%2;FRC=%1.%2;ENC=%1.%2';
      NullGuidTok@1008 : TextConst '@@@={Locked};ENU=00000000-0000-0000-0000-000000000000;ESM=00000000-0000-0000-0000-000000000000;FRC=00000000-0000-0000-0000-000000000000;ENC=00000000-0000-0000-0000-000000000000';
      PermissionErr@1009 : TextConst 'ENU=You do not have the required permissions to install the selected app. Contact your Partner or system administrator to install the app or assign you permissions.;ESM=No tiene los permisos necesarios para instalar la aplicaci¢n seleccionada. P¢ngase en contacto con su socio o con el administrador del sistema para instalarla o para que le asignen permisos.;FRC=Vous n''avez pas les autorisations requises pour installer l''application s‚lectionn‚e. Contactez votre partenaire ou votre administrateur systŠme pour installer l''application ou vous attribuer les autorisations.;ENC=You do not have the required permissions to install the selected app. Contact your Partner or system administrator to install the app or assign you permissions.';

    [External]
    PROCEDURE IsInstalled@1(PackageID@1000 : GUID) : Boolean;
    BEGIN
      // Check if the user is entitled to make extension changes.
      IF (NOT InstalledExtensionTable.READPERMISSION) OR (NOT InstalledExtensionTable.WRITEPERMISSION) THEN
        ERROR(PermissionErr);

      InstalledExtensionTable.SETFILTER("Package ID",'%1',PackageID);
      EXIT(InstalledExtensionTable.FINDFIRST);
    END;

    [Internal]
    PROCEDURE InstallNavExtension@2(PackageID@1000 : GUID;Lcid@1001 : Integer);
    BEGIN
      AssertIsInitialized;
      Installer.ALInstallNavApp(PackageID,Lcid);
    END;

    [External]
    PROCEDURE GetExtensionInstalledDisplayString@9(PackageId@1000 : GUID) : Text[15];
    BEGIN
      IF IsInstalled(PackageId) THEN
        EXIT(InstalledTxt);

      EXIT(NotInstalledTxt);
    END;

    [Internal]
    PROCEDURE GetDependenciesForExtensionToInstall@3(PackageID@1000 : GUID) : Text;
    BEGIN
      AssertIsInitialized;
      EXIT(Installer.ALGetAppDependenciesToInstallString(PackageID));
    END;

    [Internal]
    PROCEDURE GetDependentForExtensionToUninstall@4(PackageID@1000 : GUID) : Text;
    BEGIN
      AssertIsInitialized;
      EXIT(Installer.ALGetDependentAppsToUninstallString(PackageID));
    END;

    LOCAL PROCEDURE AssertIsInitialized@6();
    BEGIN
      IF NOT InstallerHasBeenCreated THEN BEGIN
        Installer := Installer.NavAppALInstaller;
        InstallerHasBeenCreated := TRUE;
      END;
    END;

    [Internal]
    PROCEDURE UninstallNavExtension@7(PackageID@1000 : GUID);
    BEGIN
      AssertIsInitialized;
      Installer.ALUninstallNavApp(PackageID);
    END;

    [External]
    PROCEDURE GetVersionDisplayString@5(Major@1000 : Integer;Minor@1001 : Integer;Build@1002 : Integer;Revision@1003 : Integer) : Text;
    BEGIN
      IF Build <= -1 THEN
        EXIT(STRSUBSTNO(NoBuildVersionStringTxt,Major,Minor));

      IF Revision <= -1 THEN
        EXIT(STRSUBSTNO(NoRevisionVersionStringTxt,Major,Minor,Build));

      EXIT(STRSUBSTNO(FullVersionStringTxt,Major,Minor,Build,Revision));
    END;

    [External]
    PROCEDURE GetLatestVersionPackageId@8(AppId@1000 : GUID) : GUID;
    VAR
      NavAppTable@1001 : Record 2000000160;
      Result@1002 : GUID;
    BEGIN
      NavAppTable.SETFILTER(ID,'%1',AppId);
      NavAppTable.SETCURRENTKEY(Name,"Version Major","Version Minor","Version Build","Version Revision");
      NavAppTable.ASCENDING(FALSE);
      Result := NullGuidTok;
      IF NavAppTable.FINDFIRST THEN
        Result := NavAppTable."Package ID";

      EXIT(Result);
    END;

    [External]
    PROCEDURE IsInstalledNoPermissionCheck@10(ExtensionName@1000 : Text[250]) : Boolean;
    BEGIN
      InstalledExtensionTable.SETFILTER(Name,'%1',ExtensionName);
      EXIT(InstalledExtensionTable.FINDFIRST);
    END;

    PROCEDURE UnpublishNavTenantExtension@11(PackageID@1000 : GUID);
    BEGIN
      AssertIsInitialized;
      Installer.ALUnpublishNavTenantApp(PackageID);
    END;

    BEGIN
    END.
  }
}

