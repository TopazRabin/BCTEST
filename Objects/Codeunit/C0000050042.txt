OBJECT Codeunit 50042 Delete SRO
{
  OBJECT-PROPERTIES
  {
    Date=07/01/20;
    Time=12:00:00 AM;
    Modified=Yes;
    Version List=TPZ000.00.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            //<TPZ1931>
            DeleteSRO(90);
            //</TPZ1931>
          END;

  }
  CODE
  {

    PROCEDURE DeleteSRO@1000000001(DaysRequired@1000000003 : Integer);
    VAR
      SalesHeader@1000000000 : Record 36;
      SalesLine@1000000001 : Record 37;
      CheckDelete@1000000002 : Boolean;
      CheckDelete1@1000000004 : Boolean;
    BEGIN
      //<TPZ1931>
      SalesHeader.RESET;
      SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::"Return Order");
      SalesHeader.SETFILTER("Document Date",'%1..%2',0D,WORKDATE-DaysRequired);
      IF SalesHeader.FINDSET THEN BEGIN
        CheckDelete := TRUE;
        CheckDelete1 := TRUE;
        REPEAT
          SalesLine.RESET;
          SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
          SalesLine.SETRANGE("Document No.",SalesHeader."No.");
          SalesLine.SETFILTER(SalesLine."No.",'<>%1','');
          SalesLine.SETFILTER(SalesLine.Quantity,'<>%1',0);
          IF SalesLine.FINDSET THEN BEGIN
            REPEAT
              IF SalesLine."Return Qty. Received" = SalesLine."Quantity Invoiced" THEN
                CheckDelete1 := TRUE
              ELSE
                CheckDelete1 := FALSE;

              CheckDelete := WhseLinesExist(DATABASE::"Sales Line", SalesLine."Document Type",
                   SalesLine."Document No.",SalesLine."Line No.",0, SalesLine.Quantity)

            UNTIL ((SalesLine.NEXT = 0) OR(CheckDelete = TRUE) OR (CheckDelete1 = TRUE));
          END;
          IF ((CheckDelete) AND (CheckDelete1)) THEN BEGIN
            SalesHeader.SkipReceive(TRUE);
            SalesHeader.DELETE(TRUE);
          END;
        UNTIL SalesHeader.NEXT = 0;
      END;
      //</TPZ1931>
    END;

    PROCEDURE WhseLinesExist@6(SourceType@1000 : Integer;SourceSubType@1001 : Option;SourceNo@1002 : Code[20];SourceLineNo@1003 : Integer;SourceSublineNo@1004 : Integer;SourceQty@1007 : Decimal) : Boolean;
    VAR
      WhseRcptLine@1006 : Record 7317;
      WhseShptLine@1005 : Record 7321;
      WhseActivLine@1000000000 : Record 5767;
    BEGIN
      //<TPZ1931>
      IF ((SourceType = DATABASE::"Sales Line") AND (SourceSubType = 5) AND (SourceQty >= 0))
      THEN BEGIN
        WhseRcptLine.SETCURRENTKEY(
          "Source Type","Source Subtype","Source No.","Source Line No.");
        WhseRcptLine.SETRANGE("Source Type",SourceType);
        WhseRcptLine.SETRANGE("Source Subtype",SourceSubType);
        WhseRcptLine.SETRANGE("Source No.",SourceNo);
        WhseRcptLine.SETRANGE("Source Line No.",SourceLineNo);
        IF NOT WhseRcptLine.ISEMPTY THEN BEGIN
          EXIT(FALSE);
        END;
      END;

      IF ((SourceType = DATABASE::"Sales Line") AND (SourceSubType = 5) AND (SourceQty < 0))
      THEN BEGIN
        WhseShptLine.SETCURRENTKEY(
          "Source Type","Source Subtype","Source No.","Source Line No.");
        WhseShptLine.SETRANGE("Source Type",SourceType);
        WhseShptLine.SETRANGE("Source Subtype",SourceSubType);
        WhseShptLine.SETRANGE("Source No.",SourceNo);
        WhseShptLine.SETRANGE("Source Line No.",SourceLineNo);
        IF NOT WhseShptLine.ISEMPTY THEN BEGIN
          EXIT(FALSE);
        END;
      END;

      WhseActivLine.SETCURRENTKEY(
        "Source Type","Source Subtype","Source No.","Source Line No.","Source Subline No.");
      WhseActivLine.SETRANGE("Source Type",SourceType);
      WhseActivLine.SETRANGE("Source Subtype",SourceSubType);
      WhseActivLine.SETRANGE("Source No.",SourceNo);
      WhseActivLine.SETRANGE("Source Line No.",SourceLineNo);
      WhseActivLine.SETRANGE("Source Subline No.",SourceSublineNo);
      IF NOT WhseActivLine.ISEMPTY THEN BEGIN
        EXIT(FALSE);
      END;

      EXIT(TRUE);
      //</TPZ1931>
    END;

    BEGIN
    {
      2017-08-23 TPZ1931 DKUMAR
        Batch job to delete open and expired SRO's
    }
    END.
  }
}

