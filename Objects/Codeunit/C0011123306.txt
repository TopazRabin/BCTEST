OBJECT Codeunit 11123306 SC - Request Dispatcher
{
  OBJECT-PROPERTIES
  {
    Date=04/18/17;
    Time=[ 1:00:00 PM];
    Version List=SCW19.2.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            Dispatch;
          END;

  }
  CODE
  {
    VAR
      OperationRecGlobal@11123303 : Record 11123302;
      Context@11123302 : Codeunit 11123305;
      Text11123302@11123304 : TextConst 'ENU=Codeunit %1 does not exist.';

    PROCEDURE Dispatch@11123302();
    VAR
      UpgradeMgt@11123303 : Codeunit 11123380;
      OperationName@11123302 : Text[50];
    BEGIN
      OperationName := Context.GetCurrentOperationName;
      UpgradeMgt.RUN;
      SetOperationDefaults;
      GetAndCheckOperation(OperationName,TRUE);
      OperationRecGlobal.Invoke;
    END;

    PROCEDURE DispatchInternal@11123303(OperationName@11123302 : Code[80];VAR Params@11123303 : Record 11123310);
    BEGIN
      IF GetAndCheckOperation(OperationName,FALSE) THEN BEGIN
        Context.SetParams(Params);
        OperationRecGlobal.Invoke;
        Context.GetParams(Params);
      END;
    END;

    PROCEDURE GetAndCheckOperation@11123306(OperationName@11123302 : Code[80];CauseError@11123303 : Boolean) : Boolean;
    VAR
      WebsiteID@11123305 : Code[50];
      Valid@11123304 : Boolean;
    BEGIN
      WebsiteID := Context.GetCurrentWebsiteID;

      IF CauseError THEN BEGIN
        IF NOT OperationRecGlobal.GET(OperationName,WebsiteID) THEN
          OperationRecGlobal.GET(OperationName,'');
        OperationRecGlobal.TESTFIELD(Enabled,TRUE);
        IF NOT CodeunitExists(OperationRecGlobal."Codeunit ID") THEN
          ERROR(Text11123302,OperationRecGlobal."Codeunit ID");
        Valid := TRUE;
      END ELSE BEGIN
        Valid := TRUE;
        IF OperationName <> OperationRecGlobal."Operation Name" THEN BEGIN
          Valid := OperationRecGlobal.GET(OperationName,WebsiteID);
          IF NOT Valid THEN
            Valid := OperationRecGlobal.GET(OperationName,'');
          Valid := Valid AND OperationRecGlobal.Enabled;
          IF Valid THEN
            Valid := CodeunitExists(OperationRecGlobal."Codeunit ID");
        END;
      END;
      EXIT(Valid);
    END;

    LOCAL PROCEDURE CodeunitExists@11123304(CodeunitID@11123302 : Integer) : Boolean;
    VAR
      AllObjWithCaption@11123303 : Record 2000000058;
    BEGIN
      AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::Codeunit);
      AllObjWithCaption.SETRANGE("Object ID",CodeunitID);
      EXIT(NOT AllObjWithCaption.ISEMPTY);
    END;

    PROCEDURE SetOperationDefaults@11123305();
    VAR
      CountAll@11123302 : Integer;
      CountAllEnabled@11123303 : Integer;
      SanaCommerceSetup@11123304 : Record 11123305;
    BEGIN
      IF OperationRecGlobal.ISEMPTY THEN BEGIN
        OperationRecGlobal.SetDefaults;
        COMMIT;
      END ELSE
        IF NOT Context.GetOperationUpgraded THEN BEGIN
          OperationRecGlobal.RESET;
          CountAll := OperationRecGlobal.COUNT;
          OperationRecGlobal.SETRANGE(Enabled,FALSE);
          CountAllEnabled := OperationRecGlobal.COUNT;
          IF CountAllEnabled = CountAll THEN BEGIN
            OperationRecGlobal.MODIFYALL(Enabled,TRUE,FALSE);
            SanaCommerceSetup.GET;
            SanaCommerceSetup.Upgraded := TRUE;
            SanaCommerceSetup.MODIFY;
            COMMIT;
          END;
        END;
    END;

    BEGIN
    END.
  }
}

