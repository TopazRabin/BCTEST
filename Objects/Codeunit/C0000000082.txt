OBJECT Codeunit 82 Sales-Post + Print
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    TableNo=36;
    OnRun=VAR
            SalesHeader@1000 : Record 36;
          BEGIN
            SalesHeader.COPY(Rec);
            Code(SalesHeader);
            Rec := SalesHeader;
          END;

  }
  CODE
  {
    VAR
      ShipInvoiceQst@1000 : TextConst 'ENU=&Ship,&Invoice,Ship &and Invoice;ESM=&Enviar,&Facturar,E&nviar y facturar;FRC=&Livrer,&Facturer,Livrer &et Facturer;ENC=&Ship,&Invoice,Ship &and Invoice';
      PostAndPrintQst@1001 : TextConst '@@@="%1 = Document Type";ENU=Do you want to post and print the %1?;ESM=¨Confirma que desea registrar e imprimir el/la %1?;FRC=Voulez-vous reporter et imprimer la %1?;ENC=Do you want to post and print the %1?';
      PostAndEmailQst@1016 : TextConst '@@@="%1 = Document Type";ENU=Do you want to post and email the %1?;ESM=¨Confirma que desea registrar y enviar por correo electr¢nico el/la %1?;FRC=Souhaitez-vous valider %1 et l''envoyer par e-mailÿ?;ENC=Do you want to post and email the %1?';
      ReceiveInvoiceQst@1002 : TextConst 'ENU=&Receive,&Invoice,Receive &and Invoice;ESM=&Recibir,&Facturar,R&ecibir y facturar;FRC=&R‚ception,&Facture,R‚ception &et Facture;ENC=&Receive,&Invoice,Receive &and Invoice';
      SendReportAsEmail@1009 : Boolean;
      SalesShptHeader@1000000004 : Record 110;
      SalesInvHeader@1000000003 : Record 112;
      SalesCrMemoHeader@1000000002 : Record 114;
      ReturnRcptHeader@1000000001 : Record 6660;
      ReportSelection@1000000000 : Record 77;
      SalesInvHeaderDef@1000000005 : RecordRef;

    [Internal]
    PROCEDURE PostAndEmail@10(VAR ParmSalesHeader@1000 : Record 36);
    VAR
      SalesHeader@1001 : Record 36;
    BEGIN
      SendReportAsEmail := TRUE;
      SalesHeader.COPY(ParmSalesHeader);
      Code(SalesHeader);
      ParmSalesHeader := SalesHeader;
    END;

    LOCAL PROCEDURE Code@2(VAR SalesHeader@1002 : Record 36);
    VAR
      SalesSetup@1001 : Record 311;
      SalesPostViaJobQueue@1000 : Codeunit 88;
      HideDialog@1003 : Boolean;
    BEGIN
      HideDialog := FALSE;

      OnBeforeConfirmPost(SalesHeader,HideDialog);
      IF NOT HideDialog THEN
        IF NOT ConfirmPost(SalesHeader) THEN
          EXIT;

      SalesSetup.GET;
      IF SalesSetup."Post & Print with Job Queue" AND NOT SendReportAsEmail THEN
        SalesPostViaJobQueue.EnqueueSalesDoc(SalesHeader)
      ELSE BEGIN
        CODEUNIT.RUN(CODEUNIT::"Sales-Post",SalesHeader);
        GetReport(SalesHeader);
      END;

      OnAfterPost(SalesHeader);
      COMMIT;
    END;

    [External]
    PROCEDURE GetReport@3(VAR SalesHeader@1000 : Record 36);
    BEGIN
      WITH SalesHeader DO
        CASE "Document Type" OF
          "Document Type"::Order:
            BEGIN
              IF Ship THEN
                PrintShip(SalesHeader);
              IF Invoice THEN
                PrintInvoice(SalesHeader);
            END;
          "Document Type"::Invoice:
            PrintInvoice(SalesHeader);
          "Document Type"::"Return Order":
            BEGIN
              IF Receive THEN
                PrintReceive(SalesHeader);
              IF Invoice THEN
                PrintCrMemo(SalesHeader);
            END;
          "Document Type"::"Credit Memo":
            PrintCrMemo(SalesHeader);
        END;
    END;

    LOCAL PROCEDURE ConfirmPost@9(VAR SalesHeader@1000 : Record 36) : Boolean;
    VAR
      Selection@1001 : Integer;
    BEGIN
      WITH SalesHeader DO BEGIN
        CASE "Document Type" OF
          "Document Type"::Order:
            BEGIN
              Selection := STRMENU(ShipInvoiceQst,3);
              IF Selection = 0 THEN
                EXIT(FALSE);
              Ship := Selection IN [1,3];
              Invoice := Selection IN [2,3];
            END;
          "Document Type"::"Return Order":
            BEGIN
              Selection := STRMENU(ReceiveInvoiceQst,3);
              IF Selection = 0 THEN
                EXIT(FALSE);
              Receive := Selection IN [1,3];
              Invoice := Selection IN [2,3];
            END
          ELSE
            IF NOT CONFIRM(ConfirmationMessage,FALSE,"Document Type") THEN
              EXIT(FALSE);
        END;
        "Print Posted Documents" := TRUE;
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ConfirmationMessage@4() : Text;
    BEGIN
      IF SendReportAsEmail THEN
        EXIT(PostAndEmailQst);
      EXIT(PostAndPrintQst);
    END;

    LOCAL PROCEDURE PrintReceive@1(SalesHeader@1001 : Record 36);
    VAR
      ReturnRcptHeader@1000 : Record 6660;
    BEGIN
      ReturnRcptHeader."No." := SalesHeader."Last Return Receipt No.";
      IF ReturnRcptHeader.FIND THEN;
      ReturnRcptHeader.SETRECFILTER;

      IF SendReportAsEmail THEN
        ReturnRcptHeader.EmailRecords(TRUE)
      ELSE
        ReturnRcptHeader.PrintRecords(FALSE);
    END;

    LOCAL PROCEDURE PrintInvoice@5(SalesHeader@1000 : Record 36);
    VAR
      SalesInvHeader@1003 : Record 112;
    BEGIN
      IF SalesHeader."Last Posting No." = '' THEN
        SalesInvHeader."No." := SalesHeader."No."
      ELSE
        SalesInvHeader."No." := SalesHeader."Last Posting No.";
      SalesInvHeader.FIND;
      SalesInvHeader.SETRECFILTER;

      IF SendReportAsEmail THEN
        SalesInvHeader.EmailRecords(TRUE)
      ELSE
        SalesInvHeader.PrintRecords(FALSE);
    END;

    LOCAL PROCEDURE PrintShip@7(SalesHeader@1000 : Record 36);
    VAR
      SalesShptHeader@1001 : Record 110;
    BEGIN
      SalesShptHeader."No." := SalesHeader."Last Shipping No.";
      IF SalesShptHeader.FIND THEN;
      SalesShptHeader.SETRECFILTER;

      IF SendReportAsEmail THEN
        SalesShptHeader.EmailRecords(TRUE)
      ELSE
        SalesShptHeader.PrintRecords(FALSE);
    END;

    LOCAL PROCEDURE PrintCrMemo@8(SalesHeader@1000 : Record 36);
    VAR
      SalesCrMemoHeader@1003 : Record 114;
    BEGIN
      IF SalesHeader."Last Posting No." = '' THEN
        SalesCrMemoHeader."No." := SalesHeader."No."
      ELSE
        SalesCrMemoHeader."No." := SalesHeader."Last Posting No.";
      SalesCrMemoHeader.FIND;
      SalesCrMemoHeader.SETRECFILTER;

      IF SendReportAsEmail THEN
        SalesCrMemoHeader.EmailRecords(TRUE)
      ELSE
        SalesCrMemoHeader.PrintRecords(FALSE);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPost@11(VAR SalesHeader@1000 : Record 36);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeConfirmPost@6(VAR SalesHeader@1000 : Record 36;VAR HideDialog@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    {
      2015-07-21 TPZ162 TAKHMETO
        GetInvoiceReport function has been added
      2018-01-19 TPZ2181 EBAGIM
        PrintReportCustom function has been added to replace the PrintReport function
    }
    END.
  }
}

