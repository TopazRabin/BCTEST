OBJECT Codeunit 14123509 ES NAS Processing
{
  OBJECT-PROPERTIES
  {
    Date=06/20/17;
    Time=12:00:00 PM;
    Version List=ES1.41.36;
  }
  PROPERTIES
  {
    TableNo=14123541;
    OnRun=BEGIN
            CASE Type OF
              Type::"Get New Data and Publish":
                SyncAndPublish(TRUE,TRUE);
              Type::"Get New Data":
                SyncAndPublish(TRUE,FALSE);
              Type::Publish:
                SyncAndPublish(FALSE,TRUE);
              Type::"Live Restore Point":
                CreateRestorePoint(TRUE,FALSE);
              Type::"Easy Security Restore Point":
                CreateRestorePoint(FALSE,TRUE);
              Type::"Update FLADS Lookup Data":
                UpdateFLADSLookupData;
              Type::"Copy FLADS Setup Data":
                CopyFLADSSetupData;
              Type::"Copy Windows Groups":
                UpdateWindowsGroups;
              ELSE
                ERROR(Text001,STRSUBSTNO('%1=%2',FIELDCAPTION(Type),Type));
            END;
          END;

  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Wrong Data %1.';

    PROCEDURE SyncAndPublish@1240520008(Synchronize@1240520009 : Boolean;Publish@1240520010 : Boolean);
    VAR
      ESMgt@1240520008 : Codeunit 14123501;
    BEGIN
      IF Synchronize THEN BEGIN
        CLEAR(ESMgt);
        ESMgt.SynchronizeLiveToEasySecurity(STRSUBSTNO('NAS %1',CURRENTDATETIME),TRUE,TRUE,TRUE,TRUE);
      END;

      IF Publish THEN BEGIN
        CLEAR(ESMgt);
        ESMgt.PublishSecurity(
          STRSUBSTNO('NAS %1',CURRENTDATETIME),TRUE,TRUE,TRUE,TRUE,TRUE,TRUE,TRUE,TRUE);
      END;
    END;

    PROCEDURE CreateRestorePoint@1240520009(Live@1240520008 : Boolean;EasySecurity@1240520009 : Boolean);
    VAR
      ESRestorePoint@1240520010 : Record 14123523;
    BEGIN
      IF Live THEN BEGIN
        CLEAR(ESRestorePoint);
        ESRestorePoint.Description := STRSUBSTNO('NAS %1',CURRENTDATETIME);
        ESRestorePoint.INSERT(TRUE);
        ESRestorePoint.CreateRestPntFromLive(TRUE);
      END;

      IF EasySecurity THEN BEGIN
        ESRestorePoint.Description := STRSUBSTNO('NAS %1',CURRENTDATETIME);
        CLEAR(ESRestorePoint);
        ESRestorePoint.INSERT(TRUE);
        ESRestorePoint.CreateRestPntFromES(TRUE);
      END;
    END;

    PROCEDURE UpdateFLADSLookupData@1240520001();
    VAR
      Company@1240520002 : Record 2000000006;
      Object@1240520003 : Record 2000000001;
      ESFLADSSetup@1240520001 : Record 14123801;
      ESFLADSLookupData@1240520004 : Codeunit 14123813;
    BEGIN
      Company.RESET;
      IF Company.FIND('-') THEN BEGIN
        Object.GET(Object.Type::Codeunit,'',CODEUNIT::"ES FLADS Lookup Data");

        REPEAT
          ESFLADSSetup.RESET;
          ESFLADSSetup.CHANGECOMPANY(Company.Name);
          IF ESFLADSSetup.GET THEN BEGIN
            IF NOT ESFLADSSetup."Install Allowed" THEN BEGIN
              ESFLADSSetup."Lookup Data Date" := Object.Date;
              ESFLADSSetup."Lookup Data Time" := Object.Time;
              ESFLADSSetup.MODIFY;
            END;

            ESFLADSLookupData.InsertLookupData(Company.Name);
          END;
        UNTIL Company.NEXT = 0;
      END;
    END;

    PROCEDURE CopyFLADSSetupData@1240520002();
    VAR
      ESFLADSCompanyMgt@1240520001 : Codeunit 14123804;
    BEGIN
      ESFLADSCompanyMgt.UpdateAllCompanies(TRUE,TRUE);
    END;

    PROCEDURE UpdateWindowsGroups@1240520003();
    VAR
      ESFLADSGroupMember@1240520002 : Record 14123817;
    BEGIN
      ESFLADSGroupMember.CopyFromESWindowsGroupMember(TRUE,TRUE);
    END;

    BEGIN
    END.
  }
}

