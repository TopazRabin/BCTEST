OBJECT Codeunit 14002402 Routing Request Management
{
  OBJECT-PROPERTIES
  {
    Date=03/06/15;
    Time=12:00:00 PM;
    Version List=SE0.60.06;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      EDIRoutingRequestSend@1240030000 : Codeunit 14002403;
      Text001@1001 : TextConst 'ENU=Document was sent on %1.  Do you wish to send again?';
      Text002@1002 : TextConst 'ENU=Routing Request Sent.';
      Text003@1240020000 : TextConst 'ENU=Resend of Routing Request %1 cancelled.';

    PROCEDURE ReleaseRoutingReq@1(VAR CurrentRoutingReq@1240030000 : Record 14002402);
    BEGIN
      WITH CurrentRoutingReq DO BEGIN
        TESTFIELD(Released,FALSE);
        TESTFIELD("Trade Partner No.");
        TESTFIELD("Document Date");
        TESTFIELD("Ready To Ship Date");
        TESTFIELD("Ready To Ship Time");
        Released := TRUE;
        "Pallet Count" := GetPalletCount(CurrentRoutingReq);
        MODIFY;
      END;
    END;

    PROCEDURE ReopenRoutingReq@2(VAR CurrentRoutingReq@1240030000 : Record 14002402);
    BEGIN
      WITH CurrentRoutingReq DO BEGIN
        TESTFIELD(Released,TRUE);
        Released := FALSE;
        MODIFY;
      END;
    END;

    PROCEDURE SendRoutingReq@3(CurrentRoutingReq@1240030000 : Record 14002402);
    BEGIN
      WITH CurrentRoutingReq DO BEGIN
        TESTFIELD(Released);
        IF "EDI Request Generated" THEN
          IF GUIALLOWED THEN BEGIN
            IF NOT CONFIRM(
                   Text001,
                    FALSE,"EDI Req. Gen. Date")
            THEN
              EXIT;
           END ELSE
             ERROR(Text003,"No.");;
        CLEAR(EDIRoutingRequestSend);
        EDIRoutingRequestSend.RUN(CurrentRoutingReq);
        MESSAGE(Text002);
      END;
    END;

    PROCEDURE GetPalletCount@1240020000(RoutingReqHdr@1240020000 : Record 14002402) PalletCount : Integer;
    VAR
      RoutingReqLine@1240020001 : Record 14002403;
      SalesHeader@1240020002 : Record 36;
      Package@1240020003 : Record 14000701;
      PackageLine@1240020004 : Record 14000702;
    BEGIN
      RoutingReqLine.RESET;
      RoutingReqLine.SETRANGE("Routing Request No.",RoutingReqHdr."No.");
      IF RoutingReqLine.FIND('-') THEN
        REPEAT
          IF SalesHeader.GET(
            SalesHeader."Document Type"::Order,RoutingReqLine."Document No.")
          THEN BEGIN
            Package.RESET;
            Package.SETCURRENTKEY(
              "Source Type","Source Subtype","Source ID","Location Code");
            Package.SETRANGE("Source Type",DATABASE::"Sales Header");
            Package.SETRANGE("Source Subtype",1);
            Package.SETRANGE("Source ID",SalesHeader."No.");
            IF Package.FIND('-') THEN
              REPEAT
                PackageLine.RESET;
                PackageLine.SETRANGE("Package No.",Package."No.");
                PackageLine.SETRANGE(Type,PackageLine.Type::Package);
                IF PackageLine.FIND('-') THEN
                  PalletCount := PalletCount + 1;
              UNTIL Package.NEXT = 0;
          END;
        UNTIL RoutingReqLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

