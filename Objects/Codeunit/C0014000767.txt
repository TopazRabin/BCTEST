OBJECT Codeunit 14000767 UPS Management
{
  OBJECT-PROPERTIES
  {
    Date=06/28/17;
    Time=12:00:00 PM;
    Version List=SE0.60.13.02;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1240020000 : TextConst 'ENU=Dimensions cannot be zero.';
      Text002@1240020001 : TextConst 'ENU=The %1 characters cannot be included in this field.';
      Text003@1240020002 : TextConst 'ENU=%1 formatted is %2 with %3 decimals.';
      Text004@1240020003 : TextConst 'ENU=Country Code, %1,  cannot be found.';
      Text006@1240020005 : TextConst 'ENU=Shipping Agent Services have been added/updated.';
      Text007@1240020004 : TextConst 'ENU=Valid %1 must be 6 characters.';

    PROCEDURE AddElement@1240030000(VAR XMLNode@1240030000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";NodeName@1240030001 : Text[250];NodeText@1240030002 : Text[250];NameSpace@1240030003 : Text[250];VAR CreatedXMLNode@1240030004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode") ExitStatus : Integer;
    VAR
      NewChildNode@1240030005 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      NewChildNode := XMLNode.OwnerDocument.CreateNode('element', NodeName, NameSpace);

      IF ISNULL(NewChildNode) THEN BEGIN
        ExitStatus := 50;
        EXIT;
      END;

      IF NodeText <> '' THEN
        NewChildNode.InnerText := NodeText;

      XMLNode.AppendChild(NewChildNode);
      CreatedXMLNode := NewChildNode;

      ExitStatus := 0;
    END;

    PROCEDURE AddAttribute@1240030003(VAR XMLNode@1240030000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";Name@1240030001 : Text[250];NodeValue@1240030002 : Text[250];NormalCaseMode@1240030003 : Boolean) ExitStatus : Integer;
    VAR
      XMLNewAttributeNode@1240030004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      IF NOT NormalCaseMode THEN
        Name := UPPERCASE(Name);
      XMLNewAttributeNode := XMLNode.OwnerDocument.CreateAttribute(Name);

      IF ISNULL(XMLNewAttributeNode) THEN BEGIN
        ExitStatus := 60;
        EXIT(ExitStatus)
      END;

      IF NodeValue <> '' THEN
        XMLNewAttributeNode.Value := NodeValue;

      XMLNode.Attributes.SetNamedItem(XMLNewAttributeNode);
    END;

    PROCEDURE FindNode@1240030004(XMLRootNode@1240030000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";NodePath@1240030001 : Text[250];VAR FoundXMLNode@1240030002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";XMLNsMgr@1240020000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNamespaceManager") : Boolean;
    BEGIN
      IF ISNULL(XMLRootNode) THEN
        EXIT(FALSE);

      FoundXMLNode := XMLRootNode.SelectSingleNode(NodePath,XMLNsMgr);

      IF ISNULL(FoundXMLNode) THEN
        EXIT(FALSE)
      ELSE
        EXIT(TRUE);
    END;

    PROCEDURE FindNodeText@1240030005(XMLRootNode@1240030000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";NodePath@1240030001 : Text[250]) : Text[250];
    VAR
      FoundXMLNode@1240030002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      IF ISNULL(XMLRootNode) THEN
        EXIT('');

      FoundXMLNode := XMLRootNode.SelectSingleNode(NodePath);

      IF ISNULL(FoundXMLNode) THEN
        EXIT('')
      ELSE
        EXIT(FoundXMLNode.InnerText);
    END;

    PROCEDURE FindNodes@1240030006(XMLRootNode@1240030000 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";NodePath@1240030001 : Text[250];VAR ReturnedXMLNodeList@1240030002 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF82-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNodeList") : Boolean;
    BEGIN
      ReturnedXMLNodeList := XMLRootNode.selectNodes(NodePath);

      IF ISCLEAR(ReturnedXMLNodeList) THEN
        EXIT(FALSE)
      ELSE
        EXIT(TRUE);
    END;

    PROCEDURE FormatCleanNameAddress@1240020016(OldValue@1240030001 : Text[250];Length@1240030005 : Integer;SetToUppercase@1240030011 : Boolean) NewValue@1240030000 : Text[250];
    VAR
      SingleQuote@1240030010 : Char;
    BEGIN
      SingleQuote := 39;

      IF SetToUppercase THEN
        NewValue := UPPERCASE(OldValue)
      ELSE
        NewValue := OldValue;

      NewValue := DELCHR(NewValue,'=',ReturnCleanNameAddressString);
      NewValue := CONVERTSTR(NewValue,'-',' ');
      NewValue := CONVERTSTR(NewValue,FORMAT(SingleQuote),' ');
      NewValue := CONVERTSTR(NewValue,'\',' ');
      NewValue := COPYSTR(NewValue,1,Length);

      EXIT(StripInvalidChar(NewValue));
    END;

    PROCEDURE FormatCleanString@1240020015(OldValue@1240030001 : Text[250]) NewValue@1240030000 : Text[250];
    BEGIN
      NewValue := DELCHR(UPPERCASE(OldValue),'=',ReturnCleanString);

      EXIT(StripInvalidChar(NewValue));
    END;

    PROCEDURE FormatDateYYYYMMDD@1240030002(DatetoFormat@1240030001 : Date) DateFormatted : Text[10];
    BEGIN
      DateFormatted := FORMAT(DatetoFormat,0,'<Year4><Month,2><Day,2>')
    END;

    PROCEDURE FormatEndorsement@1240020009(UPSOptionPage@1240020000 : Record 14000761) ReturnValue : Text[1];
    BEGIN
      CASE UPSOptionPage."USPS Endorsement" OF
        UPSOptionPage."USPS Endorsement"::"Address Service":
          ReturnValue := '3';
        UPSOptionPage."USPS Endorsement"::"Carrier-Leave if No Response":
          ReturnValue := '';
        UPSOptionPage."USPS Endorsement"::"Change Service":
          ReturnValue := '4';
        UPSOptionPage."USPS Endorsement"::"Forwarding Service":
          ReturnValue := '2';
        UPSOptionPage."USPS Endorsement"::"Return Service":
          ReturnValue := '1';
      ELSE
        ReturnValue := '';
      END;
    END;

    PROCEDURE FormatImageFormat@1240030031(PrinterType@1240030000 : ' ,Eltron Orion,Zebra') NewValue : Text[30];
    BEGIN
      CASE PrinterType OF
        PrinterType::"Eltron Orion":
          NewValue := 'EPL';
        PrinterType::Zebra:
          NewValue := 'ZPL';
        ELSE
          ERROR('error');
      END;

      EXIT(NewValue);
    END;

    PROCEDURE FormatFormFormat@1240020014(FormType@1240030000 : 'PDF,Label';CarrierPackingStation@1240020000 : Record 14000729) NewValue : Text[30];
    BEGIN
      CASE FormType OF
        FormType::PDF:
          NewValue := 'pdf';
        FormType::Label:BEGIN
          CASE CarrierPackingStation."UPS Label Printer Type" OF
            CarrierPackingStation."UPS Label Printer Type"::"Eltron Orion":
              NewValue := 'epl2';
            CarrierPackingStation."UPS Label Printer Type"::Zebra:
              NewValue := 'zpl';
          END;
        END ELSE
          ERROR('error');
      END;

      EXIT(NewValue);
    END;

    PROCEDURE FormatSubclassification@1240020017(UPSOptionPage@1240020000 : Record 14000761) ReturnValue : Text[2];
    BEGIN
      CASE UPSOptionPage."USPS SubClassification" OF
        UPSOptionPage."USPS SubClassification"::Machineable:
          ReturnValue := 'MA';
        UPSOptionPage."USPS SubClassification"::Irregular:
          ReturnValue := 'IR';
      ELSE
        ReturnValue := '';
      END;
    END;

    PROCEDURE FormatCN22Type@1240020018(UPSOptionPage@1240020000 : Record 14000761) ReturnValue : Text[2];
    BEGIN
      CASE UPSOptionPage."CN22 Contents Type" OF
        UPSOptionPage."CN22 Contents Type"::Gifts:
          ReturnValue := '1';
        UPSOptionPage."CN22 Contents Type"::Documents:
          ReturnValue := '2';
        UPSOptionPage."CN22 Contents Type"::"Commercial Sample":
          ReturnValue := '3';
        UPSOptionPage."CN22 Contents Type"::Other:
          ReturnValue := '4';
      ELSE
        ReturnValue := '';
      END;
    END;

    PROCEDURE FormatPackagingType@1240020010(UPSOptionPage@1240020000 : Record 14000761) ReturnValue : Text[2];
    BEGIN
      CASE UPSOptionPage."UPS Packaging Type" OF
        UPSOptionPage."UPS Packaging Type"::"UPS Letter":
          ReturnValue := '01';
        UPSOptionPage."UPS Packaging Type"::Package:
          ReturnValue := '02';
        UPSOptionPage."UPS Packaging Type"::"UPS Express Tube":
          ReturnValue := '03';
        UPSOptionPage."UPS Packaging Type"::"UPS Express Pak":
          ReturnValue := '04';
        UPSOptionPage."UPS Packaging Type"::"UPS 25kg Box":
          ReturnValue := '24';
        UPSOptionPage."UPS Packaging Type"::"UPS 10kg Box":
          ReturnValue := '25';
        UPSOptionPage."UPS Packaging Type"::Palletized:
          ReturnValue := '30';
        UPSOptionPage."UPS Packaging Type"::"UPS Express Box Small":
          ReturnValue := '2a';
        UPSOptionPage."UPS Packaging Type"::"UPS Express Box Medium":
          ReturnValue := '2b';
        UPSOptionPage."UPS Packaging Type"::"UPS Express Box Large":
          ReturnValue := '2c';
        ELSE
          ReturnValue := '00';
      END;
    END;

    PROCEDURE ConvertBooleanToTrueFalse@1240030011(OldValue@1240030000 : Boolean) NewValue : Text[30];
    BEGIN
      IF OldValue THEN
        NewValue := 'TRUE'
      ELSE
        NewValue := 'FALSE';

      EXIT(NewValue);
    END;

    PROCEDURE ConvertBooleanToYN@1240030051(OldValue@1240030000 : Boolean) NewValue : Text[30];
    BEGIN
      IF OldValue THEN
        NewValue := 'Y'
      ELSE
        NewValue := 'N';

      EXIT(NewValue);
    END;

    PROCEDURE ConvertDecimalToText@1240030009(OldValue@1240030000 : Decimal;UsePrecision@1240030001 : Decimal;UseDecimalPlaces@1240030003 : Code[10]) NewValue : Text[30];
    VAR
      NewDec@1240030002 : Decimal;
    BEGIN
      NewDec := ROUND(OldValue,UsePrecision);
      IF UseDecimalPlaces = '' THEN
        NewValue := FORMAT(NewDec,0,'<Standard Format,1>')
      ELSE
        NewValue := FORMAT(NewDec,0,STRSUBSTNO('<Precision,%1><Standard Format,1>',UseDecimalPlaces));

      EXIT(NewValue);
    END;

    PROCEDURE ConvertWeightToOzToText@1240030021(WeightLbs@1240030000 : Decimal) NewValue : Text[30];
    VAR
      WeightOz@1240030001 : Decimal;
    BEGIN
      WeightOz := ROUND(WeightLbs * 16,1,'>');
      NewValue := FORMAT(WeightOz,0,1);

      EXIT(NewValue);
    END;

    PROCEDURE Decimal2Text@36(CalcDecimal@1240030000 : Decimal;DecimalToShow@1240030001 : Integer) : Text[30];
    VAR
      TempText@1240030002 : Text[30];
      ThousandChar@1240030003 : Text[1];
    BEGIN
      ThousandChar := COPYSTR(FORMAT(1000.1),2,1);
      TempText := FORMAT(CalcDecimal);
      TempText := DELCHR(TempText,'=',ThousandChar);
      TempText := CONVERTSTR(TempText,',','.');

      IF DecimalToShow <> 0 THEN BEGIN
        IF STRPOS(TempText,'.') = 0 THEN
          TempText := TempText + '.000000'
        ELSE
          TempText := TempText + '00000';
        TempText := COPYSTR(TempText,1,STRPOS(TempText,'.') + DecimalToShow);

        IF (STRLEN(TempText) - STRPOS(TempText,'.') <> DecimalToShow) OR (STRPOS(TempText,'.') = 0) THEN
          ERROR('%1 formatted is %2 with %3 decimals.',CalcDecimal,TempText,DecimalToShow);
      END;

      EXIT(TempText);
    END;

    PROCEDURE FixPhoneNo@1240030016(OriginalPhone@1240030000 : Text[30]) ReturnPhone : Text[30];
    VAR
      i@1240030001 : Integer;
    BEGIN
      FOR i := 1 TO STRLEN(OriginalPhone) DO BEGIN
        IF (OriginalPhone[i] >= 48) AND (OriginalPhone[i] <= 57) THEN
          ReturnPhone := ReturnPhone + STRSUBSTNO('%1',OriginalPhone[i]);
      END;
    END;

    PROCEDURE FixZIPCode@1240030017(ZIPCodeText@1240030000 : Text[30]) : Text[9];
    BEGIN
      EXIT(COPYSTR(DELCHR(ZIPCodeText,'=',' -'),1,9));
    END;

    PROCEDURE FixState@1240030018(StateText@1240030000 : Text[30]) : Text[2];
    BEGIN
      EXIT(COPYSTR(DELCHR(UPPERCASE(StateText),'>',' '),1,2));
    END;

    PROCEDURE GetVersion@1240020001() : Text[250];
    VAR
      Object@1240020000 : Record 2000000001;
      Version@1240020001 : Text[250];
    BEGIN
      Version := '';

      Object.RESET;
      Object.SETFILTER("Version List",'SE0*');
      IF Object.FIND('-') THEN
        REPEAT
          IF Object."Version List" > Version THEN
            Version := COPYSTR(Object."Version List",1,16);
        UNTIL Object.NEXT = 0;

      EXIT(Version);
    END;

    PROCEDURE GetLongestDim@1240020005(Length@1240020000 : Integer;Width@1240020001 : Integer;Height@1240020002 : Integer) : Integer;
    BEGIN
      IF (Length <> 0) AND (Width <> 0) AND (Height <> 0) THEN BEGIN
        CASE TRUE OF
          (Height >= Width) AND (Height >= Length):
            EXIT(Height);
          (Width >= Height) AND (Width >= Length):
            EXIT(Width);
          ELSE
            EXIT(Length);
        END;
      END ELSE
        ERROR(Text001);
    END;

    PROCEDURE GetSecondLongestDim@1240020007(Length@1240020000 : Integer;Width@1240020001 : Integer;Height@1240020002 : Integer) : Integer;
    BEGIN
      IF (Length <> 0) AND (Width <> 0) AND (Height <> 0) THEN BEGIN
        CASE TRUE OF
          (Height >= Width) AND
          (Height >= Length):
            BEGIN
              IF Width > Length THEN
                EXIT(Width)
              ELSE
                EXIT(Length);
            END;
          (Width >= Height) AND
          (Width >= Length):
            BEGIN
              IF Height > Length THEN
                EXIT(Height)
              ELSE
                EXIT(Length);
            END;
          ELSE
            BEGIN
              IF Height > Width THEN
                EXIT(Height)
              ELSE
                EXIT(Width);
            END;
        END;
      END ELSE
        ERROR(Text001);
    END;

    PROCEDURE Integer2Text@38(CalcInteger@1240030000 : Integer) : Text[30];
    VAR
      TempText@1240030001 : Text[30];
      ThousandChar@1240030002 : Text[1];
    BEGIN
      ThousandChar := COPYSTR(FORMAT(1000.1),2,1);
      TempText := FORMAT(CalcInteger);
      TempText := DELCHR(TempText,'=',ThousandChar);
      TempText := CONVERTSTR(TempText,',','.');
      EXIT(TempText);
    END;

    PROCEDURE Text2Integer@41(CalcText@1240030000 : Text[30]) : Integer;
    VAR
      TempInteger@1240030001 : Integer;
    BEGIN
      IF CalcText = '' THEN
        EXIT(0);

      EVALUATE(TempInteger,CalcText);
      EXIT(TempInteger);
    END;

    PROCEDURE Text2Decimal@29(CalcText@1240030000 : Text[30]) : Decimal;
    VAR
      TempDecimal@1240030001 : Decimal;
    BEGIN
      IF CalcText = '' THEN
        EXIT(0);

      EVALUATE(TempDecimal,CalcText);
      EXIT(TempDecimal);
    END;

    PROCEDURE ReturnCleanNameAddressString@1240030090() CleanString : Text[250];
    BEGIN
      CleanString := '"().,#$@%^&*+!~`:;/?><';

      EXIT(CleanString);
    END;

    PROCEDURE ReturnCleanString@1240030091() CleanString : Text[250];
    BEGIN
      CleanString := '.,#$@%^&*+!~`:;/?><';

      EXIT(CleanString);
    END;

    PROCEDURE ReturnISOCountryCode@1240020013(CountryCode@1240020000 : Code[10]) : Code[10];
    VAR
      Country@1240020001 : Record 9;
    BEGIN
      IF CountryCode <> '' THEN BEGIN
        IF Country.GET(CountryCode) THEN
          EXIT(Country."ISO 2 char Country Code")
        ELSE
          ERROR(Text004,CountryCode);
      END ELSE
        EXIT('US');
    END;

    PROCEDURE ReturnShippingAcctPostalCode@1240020019(Package@1240020000 : Record 14000701) : Code[9];
    VAR
      ShippingAccount@1240020001 : Record 14000714;
    BEGIN
      CLEAR(ShippingAccount);
      ShippingAccount.SETCURRENTKEY("Ship-to Type","Ship-to No.","Ship-to Code",
        "Shipping Agent Code","Account No.");
      ShippingAccount.SETRANGE("Ship-to Type",ShippingAccount."Ship-to Type"::Customer);
        ShippingAccount.SETRANGE(ShippingAccount."Shipping Agent Code",
        Package."Shipping Agent Code");
      ShippingAccount.SETRANGE("Account No.",Package."Third Party Ship. Account No.");
      IF ShippingAccount.FIND('-') THEN BEGIN
        ShippingAccount.TESTFIELD(ShippingAccount."Third Party ZIP Code");
        EXIT(ShippingAccount."Third Party ZIP Code");
      END;
    END;

    PROCEDURE ReturnShippingAcctCtryCode@1240020020(Package@1240020000 : Record 14000701) : Code[2];
    VAR
      ShippingAccount@1240020001 : Record 14000714;
    BEGIN
      CLEAR(ShippingAccount);
      ShippingAccount.SETCURRENTKEY("Ship-to Type","Ship-to No.","Ship-to Code",
        "Shipping Agent Code","Account No.");
      ShippingAccount.SETRANGE("Ship-to Type",ShippingAccount."Ship-to Type"::Customer);
        ShippingAccount.SETRANGE(ShippingAccount."Shipping Agent Code",
        Package."Shipping Agent Code");
      ShippingAccount.SETRANGE("Account No.",Package."Third Party Ship. Account No.");
      IF ShippingAccount.FIND('-') THEN BEGIN
        ShippingAccount.TESTFIELD(ShippingAccount."Third Party Country Code");
        EXIT(ReturnISOCountryCode(ShippingAccount."Third Party Country Code"));
      END;
    END;

    LOCAL PROCEDURE StripInvalidChar@1000000000(InText@1000000001 : Text[250]) OutText : Text[250];
    VAR
      i@1000000000 : Integer;
      Ch@1240020000 : Text[3];
    BEGIN
      OutText := InText;
      FOR i := 1 TO STRLEN(InText) DO
        IF (InText[i] < ' ') OR
           (InText[i] > '~')
        THEN
          OutText := DELCHR(OutText,'=',FORMAT(InText[i]));

      //Remove invisible chars
      Ch[1] := 9; //TAB
      Ch[2] := 10; //LF
      Ch[3] := 13; //CR
      OutText := DELCHR(OutText,'=',Ch);

      OutText := LeftString(OutText);
    END;

    LOCAL PROCEDURE LeftString@48(TextString@1240030000 : Text[250]) OutText : Text[250];
    BEGIN
      IF STRLEN(TextString) > 0 THEN
        OutText := DELCHR(TextString,'<',' ')
      ELSE
        OutText := '';
    END;

    PROCEDURE IsPOBox@1240020002(AddressLine@1240020000 : Text[50]) : Boolean;
    VAR
      Text001@1240020003 : TextConst 'ENU=pobox';
      Text002@1240020002 : TextConst 'ENU=postoffice';
      Text003@1240020001 : TextConst 'ENU=postbox';
    BEGIN
      AddressLine := LOWERCASE(DELCHR(AddressLine,'=','.| |#'));

      IF (STRPOS(AddressLine,Text001) > 0 ) OR
         (STRPOS(AddressLine,Text002) > 0 ) OR
         (STRPOS(AddressLine,Text003) > 0 ) THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE IsNonmachineable@1240020011(CurrentUPSOptionPage@1240020001 : Record 14000761;Weight@1240020002 : Decimal;Length@1240020003 : Decimal;Width@1240020004 : Decimal;Height@1240020005 : Decimal) : Boolean;
    BEGIN
      IF (Weight > 35) OR
         (CurrentUPSOptionPage."UPS Packaging Type" = CurrentUPSOptionPage."UPS Packaging Type"::"UPS Express Tube") OR
         (GetLongestDim(Length,Width,Height) > 34) OR
         (GetSecondLongestDim(Length,Width,Height) > 17)
      THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE Cipher@1240020006(Value1@1240020000 : Text[30];Encrypt@1240020003 : Boolean;View@1240020001 : Boolean) ReturnValue : Text[30];
    VAR
      Text001@1240020010 : TextConst 'ENU=4';
      Text002@1240020011 : TextConst 'ENU=94';
      Text003@1240020012 : TextConst 'ENU=33';
      Int1@1240020013 : Integer;
      Int2@1240020009 : Integer;
      Int3@1240020008 : Integer;
      Int4@1240020007 : Integer;
      i@1240020014 : Integer;
      BeforeChar@1240020015 : Char;
      AfterChar@1240020016 : Char;
    BEGIN
      EVALUATE(Int1,Text001); //Key
      EVALUATE(Int2,Text002); //#Chars
      EVALUATE(Int3,Text003); //1st Char
      Int4 := Int2 - Int1;    //Decryption Key

      FOR i := 1 TO STRLEN(Value1) DO BEGIN
        BeforeChar := Value1[i] - Int3;
        IF Encrypt THEN
          AfterChar := ((BeforeChar + Int1) MOD Int2) + Int3
        ELSE
          AfterChar := ((BeforeChar + Int4) MOD Int2) + Int3;
        ReturnValue += FORMAT(AfterChar);
      END;

      IF View THEN
        MESSAGE('Before: %1/nAfter: %2',Value1,ReturnValue);
    END;

    PROCEDURE CreateUnique@1240020000(Value1@1240020000 : Integer) ReturnValue : Text[26];
    VAR
      Text001@1240020001 : TextConst 'ENU=!@#$%^&';
      day@1240020009 : Text[30];
      i@1240020007 : Integer;
      UseSymbol@1240020006 : Text[30];
      Symbols@1240020004 : Text[30];
    BEGIN
      RANDOMIZE;
      Symbols := Text001;
      FOR i := 1 TO 7 DO BEGIN
        IF FORMAT(TODAY,0,'<Weekday>') = FORMAT(i) THEN
          UseSymbol := FORMAT(Symbols[i]);
      END;

      CASE Value1 OF
        1:
          BEGIN
            ReturnValue := COPYSTR(FORMAT(TODAY,0,'<Weekday Text>'),1,4) + FORMAT(RANDOM(9)) + COPYSTR(FORMAT(TODAY,0,'<Year4>'),3,2) +
              FORMAT(RANDOM(9)) + COPYSTR(FORMAT(TODAY,0,'<Month Text>'),1,3);
          END;
        2:
          BEGIN
            ReturnValue := COPYSTR(FORMAT(TODAY,0,'<Month Text>'),1,3) + COPYSTR(FORMAT(TODAY,0,'<Year4>'),1,2) + UseSymbol +
              COPYSTR(FORMAT(TODAY,0,'<Year4>'),3,2) + FORMAT(TIME,0,'<Seconds,2>') +
              FORMAT(TODAY,0,'<Day,2>') + UseSymbol + FORMAT(TIME,0,'<Minutes,2>');
          END;
      END;
    END;

    PROCEDURE InsertUPSWSServices@1240020003(ShowMessage@1240020002 : Boolean);
    VAR
      EShipAgentService@1240020001 : Record 14000708;
      ShippingAgent@1240020000 : Record 291;
    BEGIN
      EShipAgentService.RESET;
      ShippingAgent.RESET;
      ShippingAgent.SETRANGE("Shipper Type",ShippingAgent."Shipper Type"::UPS);
      IF ShippingAgent.FIND('-') THEN BEGIN
        EShipAgentService.SETCURRENTKEY("Shipping Agent Code",Code,"World Wide Service");
        EShipAgentService.SETRANGE("Shipping Agent Code",ShippingAgent.Code);
        EShipAgentService.SETFILTER(Code,'%1|%2','SUREPOST' +'*','GFP' + '*');
        IF EShipAgentService.FIND('-') THEN
          EShipAgentService.DELETEALL(TRUE);
      END;

      InsertUPSWSService('SurePost Less than 1 lb','UPS SurePost” Less than 1 LB','92',FALSE,3,'Ground');
      InsertUPSWSService('SurePost 1 lb or Greater','UPS SurePost” 1 LB or Greater','93',FALSE,3,'Ground');
      InsertUPSWSService('SurePost BPM','UPS SurePost” Bound Printed Matter','94',FALSE,3,'Ground');
      InsertUPSWSService('SurePost Media','UPS SurePost” Media','95',FALSE,3,'Ground');
      InsertUPSWSService('GFP','UPS Ground with Freight Pricing','GFP',FALSE,3,'Ground');

      IF ShowMessage THEN
        MESSAGE(Text006);
    END;

    PROCEDURE InsertUPSWSService@1240020004(ServiceCode@1240020005 : Code[30];Description@1240020004 : Text[40];ServiceIndicator@1240020003 : Text[10];WorldWideService@1240020002 : Boolean;DeliveryDays@1240020001 : Integer;TransportMethodType@1240020000 : Text[10]);
    VAR
      EShipAgentService@1240020007 : Record 14000708;
      ShippingAgent@1240020006 : Record 291;
    BEGIN
      EShipAgentService.RESET;
      ShippingAgent.RESET;
      ShippingAgent.SETRANGE("Shipper Type",ShippingAgent."Shipper Type"::UPS);
      IF ShippingAgent.FIND('-') THEN BEGIN
        EShipAgentService."Shipping Agent Code" := ShippingAgent.Code;
        EShipAgentService.Code := ServiceCode;
        EShipAgentService.Description := Description;
        EShipAgentService."Service Indicator" := ServiceIndicator;
        EShipAgentService."World Wide Service" := WorldWideService;
        EShipAgentService."Delivery Days" := DeliveryDays;
        CASE TransportMethodType OF
          'Air':
            EShipAgentService."Transport Method Type" := EShipAgentService."Transport Method Type"::Air;
          'Ground':
            EShipAgentService."Transport Method Type" := EShipAgentService."Transport Method Type"::Ground;
        END;
        EShipAgentService.INSERT(TRUE);
      END;
    END;

    PROCEDURE UTACheck@1240020008() : Boolean;
    VAR
      UPSInfo@1240020000 : Record 14000778;
      UPSWSSetup@1240020001 : Record 14000777;
    BEGIN
      IF UPSInfo.GET THEN BEGIN
        UPSInfo.CALCFIELDS("License Agreement");
        IF UPSInfo."License Agreement".HASVALUE THEN BEGIN
          UPSWSSetup.GET;
          UPSWSSetup."Get Terms and Conditions" := TRUE;
          UPSWSSetup.MODIFY(FALSE);
          EXIT(TRUE)
        END ELSE BEGIN
          UPSWSSetup."Get Terms and Conditions" := FALSE;
          UPSWSSetup.MODIFY;
          EXIT(FALSE)
        END;
      END ELSE
        EXIT(FALSE);
    END;

    PROCEDURE MoveFileToClient@1240020012(FilenamePath@1240020000 : Text[250]);
    VAR
      FileManagement@1240020001 : Codeunit 419;
    BEGIN
      FileManagement.DownloadToFile(FilenamePath,FilenamePath);
    END;

    BEGIN
    END.
  }
}

