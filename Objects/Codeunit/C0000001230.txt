OBJECT Codeunit 1230 SEPA DD-Export File
{
  OBJECT-PROPERTIES
  {
    Date=02/01/20;
    Time=12:00:00 PM;
    Version List=NAVW111.00.00.40505;
  }
  PROPERTIES
  {
    TableNo=1208;
    OnRun=VAR
            DirectDebitCollection@1003 : Record 1207;
            DirectDebitCollectionEntry@1005 : Record 1208;
            BankAccount@1004 : Record 270;
            GLSetup@1002 : Record 98;
          BEGIN
            DirectDebitCollectionEntry.COPY(Rec);
            TESTFIELD("Direct Debit Collection No.");
            DirectDebitCollection.GET("Direct Debit Collection No.");
            DirectDebitCollection.TESTFIELD("To Bank Account No.");
            BankAccount.GET(DirectDebitCollection."To Bank Account No.");
            GLSetup.GET;
            IF NOT GLSetup."SEPA Export w/o Bank Acc. Data" THEN
              BankAccount.TESTFIELD(IBAN)
            ELSE BEGIN
              IF (BankAccount."Bank Account No." = '') OR (BankAccount."Bank Branch No." = '') THEN
                IF BankAccount.IBAN = '' THEN
                  ERROR(ExportWithoutIBANErr,BankAccount.TABLECAPTION,BankAccount."No.")
            END;

            DirectDebitCollection.LOCKTABLE;
            DirectDebitCollection.DeletePaymentFileErrors;
            COMMIT;
            IF NOT Export(Rec,BankAccount.GetDDExportXMLPortID,DirectDebitCollection.Identifier) THEN
              ERROR('');

            DirectDebitCollectionEntry.SETRANGE("Direct Debit Collection No.",DirectDebitCollection."No.");
            DirectDebitCollectionEntry.MODIFYALL(Status,DirectDebitCollectionEntry.Status::"File Created");
            DirectDebitCollection.Status := DirectDebitCollection.Status::"File Created";
            DirectDebitCollection.MODIFY;
          END;

  }
  CODE
  {
    VAR
      ExportToServerFile@1000 : Boolean;
      ExportWithoutIBANErr@1001 : TextConst '@@@="%1= table name, %2=key field value. Example: Either the Bank Account No. and Bank Branch No. fields or the IBAN field must be filled in for Bank Account WWB-OPERATING.";ENU=Either the Bank Account No. and Bank Branch No. fields or the IBAN field must be filled in for %1 %2.;ESM=Debe rellenar para %1 %2 los campos N.§ cuenta bancaria y C¢d. sucursal banco, o bien el campo IBAN.;FRC=Vous devez renseigner les champs Nø compte bancaire et Code ‚tablissement ou le champ IBAN pour %1 %2.;ENC=Either the Bank Account No. and Bank Branch No. fields or the IBAN field must be filled in for %1 %2.';

    LOCAL PROCEDURE Export@2(VAR DirectDebitCollectionEntry@1006 : Record 1208;XMLPortID@1004 : Integer;FileName@1003 : Text) : Boolean;
    VAR
      TempBlob@1002 : Record 99008535;
      FileManagement@1001 : Codeunit 419;
      OutStr@1000 : OutStream;
    BEGIN
      TempBlob.INIT;
      TempBlob.Blob.CREATEOUTSTREAM(OutStr);
      XMLPORT.EXPORT(XMLPortID,OutStr,DirectDebitCollectionEntry);
      EXIT(FileManagement.BLOBExport(TempBlob,STRSUBSTNO('%1.XML',FileName),NOT ExportToServerFile) <> '');
    END;

    [External]
    PROCEDURE EnableExportToServerFile@1();
    BEGIN
      ExportToServerFile := TRUE;
    END;

    BEGIN
    END.
  }
}

