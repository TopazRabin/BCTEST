OBJECT Codeunit 14000572 AFP Document Synchronize
{
  OBJECT-PROPERTIES
  {
    Date=05/12/08;
    Time=12:00:00 PM;
    Version List=UBP2.40;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE PurchLinesToTransferLines@1240030000(CurrPurchHeader@1240030003 : Record 38);
    VAR
      PurchaseLine@1240030000 : Record 39;
      TransferHeader@1240030002 : Record 5740;
      TransferHeaderTmp@1240030005 : TEMPORARY Record 5740;
      TransferLine@1240030001 : Record 5741;
      ReleaseTransferDocumnet@1240030006 : Codeunit 5708;
      LineFound@1240030004 : Boolean;
      RecModified@1240030007 : Boolean;
    BEGIN
      IF CurrPurchHeader."Suggested Order No." = '' THEN
        EXIT;

      PurchaseLine.RESET;
      PurchaseLine.SETRANGE("Document Type",CurrPurchHeader."Document Type");
      PurchaseLine.SETRANGE("Document No.",CurrPurchHeader."No.");
      IF PurchaseLine.FIND('-') THEN
        REPEAT
          TransferHeader.RESET;
          TransferHeader.SETCURRENTKEY("Suggested Order No.");
          TransferHeader.SETRANGE("Suggested Order No.",CurrPurchHeader."Suggested Order No.");
          TransferHeader.SETRANGE("Transfer-from Code",PurchaseLine."Location Code");
          IF TransferHeader.FIND('-') THEN
            REPEAT
              IF TransferHeader.Status = TransferHeader.Status::Released THEN BEGIN
                TransferHeaderTmp.COPY(TransferHeader);
                IF NOT TransferHeaderTmp.INSERT THEN;
                ReleaseTransferDocumnet.Reopen(TransferHeader);
              END;
              RecModified := FALSE;
              TransferLine.RESET;
              TransferLine.SETRANGE("Document No.",TransferHeader."No.");
              TransferLine.SETRANGE("Item No.",PurchaseLine."No.");
              TransferLine.SETRANGE("Variant Code",PurchaseLine."Variant Code");
              TransferLine.SETRANGE("Line No.",PurchaseLine."Line No.");
              IF TransferLine.FIND('-') THEN BEGIN
                IF PurchaseLine."Expected Receipt Date" <> TransferLine."Shipment Date" THEN BEGIN
                  TransferLine.VALIDATE("Shipment Date",PurchaseLine."Expected Receipt Date");
                  RecModified := TRUE;
                END;
                IF PurchaseLine.Quantity <> TransferLine.Quantity THEN BEGIN
                  TransferLine.VALIDATE(Quantity,PurchaseLine.Quantity);
                  RecModified := TRUE;
                END;
                LineFound := TRUE;
                IF RecModified THEN
                  TransferLine.MODIFY;
              END;
            UNTIL (LineFound = TRUE) OR
                  (TransferHeader.NEXT = 0);
          LineFound := FALSE;
        UNTIL PurchaseLine.NEXT = 0;

      IF TransferHeaderTmp.FIND('-') THEN
        REPEAT
          TransferHeader.GET(TransferHeaderTmp."No.");
          ReleaseTransferDocumnet.RUN(TransferHeader);
        UNTIL TransferHeaderTmp.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

