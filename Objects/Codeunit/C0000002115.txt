OBJECT Codeunit 2115 Display Coupons
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      GraphIntContact@1000 : Codeunit 5461;
      ClientTypeManagement@1001 : Codeunit 4;

    PROCEDURE ShowCoupons@2(VAR CouponsNotification@1000 : Notification);
    VAR
      DocumentType@1001 : Option;
    BEGIN
      EVALUATE(DocumentType,CouponsNotification.GETDATA('DocumentType'));
      ShowCouponsForDocument(DocumentType,CouponsNotification.GETDATA('DocumentNo'));
    END;

    PROCEDURE ShowCouponsForDocument@1(DocumentType@1002 : Option;DocumentNo@1000 : Code[20]);
    VAR
      O365CouponClaim@1001 : Record 2115;
      SalesHeader@1003 : Record 36;
      GraphContactID@1006 : Text[250];
    BEGIN
      IF NOT SalesHeader.GET(DocumentType,DocumentNo) THEN
        EXIT;

      IF NOT GraphIntContact.FindGraphContactIdFromCustomerNo(GraphContactID,SalesHeader."Sell-to Customer No.") THEN
        EXIT;

      O365CouponClaim.SETRANGE("Graph Contact ID",GraphContactID);
      O365CouponClaim.SETRANGE("Document Type Filter",DocumentType);
      O365CouponClaim.SETRANGE("Document No. Filter",DocumentNo);

      CASE O365CouponClaim.COUNT OF
        0:
          EXIT;
        1:
          BEGIN
            IF O365CouponClaim.FINDFIRST THEN
              IF ClientTypeManagement.GetCurrentClientType = CLIENTTYPE::Tablet THEN
                PAGE.RUNMODAL(PAGE::"BC O365 Coupon",O365CouponClaim)
              ELSE
                PAGE.RUNMODAL(PAGE::"O365 Coupon",O365CouponClaim);
          END;
        ELSE
          IF ClientTypeManagement.GetCurrentClientType = CLIENTTYPE::Tablet THEN
            PAGE.RUNMODAL(PAGE::"BC O365 Coupon List",O365CouponClaim)
          ELSE
            PAGE.RUNMODAL(PAGE::"O365 Coupon List",O365CouponClaim);
      END;
    END;

    BEGIN
    END.
  }
}

