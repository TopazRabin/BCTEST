OBJECT Codeunit 2151 O365 Sales Email Management
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [Internal]
    PROCEDURE ShowEmailDialog@2(DocumentNo@1000 : Code[20]) : Boolean;
    VAR
      SalesInvoiceHeader@1001 : Record 112;
      TempEmailItem@1004 : TEMPORARY Record 9500;
      ReportSelections@1010 : Record 77;
      TempReportSelections@1005 : TEMPORARY Record 77;
      DocumentMailing@1008 : Codeunit 260;
      O365HTMLTemplMgt@1006 : Codeunit 2114;
      O365SalesEmailDialog@1002 : Page 2150;
      DocumentRecordVariant@1013 : Variant;
      CustomerNo@1014 : Code[20];
      EmailAddress@1007 : Text[250];
      EmailSubject@1011 : Text[250];
      ReportUsage@1009 : Integer;
      HasBeenSent@1012 : Boolean;
    BEGIN
      IF NOT InitializeDocumentSpecificVariables(DocumentRecordVariant,ReportUsage,CustomerNo,DocumentNo) THEN
        EXIT;

      IF NOT ReportSelections.GetEmailBody(
           TempEmailItem."Body File Path",ReportUsage,DocumentRecordVariant,CustomerNo,EmailAddress)
      THEN
        ;
      IF ReportSelections.FindEmailAttachmentUsage(ReportUsage,CustomerNo,TempReportSelections) THEN BEGIN
        // Create attachment
        TempReportSelections.GetPdfReport(
          TempEmailItem."Attachment File Path",ReportUsage,
          DocumentRecordVariant,CustomerNo);

        DocumentMailing.GetAttachmentFileName(
          TempEmailItem."Attachment Name",
          DocumentNo,
          SalesInvoiceHeader.GetDefaultEmailDocumentName,
          ReportUsage);
      END;

      EmailSubject := DocumentMailing.GetEmailSubject(DocumentNo,SalesInvoiceHeader.GetDefaultEmailDocumentName,ReportUsage);

      TempEmailItem.Subject := EmailSubject;
      TempEmailItem."Send to" := EmailAddress;
      TempEmailItem.AddCcBcc;
      TempEmailItem.AttachIncomingDocuments(DocumentNo);
      TempEmailItem.INSERT(TRUE);
      COMMIT;

      O365SalesEmailDialog.SetValues(DocumentRecordVariant,TempEmailItem);
      HasBeenSent := O365SalesEmailDialog.RUNMODAL = ACTION::OK;
      O365SalesEmailDialog.GETRECORD(TempEmailItem);
      IF EmailAddress <> TempEmailItem."Send to" THEN
        O365HTMLTemplMgt.ReplaceBodyFileSendTo(
          TempEmailItem."Body File Path",EmailAddress,TempEmailItem."Send to");
      SaveEmailParametersIfChanged(
        DocumentNo,ReportUsage,EmailAddress,TempEmailItem."Send to",EmailSubject,TempEmailItem.Subject);
      EXIT(HasBeenSent);
    END;

    LOCAL PROCEDURE InitializeDocumentSpecificVariables@12(VAR DocumentRecordVariant@1001 : Variant;VAR ReportUsage@1005 : Integer;VAR CustomerNo@1000 : Code[20];DocumentNo@1002 : Code[20]) : Boolean;
    VAR
      SalesInvoiceHeader@1003 : Record 112;
      SalesHeader@1004 : Record 36;
      ReportSelections@1006 : Record 77;
    BEGIN
      IF SalesInvoiceHeader.GET(DocumentNo) THEN BEGIN
        SalesInvoiceHeader.SETRECFILTER;
        DocumentRecordVariant := SalesInvoiceHeader;
        CustomerNo := SalesInvoiceHeader."Bill-to Customer No.";
        ReportUsage := ReportSelections.Usage::"S.Invoice";
        EXIT(TRUE);
      END;

      SalesHeader.SETFILTER("Document Type",'%1|%2',SalesHeader."Document Type"::Quote,SalesHeader."Document Type"::Invoice);
      SalesHeader.SETRANGE("No.",DocumentNo);
      IF SalesHeader.FINDFIRST THEN BEGIN
        SalesHeader.SETRECFILTER;
        DocumentRecordVariant := SalesHeader;
        CustomerNo := SalesHeader."Bill-to Customer No.";
        IF SalesHeader."Document Type" = SalesHeader."Document Type"::Invoice THEN
          ReportUsage := ReportSelections.Usage::"S.Invoice Draft"
        ELSE
          ReportUsage := ReportSelections.Usage::"S.Quote";
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    PROCEDURE SaveEmailParametersIfChanged@9(DocumentNo@1001 : Code[20];ReportUsage@1002 : Integer;OldEmailAddress@1004 : Text[250];NewEmailAddress@1005 : Text[250];OldEmailSubject@1007 : Text[250];NewEmailSubject@1006 : Text[250]);
    VAR
      EmailParameter@1003 : Record 9510;
    BEGIN
      IF OldEmailAddress <> NewEmailAddress THEN
        EmailParameter.SaveParameterValueWithReportUsage(
          DocumentNo,ReportUsage,EmailParameter."Parameter Type"::Address,NewEmailAddress);
      IF OldEmailSubject <> NewEmailSubject THEN
        EmailParameter.SaveParameterValueWithReportUsage(
          DocumentNo,ReportUsage,EmailParameter."Parameter Type"::Subject,NewEmailSubject);
    END;

    BEGIN
    END.
  }
}

