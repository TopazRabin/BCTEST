OBJECT Codeunit 11123399 SC - Uninstall
{
  OBJECT-PROPERTIES
  {
    Date=04/18/17;
    Time=[ 1:00:00 PM];
    Version List=SCW19.2.0;
  }
  PROPERTIES
  {
    OnRun=VAR
            RecordRef@11123302 : RecordRef;
            TableID@11123303 : Integer;
            ModifiedTableFields@11123304 : ARRAY [100,100] OF Integer;
            I@11123305 : Integer;
            J@11123306 : Integer;
            FieldRef@11123307 : FieldRef;
            FieldNo@11123308 : Integer;
            FieldType@11123309 : Text[30];
            TempRecord@11123310 : TEMPORARY Record 36;
            TempRecordRef@11123311 : RecordRef;
            TempFieldRef@11123312 : FieldRef;
            Permission@11123313 : Record 2000000005;
            UserRole@11123314 : Record 2000000004;
            Profile@11123315 : Record 2000000072;
            Object@11123316 : Record 2000000001;
            RangeMinimumID@11123317 : Integer;
            RangeMaximumID@11123318 : Integer;
            WebService@11123319 : Record 2000000076;
            Company@11123320 : Record 2000000006;
            FieldRec@11123321 : Record 2000000041;
          BEGIN
            IF NOT CONFIRM(Text11123302,FALSE) THEN
              EXIT;

            IF Company.FINDSET THEN
              REPEAT
                RangeMinimumID := 11123302;
                RangeMaximumID := 11123401;
                I := 0;
                J := 0;

                //Unpublishing webservice
                WebService.CHANGECOMPANY(Company.Name);
                WebService.SETRANGE("Object ID",RangeMinimumID,RangeMaximumID);
                WebService.DELETEALL;

                Object.RESET;
                Object.SETRANGE(Type,Object.Type::Table);
                Object.SETRANGE(ID,RangeMinimumID,RangeMaximumID);
                IF Object.FINDSET THEN
                  REPEAT
                    RecordRef.OPEN(Object.ID,FALSE,Company.Name);
                    RecordRef.DELETEALL;
                    RecordRef.CLOSE;
                  UNTIL Object.NEXT = 0;

                Object.RESET;
                Object.SETRANGE(Type,Object.Type::Table);
                Object.SETFILTER(ID,'<%1|>%2',RangeMinimumID,RangeMaximumID);
                IF Object.FINDSET THEN
                  REPEAT
                    FieldRec.RESET;
                    FieldRec.SETRANGE(TableNo,Object.ID);
                    FieldRec.SETRANGE("No.",RangeMinimumID,RangeMaximumID);
                    IF FieldRec.FINDSET THEN BEGIN
                      I := I + 1;
                      J := 1;
                      ModifiedTableFields[I][J] := Object.ID;
                      REPEAT
                        J := J + 1;
                        ModifiedTableFields[I][J] := FieldRec."No.";
                      UNTIL FieldRec.NEXT = 0;
                    END;
                  UNTIL Object.NEXT = 0;

                //Clear modified tables data
                FOR I := 1 TO ARRAYLEN(ModifiedTableFields,1) DO BEGIN
                  TableID := ModifiedTableFields[I][1];
                  IF TableID <> 0 THEN BEGIN
                    RecordRef.OPEN(TableID,FALSE,Company.Name);
                    IF RecordRef.FINDSET(TRUE,FALSE) THEN BEGIN
                      REPEAT
                        FOR J := 2 TO ARRAYLEN(ModifiedTableFields,2) DO BEGIN
                          FieldNo := ModifiedTableFields[I][J];
                          IF FieldNo <> 0 THEN BEGIN
                            FieldRef := RecordRef.FIELD(FieldNo);
                            FieldType := FORMAT(FieldRef.TYPE);
                            CASE FieldType OF
                              'Code' :  FieldRef.VALUE('');
                              'Text' :  FieldRef.VALUE('');
                              'Boolean' : FieldRef.VALUE(FALSE);
                              'Integer' : FieldRef.VALUE(0);
                              'Option' : FieldRef.VALUE(0);
                              'Date' : FieldRef.VALUE(0D);
                              'Time' : FieldRef.VALUE(0T);
                              'DateTime' : FieldRef.VALUE(0DT);
                              'BLOB' :
                                BEGIN
                                  //Here we clean BLOB field (Custom Fields in Sales Header Table).
                                  //Temporary record with empty BLOB is used.
                                  CLEAR(TempRecord);
                                  TempRecord.INSERT;
                                  TempRecordRef.GETTABLE(TempRecord);
                                  TempFieldRef := TempRecordRef.FIELD(FieldRef.NUMBER);
                                  TempFieldRef.CALCFIELD;
                                  FieldRef.VALUE(TempFieldRef.VALUE);
                                  TempRecord.DELETE;
                                END;
                            END;
                          END;
                        END;
                        RecordRef.MODIFY;
                      UNTIL RecordRef.NEXT = 0;
                    END;
                  END;
                  RecordRef.CLOSE;
                END;

                //Deleting SC-specific Permissions, Roles, and Profiles
                Permission.CHANGECOMPANY(Company.Name);
                Permission.SETFILTER("Role ID",'SC-MANAGER|SC-SYSADMIN|SC-USER');
                Permission.DELETEALL;

                UserRole.CHANGECOMPANY(Company.Name);
                UserRole.SETFILTER("Role ID",'SC-MANAGER|SC-SYSADMIN|SC-USER');
                UserRole.DELETEALL;

                Profile.CHANGECOMPANY(Company.Name);
                Profile.SETFILTER("Profile ID",'E-COMMERCE MANAGER|E-COMMERCE SYS. ADMINISTRATOR');
                Profile.DELETEALL;
              UNTIL Company.NEXT = 0;

            //Deleting Objects
            Object.RESET;
            Object.SETFILTER(Type,'<>%1&<>%2&<>%3',Object.Type::TableData,Object.Type::System,Object.Type::FieldNumber);
            Object.SETRANGE(ID,RangeMinimumID,RangeMaximumID);
            IF NOT Object.ISEMPTY THEN
              Object.DELETEALL(TRUE);

            //Deleting Menus
            IF Object.GET(Object.Type::MenuSuite,'',52) THEN
              Object.DELETE(TRUE);

            IF Object.GET(Object.Type::MenuSuite,'',1052) THEN
              Object.DELETE(TRUE);

            MESSAGE(Text11123303);
          END;

  }
  CODE
  {
    VAR
      Text11123302@11123302 : TextConst 'ENU=Are you sure that you want to uninstall all Sana Commerce objects?';
      Text11123303@11123303 : TextConst 'ENU=Uninstall procedure completed successfully.';

    BEGIN
    END.
  }
}

