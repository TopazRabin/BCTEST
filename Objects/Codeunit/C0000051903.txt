OBJECT Codeunit 51903 Codeunit12EventSubcriber
{
  OBJECT-PROPERTIES
  {
    Date=07/01/20;
    Time=12:00:00 AM;
    Modified=Yes;
    Version List=TPZ000.00.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE Cu12_CalcOverdueBalanceGracePeriod@1000000000(VAR GenJnlLine@1003 : Record 81;DueDate@1001 : Date;VAR OverdueBalanceGraceDate@1002 : Date);
    VAR
      PaymentTerms@1000000000 : Record 3;
      CustDivision@1000000001 : Record 50007;
      GLSetup@1000000002 : Record 98;
    BEGIN
      // <TPZ1131>
      WITH GenJnlLine DO BEGIN
        GLSetup.GET;//NAVEVENT
        IF "Document Type" IN ["Document Type"::Invoice,"Document Type"::"Credit Memo"] THEN BEGIN
          IF DueDate <> 0D THEN
            IF CustDivision.GET("Account No.","Shortcut Dimension 5 Code") AND
               PaymentTerms.GET(CustDivision."Payment Terms Code") AND
               (FORMAT(PaymentTerms."Overdue Balance Grace Period") <> '')
            THEN BEGIN
              OverdueBalanceGraceDate :=
                CALCDATE(PaymentTerms."Payment Discount Grace Period",DueDate);
              "Overdue Balance Grace Period" := PaymentTerms."Overdue Balance Grace Period";
            END ELSE BEGIN
              OverdueBalanceGraceDate :=
                CALCDATE(GLSetup."Overdue Balance Grace Period",DueDate);
              "Overdue Balance Grace Period" := GLSetup."Overdue Balance Grace Period";
            END
          ELSE
            OverdueBalanceGraceDate := DueDate;
        END;
      END;
      // </TPZ1131>
    END;

    [EventSubscriber(Codeunit,51902,Cu12_OnPostCust)]
    LOCAL PROCEDURE Cu12_PostCust@1000000001(VAR Sender@1000000000 : Codeunit 51902;VAR GenJnlLine@1000000001 : Record 81;DueDate@1000000002 : Date;VAR OverdueBalanceGraceDate@1000000003 : Date);
    BEGIN
      Cu12_CalcOverdueBalanceGracePeriod(GenJnlLine,DueDate,OverdueBalanceGraceDate);
    END;

    [EventSubscriber(Codeunit,51902,Cu12_OnCalcPmtTolerancePossible)]
    LOCAL PROCEDURE Cu12_CalcPmtTolerancePossible@1000000002(VAR Sender@1000000000 : Codeunit 51902;VAR GenJnlLine@1000000001 : Record 81;PmtDiscountDate@1000000003 : Date;VAR PmtDiscToleranceDate@1000000005 : Date);
    VAR
      PaymentTerms@1000000002 : Record 3;
      GLSetup@1000000004 : Record 98;
    BEGIN
      WITH GenJnlLine DO BEGIN
        GLSetup.GET;//NAVEVENT
        IF "Document Type" IN ["Document Type"::Invoice,"Document Type"::"Credit Memo"] THEN BEGIN
          // <TPZ369>
          IF PmtDiscountDate <> 0D THEN
            IF PaymentTerms.GET("Payment Terms Code") AND (FORMAT(PaymentTerms."Payment Discount Grace Period") <> '') THEN BEGIN
            PmtDiscToleranceDate :=
                CALCDATE(PaymentTerms."Payment Discount Grace Period",PmtDiscountDate);
              "Payment Discount Grace Period" := PaymentTerms."Payment Discount Grace Period";
            END ELSE BEGIN
              PmtDiscToleranceDate :=
                CALCDATE(GLSetup."Payment Discount Grace Period",PmtDiscountDate);
              "Payment Discount Grace Period" := GLSetup."Payment Discount Grace Period";
            END
          // </TPZ369>
          ELSE
            PmtDiscToleranceDate := PmtDiscountDate;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

