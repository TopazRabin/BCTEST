OBJECT Codeunit 7310 Whse.-Shipment Release
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1005 : TextConst 'ENU=There is nothing to release for %1 %2.;ESM=No hay nada para lanzar para %1 %2.;FRC=Il n''y a rien � lib�rer pour %1 %2.;ENC=There is nothing to release for %1 %2.';
      Text001@1000 : TextConst 'ENU=You cannot reopen the shipment because warehouse worksheet lines exist that must first be handled or deleted.;ESM=No puede volver a abrir el env�o porque existen l�neas de hoja de almac�n que se deben procesar o eliminar primero.;FRC=Vous ne pouvez pas rouvrir la livraison car les lignes feuille entrep�t existantes doivent d''abord �tre trait�es ou supprim�es.;ENC=You cannot reopen the shipment because warehouse worksheet lines exist that must first be handled or deleted.';
      Text002@1002 : TextConst 'ENU=You cannot reopen the shipment because warehouse activity lines exist that must first be handled or deleted.;ESM=No puede volver a abrir el env�o porque existen l�neas de actividad de almac�n que se deben procesar o eliminar primero.;FRC=Vous ne pouvez pas rouvrir la livraison car les lignes activit� entrep�t existantes doivent d''abord �tre trait�es ou supprim�es.;ENC=You cannot reopen the shipment because warehouse activity lines exist that must first be handled or deleted.';

    [External]
    PROCEDURE Release@1(VAR WhseShptHeader@1000 : Record 7320);
    VAR
      Location@1004 : Record 14;
      WhsePickRqst@1003 : Record 7325;
      WhseShptLine@1001 : Record 7321;
      ATOLink@1002 : Record 904;
      AsmLine@1005 : Record 901;
      item@1000000000 : Record 27;
      Codeunit7310EventPublisher@1000000001 : Codeunit 51929;
    BEGIN
      WITH WhseShptHeader DO BEGIN
        IF Status = Status::Released THEN
          EXIT;


        OnBeforeRelease(WhseShptHeader);


        WhseShptLine.SETRANGE("No.","No.");
        WhseShptLine.SETFILTER(Quantity,'<>0');
        IF NOT WhseShptLine.FIND('-') THEN
          ERROR(Text000,TABLECAPTION,"No.");
        IF "Location Code" <> '' THEN
          Location.GET("Location Code");
        REPEAT
          WhseShptLine.TESTFIELD("Item No.");
          WhseShptLine.TESTFIELD("Unit of Measure Code");
          Codeunit7310EventPublisher.CU7310_Release(WhseShptLine,Location);  //TPZ2830
          //IF item.GET(WhseShptLine."No.") THEN BEGIN //<TPZ2482>
            //IF item.Type = item.Type::Inventory THEN BEGIN //<TPZ2482>
              IF Location."Directed Put-away and Pick" THEN
                WhseShptLine.TESTFIELD("Zone Code");
           //END;//<TPZ2482>
          //END;//<TPZ2482>
          IF Location."Bin Mandatory" THEN BEGIN
            //IF item.GET(WhseShptLine."No.") THEN BEGIN //<TPZ2482>
              //IF item.Type = item.Type::Inventory THEN BEGIN //<TPZ2482>
                WhseShptLine.TESTFIELD("Bin Code");
              //END;//<TPZ2482>
            //END; //<TPZ2482>
            IF WhseShptLine."Assemble to Order" THEN BEGIN
              ATOLink.AsmExistsForWhseShptLine(WhseShptLine);
              AsmLine.SETCURRENTKEY("Document Type","Document No.",Type);
              AsmLine.SETRANGE("Document Type",ATOLink."Assembly Document Type");
              AsmLine.SETRANGE("Document No.",ATOLink."Assembly Document No.");
              AsmLine.SETRANGE(Type,AsmLine.Type::Item);
              IF AsmLine.FINDSET THEN
                REPEAT
                  IF AsmLine.CalcQtyToPickBase > 0 THEN
                    AsmLine.TESTFIELD("Bin Code");
                UNTIL AsmLine.NEXT = 0;
            END;
          END;
        UNTIL WhseShptLine.NEXT = 0;

        Status := Status::Released;
        MODIFY;

        CreateWhsePickRqst(WhseShptHeader);

        WhsePickRqst.SETRANGE("Document Type",WhsePickRqst."Document Type"::Shipment);
        WhsePickRqst.SETRANGE("Document No.","No.");
        WhsePickRqst.SETRANGE(Status,Status::Open);
        IF NOT WhsePickRqst.ISEMPTY THEN
          WhsePickRqst.DELETEALL(TRUE);

        COMMIT;
      END;
    END;

    [External]
    PROCEDURE Reopen@2(WhseShptHeader@1000 : Record 7320);
    VAR
      WhsePickRqst@1001 : Record 7325;
      PickWkshLine@1002 : Record 7326;
      WhseActivLine@1003 : Record 5767;
    BEGIN
      WITH WhseShptHeader DO BEGIN
        IF Status = Status::Open THEN
          EXIT;

        PickWkshLine.SETCURRENTKEY("Whse. Document Type","Whse. Document No.");
        PickWkshLine.SETRANGE("Whse. Document Type",PickWkshLine."Whse. Document Type"::Shipment);
        PickWkshLine.SETRANGE("Whse. Document No.","No.");
        IF NOT PickWkshLine.ISEMPTY THEN
          ERROR(Text001);

        WhseActivLine.SETCURRENTKEY("Whse. Document No.","Whse. Document Type","Activity Type");
        WhseActivLine.SETRANGE("Whse. Document No.","No.");
        WhseActivLine.SETRANGE("Whse. Document Type",WhseActivLine."Whse. Document Type"::Shipment);
        WhseActivLine.SETRANGE("Activity Type",WhseActivLine."Activity Type"::Pick);
        IF NOT WhseActivLine.ISEMPTY THEN
          ERROR(Text002);

        WhsePickRqst.SETRANGE("Document Type",WhsePickRqst."Document Type"::Shipment);
        WhsePickRqst.SETRANGE("Document No.","No.");
        WhsePickRqst.SETRANGE(Status,Status::Released);
        IF NOT WhsePickRqst.ISEMPTY THEN
          WhsePickRqst.MODIFYALL(Status,WhsePickRqst.Status::Open);

        Status := Status::Open;
        MODIFY;
      END;
    END;

    LOCAL PROCEDURE CreateWhsePickRqst@3(VAR WhseShptHeader@1000 : Record 7320);
    VAR
      WhsePickRqst@1001 : Record 7325;
      Location@1002 : Record 14;
    BEGIN
      WITH WhseShptHeader DO
        IF Location.RequirePicking("Location Code") THEN BEGIN
          WhsePickRqst."Document Type" := WhsePickRqst."Document Type"::Shipment;
          WhsePickRqst."Document No." := "No.";
          WhsePickRqst.Status := Status;
          WhsePickRqst."Location Code" := "Location Code";
          WhsePickRqst."Zone Code" := "Zone Code";
          WhsePickRqst."Bin Code" := "Bin Code";
          CALCFIELDS("Completely Picked");
          WhsePickRqst."Completely Picked" := "Completely Picked";
          IF NOT WhsePickRqst.INSERT THEN
            WhsePickRqst.MODIFY;
        END;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeRelease@4(VAR WarehouseShipmentHeader@1000 : Record 7320);
    BEGIN
    END;

    BEGIN
    {
      2019-07-12 TPZ2482 UCHOUHAN
        Changes for OverWrap Sevice Item.
    }
    END.
  }
}

