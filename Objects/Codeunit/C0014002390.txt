OBJECT Codeunit 14002390 E.D.I. Load Text Fields
{
  OBJECT-PROPERTIES
  {
    Date=07/13/20;
    Time=12:00:00 PM;
    Version List=SE0.62;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE LoadTextFields@1240030000(DestinationField@1240030000 : Text[250];LocEDIRecDocField@1240030001 : Record 14002359) ReturnTextField : Text;
    VAR
      SourceField@1240030002 : Text[250];
      Separator@1240020000 : Text[3];
      InterimTextField@1240020001 : Text[250];
    BEGIN
      CASE LocEDIRecDocField."Separator Type" OF
        LocEDIRecDocField."Separator Type"::" ":
          Separator := '';
        LocEDIRecDocField."Separator Type"::Space:
          Separator := '%1?';
        LocEDIRecDocField."Separator Type"::Hyphen:
          Separator := '-';
      END;

      ReturnTextField := '';
      SourceField := LocEDIRecDocField."Field Text Value";
      IF (NOT LocEDIRecDocField."Append to Field Contents") AND
         (NOT LocEDIRecDocField."Insert into Field Contents")
      THEN BEGIN
        IF LocEDIRecDocField."Copy String Position" = 0 THEN
          ReturnTextField := SourceField
        ELSE
          ReturnTextField :=
            COPYSTR(SourceField,LocEDIRecDocField."Copy String Position",
              LocEDIRecDocField."Copy String Length");
      END;

      IF LocEDIRecDocField."Append to Field Contents" THEN BEGIN
        IF LocEDIRecDocField."Copy String Position" <> 0 THEN
          ReturnTextField :=
            DestinationField + Separator +
            COPYSTR(SourceField,LocEDIRecDocField."Copy String Position",
              LocEDIRecDocField."Copy String Length")
        ELSE
          ReturnTextField :=
            DestinationField + Separator + SourceField;
      END;

      IF LocEDIRecDocField."Insert into Field Contents" THEN BEGIN
        ReturnTextField := DestinationField;
        IF LocEDIRecDocField."Copy String Length" <> 0 THEN BEGIN
          InterimTextField := COPYSTR(SourceField,LocEDIRecDocField."Copy String Position",
          LocEDIRecDocField."Copy String Length");
          IF ReturnTextField <> '' THEN BEGIN
            IF LocEDIRecDocField."Insert Position" > 1 THEN
              InterimTextField := Separator + InterimTextField + Separator
            ELSE
              InterimTextField := InterimTextField + Separator;
          END;
          ReturnTextField := INSSTR(ReturnTextField,InterimTextField,
            LocEDIRecDocField."Insert Position");
        END ELSE BEGIN
          IF ReturnTextField <> '' THEN BEGIN
            IF LocEDIRecDocField."Insert Position" > 1 THEN
              SourceField := Separator + SourceField + Separator
            ELSE
             SourceField := SourceField + Separator;
          END;
          ReturnTextField := INSSTR(ReturnTextField,SourceField,
           LocEDIRecDocField."Insert Position");
        END;
      END;
      CASE LocEDIRecDocField."Separator Type" OF
          LocEDIRecDocField."Separator Type"::Space:
            BEGIN
              ReturnTextField := STRSUBSTNO(ReturnTextField,' ','%1');
              ReturnTextField := DELCHR(ReturnTextField,'=', '?');
            END;
      END;
      EXIT(ReturnTextField);
    END;

    BEGIN
    END.
  }
}

