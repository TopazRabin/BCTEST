OBJECT Codeunit 14002602 Forecast Process
{
  OBJECT-PROPERTIES
  {
    Date=03/27/19;
    Time=12:00:00 PM;
    Version List=UBP3.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE AddSoldItems@1240030000(ForecastCust@1240030000 : Record 14002601;ProcUnit@1240030008 : Record 14000555);
    VAR
      ForecastCustItem@1240030001 : Record 14002602;
      UsageLedgerEntry@1240030002 : Record 14000556;
    BEGIN
      ForecastCust.TESTFIELD(Released,FALSE);

      UsageLedgerEntry.RESET;
      UsageLedgerEntry.SETCURRENTKEY(
        "Entry Type","Source No.","Location Code","Item No.","Variant Code",
        "Exclude from Usage","Usage Date","Forecast Customer");
      UsageLedgerEntry.SETRANGE("Entry Type",UsageLedgerEntry."Entry Type"::Sale);
      UsageLedgerEntry.SETRANGE("Source No.",ForecastCust."Customer No.");
      UsageLedgerEntry.SETRANGE("Location Code",ProcUnit."Location Code");
      UsageLedgerEntry.SETRANGE("Item No.",ProcUnit."Item No.");
      UsageLedgerEntry.SETRANGE("Variant Code",ProcUnit."Variant Code");
      IF UsageLedgerEntry.FIND('-') THEN
        REPEAT
          IF NOT ForecastCustItem.GET(
            UsageLedgerEntry."Source No.",UsageLedgerEntry."Location Code",
            UsageLedgerEntry."Item No.",UsageLedgerEntry."Variant Code") THEN BEGIN
            ForecastCustItem.INIT;
            ForecastCustItem."Customer No." := ForecastCust."Customer No.";
            ForecastCustItem."Location Code" := UsageLedgerEntry."Location Code";
            ForecastCustItem.VALIDATE("Item No.",UsageLedgerEntry."Item No.");
            ForecastCustItem.VALIDATE("Variant Code",UsageLedgerEntry."Variant Code");
            ForecastCustItem.INSERT(TRUE);
          END;
        UNTIL UsageLedgerEntry.NEXT = 0;
    END;

    PROCEDURE ForecastByCustomer@1240030001(CurrForecastCust@1240030000 : Record 14002601);
    VAR
      ForecastCustItem@1240030006 : Record 14002602;
      InvMgtSetup@1240030002 : Record 14000551;
      ItemUsage@1240030001 : Record 14000557;
      LocInvPeriod@1240030005 : Record 14000553;
      ProcUnit@1240030007 : Record 14000555;
      PeriodEndCalc@1240030003 : Codeunit 14000557;
      CreateProcUnit@1240020000 : Codeunit 14000575;
      UBP@1240020002 : Codeunit 14000564;
      ProcessWindow@1240030008 : Dialog;
      EntryNo@1240030004 : Integer;
      Text000@1240030009 : TextConst 'ENU=Reforecasting Customer Items';
      Text001@1240030010 : TextConst 'ENU=Customer';
      Text002@1240030011 : TextConst 'ENU=Location';
      Text003@1240030012 : TextConst 'ENU=Item';
      FirstTranDate@1240020001 : Date;
    BEGIN
      IF GUIALLOWED THEN
        ProcessWindow.OPEN(
          Text000 + '\' +
          PADSTR(Text001,20) + '#1#################' + '\' +
          PADSTR(Text002,20) + '#2#################' + '\' +
          PADSTR(Text003,20) + '#3#################');

      ItemUsage.RESET;
      ItemUsage.SETCURRENTKEY("Source No.");
      ItemUsage.SETRANGE("Source No.",CurrForecastCust."Customer No.");
      ItemUsage.DELETEALL;

      ItemUsage.RESET;
      IF ItemUsage.FIND('+') THEN
        EntryNo := ItemUsage."Entry No."
      ELSE
        EntryNo := 0;
      InvMgtSetup.GET;
      ForecastCustItem.RESET;
      ForecastCustItem.SETRANGE("Customer No.",CurrForecastCust."Customer No.");
      IF ForecastCustItem.FIND('-') THEN
        REPEAT
          IF GUIALLOWED THEN BEGIN
            ProcessWindow.UPDATE(1,ForecastCustItem."Customer No.");
            ProcessWindow.UPDATE(2,ForecastCustItem."Location Code");
            ProcessWindow.UPDATE(3,ForecastCustItem."Item No.");
          END;

          IF NOT ProcUnit.GET(ForecastCustItem."Location Code",ForecastCustItem."Item No.",
              ForecastCustItem."Variant Code") THEN BEGIN
            ProcUnit.INIT;
            ProcUnit."Location Code" := ForecastCustItem."Location Code";
            ProcUnit."Item No." := ForecastCustItem."Item No.";
            ProcUnit."Variant Code" := ForecastCustItem."Variant Code";
            ProcUnit.SetAutoInsert;
            ProcUnit."User ID" := USERID;
            ProcUnit."Date Created" := TODAY;
            ProcUnit.INSERT(TRUE);
            CreateProcUnit.CheckReplPath(ProcUnit);
          END;

          FirstTranDate := UBP.GetFirstStockedDate(ProcUnit,ForecastCustItem."Customer No.");

          LocInvPeriod.RESET;
          LocInvPeriod.SETFILTER("Starting Date",'%1..', FirstTranDate);
          LocInvPeriod.SETRANGE("Location Code",ForecastCustItem."Location Code");
          LocInvPeriod.SETRANGE("Period End Closed",TRUE);
          IF LocInvPeriod.FIND('-') THEN
            REPEAT
              EntryNo := EntryNo + 1;
              ItemUsage.INIT;
              ItemUsage."Entry No." := EntryNo;
              ItemUsage."Location Code" := ForecastCustItem."Location Code";
              ItemUsage."Item No." := ForecastCustItem."Item No.";
              ItemUsage."Variant Code" := ForecastCustItem."Variant Code";
              ItemUsage."Starting Date" := LocInvPeriod."Starting Date";
              ItemUsage."Ending Date" := LocInvPeriod."Ending Date";
              ItemUsage."Source No." := ForecastCustItem."Customer No.";
              ItemUsage.INSERT;
              ProcUnit.GET(ItemUsage."Location Code",ItemUsage."Item No.",ItemUsage."Variant Code");
              CLEAR(PeriodEndCalc);
              PeriodEndCalc.PeriodForecast(LocInvPeriod,ProcUnit,ForecastCustItem."Customer No.");
            UNTIL LocInvPeriod.NEXT = 0;
        UNTIL ForecastCustItem.NEXT = 0;
      IF GUIALLOWED THEN
        ProcessWindow.CLOSE;
    END;

    BEGIN
    END.
  }
}

