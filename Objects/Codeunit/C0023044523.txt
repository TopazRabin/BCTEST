OBJECT Codeunit 23044523 IWX User Permission Mgmt.
{
  OBJECT-PROPERTIES
  {
    Date=02/23/17;
    Time=[ 8:56:09 AM];
    Version List=IWX2.4.7684.0;
  }
  PROPERTIES
  {
    SingleInstance=No;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      recFunctionPerms@1000000000 : Record 23044514;
      rrefPerms@1000000002 : RecordRef;
      bGotUser@1000000001 : Boolean;
      bInitialized@1000000003 : Boolean;

    PROCEDURE isAllowed@1000000001(cdPermName@1000000000 : Code[40]) : Boolean;
    VAR
      nIndex@1000000001 : Integer;
    BEGIN
      // cdPermName corresponds to a field name in the user function perms table
      // this function iterates through the fields to find the name and return its value

      IF NOT bInitialized THEN initialize;

      IF bGotUser THEN BEGIN
        // start at 2 - field 1 is user ID
        FOR nIndex:=2 TO rrefPerms.FIELDCOUNT DO BEGIN
          IF UPPERCASE(rrefPerms.FIELDINDEX(nIndex).NAME) = cdPermName THEN
            EXIT(rrefPerms.FIELDINDEX(nIndex).VALUE);
        END;
      END;

      EXIT(FALSE);
    END;

    PROCEDURE initialize@1000000000();
    BEGIN
      bGotUser := recFunctionPerms.GET(USERID);
      rrefPerms.GETTABLE(recFunctionPerms);
      bInitialized := TRUE;
    END;

    PROCEDURE TestAllowed@1000000002(sPermName@1000000000 : Text[40]);
    VAR
      nIndex@1000000001 : Integer;
      txtError@1000000002 : TextConst 'ENU=User "%1" does not have the "%2" permission.';
    BEGIN
      // sPermName corresponds to a field name in the user function perms table
      // Throws an error if not allowed

      IF NOT isAllowed(sPermName) THEN
        ERROR(txtError, USERID, sPermName);
    END;

    BEGIN
    {
      ************************
      Copyright Notice
      This objects content is copyright of Insight Works 2011.  All rights reserved.
      Any redistribution or reproduction of part or all of the contents in any form is prohibited.
      ************************
    }
    END.
  }
}

