OBJECT Codeunit 23044534 WHI Job Queue Printing
{
  OBJECT-PROPERTIES
  {
    Date=04/15/19;
    Time=[ 6:21:39 PM];
    Version List=WHI2.4.7684.0;
  }
  PROPERTIES
  {
    TableNo=472;
    OnRun=VAR
            lrrefValue@1000000001 : RecordRef;
            lfrefFieldNo@1000000000 : FieldRef;
            lrecJobQueuePrinting@1000000003 : Record 23044530;
            lcuPrintingMgt@1000000005 : Codeunit 23044525;
            ltrecEventParams@1000000002 : TEMPORARY Record 23044511;
          BEGIN
            //  This codeunit will look up Job Queue Printing table
            //

            lrecJobQueuePrinting.RESET();
            lrecJobQueuePrinting.SETRANGE(Status,lrecJobQueuePrinting.Status::Queued );
            IF (lrecJobQueuePrinting.FINDSET(TRUE)) THEN BEGIN

              REPEAT
                lrrefValue.OPEN(lrecJobQueuePrinting."Table No.");

                IF ( lrecJobQueuePrinting.Filters <> '' ) THEN BEGIN
                  lrrefValue.SETVIEW(lrecJobQueuePrinting.Filters);
                  IF (lrrefValue.FIND('-')) THEN BEGIN
                    ltrecEventParams.INIT();
                    ltrecEventParams.setValue( 'event', FORMAT( -1 ) );
                    ltrecEventParams.setEvent(-1);
                    ltrecEventParams.setValue('recordid', FORMAT( lrrefValue.RECORDID ) );
                    ltrecEventParams.setValue('tableno', FORMAT( lrrefValue.NUMBER )  );
                    ltrecEventParams.setValue('Report No.', FORMAT( lrecJobQueuePrinting."Report No." )  );
                    ltrecEventParams.setValue('Printer Name', lrecJobQueuePrinting."Printer Name"  );

                    COMMIT();
                    lrecJobQueuePrinting.Status := lrecJobQueuePrinting.Status::Errored; // default to errored.

                    IF ( CODEUNIT.RUN( CODEUNIT::"WHI Printing Management", ltrecEventParams ) ) THEN BEGIN
                      lrecJobQueuePrinting.Status := lrecJobQueuePrinting.Status::Printed;
                      lrecJobQueuePrinting."Printed DateTime" := CURRENTDATETIME;
                    END ELSE BEGIN
                      lrecJobQueuePrinting.Status := lrecJobQueuePrinting.Status::Errored;
                      lrecJobQueuePrinting.Message :=
                        COPYSTR( GETLASTERRORTEXT(), 1, MAXSTRLEN(lrecJobQueuePrinting.Message  ) );
                      lrecJobQueuePrinting."Printed DateTime" := CURRENTDATETIME;
                    END;
                  END ELSE BEGIN
                    lrecJobQueuePrinting.Status := lrecJobQueuePrinting.Status::Errored;
                    lrecJobQueuePrinting.Message :=
                      COPYSTR( STRSUBSTNO(cuTranslationMgmt.getTranslatedString('ERR_NO_RECORDS_FOUND_IN_FILTER', ''),lrrefValue.GETFILTERS), 1, MAXSTRLEN(lrecJobQueuePrinting.Message  ) );
                    lrecJobQueuePrinting."Printed DateTime" := CURRENTDATETIME;
                  END;
                END ELSE BEGIN
                  lrecJobQueuePrinting.Message := COPYSTR( cuTranslationMgmt.getTranslatedString('ERR_FILTER_MANDATORY', ''), 1, MAXSTRLEN(lrecJobQueuePrinting.Message) );
                  lrecJobQueuePrinting.Status := lrecJobQueuePrinting.Status::Errored;

                END;
                lrecJobQueuePrinting.MODIFY(TRUE);
                lrrefValue.CLOSE();
              UNTIL (lrecJobQueuePrinting.NEXT = 0);
            END;
          END;

  }
  CODE
  {
    VAR
      cuTranslationMgmt@1000000000 : Codeunit 23044526;

    BEGIN
    {
      ************************
      Copyright Notice
      This objects are copyright of Insight Works 2011.  All rights reserved.
      Any redistribution or reproduction of part or all of the contents in any form is prohibited.
      ************************
    }
    END.
  }
}

