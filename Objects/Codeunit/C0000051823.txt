OBJECT Codeunit 51823 Page7324EventSubscriber
{
  OBJECT-PROPERTIES
  {
    Date=06/30/21;
    Time=12:26:10 PM;
    Modified=Yes;
    Version List=NAVEVT0.0.0,3224;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [EventSubscriber(Page,7324,OnBeforeActionEvent,Action35)]
    LOCAL PROCEDURE "Pg7324_Register&Print"@1000000023(VAR Rec@1000000000 : Record 7311);
    BEGIN
      //<TPZ2861>
      Pg7324_ReasonCodeCheck(Rec);
      //</TPZ2861>
    END;

    [EventSubscriber(Page,7324,OnBeforeActionEvent,Action34)]
    LOCAL PROCEDURE Pg7324_Register@1000000000(VAR Rec@1000000000 : Record 7311);
    BEGIN
      //<TPZ2861>
      Pg7324_ReasonCodeCheck(Rec);
      //</TPZ2861>
    END;

    PROCEDURE Pg7324_ReasonCodeCheck@1000000001(Rec@1000000001 : Record 7311);
    VAR
      WarehouseJournalLine@1000000000 : Record 7311;
    BEGIN
      //<TPZ1974>
      WarehouseJournalLine.COPY(Rec);
      WarehouseJournalLine.SETRANGE("Journal Template Name",Rec."Journal Template Name");
      WarehouseJournalLine.SETRANGE("Journal Batch Name",Rec."Journal Batch Name");
      IF WarehouseJournalLine.FINDSET THEN BEGIN
        REPEAT
          WarehouseJournalLine.TESTFIELD("Reason Code");
        UNTIL WarehouseJournalLine.NEXT = 0;
      END;
      //</TPZ1974>
    END;

    [EventSubscriber(Page,7324,OnAfterActionEvent,"Import Entries from Excel")]
    LOCAL PROCEDURE PG7324_ImportEntriesFromExcel@1000000002(VAR Rec@1000000000 : Record 7311);
    BEGIN
      ImportExcelFile(Rec);
    END;

    LOCAL PROCEDURE ImportExcelFile@1000000022(WhseJnlLine@1000000019 : Record 7311);
    VAR
      ExcelBuff@1000000006 : TEMPORARY Record 370;
      Window@1000000005 : Dialog;
      ServerFileName@1000000004 : Text;
      SheetName@1000000003 : Text[250];
      RecNo@1000000002 : Integer;
      TotalRecNo@1000000001 : Integer;
      RowNo@1000000000 : Integer;
      FileMgt@1000000007 : Codeunit 419;
      Text51000@1000000010 : TextConst 'ENU=Import Excel File;ESM=Importar fich. Excel;FRC=Importer fichier Excel;ENC=Import Excel File';
      Text51001@1000000009 : TextConst 'ENU=Importing Data...\\;ESM=Analizar Datos...\\;FRC=Analyse des donn‚es...\\;ENC=Analysing Data...\\';
      ExcelExtensionTok@1000000008 : TextConst '@@@={Locked};ENU=.xlsx;ESM=.xlsx;FRC=.xlsx;ENC=.xlsx';
      IntegerRec@1000000011 : Record 2000000026;
      unitCost@1000000018 : Decimal;
      QuantityVar@1000000012 : Decimal;
      ColText@1000000014 : ARRAY [40] OF Text[250];
    BEGIN
      ServerFileName := FileMgt.UploadFile(Text51000,ExcelExtensionTok);
      IF ServerFileName = '' THEN
        EXIT;

      SheetName := ExcelBuff.SelectSheetsName(ServerFileName);
      IF SheetName = '' THEN
        EXIT;
      ExcelBuff.LOCKTABLE;
      ExcelBuff.OpenBook(ServerFileName,SheetName);
      ExcelBuff.ReadSheet;

      Window.OPEN(Text51000 + '@1@@@@@@@@@@@@@@@@@@@@@@@@@\');
      TotalRecNo := ExcelBuff.COUNT;
      IntegerRec.RESET;
      IntegerRec.SETRANGE(Number,2,TotalRecNo);
      REPEAT
        CLEAR(ColText);
        ExcelBuff.SETRANGE("Row No.",IntegerRec.Number);
        IF ExcelBuff.FIND('-') THEN
          REPEAT
            ColText[ExcelBuff."Column No."] := ExcelBuff."Cell Value as Text";
          UNTIL ExcelBuff.NEXT = 0;
          //Item no, Location code, Quantity, Unit Cost, Bin Code
          IF ColText[1] <> '' THEN BEGIN
            IF ColText[3] <> '' THEN EVALUATE(QuantityVar,ColText[3]);
            IF ColText[4] <> '' THEN EVALUATE(unitCost,ColText[4]);
            CreateWhseItemJournal(WhseJnlLine,ColText[1],ColText[2],QuantityVar,unitCost,ColText[5]);
          END;
      UNTIL IntegerRec.NEXT = 0;
      ExcelBuff.DELETEALL;
    END;

    LOCAL PROCEDURE CreateWhseItemJournal@1000000004(WhseJnlLine@1000000000 : Record 7311;ItemNoPara@1000000001 : Code[20];LocCodePara@1000000002 : Code[10];QuantityPara@1000000003 : Decimal;UnitCostPara@1000000004 : Decimal;BinCodePara@1000000005 : Code[20]);
    VAR
      WhseJnlLineLoc@1000000006 : Record 7311;
    BEGIN
      WhseJnlLineLoc.INIT;
      WhseJnlLineLoc."Journal Template Name" := WhseJnlLine."Journal Template Name";
      WhseJnlLineLoc."Journal Batch Name" := WhseJnlLine."Journal Batch Name";
      WhseJnlLineLoc."Location Code" := WhseJnlLine."Location Code";
      WhseJnlLineLoc."Line No." := GetLine(WhseJnlLineLoc."Journal Template Name",WhseJnlLineLoc."Journal Batch Name",WhseJnlLineLoc."Location Code");
      WhseJnlLineLoc.SetUpNewLine(WhseJnlLineLoc);
      WhseJnlLineLoc."Whse. Document No." := WhseJnlLine."Journal Batch Name";
      //WhseJnlLineLoc.VALIDATE("Entry Type" , WhseJnlLineLoc."Entry Type"::"Positive Adjmt.");
      WhseJnlLineLoc.VALIDATE("Registering Date",WORKDATE);
      WhseJnlLineLoc.VALIDATE("Item No.", ItemNoPara);
      WhseJnlLineLoc.VALIDATE("Zone Code",'PICKPUT');
      WhseJnlLineLoc.VALIDATE("Bin Code",BinCodePara);
      WhseJnlLineLoc.VALIDATE(Quantity,QuantityPara);
      WhseJnlLineLoc."Reason Code" := 'ADJUSTMENT';
      WhseJnlLineLoc.INSERT(TRUE);
    END;

    LOCAL PROCEDURE GetLine@1000000003(JnlTemplate@1000000000 : Code[10];JnlBatch@1000000001 : Code[10];LocCode@1000000002 : Code[10]) : Integer;
    VAR
      WhseJnlLineLoc@1000000003 : Record 7311;
    BEGIN
      WhseJnlLineLoc.RESET;
      WhseJnlLineLoc.SETRANGE("Journal Template Name",JnlTemplate);
      WhseJnlLineLoc.SETRANGE("Journal Batch Name",JnlBatch);
      WhseJnlLineLoc.SETRANGE("Location Code",LocCode);
      IF WhseJnlLineLoc.FINDLAST THEN
        EXIT(WhseJnlLineLoc."Line No." + 1000)
      ELSE
        EXIT(10000);
    END;

    BEGIN
    {
      TPZ2861 RSAH 05192020
        - Event Conversion
    }
    END.
  }
}

