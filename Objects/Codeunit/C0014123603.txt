OBJECT Codeunit 14123603 ES Manual Relation Management
{
  OBJECT-PROPERTIES
  {
    Date=04/30/18;
    Time=12:00:00 PM;
    Version List=ES1.41.51;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Manual Relation already exists.';
      Text002@1240520009 : TextConst 'ENU=Adding Manual Relations\';
      Text003@1240520008 : TextConst 'ENU=Process #1################################################';
      Text004@1002 : TextConst 'ENU=%1 Manual Relations have been inserted.';
      ESSecuritySetup@1240520001 : Record 14123521;

    PROCEDURE AddManualRelation@1240520009(CurrentESRelation@1240520012 : Record 14123601);
    VAR
      ESRelation@1240520008 : Record 14123601;
      ESInsertManualRelation@1240520009 : Page 14123604;
    BEGIN
      CLEAR(ESInsertManualRelation);
      ESInsertManualRelation.SetRelationInfo(
        FALSE,CurrentESRelation."Object Type",CurrentESRelation."Object ID",
        CurrentESRelation."Relation Object Type",CurrentESRelation."Relation Object ID",
        CurrentESRelation."Source Code");
      ESInsertManualRelation.LOOKUPMODE(TRUE);
      IF ESInsertManualRelation.RUNMODAL = ACTION::LookupOK THEN BEGIN
        CLEAR(ESRelation);
        ESInsertManualRelation.GetRelattionInfo(
          ESRelation."Object Type",ESRelation."Object ID",
          ESRelation."Relation Object Type",ESRelation."Relation Object ID",
          ESRelation."Source Code");
        ESRelation.Type := ESRelation.Type::Manual;

        AddManualRelation2(ESRelation,TRUE);
      END;
    END;

    PROCEDURE AddManualRelation2@1240520017(ESRelation@1240520014 : Record 14123601;GiveError@1240520011 : Boolean) : Boolean;
    VAR
      ESRelation2@1240520010 : Record 14123601;
    BEGIN
      ESRelation2.RESET;
      ESRelation2.SETRANGE("Object Type",ESRelation."Object Type");
      ESRelation2.SETRANGE("Object ID",ESRelation."Object ID");
      ESRelation2.SETRANGE("Relation Object ID",ESRelation."Relation Object ID");
      ESRelation2.SETRANGE(Type,ESRelation2.Type::Manual);
      IF ESRelation2.FIND('-') THEN
        IF GiveError THEN
          ERROR(Text001)
        ELSE
          EXIT(FALSE);

      ESRelation2.RESET;
      ESRelation2.SETRANGE("Object Type",ESRelation."Object Type");
      ESRelation2.SETRANGE("Object ID",ESRelation."Object ID");
      IF ESRelation2.FIND('-') THEN
        IF ESRelation2."Entry No." < 0 THEN
          ESRelation."Entry No." := ESRelation2."Entry No." - 1
        ELSE
          ESRelation."Entry No." := -1
      ELSE
        ESRelation."Entry No." := -1;
      ESRelation.Origin := ESRelation.Origin::Manual;

      ESRelation.INSERT(TRUE);

      CASE ESRelation."Object Type" OF
        ESRelation."Object Type"::TableData:
          BEGIN
            ESRelation."Object Type" := ESRelation."Object Type"::Table;
            IF NOT ESRelation.INSERT(TRUE) THEN
              ;
          END;
        ESRelation."Object Type"::Table:
          BEGIN
            ESRelation."Object Type" := ESRelation."Object Type"::TableData;
            IF NOT ESRelation.INSERT(TRUE) THEN
              ;
          END;
      END;

      EXIT(TRUE);
    END;

    PROCEDURE AddSelectedToManualRelations@1240520014(VAR CurrentESRelation@1240520008 : Record 14123601;ShowStatus@1240520009 : Boolean);
    VAR
      ESRelation@1240520010 : Record 14123601;
      Window@1240520012 : Dialog;
      InsertedRecords@1240520011 : Integer;
    BEGIN
      IF GUIALLOWED THEN
        Window.OPEN(ESSecuritySetup.ExtendProgressText(Text002) + Text003);

      IF CurrentESRelation.FIND('+') THEN
        REPEAT
          IF GUIALLOWED THEN
            Window.UPDATE(
              1,STRSUBSTNO('%1 %2',CurrentESRelation."Object Type",CurrentESRelation."Object ID"));

          CLEAR(ESRelation);
          ESRelation."Object Type" := CurrentESRelation."Object Type";
          ESRelation."Object ID" := CurrentESRelation."Object ID";
          ESRelation."Relation Object ID" := CurrentESRelation."Relation Object ID";
          ESRelation."Source Code" := CurrentESRelation."Source Code";
          ESRelation.Type := ESRelation.Type::Manual;
          IF AddManualRelation2(ESRelation,FALSE) THEN
            InsertedRecords := InsertedRecords + 1;
        UNTIL CurrentESRelation.NEXT(-1) = 0;

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(Text004,InsertedRecords);
    END;

    PROCEDURE AddManualLiveRelation@1240520010(CurrentESLiveRelation@1240520012 : Record 14123602);
    VAR
      ESLiveRelation@1240520008 : Record 14123602;
      ESInsertManualRelation@1240520009 : Page 14123604;
    BEGIN
      CLEAR(ESInsertManualRelation);
      ESInsertManualRelation.SetRelationInfo(
        TRUE,CurrentESLiveRelation."Object Type",CurrentESLiveRelation."Object ID",
        CurrentESLiveRelation."Relation Object Type",CurrentESLiveRelation."Relation Object ID",
        CurrentESLiveRelation."Source Code");
      ESInsertManualRelation.LOOKUPMODE(TRUE);
      IF ESInsertManualRelation.RUNMODAL = ACTION::LookupOK THEN BEGIN
        CLEAR(ESLiveRelation);
        ESInsertManualRelation.GetRelattionInfo(
          ESLiveRelation."Object Type",ESLiveRelation."Object ID",
          ESLiveRelation."Relation Object Type",ESLiveRelation."Relation Object ID",
          ESLiveRelation."Source Code");
        ESLiveRelation.Type := ESLiveRelation.Type::Manual;

        AddManualLiveRelation2(ESLiveRelation,TRUE);
      END;
    END;

    PROCEDURE AddManualLiveRelation2@1240520012(ESLiveRelation@1240520013 : Record 14123602;GiveError@1240520008 : Boolean) : Boolean;
    VAR
      ESLiveRelation2@1240520010 : Record 14123602;
    BEGIN
      ESLiveRelation2.RESET;
      ESLiveRelation2.SETRANGE("Object Type",ESLiveRelation."Object Type");
      ESLiveRelation2.SETRANGE("Object ID",ESLiveRelation."Object ID");
      ESLiveRelation2.SETRANGE("Relation Object ID",ESLiveRelation."Relation Object ID");
      ESLiveRelation2.SETRANGE(Type,ESLiveRelation2.Type::Manual);
      IF ESLiveRelation2.FIND('-') THEN
        IF GiveError THEN
          ERROR(Text001)
        ELSE
          EXIT(FALSE);

      ESLiveRelation2.RESET;
      ESLiveRelation2.SETRANGE("Object Type",ESLiveRelation."Object Type");
      ESLiveRelation2.SETRANGE("Object ID",ESLiveRelation."Object ID");
      IF ESLiveRelation2.FIND('-') THEN
        IF ESLiveRelation2."Entry No." < 0 THEN
          ESLiveRelation."Entry No." := ESLiveRelation2."Entry No." - 1
        ELSE
          ESLiveRelation."Entry No." := -1
      ELSE
        ESLiveRelation."Entry No." := -1;
      ESLiveRelation.Origin := ESLiveRelation.Origin::Manual;

      ESLiveRelation.INSERT(TRUE);

      CASE ESLiveRelation."Object Type" OF
        ESLiveRelation."Object Type"::TableData:
          BEGIN
            ESLiveRelation."Object Type" := ESLiveRelation."Object Type"::Table;
            ESLiveRelation.INSERT(TRUE);
          END;
        ESLiveRelation."Object Type"::Table:
          BEGIN
            ESLiveRelation."Object Type" := ESLiveRelation."Object Type"::TableData;
            ESLiveRelation.INSERT(TRUE);
          END;
      END;

      EXIT(TRUE);
    END;

    PROCEDURE AddSelectedToLiveManRelations@1240520016(VAR CurrentESLiveRelation@1240520008 : Record 14123602;ShowStatus@1240520009 : Boolean);
    VAR
      ESLiveRelation@1240520010 : Record 14123602;
      Window@1240520012 : Dialog;
      InsertedRecords@1240520011 : Integer;
    BEGIN
      IF GUIALLOWED THEN
        Window.OPEN(ESSecuritySetup.ExtendProgressText(Text002) + Text003);

      IF CurrentESLiveRelation.FIND('+') THEN
        REPEAT
          IF GUIALLOWED THEN
            Window.UPDATE(
              1,
              STRSUBSTNO(
                '%1 %2',CurrentESLiveRelation."Object Type",CurrentESLiveRelation."Object ID"));

          CLEAR(ESLiveRelation);
          ESLiveRelation."Object Type" := CurrentESLiveRelation."Object Type";
          ESLiveRelation."Object ID" := CurrentESLiveRelation."Object ID";
          ESLiveRelation."Relation Object ID" := CurrentESLiveRelation."Relation Object ID";
          ESLiveRelation."Source Code" := CurrentESLiveRelation."Source Code";
          ESLiveRelation.Type := ESLiveRelation.Type::Manual;
          IF AddManualLiveRelation2(ESLiveRelation,FALSE) THEN
            InsertedRecords := InsertedRecords + 1;
        UNTIL CurrentESLiveRelation.NEXT(-1) = 0;

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(Text004,InsertedRecords);
    END;

    PROCEDURE LookupObjectID@1240520011(VAR ObjectType@1240520009 : Option;VAR ObjectID@1240520008 : Integer;VAR ObjectCaption@1240520010 : Text[1000];UseLive@1240520013 : Boolean);
    VAR
      ESObjectInfo@1240520012 : Record 14123509;
      AllObjWithCaption@1240520011 : Record 2000000058;
    BEGIN
      IF UseLive THEN BEGIN
        CLEAR(AllObjWithCaption);
        AllObjWithCaption."Object Type" := ObjectType;
        AllObjWithCaption."Object ID" := ObjectID;
        AllObjWithCaption.SETRANGE("Object Type",ObjectType);
        IF PAGE.RUNMODAL(PAGE::"ES Live Object Informations",AllObjWithCaption) = ACTION::LookupOK THEN BEGIN
          ObjectType := AllObjWithCaption."Object Type";
          ObjectID := AllObjWithCaption."Object ID";
          ObjectCaption := AllObjWithCaption."Object Caption";
        END;
      END ELSE BEGIN
        CLEAR(ESObjectInfo);
        ESObjectInfo."Object Type" := ObjectType;
        ESObjectInfo."Object ID" := ObjectID;
        ESObjectInfo.SETRANGE("Object Type",ObjectType);
        IF PAGE.RUNMODAL(0,ESObjectInfo) = ACTION::LookupOK THEN BEGIN
          ObjectType := ESObjectInfo."Object Type";
          ObjectID := ESObjectInfo."Object ID";
          ObjectCaption := ESObjectInfo."Object Caption";
        END;
      END;
    END;

    PROCEDURE LookupTableID@1240520008(VAR TableID@1240520009 : Integer;VAR TableCaption@1240520008 : Text[1000];UseLive@1240520012 : Boolean);
    VAR
      ESObjectInfo@1240520011 : Record 14123509;
      AllObjWithCaption@1240520010 : Record 2000000058;
    BEGIN
      IF UseLive THEN BEGIN
        CLEAR(AllObjWithCaption);
        AllObjWithCaption."Object ID" := TableID;
        AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::Table);
        IF PAGE.RUNMODAL(PAGE::"ES Live Object Informations",AllObjWithCaption) = ACTION::LookupOK THEN BEGIN
          AllObjWithCaption.TESTFIELD("Object Type",AllObjWithCaption."Object Type"::Table);
          TableID := AllObjWithCaption."Object ID";
          TableCaption := AllObjWithCaption."Object Caption";
        END;
      END ELSE BEGIN
        CLEAR(ESObjectInfo);
        ESObjectInfo."Object ID" := TableID;
        ESObjectInfo.SETRANGE("Object Type",ESObjectInfo."Object Type"::Table);
        IF PAGE.RUNMODAL(0,ESObjectInfo) = ACTION::LookupOK THEN BEGIN
          ESObjectInfo.TESTFIELD("Object Type",ESObjectInfo."Object Type"::Table);
          TableID := ESObjectInfo."Object ID";
          TableCaption := ESObjectInfo."Object Caption";
        END;
      END;
    END;

    PROCEDURE GetCaption@1240520013(ObjectType@1240520009 : Option;ObjectID@1240520008 : Integer;UseLive@1240520012 : Boolean) : Text[1000];
    VAR
      ESObjectInfo@1240520011 : Record 14123509;
      AllObjWithCaption@1240520010 : Record 2000000058;
    BEGIN
      IF UseLive THEN BEGIN
        IF NOT AllObjWithCaption.GET(ObjectType,ObjectID) THEN
          CLEAR(AllObjWithCaption);
        EXIT(AllObjWithCaption."Object Caption");
      END ELSE BEGIN
        IF NOT ESObjectInfo.GET(ObjectType,ObjectID) THEN
          CLEAR(ESObjectInfo);
        EXIT(ESObjectInfo."Object Caption");
      END;
    END;

    BEGIN
    END.
  }
}

