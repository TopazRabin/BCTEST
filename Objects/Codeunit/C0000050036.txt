OBJECT Codeunit 50036 Purge Warehouse Shipment
{
  OBJECT-PROPERTIES
  {
    Date=07/01/20;
    Time=12:00:00 AM;
    Modified=Yes;
    Version List=TPZ000.00.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            WhseShptHeader@1000000000 : Record 7320;
            AllowToDelete@1000000001 : Boolean;
          BEGIN
            WITH WhseShptHeader DO BEGIN
              IF FINDSET THEN
              REPEAT
                AllowToDelete := AllowDeleteWhseShpt(WhseShptHeader);


                IF AllowToDelete THEN BEGIN
                  IF WhseShptHeader.Status <> WhseShptHeader.Status::Open THEN BEGIN
                    WhseShptHeader.Status := WhseShptHeader.Status::Open;
                    WhseShptHeader.MODIFY;
                  END;
                  WhseShptHeader.DELETE(TRUE);
                END;
              UNTIL NEXT = 0;
              //MESSAGE('DONE');
            END;
          END;

  }
  CODE
  {

    PROCEDURE FindOpenPick@1000000003(VAR WhseShptLine@1000000000 : Record 7321) : Boolean;
    VAR
      WhseActLine@1000000002 : Record 5767;
    BEGIN
      WhseActLine.SETCURRENTKEY("Source Type","Source Subtype","Source No.","Source Line No.");
      WhseActLine.SETRANGE("Source Type", WhseShptLine."Source Type");
      WhseActLine.SETRANGE("Source Subtype", WhseShptLine."Source Subtype");
      WhseActLine.SETRANGE("Source No.", WhseShptLine."Source No.");
      IF WhseActLine.ISEMPTY = FALSE THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE FindNothingPacked@1000000006(VAR WhseShptLine@1000000002 : Record 7321) : Boolean;
    VAR
      Package@1000000001 : Record 14000701;
    BEGIN
      Package.SETCURRENTKEY("Source Type","Source Subtype","Source ID","Location Code");
      Package.SETRANGE("Source Type", WhseShptLine."Source Type" - 1);
      Package.SETRANGE("Source Subtype", WhseShptLine."Source Subtype");
      Package.SETRANGE("Source ID", WhseShptLine."Source No.");

      IF Package.ISEMPTY = TRUE THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE FindPickedButNotShipped@1000000008(VAR WarehouseShipmentHeader@1000000000 : Record 7320) : Boolean;
    VAR
      WhseShipmentLine@1000000001 : Record 7321;
    BEGIN
      WhseShipmentLine.SETRANGE("No.", WarehouseShipmentHeader."No.");
      IF WhseShipmentLine.FINDSET THEN
      REPEAT
         IF WhseShipmentLine."Qty. Shipped" < WhseShipmentLine."Qty. Picked" THEN
           EXIT(TRUE);
      UNTIL WhseShipmentLine.NEXT = 0;
      EXIT(FALSE);
    END;

    PROCEDURE AllowDeleteWhseShpt@1000000015(VAR WhseShptHeader@1000000001 : Record 7320) : Boolean;
    VAR
      WhseShipmentLine@1000000002 : Record 7321;
      WhseShptLine@1000000004 : Record 7321;
    BEGIN
      WhseShptLine.SETCURRENTKEY("No.", "Source Type", "Source Subtype", "Source No.", "Source Line No.");
      WhseShptLine.SETRANGE("No.", WhseShptHeader."No.");
      IF WhseShptLine.FIND('-') THEN
      REPEAT
        WhseShptLine.SETRANGE("Source Type", WhseShptLine."Source Type");
        WhseShptLine.SETRANGE("Source Subtype", WhseShptLine."Source Subtype");
        WhseShptLine.SETRANGE("Source No.", WhseShptLine."Source No.");
        WhseShptLine.FIND('+');

        //Don't delete if Pick Ticket exist.
        IF FindOpenPick(WhseShptLine) THEN
          EXIT(FALSE);

        //<TPZ1914>
       // IF FindRegisteredPick(WhseShptLine) THEN
         // EXIT(FALSE);
        //</TPZ1914>

        //check if order is packed or not.
        IF FindNothingPacked(WhseShptLine) = FALSE THEN
          EXIT(FALSE);

        WhseShptLine.SETRANGE("Source Type");
        WhseShptLine.SETRANGE("Source Subtype");
        WhseShptLine.SETRANGE("Source No.");

      UNTIL WhseShptLine.NEXT = 0;


      //Do not delete if there's any picked but not shipped items.
      IF FindPickedButNotShipped(WhseShptHeader) THEN
        EXIT(FALSE);

      //MESSAGE('Allow To Delete: %1', AllowToDelete);
      //EXIT;

      EXIT(TRUE);
    END;

    PROCEDURE FindRegisteredPick@1000000002(VAR WhseShptLine@1000000000 : Record 7321) : Boolean;
    VAR
      RegisteredWhseActivityLine@1000000002 : Record 5773;
    BEGIN
      //<TPZ1914>
      RegisteredWhseActivityLine.SETCURRENTKEY("Source Type","Source Subtype","Source No.","Source Line No.");
      RegisteredWhseActivityLine.SETRANGE("Source Type", WhseShptLine."Source Type");
      RegisteredWhseActivityLine.SETRANGE("Source Subtype", WhseShptLine."Source Subtype");
      RegisteredWhseActivityLine.SETRANGE("Source No.", WhseShptLine."Source No.");
      IF RegisteredWhseActivityLine.ISEMPTY = FALSE THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
      //</TPZ1914>
    END;

    BEGIN
    {
      2017-08-21 TPZ1914 DKUMAR
        Shedule Daily Job to Purge Warehouse Shipment Document
      2018-07-19 TPZ1914 UCHOUHAN
        Added Function to find Registered pick lines.
    }
    END.
  }
}

