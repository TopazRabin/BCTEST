OBJECT Codeunit 52006 Dynamics.is JSon Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=09/01/21;
    Time=[ 2:31:54 PM];
    Modified=Yes;
    Version List=;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      StringBuilder@1002 : DotNet "'mscorlib'.System.Text.StringBuilder";
      StringWriter@1004 : DotNet "'mscorlib'.System.IO.StringWriter";
      StringReader@1003 : DotNet "'mscorlib'.System.IO.StringReader";
      JsonTextWriter@1006 : DotNet "'Newtonsoft.Json, Version=4.5.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.JsonTextWriter";
      JsonTextReader@1001 : DotNet "'Newtonsoft.Json, Version=4.5.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.JsonTextReader";

    LOCAL PROCEDURE Initialize@15();
    VAR
      Formatting@1000 : DotNet "'Newtonsoft.Json, Version=4.5.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Formatting";
    BEGIN
      StringBuilder := StringBuilder.StringBuilder;
      StringWriter := StringWriter.StringWriter(StringBuilder);
      JsonTextWriter := JsonTextWriter.JsonTextWriter(StringWriter);
      JsonTextWriter.Formatting := Formatting.Indented;
    END;

    PROCEDURE StartJSon@1();
    BEGIN
      IF ISNULL(StringBuilder) THEN
        Initialize;
      JsonTextWriter.WriteStartObject;
    END;

    PROCEDURE StartJSonArray@14();
    BEGIN
      IF ISNULL(StringBuilder) THEN
        Initialize;
      JsonTextWriter.WriteStartArray;
    END;

    PROCEDURE AddJSonBranch@6(BranchName@1001 : Text);
    BEGIN
      JsonTextWriter.WritePropertyName(BranchName);
      JsonTextWriter.WriteStartObject;
    END;

    PROCEDURE AddToJSon@4(VariableName@1001 : Text;Variable@1002 : Variant);
    BEGIN
      JsonTextWriter.WritePropertyName(VariableName);
      JsonTextWriter.WriteValue(FORMAT(Variable,0,9));
    END;

    PROCEDURE EndJSonBranch@7();
    BEGIN
      JsonTextWriter.WriteEndObject;
    END;

    PROCEDURE EndJSonArray@19();
    BEGIN
      JsonTextWriter.WriteEndArray;
    END;

    PROCEDURE EndJSon@3();
    BEGIN
      JsonTextWriter.WriteEndObject;
    END;

    PROCEDURE GetJSon@20() JSon : Text;
    BEGIN
      JSon := StringBuilder.ToString;
      Initialize;
    END;

    PROCEDURE ReadJSon@5(VAR String@1000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.String";VAR TempPostingExchField@1002 : TEMPORARY Record 1221);
    VAR
      JsonToken@1001 : DotNet "'Newtonsoft.Json, Version=4.5.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.JsonToken";
      PrefixArray@1006 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array";
      PrefixString@1005 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.String";
      PropertyName@1008 : Text;
      ColumnNo@1003 : Integer;
      InArray@1007 : ARRAY [250] OF Boolean;
    BEGIN
      PrefixArray := PrefixArray.CreateInstance(GETDOTNETTYPE(String),250);
      StringReader := StringReader.StringReader(String);
      JsonTextReader := JsonTextReader.JsonTextReader(StringReader);
      WHILE JsonTextReader.Read DO
        CASE TRUE OF
          JsonTextReader.TokenType.CompareTo(JsonToken.StartObject) = 0 :
            ;
          JsonTextReader.TokenType.CompareTo(JsonToken.StartArray) = 0 :
            BEGIN
              InArray[JsonTextReader.Depth + 1] := TRUE;
              ColumnNo := 0;
            END;
          JsonTextReader.TokenType.CompareTo(JsonToken.StartConstructor) = 0 :
            ;
          JsonTextReader.TokenType.CompareTo(JsonToken.PropertyName) = 0 :
            BEGIN
              PrefixArray.SetValue(JsonTextReader.Value,JsonTextReader.Depth - 1);
              IF JsonTextReader.Depth > 1 THEN BEGIN
                PrefixString := PrefixString.Join('.',PrefixArray,0,JsonTextReader.Depth - 1);
                IF PrefixString.Length > 0 THEN
                  PropertyName := PrefixString.ToString + '.' + FORMAT(JsonTextReader.Value,0,9)
                ELSE
                  PropertyName := FORMAT(JsonTextReader.Value,0,9);
              END ELSE
                PropertyName := FORMAT(JsonTextReader.Value,0,9);
            END;
          JsonTextReader.TokenType.CompareTo(JsonToken.String) = 0 ,
          JsonTextReader.TokenType.CompareTo(JsonToken.Integer) = 0 ,
          JsonTextReader.TokenType.CompareTo(JsonToken.Float) = 0 ,
          JsonTextReader.TokenType.CompareTo(JsonToken.Boolean) = 0 ,
          JsonTextReader.TokenType.CompareTo(JsonToken.Date) = 0 ,
          JsonTextReader.TokenType.CompareTo(JsonToken.Bytes) = 0 :
            BEGIN
              {TempPostingExchField."Posting Exch. No." := JsonTextReader.Depth;
              TempPostingExchField."Line No." := JsonTextReader.LineNumber;
              TempPostingExchField."Column No." := ColumnNo;
              TempPostingExchField."Node ID" := PropertyName;
              TempPostingExchField.Value := FORMAT(JsonTextReader.Value,0,9);
              TempPostingExchField."Posting Exch. Line Def Code" := JsonTextReader.TokenType.ToString;
              TempPostingExchField.INSERT;}// vah
            END;
          JsonTextReader.TokenType.CompareTo(JsonToken.EndConstructor) = 0 :
            ;
          JsonTextReader.TokenType.CompareTo(JsonToken.EndArray) = 0 :
            InArray[JsonTextReader.Depth + 1] := FALSE;
          JsonTextReader.TokenType.CompareTo(JsonToken.EndObject) = 0 :
            IF JsonTextReader.Depth > 0 THEN
              IF InArray[JsonTextReader.Depth] THEN ColumnNo += 1;
        END;
    END;

    PROCEDURE ReadFirstJSonValue@10000200(VAR String@10000200 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.String";ParameterName@10000201 : Text) ParameterValue : Text;
    VAR
      JsonToken@10000203 : DotNet "'Newtonsoft.Json, Version=4.5.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.JsonToken";
      PropertyName@10000202 : Text;
    BEGIN
      StringReader := StringReader.StringReader(String);
      JsonTextReader := JsonTextReader.JsonTextReader(StringReader);
      WHILE JsonTextReader.Read DO
        CASE TRUE OF
          JsonTextReader.TokenType.CompareTo(JsonToken.PropertyName) = 0 :
            PropertyName := FORMAT(JsonTextReader.Value,0,9);
          (PropertyName = ParameterName) AND NOT ISNULL(JsonTextReader.Value)  :
            BEGIN
              ParameterValue := FORMAT(JsonTextReader.Value,0,9);
              EXIT;
            END;
        END;
    END;

    PROCEDURE UploadJSon@8(WebServiceURL@1002 : Text;UserName@1001 : Text;Password@1009 : Text;VAR String@1013 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.String");
    VAR
      HttpWebRequest@1007 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebRequest";
      HttpWebResponse@1006 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.WebResponse";
    BEGIN
      CreateWebRequest(HttpWebRequest,WebServiceURL,'POST');
      CreateCredentials(HttpWebRequest,UserName,Password);
      SetRequestStream(HttpWebRequest,String);
      DoWebRequest(HttpWebRequest,HttpWebResponse,'');
      GetResponseStream(HttpWebResponse,String);
    END;

    PROCEDURE DownloadString@10(Url@1001 : Text;UserName@1003 : Text;Password@1004 : Text) : Text;
    VAR
      WebClient@1000 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.WebClient";
      Credential@1002 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.NetworkCredential";
    BEGIN
      Credential := Credential.NetworkCredential;
      Credential.UserName := UserName;
      Credential.Password := Password;

      WebClient := WebClient.WebClient;
      WebClient.Credentials := Credential;
      EXIT(WebClient.DownloadString(Url));
    END;

    PROCEDURE CreateWebRequest@13(VAR HttpWebRequest@1000 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebRequest";WebServiceURL@1002 : Text;Method@1001 : Text);
    BEGIN
      HttpWebRequest := HttpWebRequest.Create(WebServiceURL);
      HttpWebRequest.Timeout := 30000;
      HttpWebRequest.Method := Method;
      HttpWebRequest.Accept := 'application/json';
    END;

    PROCEDURE CreateCredentials@16(VAR HttpWebRequest@1001 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebRequest";UserName@1002 : Text;Password@1003 : Text);
    VAR
      Credential@1000 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.NetworkCredential";
    BEGIN
      Credential := Credential.NetworkCredential;
      Credential.UserName := UserName;
      Credential.Password := Password;
      HttpWebRequest.Credentials := Credential;
    END;

    PROCEDURE SetRequestStream@18(VAR HttpWebRequest@1000 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebRequest";VAR String@1013 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.String");
    VAR
      StreamWriter@1003 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.StreamWriter";
      Encoding@1004 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.Encoding";
    BEGIN
      StreamWriter := StreamWriter.StreamWriter(HttpWebRequest.GetRequestStream,Encoding.GetEncoding('iso8859-1'));
      StreamWriter.Write(String);
      StreamWriter.Close;
    END;

    PROCEDURE DoWebRequest@21(VAR HttpWebRequest@1000 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebRequest";VAR HttpWebResponse@1001 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.WebResponse";IgnoreCode@1003 : Code[10]);
    VAR
      NAVWebRequest@1008 : DotNet "'NAVWebRequest, Version=1.0.0.0, Culture=neutral, PublicKeyToken=f53f0925d26e1382'.NAVWebRequest.NAVWebRequest";
      HttpWebException@1005 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.WebException";
      HttpWebRequestError@1002 : TextConst 'ENU=Error: %1\%2;ISL=St”Ðuvilla: %1\%2';
    BEGIN
      NAVWebRequest := NAVWebRequest.NAVWebRequest;
      IF NOT NAVWebRequest.doRequest(HttpWebRequest,HttpWebException,HttpWebResponse) THEN
        IF (IgnoreCode = '') OR (STRPOS(HttpWebException.Message,IgnoreCode) = 0) THEN
          ERROR(HttpWebRequestError,HttpWebException.Status.ToString,HttpWebException.Message);
    END;

    PROCEDURE GetResponseStream@23(VAR HttpWebResponse@1000 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.WebResponse";VAR String@1013 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.String");
    VAR
      StreamReader@1011 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.StreamReader";
      MemoryStream@1001 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
    BEGIN
      StreamReader := StreamReader.StreamReader(HttpWebResponse.GetResponseStream);
      String := StreamReader.ReadToEnd;
    END;

    PROCEDURE GetValueFromJsonString@9(VAR String@1001 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.String";ParameterName@1002 : Text) : Text;
    VAR
      TempPostingExchField@1000 : TEMPORARY Record 1221;
    BEGIN
      ReadJSon(String,TempPostingExchField);
      EXIT(GetJsonValue(TempPostingExchField,ParameterName));
    END;

    PROCEDURE GetJsonValue@12(VAR TempPostingExchField@1001 : TEMPORARY Record 1221;ParameterName@1000 : Text) : Text;
    BEGIN
      WITH TempPostingExchField DO BEGIN
        SETRANGE("Node ID",ParameterName);
        IF FINDFIRST THEN EXIT(Value);
      END;
    END;

    BEGIN
    END.
  }
}

