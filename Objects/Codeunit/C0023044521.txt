OBJECT Codeunit 23044521 IWX Remove LP Usage
{
  OBJECT-PROPERTIES
  {
    Date=08/28/20;
    Time=[ 9:56:29 AM];
    Version List=IWX2.4.7684.0;
  }
  PROPERTIES
  {
    TableNo=23044507;
    OnRun=VAR
            lrecLPHeader@1000000000 : Record 23044505;
          BEGIN
            // delete the usage from the appropriate document
            IF( lrecLPHeader.GET("License Plate No.") ) THEN BEGIN
              lrecLPHeader."Processing State" := lrecLPHeader."Processing State"::Removing;
              lrecLPHeader.MODIFY;

              IF( lrecLPHeader."Source Document Type" IN [
                  lrecLPHeader."Source Document Type"::"Put-away",
                  lrecLPHeader."Source Document Type"::Pick,
                  lrecLPHeader."Source Document Type"::Movement,
                  lrecLPHeader."Source Document Type"::"Invt. Put-away",
                  lrecLPHeader."Source Document Type"::"Invt. Pick",
                  lrecLPHeader."Source Document Type"::"Invt. Movement"] ) THEN BEGIN //<IW author="h.z" date="4/18/17" issue="TFS3038" />
                removeWhseActivityLineUsage(lrecLPHeader,Rec);
              END
              ELSE IF( lrecLPHeader."Source Document Type" = lrecLPHeader."Source Document Type"::Receipt ) THEN BEGIN
                removeReceiptLineUsage(lrecLPHeader,Rec);
              END
              ELSE IF( lrecLPHeader."Source Document Type" = lrecLPHeader."Source Document Type"::"Purchase Order" ) THEN BEGIN
                removePurchaseLineUsage(lrecLPHeader,Rec);
              END
              ELSE IF( lrecLPHeader."Source Document Type" = lrecLPHeader."Source Document Type"::"Sales Order" ) THEN BEGIN
                removeSalesLineUsage(lrecLPHeader,Rec);
              END
              ELSE IF( (lrecLPHeader."Source Document Type" = lrecLPHeader."Source Document Type"::"Inbound Transfer") OR
                       (lrecLPHeader."Source Document Type" = lrecLPHeader."Source Document Type"::"Outbound Transfer") ) THEN BEGIN
                removeTransferLineUsage(lrecLPHeader,Rec);
              END
              ELSE IF( lrecLPHeader."Source Document Type" = lrecLPHeader."Source Document Type"::Shipment) THEN BEGIN
                removeShipmentLineUsage(lrecLPHeader,Rec);
              END
              ELSE IF( lrecLPHeader."Source Document Type" = lrecLPHeader."Source Document Type"::Reclass) THEN BEGIN
                removeReclassLineUsage(lrecLPHeader,Rec,TRUE);
              END
              ELSE IF( lrecLPHeader."Source Document Type" = lrecLPHeader."Source Document Type"::"Purchase Return Order") THEN BEGIN
                removePurchaseReturnLineUsage(lrecLPHeader,Rec);
              END;

              lrecLPHeader."Processing State" := lrecLPHeader."Processing State"::" ";
              lrecLPHeader.MODIFY;

            END;

            //resetShippedStatus(Rec);
          END;

  }
  CODE
  {
    VAR
      cuCommonBase@1000000005 : Codeunit 23044519;
      cuResMgmt@1000000001 : Codeunit 23044514;
      cuLPCommon@1000000000 : Codeunit 23044541;
      codPreLicensePlateNo@1000000004 : Code[20];
      codCurrentLicensePlateNo@1000000003 : Code[20];

    PROCEDURE removeWhseActivityLineUsage@1000000029(precLPHeader@1000000001 : Record 23044505;precLineUsage@1000000002 : Record 23044507);
    VAR
      lrecWhseActivityHeader@1000000000 : Record 5766;
      lrecWhseActivityLine@1000000004 : Record 5767;
      lbHeaderFound@1000000003 : Boolean;
      lrecLPLine@1000000005 : Record 23044506;
      lbAutoShip@1000000006 : Boolean;
      ltrecCommentLine@1000000007 : TEMPORARY Record 97;
      lrecItem@1000000009 : Record 27;
      lrecTrackingCode@1000000008 : Record 6502;
      lbWhseTracked@1000000010 : Boolean;
    BEGIN
      //////////////////////////////////////////////
      //<Summary>
      // Removes the usage of the License Plate from the Warehouse Acitivity document.
      //</Summary>
      //<Author>R.Trudeau</Author>
      //<Date>5/26/2011</Date>
      //<Issue>293</Issue>
      //<Version>WHI1.4</Version>
      //////////////////////////////////////////////

      lbHeaderFound := FALSE;

      // get the appropriate whse document
      IF( precLPHeader."Source Document Type" = precLPHeader."Source Document Type"::"Put-away" ) THEN
        lbHeaderFound := lrecWhseActivityHeader.GET(lrecWhseActivityHeader.Type::"Put-away",precLPHeader."Source Document No.")
      ELSE IF( precLPHeader."Source Document Type" = precLPHeader."Source Document Type"::Pick ) THEN
        lbHeaderFound := lrecWhseActivityHeader.GET(lrecWhseActivityHeader.Type::Pick,precLPHeader."Source Document No.")
      ELSE IF( precLPHeader."Source Document Type" = precLPHeader."Source Document Type"::Movement ) THEN
        lbHeaderFound := lrecWhseActivityHeader.GET(lrecWhseActivityHeader.Type::Movement,precLPHeader."Source Document No.")
      ELSE IF( precLPHeader."Source Document Type" = precLPHeader."Source Document Type"::"Invt. Put-away" ) THEN
        lbHeaderFound := lrecWhseActivityHeader.GET(lrecWhseActivityHeader.Type::"Invt. Put-away",precLPHeader."Source Document No.")
      ELSE IF( precLPHeader."Source Document Type" = precLPHeader."Source Document Type"::"Invt. Movement" ) THEN BEGIN
        lbHeaderFound := lrecWhseActivityHeader.GET(lrecWhseActivityHeader.Type::"Invt. Movement",precLPHeader."Source Document No.")
      END
      ELSE
        lbHeaderFound := lrecWhseActivityHeader.GET(lrecWhseActivityHeader.Type::"Invt. Pick",precLPHeader."Source Document No.");

      IF( lbHeaderFound ) THEN BEGIN
        IF (lrecWhseActivityLine.GET(lrecWhseActivityHeader.Type,lrecWhseActivityHeader."No.",precLineUsage."Source Line No.")) THEN BEGIN

          lbWhseTracked := FALSE;
          lrecItem.GET(lrecWhseActivityLine."Item No.");
          IF(lrecTrackingCode.GET(lrecItem."Item Tracking Code") ) THEN BEGIN
            IF (lrecTrackingCode."Lot Warehouse Tracking" OR lrecTrackingCode."SN Warehouse Tracking") THEN BEGIN
              lbWhseTracked := TRUE;
              lrecWhseActivityLine."Lot No." := '';
              lrecWhseActivityLine."Serial No." := '';
            END;
          END;

          lrecWhseActivityLine.VALIDATE("Qty. to Handle", lrecWhseActivityLine."Qty. to Handle" - precLineUsage.Quantity);
          lrecWhseActivityLine.MODIFY(TRUE);

          lbAutoShip := lrecWhseActivityHeader.Type = lrecWhseActivityHeader.Type::Pick;
          lrecLPLine.GET(precLineUsage."License Plate No.",precLineUsage."License Plate Line No.");

          IF( lbAutoShip AND (lrecWhseActivityLine."Action Type"=lrecWhseActivityLine."Action Type"::Take)) THEN BEGIN
            cuCommonBase.processAutoShip(
              lrecWhseActivityLine,
              lrecLPLine."Serial No.",
              lrecLPLine."Lot No.",
              TRUE,
              -precLineUsage.Quantity,
              lrecLPLine."Expiration Date"
              );//<IW author="h.z" date="10/21/16" issue="TFS2710" />
          END;

          //<IW author="Howie Zhao" date="10/08/15" issue="TFS2016" >
          // check it's inventory picks/put-away in this mehtod
          IF (((lrecLPLine."Lot No." <> '') OR (lrecLPLine."Serial No." <> '')) AND (NOT lbWhseTracked)) THEN BEGIN
            cuResMgmt.AddRemoveInvtPickPutAwayTrack(
                    lrecWhseActivityLine, lrecLPLine."Serial No.",
                    lrecLPLine."Lot No.", -precLineUsage.Quantity, 0D);
          END;

        END;
      END;
    END;

    PROCEDURE removeSalesLineUsage@1000000004(precLPHeader@1000000001 : Record 23044505;precLineUsage@1000000000 : Record 23044507);
    VAR
      lrecSalesLine@1000000002 : Record 37;
      lrecLPLine@1000000003 : Record 23044506;
      ltrecCommentLine@1000000004 : TEMPORARY Record 97;
      lrecNotUsedWhseActivityLine@1000000005 : Record 5767;
    BEGIN
      //////////////////////////////////////////////
      //<Summary>
      // Removes the usage of the License Plate from the Sales document.
      //</Summary>
      //<Author>R.Trudeau</Author>
      //<Date>6/3/2011</Date>
      //<Issue>293</Issue>
      //<Version>WHI1.4</Version>
      //////////////////////////////////////////////

      IF( lrecSalesLine.GET(lrecSalesLine."Document Type"::Order,precLPHeader."Source Document No.",precLineUsage."Source Line No.") ) THEN BEGIN

        IF( lrecSalesLine.Quantity > 0 ) THEN BEGIN
          IF( lrecSalesLine."Qty. to Ship" >= precLineUsage.Quantity) THEN
            lrecSalesLine.VALIDATE("Qty. to Ship",lrecSalesLine."Qty. to Ship" - precLineUsage.Quantity)
          ELSE
            lrecSalesLine.VALIDATE("Qty. to Ship",0); // just being extra safe
        END
        ELSE BEGIN
          IF( lrecSalesLine."Qty. to Ship" <= precLineUsage.Quantity ) THEN
            lrecSalesLine.VALIDATE("Qty. to Ship", lrecSalesLine."Qty. to Ship" - precLineUsage.Quantity)
          ELSE
            lrecSalesLine.VALIDATE("Qty. to Ship",0); // just being extra safe
        END;

        lrecSalesLine.MODIFY(TRUE);

        lrecLPLine.GET(precLineUsage."License Plate No.",precLineUsage."License Plate Line No.");

        // add/remove the serial number
        IF( (lrecLPLine."Lot No." <> '') OR (lrecLPLine."Serial No." <> '') ) THEN BEGIN
          cuResMgmt.AddRemoveSalesShipTracking(
            lrecSalesLine,
            lrecLPLine."Serial No.",
            lrecLPLine."Lot No.",
            -precLineUsage.Quantity, // usage is in document uom
            lrecSalesLine."Unit of Measure Code",
            lrecLPLine."Expiration Date",
            lrecNotUsedWhseActivityLine    //<IW author="h.z" date="02/24/16" issue="TFS2292" />

            );
        END;
      END;
    END;

    PROCEDURE removePurchaseLineUsage@1000000005(precLPHeader@1000000001 : Record 23044505;precLineUsage@1000000000 : Record 23044507);
    VAR
      lrecPurchaseLine@1000000002 : Record 39;
      lrecLPLine@1000000003 : Record 23044506;
      ltrecCommentLine@1000000004 : TEMPORARY Record 97;
    BEGIN
      //////////////////////////////////////////////
      //<Summary>
      // Removes the usage of the License Plate from the Purchase document.
      //</Summary>
      //<Author>R.Trudeau</Author>
      //<Date>6/3/2011</Date>
      //<Issue>293</Issue>
      //<Version>WHI1.4</Version>
      //////////////////////////////////////////////


      IF( lrecPurchaseLine.GET(lrecPurchaseLine."Document Type"::Order,precLPHeader."Source Document No.",precLineUsage."Source Line No.") )
        THEN BEGIN
        lrecPurchaseLine.VALIDATE("Qty. to Receive",lrecPurchaseLine."Qty. to Receive" - precLineUsage.Quantity);
        lrecPurchaseLine.MODIFY(TRUE);

        lrecLPLine.GET(precLineUsage."License Plate No.",precLineUsage."License Plate Line No.");
        IF( (lrecLPLine."Lot No." <> '') OR (lrecLPLine."Serial No." <> '') ) THEN BEGIN
          cuResMgmt.AddRemovePurchaseTracking(
            lrecPurchaseLine,
            lrecLPLine."Serial No.",
            lrecLPLine."Lot No.",
            -precLineUsage.Quantity,
            lrecPurchaseLine."Unit of Measure Code",//<IW author="h.z" date="05/17/16" issue="TFS2594" />
            0D);  //<IW author="h.z" date="02/19/15" issue="TFS1646" />
        END;
      END;
    END;

    PROCEDURE removeReceiptLineUsage@1000000006(precLPHeader@1000000001 : Record 23044505;precLineUsage@1000000000 : Record 23044507);
    VAR
      lrecReceiptLine@1000000002 : Record 7317;
      lrecLPLine@1000000004 : Record 23044506;
      ltrecCommentLine@1000000003 : TEMPORARY Record 97;
    BEGIN
      //////////////////////////////////////////////
      //<Summary>
      // Removes the usage of the License Plate from the Receipt document.
      //</Summary>
      //<Author>R.Trudeau</Author>
      //<Date>6/3/2011</Date>
      //<Issue>293</Issue>
      //<Version>WHI1.4</Version>
      //////////////////////////////////////////////


      IF( lrecReceiptLine.GET(precLPHeader."Source Document No.",precLineUsage."Source Line No.") ) THEN BEGIN
        lrecReceiptLine.VALIDATE("Qty. to Receive",lrecReceiptLine."Qty. to Receive" - precLineUsage.Quantity);
        lrecReceiptLine.MODIFY(TRUE);

        lrecLPLine.GET(precLineUsage."License Plate No.",precLineUsage."License Plate Line No.");

        // add/remove the serial number
        IF( (lrecLPLine."Lot No." <> '') OR (lrecLPLine."Serial No." <> '') ) THEN BEGIN
          cuResMgmt.AddRemoveWhseRecTracking(
            lrecReceiptLine,
            lrecLPLine."Serial No.",
            lrecLPLine."Lot No.",
            -precLineUsage.Quantity,
            0D, //<IW author="h.z" date="02/19/15" issue="TFS1646" />
            lrecReceiptLine."Unit of Measure Code"
            );
        END;
      END;
    END;

    PROCEDURE removeTransferLineUsage@1000000000(precLPHeader@1000000001 : Record 23044505;precLineUsage@1000000000 : Record 23044507);
    VAR
      lrecTransferLine@1000000002 : Record 5741;
      lbShipping@1000000003 : Boolean;
      lrecLPLine@1000000004 : Record 23044506;
      ltrecCommentLine@1000000005 : TEMPORARY Record 97;
    BEGIN
      //////////////////////////////////////////////
      //<Summary>
      // Removes the usage of the License Plate from the Transfer document.
      //</Summary>
      //<Author>R.Trudeau</Author>
      //<Date>6/6/2011</Date>
      //<Issue>293</Issue>
      //<Version>WHI1.4</Version>
      //////////////////////////////////////////////


      IF( lrecTransferLine.GET(precLPHeader."Source Document No.",precLineUsage."Source Line No.") ) THEN BEGIN
        lbShipping := precLineUsage."Source Document" = precLineUsage."Source Document"::"Outbound Transfer";

        IF( lbShipping ) THEN
          lrecTransferLine.VALIDATE("Qty. to Ship",lrecTransferLine."Qty. to Ship" - precLineUsage.Quantity)
        ELSE
          lrecTransferLine.VALIDATE("Qty. to Receive",lrecTransferLine."Qty. to Receive" - precLineUsage.Quantity);

        lrecTransferLine.MODIFY(TRUE);

        lrecLPLine.GET(precLineUsage."License Plate No.",precLineUsage."License Plate Line No.");

        IF( (lrecLPLine."Lot No." <> '') OR (lrecLPLine."Serial No." <> '') ) THEN BEGIN
          cuResMgmt.AddRemoveTransferTracking(
            lrecTransferLine,
            lrecLPLine."Serial No.",
            lrecLPLine."Lot No.",
            lbShipping,
            -precLineUsage.Quantity,
            0D);  //<IW author="h.z" date="02/19/15" issue="TFS1646" />
        END;
      END;
    END;

    PROCEDURE removeShipmentLineUsage@1000000001(precLPHeader@1000000001 : Record 23044505;precLineUsage@1000000000 : Record 23044507);
    VAR
      lrecShipmentLine@1000000002 : Record 7321;
      lrecLPLine@1000000004 : Record 23044506;
      ltrecCommentLine@1000000003 : TEMPORARY Record 97;
      lrecAssemblyBOM@1000000005 : Record 90;
      lrecItem@1000000006 : Record 27;
      ldQtyToRemove@1000000007 : Decimal;
      ldConvertedQuantity@1000000008 : Decimal;
      lbWhseSNRequired@1000000014 : Boolean;
      lbWhseLNRequired@1000000013 : Boolean;
      lrecLocation@1000000012 : Record 14;
      lbRemoveReservations@1000000011 : Boolean;
      lcuItemTrackingMgt@1000000010 : Codeunit 6500;
      lbWhseTracked@1000000009 : Boolean;
    BEGIN
      //////////////////////////////////////////////
      //<Summary>
      // Removes the usage of the License Plate from the Shipment document.
      //</Summary>
      //<Author>R.Trudeau</Author>
      //<Date>6/3/2011</Date>
      //<Issue>293</Issue>
      //<Version>WHI1.4</Version>
      //////////////////////////////////////////////

      IF( lrecShipmentLine.GET(precLPHeader."Source Document No.",precLineUsage."Source Line No.") ) THEN BEGIN
        lrecLPLine.GET(precLineUsage."License Plate No.",precLineUsage."License Plate Line No.");
        IF (lrecLPLine."No." = lrecShipmentLine."Item No.") THEN BEGIN
          lrecShipmentLine.VALIDATE("Qty. to Ship",lrecShipmentLine."Qty. to Ship" - precLineUsage.Quantity);
          lrecShipmentLine.MODIFY(TRUE);


          IF lrecLocation.GET(lrecShipmentLine."Location Code") THEN ;
          lcuItemTrackingMgt.CheckWhseItemTrkgSetup(lrecShipmentLine."Item No.",lbWhseSNRequired,lbWhseLNRequired,FALSE);
          lbWhseTracked := lbWhseSNRequired OR lbWhseLNRequired;
          lbRemoveReservations := ((lrecLPLine."Lot No." <> '') OR (lrecLPLine."Serial No." <> ''))
                                  AND
                                  ((lrecLocation."Require Pick" AND NOT lbWhseTracked) OR (NOT lrecLocation."Require Pick"));



          // add/remove the serial number
          IF(lbRemoveReservations) THEN BEGIN
            cuResMgmt.AddRemoveWhseShipTracking(
              lrecShipmentLine,
              lrecLPLine."Serial No.",
              lrecLPLine."Lot No.",
              -precLineUsage.Quantity,
              lrecShipmentLine."Unit of Measure Code",
              lrecLPLine."Expiration Date"
              );
          END;
        END ELSE BEGIN
          // Warehouse shipment with assembly order
          // Do not need to delete the item tracking lines, because the item tracking lines are in the assembly order
          // TODO: need to support item tracking for warehouse shipment items self
          codCurrentLicensePlateNo := precLineUsage."License Plate No.";
          IF (codCurrentLicensePlateNo <> codPreLicensePlateNo) THEN BEGIN
            lrecAssemblyBOM.SETRANGE("Parent Item No.", lrecShipmentLine."Item No.");
            lrecAssemblyBOM.SETRANGE("No.", lrecLPLine."No.");
            IF (lrecAssemblyBOM.FINDSET(FALSE)) THEN BEGIN
              lrecItem.GET(lrecLPLine."No.");
              ldConvertedQuantity := cuCommonBase.ConvertUnitOfMeasure(lrecItem, lrecLPLine.Quantity, lrecLPLine."Unit of Measure Code", lrecAssemblyBOM."Unit of Measure Code");
              ldQtyToRemove := ldConvertedQuantity / lrecAssemblyBOM."Quantity per";
              lrecShipmentLine.VALIDATE("Qty. to Ship", lrecShipmentLine."Qty. to Ship" - ldQtyToRemove);
              lrecShipmentLine.MODIFY(TRUE);
            END;
          END;

          codPreLicensePlateNo := precLineUsage."License Plate No.";
        END;
      END;
    END;

    PROCEDURE resetShippedStatus@1000000002(pcodLicensePlateNo@1000000000 : Code[20]);
    VAR
      lrecLineUsage@1000000001 : Record 23044507;
      lrecLPHeader@1000000002 : Record 23044505;
    BEGIN
      // deleting the line usage... if the last entry found (not including this one) is a shipment
      lrecLineUsage.SETRANGE("License Plate No.",pcodLicensePlateNo);
      //lrecLineUsage.SETFILTER("Entry No.",'<>%1',precLineUsage."Entry No.");
      IF( lrecLineUsage.FINDLAST ) THEN BEGIN
        IF( (lrecLineUsage."Source Document" = lrecLineUsage."Source Document"::"Sales Order") OR
            (lrecLineUsage."Source Document" = lrecLineUsage."Source Document"::"Outbound Transfer") ) THEN BEGIN
          IF( lrecLineUsage.Quantity > 0 ) THEN BEGIN
            lrecLPHeader.GET(pcodLicensePlateNo);
            lrecLPHeader.Status := lrecLPHeader.Status::Shipped;
            lrecLPHeader."Shipped Source Document" := lrecLineUsage."Source Document";
            lrecLPHeader."Shipped Source No." := lrecLineUsage."Source No.";
            lrecLPHeader.MODIFY;
          END;
        END;
      END;
    END;

    PROCEDURE removeReclassLineUsage@1000000007(precLPHeader@1000000001 : Record 23044505;precLineUsage@1000000000 : Record 23044507;pbForceItemTracking@1000000007 : Boolean);
    VAR
      lrecItemJnlLine@1000000002 : Record 83;
      lrecLPLine@1000000003 : Record 23044506;
      ltrecCommentLine@1000000004 : TEMPORARY Record 97;
      lcodTemplateName@1000000005 : Code[10];
      lbTryWhseReclass@1000000009 : Boolean;
      lrecWhseReclassLine@1000000010 : Record 7311;
    BEGIN
      //////////////////////////////////////////////
      //<Summary>
      // Removes the usage of the License Plate from the Reclass Journal.
      //</Summary>
      //<Author>R.Trudeau</Author>
      //<Date>16/12/2013</Date>
      //<Issue>TFS913</Issue>
      //<Version>WHI12.13</Version>
      //////////////////////////////////////////////

      IF (precLineUsage."Source No." <> '') THEN BEGIN
        lcodTemplateName := getTemplateByBatch(precLineUsage."Source No.");
      END ELSE BEGIN
        lcodTemplateName := cuCommonBase.getTemplate(PAGE::"Item Reclass. Journal");
      END;

      //<IW author="Howie Zhao" date="08/17/16" issue="TFS2620" >
      lbTryWhseReclass := FALSE;

      lrecLPLine.GET(precLineUsage."License Plate No.",precLineUsage."License Plate Line No.");
      IF( lrecItemJnlLine.GET(lcodTemplateName,precLPHeader."Source Document No.",precLineUsage."Source Line No.") ) THEN BEGIN
        //<IW author="Howie Zhao" date="08/17/16" issue="TFS2620" >
        IF (lrecItemJnlLine."Item No." = lrecLPLine."No.") THEN BEGIN
          IF( (lrecLPLine."Lot No." <> '') OR (lrecLPLine."Serial No." <> '') ) THEN BEGIN
            IF( cuCommonBase.isWhseTracked(lrecLPLine."No.") OR pbForceItemTracking) THEN BEGIN
              cuResMgmt.AddRemoveReclassTracking(
                lrecItemJnlLine,
                lrecLPLine."Serial No.",
                lrecLPLine."Lot No.",
                -precLineUsage.Quantity,
                lrecLPLine."Unit of Measure Code",   //<IW author="h.z" date="10/19/16" issue="TFS2699" />
                lrecLPLine."Expiration Date");  //<IW author="h.z" date="06/06/2017" issue="TFS3183" />
            END;
          END;

          //<IW author="Howie Zhao" date="03/19/14" issue="TFS1083" >
          // lrecItemJnlLine.VALIDATE(Quantity,lrecItemJnlLine.Quantity - precLineUsage.Quantity);
          // lrecItemJnlLine.MODIFY(TRUE);

          // IF( lrecItemJnlLine.Quantity = 0 ) THEN
          //  lrecItemJnlLine.DELETE;

          cuLPCommon.unassignReclassSpecificJnlLine(lrecItemJnlLine, lrecLPLine);     //<IW author="h.z" date="2/08/17" issue="TFS2774" />
        END ELSE BEGIN
          lbTryWhseReclass := TRUE;
        END;
      END ELSE BEGIN
        lbTryWhseReclass := TRUE;
      END;

      IF (lbTryWhseReclass) THEN BEGIN
        lcodTemplateName := cuCommonBase.getWhseTemplate(PAGE::"Whse. Reclassification Journal");
        IF (lrecWhseReclassLine.GET(lcodTemplateName, precLineUsage."Source No.", precLPHeader."Location Code", precLineUsage."Source Line No.")) THEN BEGIN
          IF( (lrecLPLine."Lot No." <> '') OR (lrecLPLine."Serial No." <> '') ) THEN BEGIN
            IF( cuCommonBase.isWhseTracked(lrecLPLine."No.") OR pbForceItemTracking) THEN BEGIN
              cuResMgmt.AddRemoveWhseReclassTracking(lrecWhseReclassLine, lrecLPLine."Serial No.", lrecLPLine."Lot No.", -precLineUsage.Quantity, lrecLPLine."Expiration Date");
            END;
          END;
        END;
      END;
    END;

    PROCEDURE getTemplateByBatch@1000000003(pcodBatchName@1000000000 : Code[20]) : Code[10];
    VAR
      lrecItemJnlTemplate@1000000001 : Record 233;
    BEGIN
      // Get template name by batch name
      //
      //

      lrecItemJnlTemplate.SETRANGE(Name, pcodBatchName);
      lrecItemJnlTemplate.SETRANGE("Template Type", lrecItemJnlTemplate."Template Type"::Transfer);
      lrecItemJnlTemplate.FINDFIRST;
      EXIT(lrecItemJnlTemplate."Journal Template Name");
    END;

    PROCEDURE removePurchaseReturnLineUsage@1000000008(precLPHeader@1000000001 : Record 23044505;precLineUsage@1000000000 : Record 23044507);
    VAR
      lrecPurchaseLine@1000000002 : Record 39;
      lrecLPLine@1000000003 : Record 23044506;
      ltrecCommentLine@1000000004 : TEMPORARY Record 97;
      lrecItem@1000000006 : Record 27;
      ldChangeQtyInBaseUOM@1000000005 : Decimal;
    BEGIN
      //////////////////////////////////////////////
      //<Summary>
      // Removes the usage of the License Plate from the Purchase return document.
      //</Summary>
      //<Author>H.Zhao</Author>
      //<Date>23/12/2014</Date>
      //<Issue>TFS1577</Issue>
      //<Version>WHI1.5</Version>
      //////////////////////////////////////////////


      IF( lrecPurchaseLine.GET(lrecPurchaseLine."Document Type"::"Return Order",precLPHeader."Source Document No.",
          precLineUsage."Source Line No.") )
      THEN BEGIN
        lrecPurchaseLine.VALIDATE("Return Qty. to Ship",lrecPurchaseLine."Return Qty. to Ship" - precLineUsage.Quantity);
        lrecPurchaseLine.MODIFY(TRUE);

        lrecLPLine.GET(precLineUsage."License Plate No.",precLineUsage."License Plate Line No.");

        IF( (lrecLPLine."Lot No." <> '') OR (lrecLPLine."Serial No." <> '') ) THEN BEGIN
          lrecItem.GET(lrecPurchaseLine."No.");
          ldChangeQtyInBaseUOM := cuCommonBase.ConvertUnitOfMeasure(lrecItem,
                                              -precLineUsage.Quantity,
                                              lrecPurchaseLine."Unit of Measure Code",  // from UOM
                                              lrecItem."Base Unit of Measure");     // to UOM
          cuResMgmt.AddRemovePurchaseReturnTrack(
            lrecPurchaseLine,
            lrecLPLine."Serial No.",
            lrecLPLine."Lot No.",
            ldChangeQtyInBaseUOM,
            0D);  //<IW author="h.z" date="02/19/15" issue="TFS1646" />
        END;
      END;
    END;

    BEGIN
    {
      ************************
      Copyright Notice
      This objects content is copyright of Insight Works 2011.  All rights reserved.
      Any redistribution or reproduction of part or all of the contents in any form is prohibited.
      ************************
    }
    END.
  }
}

