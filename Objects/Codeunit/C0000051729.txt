OBJECT Codeunit 51729 Page40EventSubscriber
{
  OBJECT-PROPERTIES
  {
    Date=06/30/21;
    Time=12:26:07 PM;
    Modified=Yes;
    Version List=NAVEVENT,3224;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [EventSubscriber(Page,40,OnBeforeActionEvent,Post)]
    PROCEDURE PG40_Post_OnAction@1000000002(VAR Rec@1000000000 : Record 83);
    BEGIN
      PG40_ReasonCodeCheck(Rec);
    END;

    [EventSubscriber(Page,40,OnBeforeActionEvent,Action35)]
    PROCEDURE "PG40_Post&Print_OnAction"@1000000003(VAR Rec@1000000000 : Record 83);
    BEGIN
      PG40_ReasonCodeCheck(Rec);
    END;

    PROCEDURE PG40_ReasonCodeCheck@1000000001(Rec@1000000001 : Record 83);
    VAR
      ItemJnlLine@1000000000 : Record 83;
    BEGIN
      //<TPZ1974>
      ItemJnlLine.COPY(Rec);
      ItemJnlLine.SETRANGE("Journal Template Name",Rec."Journal Template Name");
      ItemJnlLine.SETRANGE("Journal Batch Name",Rec."Journal Batch Name");
      IF ItemJnlLine.FINDSET THEN BEGIN
        REPEAT
          ItemJnlLine.TESTFIELD("Reason Code");
        UNTIL ItemJnlLine.NEXT = 0;
      END;
      //</TPZ1974>
    END;

    [EventSubscriber(Page,40,OnAfterActionEvent,"Update Reason Code")]
    LOCAL PROCEDURE PG40_UpdateReasonCode@1000000000(VAR Rec@1000000000 : Record 83);
    VAR
      Window@1000000001 : DotNet "'Microsoft.VisualBasic, Version=10.0.0.0, Culture=neutral, PublicKeyToken=b03f5f7f11d50a3a'.Microsoft.VisualBasic.Interaction" RUNONCLIENT;
      ReasonCodeLoc@1000000002 : Code[20];
      ItemJnlLine@1000000003 : Record 83;
    BEGIN
      //TPZ3224
      ReasonCodeLoc := Window.InputBox('Input Reason Code:', 'INPUT', '', 100, 100);
      IF ReasonCodeLoc = '' THEN
        EXIT;
      ItemJnlLine.SETRANGE("Journal Template Name",Rec."Journal Template Name");
      ItemJnlLine.SETRANGE("Journal Batch Name",Rec."Journal Batch Name");
      IF ItemJnlLine.FINDSET THEN
        ItemJnlLine.MODIFYALL("Reason Code",ReasonCodeLoc);
      MESSAGE('Updated');
    END;

    [EventSubscriber(Page,40,OnAfterActionEvent,"Import Unit Cost from Excel")]
    LOCAL PROCEDURE PG40_UpdateUnitCostFromExcel@1000000022(VAR Rec@1000000015 : Record 83);
    VAR
      ExcelBuff@1000000006 : TEMPORARY Record 370;
      Window@1000000005 : Dialog;
      ServerFileName@1000000004 : Text;
      SheetName@1000000003 : Text[250];
      RecNo@1000000002 : Integer;
      TotalRecNo@1000000001 : Integer;
      RowNo@1000000000 : Integer;
      FileMgt@1000000007 : Codeunit 419;
      Text51000@1000000010 : TextConst 'ENU=Import Excel File;ESM=Importar fich. Excel;FRC=Importer fichier Excel;ENC=Import Excel File';
      Text51001@1000000009 : TextConst 'ENU=Importing Data...\\;ESM=Analizar Datos...\\;FRC=Analyse des donn‚es...\\;ENC=Analysing Data...\\';
      ExcelExtensionTok@1000000008 : TextConst '@@@={Locked};ENU=.xlsx;ESM=.xlsx;FRC=.xlsx;ENC=.xlsx';
      IntegerRec@1000000011 : Record 2000000026;
      unitCost@1000000018 : Decimal;
      QuantityVar@1000000012 : Decimal;
      ColText@1000000014 : ARRAY [40] OF Text[250];
      ItemJnlLine@1000000013 : Record 83;
    BEGIN
      ServerFileName := FileMgt.UploadFile(Text51000,ExcelExtensionTok);
      IF ServerFileName = '' THEN
        EXIT;

      SheetName := ExcelBuff.SelectSheetsName(ServerFileName);
      IF SheetName = '' THEN
        EXIT;
      ExcelBuff.LOCKTABLE;
      ExcelBuff.OpenBook(ServerFileName,SheetName);
      ExcelBuff.ReadSheet;

      Window.OPEN(Text51000 + '@1@@@@@@@@@@@@@@@@@@@@@@@@@\');
      TotalRecNo := ExcelBuff.COUNT;
      IntegerRec.RESET;
      IntegerRec.SETRANGE(Number,2,TotalRecNo);
      REPEAT
        CLEAR(ColText);
        ExcelBuff.SETRANGE("Row No.",IntegerRec.Number);
        IF ExcelBuff.FIND('-') THEN
          REPEAT
            ColText[ExcelBuff."Column No."] := ExcelBuff."Cell Value as Text";
          UNTIL ExcelBuff.NEXT = 0;
          //Item no, Location code, Quantity, Unit Cost, Bin Code
          IF ColText[1] <> '' THEN BEGIN
            IF ColText[2] <> '' THEN EVALUATE(unitCost,ColText[4]);
            ItemJnlLine.RESET;
            ItemJnlLine.SETRANGE("Journal Template Name",Rec."Journal Template Name");
            ItemJnlLine.SETRANGE("Journal Batch Name",Rec."Journal Batch Name");
            ItemJnlLine.SETRANGE("Item No.",ColText[1]);
            IF ItemJnlLine.FINDFIRST THEN BEGIN
              ItemJnlLine.VALIDATE("Unit Cost",unitCost);
              ItemJnlLine.MODIFY;
            END;
          END;
      UNTIL IntegerRec.NEXT = 0;
      ExcelBuff.DELETEALL;
    END;

    BEGIN
    {
      2020-05008  TPZ2830  PSHUKLA Created new object
      001 TPZ3224 PKS 06182021 Added subscriber to update reason code in all lines and import unit cost from excel
    }
    END.
  }
}

