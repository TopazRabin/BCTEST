OBJECT Codeunit 14099049 OWR Menu Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=08/21/18;
    Time=12:00:00 PM;
    Version List=RF1.50.13;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            RunMenu('OWRTitleMenu');
          END;

  }
  CODE
  {
    VAR
      tempMenuLines@1240020000 : TEMPORARY Record 14099049;
      oReport@1240020001 : Integer;
      oCodeunit@1240020002 : Integer;
      oXMLPort@1240020003 : Integer;
      oPage@1240020004 : Integer;
      oSubMenu@1240020005 : Integer;
      ShowPickRequestOptions@1240020006 : Boolean;
      ShowWavePickOptions@1240020007 : Boolean;
      ShowEShipOptions@1240020008 : Boolean;
      ShowReplenOptions@1240020009 : Boolean;

    [External]
    PROCEDURE RunMenu@1240020006(MenuName@1240020000 : Text);
    BEGIN
      InitGlobals;
      CASE MenuName OF
        // OWR Menus:
        'OWRTitleMenu':      InitOWRTitleMenu;
        'OWRAppMenu':        InitOWRAppMenu;
        ELSE
          pubRunExtensionMenu(MenuName, tempMenuLines);
      END;
      PAGE.RUN(PAGE::"OWR Menus", tempMenuLines);
    END;

    [External]
    PROCEDURE InitOWRTitleMenu@1240020043();
    VAR
      ShowLine@1240020000 : Boolean;
    BEGIN
      InitGlobals;
      // ===================================================== OWR Title Main Menu =============================================================
      //    Line    Menu                               Object       Object                                                Submenu         Show
      //    No.     Caption                            Type         ID                                        ->  Bold    Name            Line
      //    ------  ---------------------------------  ----------  ---------------------------------------    --  ------  --------------- ------
      Line( 10000, 'Outbound Warehouse Request',        oSubMenu,  0,                                         0,  FALSE,  'OWRAppMenu',   TRUE);

      pubExtendOWRTitleMenu(tempMenuLines);
    END;

    LOCAL PROCEDURE InitOWRAppMenu@1240020048();
    VAR
      ReplaceMenu@1240020000 : Boolean;
      ShowLine@1240020001 : Boolean;
    BEGIN
      // ===================================================== OWR Application Menu ============================================================
      //    Line    Menu                               Object       Object                                                Submenu         Show
      //    No.     Caption                            Type         ID                                        ->  Bold    Name            Line
      //    ------  ---------------------------------  ----------  ---------------------------------------    --  ------  ----------------------
      Line( 20000,  'Lists',                           0,          0,                                         0,  TRUE,   '',             ShowPickRequestOptions);
      Line( 30000,  'Pick Requests',                   oPage,      PAGE::"Pick Request List",             0,  FALSE,  '',             ShowPickRequestOptions);
      Line( 40000,  '',                                0,          0,                                         0,  FALSE,  '',             ShowPickRequestOptions);

      Line(100000,  'Tasks',                           0,          0,                                         0,  TRUE,   '',             TRUE);
      Line(110000,  'Outbound Warehouse Request',      oCodeunit,  CODEUNIT::"Open Outbound Whse. Request",  0,  FALSE,  '',             TRUE);
      Line(120000,  '',                                0,          0,                                         0,  FALSE,  '',             TRUE);


      Line(200000,  'Reports and Analysis',            oReport,    0,                                         0,  TRUE,   '',             ShowPickRequestOptions);
      Line(210000,  'OWR Pick Request Line Report',    oReport,    REPORT::"OWR Pick Request Line Report",   0,  FALSE,  '',             ShowPickRequestOptions);
      Line(220000,  '',                                0,          0,                                         0,  FALSE,  '',             ShowPickRequestOptions);

      IF ShowPickRequestOptions OR ShowWavePickOptions THEN
        ShowLine := TRUE;
      Line(300000,  'History',                         0,          0,                                         0,  TRUE,   '',             ShowLine);
      Line(310000,  'Registered Pick Requests',        oPage,      PAGE::"Reg. Pick Request List",        0,  FALSE,  '',             ShowPickRequestOptions);
      Line(320000,  'Archived Pick Requests',          oPage,      PAGE::"Archive Pick Request List",       0,  FALSE,  '',             ShowPickRequestOptions);
      Line(330000,  'Reg. Wave Pick Whse. Requests',   oPage,      PAGE::"Reg. Wave Pick Whse. Requests",     0,  FALSE,  '',             ShowWavePickOptions);
      Line(340000,  '',                                0,          0,                                         0,  FALSE,  '',             ShowLine);

      Line(400000,  'Administration',                  0,          0,                                         0,  TRUE,   '',             TRUE);
      Line(410000,  'RF Setup',                       oPage,      PAGE::"RF Setup",                     0,  FALSE,  '',             TRUE);
      Line(420000,  'RF Employee Setup',              oPage,      PAGE::"RF Employee Setups",           0,  FALSE,  '',             TRUE);
      Line(430000,  'OWR Location Setup',              oPage,      PAGE::"OWR Location Setup List",       0,  FALSE,  '',             TRUE);
      Line(440000,  'OWR Actions',                     oPage,      PAGE::"Outbound Whse. Request Actions",                   0,  FALSE,  '',             TRUE);
      Line(450000,  'Outbound Whse. Request Filter',   oPage,      PAGE::"Outbound Whse. Request Filters",               0,  FALSE,  '',             TRUE);
      Line(460000,  'Replenishment Rules',             oPage,      PAGE::"Replenishment Rule List",       0,  FALSE,  '',             ShowReplenOptions);

      pubExtendOWRAppMenu(tempMenuLines);
    END;

    LOCAL PROCEDURE Line@1240020002(MenuLineNo@1240020000 : Integer;MenuCaption@1240020002 : Text;MenuObjType@1240020001 : Integer;MenuObjID@1240020003 : Integer;MenuIndent@1240020004 : Integer;MenuEmphasize@1240020005 : Boolean;MenuName@1240020006 : Text;ShowLine@1240020007 : Boolean);
    BEGIN
      IF ShowLine THEN
        WITH tempMenuLines DO BEGIN
          INIT;
          "Line No." := MenuLineNo;
          Caption := MenuCaption;
          "Object Type" := MenuObjType;
          "Object ID" := MenuObjID;
          Indentation := MenuIndent;
          Emphasize := MenuEmphasize;
          "Submenu Name" := MenuName;
          INSERT;
        END;
    END;

    LOCAL PROCEDURE InitGlobals@1240020001();
    BEGIN
      oReport := tempMenuLines."Object Type"::Report;
      oCodeunit := tempMenuLines."Object Type"::Codeunit;
      oXMLPort := tempMenuLines."Object Type"::XMLPort;
      oPage := tempMenuLines."Object Type"::Page;
      oSubMenu := tempMenuLines."Object Type"::Submenu;

      SetShowOptions;
    END;

    [External]
    PROCEDURE SetShowOptions@1240020000();
    VAR
      RFSetup@1240020000 : Record 14002801;
      OWRLocationSetup@1240020001 : Record 14099030;
      OutboundWhseRequestMgt@1240020003 : Codeunit 14002863;
      EShipInstalled@1240020002 : Boolean;
    BEGIN
      RFSetup.GET;

      IF RFSetup."Enable OWR Replenishments" THEN
        ShowReplenOptions := TRUE;

      IF RFSetup."Enable Wave Picks" THEN
        ShowWavePickOptions := TRUE;

      ShowPickRequestOptions := FALSE;
      OWRLocationSetup.FIND('-');
      REPEAT
        IF OWRLocationSetup."Use Pick Requests" THEN
          ShowPickRequestOptions := TRUE;
      UNTIL ShowPickRequestOptions OR (OWRLocationSetup.NEXT = 0);

      OutboundWhseRequestMgt.EShipInstalledEvent(EShipInstalled);
      IF EShipInstalled THEN
        ShowEShipOptions := TRUE;
    END;

    [Business]
    [External]
    PROCEDURE pubRunExtensionMenu@1240020029(MenuName@1240020000 : Text;VAR tempMenuLines@1240020001 : TEMPORARY Record 14099049);
    BEGIN
    END;

    [Business]
    [External]
    PROCEDURE pubExtendOWRTitleMenu@1240020049(VAR tempMenuLines@1240020000 : TEMPORARY Record 14099049);
    BEGIN
    END;

    [Business]
    [External]
    PROCEDURE pubExtendOWRAppMenu@1240020051(VAR tempMenuLines@1240020000 : TEMPORARY Record 14099049);
    BEGIN
    END;

    LOCAL PROCEDURE xMenuTemplate@1240020042();
    BEGIN
      {
      // ================================================================================================================================
      //    Line    Menu                               Object       Object                                                Submenu
      //    No.     Caption                            Type         ID                                        ->  Bold    Name
      //    ------  ---------------------------------  ----------  ---------------------------------------    --  ------  ---------------
      Line(  0000,  'Lists',                           0,          0,                                         0,  TRUE,   ''              );
      Line(  0000,  '',                                0,          0,                                         0,  FALSE,  ''              );
      Line(  0000,  '',                                0,          0,                                         0,  FALSE,  ''              );

      Line(  0000,  'Tasks',                           0,          0,                                         0,  TRUE,   ''              );
      Line(  0000,  '',                                0,          0,                                         0,  FALSE,  ''              );
      Line(  0000,  '',                                0,          0,                                         0,  FALSE,  ''              );

      Line(  0000,  'Reports and Analysis',            0,          0,                                         0,  TRUE,   ''              );
      Line(  0000,  '',                                0,          0,                                         0,  FALSE,  ''              );
      Line(  0000,  '',                                0,          0,                                         0,  FALSE,  ''              );

      Line(  0000,  'Administration',                  0,          0,                                         0,  TRUE,   ''              );
      Line(  0000,  '',                                0,          0,                                         0,  FALSE,  ''              );
      }
    END;

    BEGIN
    {
      Self-populating menu for Lanham Outbound Warehouse Request (OWR).
    }
    END.
  }
}

