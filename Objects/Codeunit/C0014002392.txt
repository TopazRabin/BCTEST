OBJECT Codeunit 14002392 E.D.I. Load DB Fields
{
  OBJECT-PROPERTIES
  {
    Date=07/13/20;
    Time=12:00:00 PM;
    Version List=SE0.62;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE LoadDBFields@1240020000(EDIElement@1240020000 : Record 14002355;VAR OutFldArray@1240020001 : ARRAY [100] OF Text[250];VAR DecimalVariable@1240020002 : Decimal;VAR IntegerVariable@1240020003 : Integer;VAR DateVariable@1240020004 : Date;VAR BooleanVariable@1240020005 : Boolean;VAR TimeVariable@1240020006 : Time;VAR DateTimeVariable@1240020007 : DateTime;i@1240020008 : Integer;TableRef@1240020013 : RecordRef);
    VAR
      Field@1240020019 : Record 2000000041;
      FieldRef@1240020017 : FieldRef;
      FieldValue@1240020011 : Text[750];
      FieldType@1240020010 : Text[100];
      NAVTableName@1240020014 : Text[40];
      TableNo@1240020012 : Integer;
      FieldNo@1240020009 : Integer;
      "0DF"@1240020015 : DateFormula;
    BEGIN
      TableNo := EDIElement."NAV Table No.";
      FieldNo := EDIElement."NAV Field No.";
      NAVTableName := EDIElement."NAV Table Name";
      CASE NAVTableName OF
        'Ship-to Country','Bill-to Country','Sell-to Country','Ship-from Country',
        'Pay-to Country','Buy-from Country':
          TableNo := 9;
        'Ship-to Location','Ship-from Location':
          TableNo := 14;
        'Bill-to Customer','Sell-to Customer':
          TableNo := 18;
        'Buy-from Vendor','Pay-to Vendor':
          TableNo := 23;
        'Purch. Comment Line Detail':
          TableNo := 43;
        'Sales Comment Line Detail':
          TableNo := 44;
        'Ship-for Address':
          TableNo := 222;
        'VAT Entry Detail':
          TableNo := 254;
      END;

      CLEAR(Field);
      Field.SETRANGE(TableNo,TableNo);
      Field.SETRANGE("No.",FieldNo);
      IF Field.FIND('-') THEN BEGIN
        IF Field.Class = Field.Class::FlowField THEN BEGIN
          FieldRef := TableRef.FIELD(Field."No.");
          FieldRef.CALCFIELD;
        END;

        FieldType := FORMAT(Field.Type);
        FieldValue := FORMAT(TableRef.FIELD(Field."No."));
        CASE UPPERCASE(FieldType) OF
          'BOOLEAN':
            EVALUATE(BooleanVariable,FieldValue);
          'CODE','TEXT','OPTION':
            OutFldArray[i] := FieldValue;
          'DATE':
            BEGIN
              EVALUATE(DateVariable,FieldValue);
              IF EDIElement."Forecast Date Formula" <> "0DF" THEN
                IF DateVariable <> 0D THEN
                   DateVariable :=  CALCDATE(EDIElement."Forecast Date Formula",DateVariable);
            END;
          'DATETIME':
            EVALUATE(DateTimeVariable,FieldValue);
          'DECIMAL':
            EVALUATE(DecimalVariable,FieldValue);
          'INTEGER':
            EVALUATE(IntegerVariable,FieldValue);
          'TIME':
            EVALUATE(TimeVariable,FieldValue);
        END;
      END;
      TableRef.CLOSE;
    END;

    BEGIN
    END.
  }
}

