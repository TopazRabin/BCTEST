OBJECT Codeunit 5497 Graph Mgt - Doc. Sent History
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      GraphMgtGeneralTools@1000 : Codeunit 5465;

    [EventSubscriber(Table,2158,OnAfterInsertEvent)]
    LOCAL PROCEDURE OnAfterInsertDocumentSentHistory@50(VAR Rec@1000 : Record 2158;RunTrigger@1001 : Boolean);
    BEGIN
      IF Rec.ISTEMPORARY THEN
        EXIT;

      IF NOT GraphMgtGeneralTools.IsApiEnabled THEN
        EXIT;

      SetLastEmailSentStatusFromDocumentSentHistory(Rec);
    END;

    [EventSubscriber(Table,2158,OnAfterModifyEvent)]
    LOCAL PROCEDURE OnAfterModifyDocumentSentHistory@1(VAR Rec@1000 : Record 2158;VAR xRec@1001 : Record 2158;RunTrigger@1002 : Boolean);
    BEGIN
      IF Rec.ISTEMPORARY THEN
        EXIT;

      IF NOT GraphMgtGeneralTools.IsApiEnabled THEN
        EXIT;

      SetLastEmailSentStatusFromDocumentSentHistory(Rec);
    END;

    LOCAL PROCEDURE SetLastEmailSentStatusQuote@3(VAR O365DocumentSentHistory@1000 : Record 2158);
    VAR
      SalesQuoteEntityBuffer@1002 : Record 5505;
      SalesHeader@1003 : Record 36;
    BEGIN
      IF NOT SalesHeader.GET(O365DocumentSentHistory."Document Type",O365DocumentSentHistory."Document No.") THEN
        EXIT;

      SalesQuoteEntityBuffer.LOCKTABLE;

      IF NOT SalesQuoteEntityBuffer.GET(O365DocumentSentHistory."Document No.") THEN
        EXIT;

      SalesHeader.CALCFIELDS("Last Email Sent Time","Last Email Sent Status");

      IF SalesQuoteEntityBuffer."Last Email Sent Status" = SalesHeader."Last Email Sent Status" THEN
        EXIT;

      SalesQuoteEntityBuffer."Last Email Sent Status" := SalesHeader."Last Email Sent Status";
      SalesQuoteEntityBuffer.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE SetLastEmailSentStatusSalesInvoice@52(VAR O365DocumentSentHistory@1000 : Record 2158);
    VAR
      SalesInvoiceEntityAggregate@1002 : Record 5475;
      SalesInvoiceHeader@1003 : Record 112;
    BEGIN
      IF NOT SalesInvoiceHeader.GET(O365DocumentSentHistory."Document No.") THEN
        EXIT;

      SalesInvoiceEntityAggregate.LOCKTABLE;

      IF NOT SalesInvoiceEntityAggregate.GET(O365DocumentSentHistory."Document No.",TRUE) THEN
        EXIT;

      SalesInvoiceHeader.CALCFIELDS("Last Email Sent Time","Last Email Sent Status");

      IF SalesInvoiceEntityAggregate."Last Email Sent Status" = SalesInvoiceHeader."Last Email Sent Status" THEN
        EXIT;

      SalesInvoiceEntityAggregate."Last Email Sent Status" := SalesInvoiceHeader."Last Email Sent Status";
      SalesInvoiceEntityAggregate.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE SetLastEmailSentStatusFromDocumentSentHistory@5(VAR O365DocumentSentHistory@1000 : Record 2158);
    BEGIN
      WITH O365DocumentSentHistory DO
        IF Posted AND ("Document Type" = "Document Type"::Invoice) THEN
          SetLastEmailSentStatusSalesInvoice(O365DocumentSentHistory)
        ELSE
          IF (NOT Posted) AND ("Document Type" = "Document Type"::Quote) THEN
            SetLastEmailSentStatusQuote(O365DocumentSentHistory);
    END;

    BEGIN
    END.
  }
}

