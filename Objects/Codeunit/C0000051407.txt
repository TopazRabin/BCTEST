OBJECT Codeunit 51407 Sales-Calc. Free Freight
{
  OBJECT-PROPERTIES
  {
    Date=07/01/20;
    Time=12:00:00 AM;
    Modified=Yes;
    Version List=TPZ000.00.00;
  }
  PROPERTIES
  {
    TableNo=36;
    OnRun=BEGIN
            "Free Freight" := IsFreeFreight(Rec);
          END;

  }
  CODE
  {
    VAR
      Item@1000000002 : Record 27;
      Cust@1000000005 : Record 18;
      CustDivision@1000000004 : Record 50007;
      FreeFreightMinOrderAmt@1000000000 : Record 51470;
      SalesLine@1000000003 : Record 37;
      ShippingAgent@1000000006 : Record 291;
      EShipAgentService@1000000007 : Record 14000708;
      Oversize@1000000001 : Boolean;

    PROCEDURE IsFreeFreight@1000000000(SalesHeader@1000000000 : Record 36) : Boolean;
    VAR
      AllLocFreeFreight@1000000002 : Boolean;
      CurrLocFreeFreight@1000000003 : Boolean;
      OneLocNotFreeFreight@1000000004 : Boolean;
      LocAmount@1000000001 : Decimal;
      MasterSalesOrder@1000000005 : Record 36;
      TEXTErrOverride@1000000006 : TextConst 'ENU=You Must Select a Free Freight Override Reason Code';
      OrderAmount@1000000007 : Decimal;
      SalesInvHeader@1000000008 : Record 112;
    BEGIN


      //<TPZ1608>
      IF (SalesHeader."Free Freight Override"=TRUE) AND (SalesHeader."Free Freight"=TRUE) AND (SalesHeader."Free Freight Override Reason"=SalesHeader."Free Freight Override Reason"::" ")  THEN
          ERROR(TEXTErrOverride);
      //</TPZ1608>

      //EB
      //<TPZ1711>
      // <TPZ874>
      // Return no free freight if E-Ship Agent Service is no set to No Free Freight
      IF ShippingAgent.GET(SalesHeader."Shipping Agent Code") AND
         EShipAgentService.GET(
           SalesHeader."Shipping Agent Code",
           SalesHeader."E-Ship Agent Service",
           EShipAgentService.WorldWideService(ShippingAgent,SalesHeader."Ship-to Country/Region Code")) AND
         EShipAgentService."No Free Freight"
      THEN
        EXIT(FALSE);
      // </TPZ874>
      //<TPZ1711>
      //<TPZ1548>


      //<TPZ1608><TPZ1999>
      IF SalesHeader."Free Freight Override"=TRUE THEN
         EXIT(SalesHeader."Free Freight");
      //</TPZ1608></TPZ1999>



      // Return free freight if Free Freight Reason Code is specified
      // <TPZ844>
      IF SalesHeader."Free Freight Reason Code" <> '' THEN
        EXIT(TRUE);
      // </TPZ844>


      // Determine if there are any oversize items in the order
      SalesLine.RESET;
      SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      SalesLine.SETRANGE(Type,SalesLine.Type::Item);
      IF SalesLine.FINDSET THEN
        REPEAT
          IF Item.GET(SalesLine."No.") AND
             Item.Oversize
          THEN
            Oversize := TRUE;
        UNTIL SalesLine.NEXT = 0;
      // Calculate order amount excluding tax
      SalesHeader.CALCFIELDS(Amount);



      // Return free freight if shipping agent is of pickup type
      // <TOP8717>
      //IF ShippingAgent.GET(SalesHeader."Shipping Agent Code") AND
      //   (ShippingAgent.Type = ShippingAgent.Type::Pickup)
      //THEN
      //  EXIT(TRUE);
      // </TOP8717>
      //<TPZ1548>
      IF SalesHeader."Master Order No."<>'' THEN BEGIN
         SalesInvHeader.SETRANGE("Order No.",SalesHeader."Master Order No.");
         IF SalesInvHeader.FINDFIRST THEN
            IF SalesInvHeader."Free Freight" THEN
               EXIT(TRUE);

         IF MasterSalesOrder.GET(SalesHeader."Document Type",SalesHeader."Master Order No.") THEN
           IF MasterSalesOrder."Free Freight" THEN
              EXIT(TRUE);

      END;
      //<TPZ1548>

      // Return free freight if customer is set to get Free Freight
      IF Cust.GET(SalesHeader."Sell-to Customer No.") AND
         Cust."Free Freight"
      THEN
        EXIT(TRUE);

      // Return free freight if customer for specific division is set to get Free Freight
      IF CustDivision.GET(SalesHeader."Sell-to Customer No.",SalesHeader."Shortcut Dimension 5 Code") AND
         CustDivision."Free Freight"
      THEN
        EXIT(TRUE);


      //<TPZ1608>
      IF SalesHeader."Free Freight Override"=TRUE THEN
         EXIT(TRUE);
      //</TPZ1608>





      //<TPZ1632>
          OrderAmount :=0;
          SalesLine.RESET;
          SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
          SalesLine.SETRANGE("Document No.",SalesHeader."No.");
          SalesLine.SETRANGE(Type,SalesLine.Type::Item);

          IF SalesLine.FINDSET THEN
            REPEAT
             OrderAmount :=OrderAmount + SalesLine.Amount; //<TPZ1632>
            UNTIL SalesLine.NEXT = 0;
      //</TPZ1632>

      // Return free freight if there is minimum order amount for customer and division
      IF CustDivision.GET(SalesHeader."Sell-to Customer No.",SalesHeader."Shortcut Dimension 5 Code") AND
         (CustDivision."Free Freight Min. Order Amount" <> 0)
      THEN
        IF OrderAmount >= CustDivision."Free Freight Min. Order Amount" THEN   //<TPZ1632>
          EXIT(TRUE)
        ELSE
          EXIT(FALSE);



      // Return free freight if there is minimum order amount for specific shipping agent (LOCAL)
      FreeFreightMinOrderAmt.RESET;
      FreeFreightMinOrderAmt.SETRANGE("Division Code",'');
      FreeFreightMinOrderAmt.SETRANGE("Shipping Agent Code",SalesHeader."Shipping Agent Code");
      IF FreeFreightMinOrderAmt.FINDFIRST AND
         (OrderAmount >= FreeFreightMinOrderAmt."Min. Order Amount")  //<TPZ1632>
      THEN
        EXIT(TRUE);

      // Return free freight if there is minimum order amount for specific division (I) and location (1|25|50)
      AllLocFreeFreight := FALSE;
      OneLocNotFreeFreight := FALSE;

      FreeFreightMinOrderAmt.RESET;
      FreeFreightMinOrderAmt.SETRANGE("Division Code",SalesHeader."Shortcut Dimension 5 Code");
      FreeFreightMinOrderAmt.SETRANGE("Shipping Agent Code",SalesHeader."Shipping Agent Code");
      FreeFreightMinOrderAmt.SETFILTER("Location Code",'<>%1','');
      IF FreeFreightMinOrderAmt.FINDSET THEN BEGIN
        REPEAT
          LocAmount := 0;
          SalesLine.RESET;
          SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
          SalesLine.SETRANGE("Document No.",SalesHeader."No.");
          SalesLine.SETRANGE(Type,SalesLine.Type::Item);
          SalesLine.SETRANGE("Location Code",FreeFreightMinOrderAmt."Location Code");
          IF SalesLine.FINDSET THEN
            REPEAT
              LocAmount := LocAmount + SalesLine.Amount;
            UNTIL SalesLine.NEXT = 0;
          CurrLocFreeFreight :=
            (LocAmount = 0) OR
            (LocAmount >= FreeFreightMinOrderAmt."Min. Order Amount");
          IF (LocAmount <> 0) AND
             (LocAmount < FreeFreightMinOrderAmt."Min. Order Amount")
          THEN
            OneLocNotFreeFreight := TRUE;
          AllLocFreeFreight := CurrLocFreeFreight AND NOT OneLocNotFreeFreight;
        UNTIL FreeFreightMinOrderAmt.NEXT = 0;
        EXIT(AllLocFreeFreight);
      END;

      // Return free freight if there is minimum order amount for specific division (E or L)
      FreeFreightMinOrderAmt.RESET;
      FreeFreightMinOrderAmt.SETRANGE("Division Code",SalesHeader."Shortcut Dimension 5 Code");
      FreeFreightMinOrderAmt.SETRANGE("Location Code",SalesHeader."Location Code");
      FreeFreightMinOrderAmt.SETRANGE(Oversize,Oversize);
      IF FreeFreightMinOrderAmt.FINDFIRST AND
         (OrderAmount >= FreeFreightMinOrderAmt."Min. Order Amount") //<TPZ1632>
      THEN
        EXIT(TRUE);

      // Return free freight in other cases (for all other divisions and locations)
      FreeFreightMinOrderAmt.RESET;
      FreeFreightMinOrderAmt.SETRANGE("Division Code",'');
      FreeFreightMinOrderAmt.SETRANGE("Location Code",'');
      FreeFreightMinOrderAmt.SETRANGE(Oversize,Oversize);
      IF FreeFreightMinOrderAmt.FINDFIRST AND
         (OrderAmount >= FreeFreightMinOrderAmt."Min. Order Amount")//<TPZ1632>
      THEN
        EXIT(TRUE);

      // Return not free freight if no conditions for free freight found
      EXIT(FALSE);
    END;

    PROCEDURE GetFreeFreightMinOrderAmount@1000000001(SalesHeader@1000000000 : Record 36) : Decimal;
    BEGIN
      // Determine if there are any oversize items in the order
      SalesLine.RESET;
      SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      SalesLine.SETRANGE(Type,SalesLine.Type::Item);
      IF SalesLine.FINDSET THEN
        REPEAT
          IF Item.GET(SalesLine."No.") AND
             Item.Oversize
          THEN
            Oversize := TRUE;
        UNTIL SalesLine.NEXT = 0;

      // Return zero free freight minimum order amount if shipping agent is of pickup type
      IF ShippingAgent.Type = ShippingAgent.Type::Pickup THEN
        EXIT(0);

      // Return zero free freight minimum order amount if customer is set to get Free Freight
      IF Cust.GET(SalesHeader."Sell-to Customer No.") AND
         Cust."Free Freight"
      THEN
        EXIT(0);

      // Return zero free freight minimum order amount if customer for specific division is set to get Free Freight
      IF CustDivision.GET(SalesHeader."Sell-to Customer No.",SalesHeader."Shortcut Dimension 5 Code") AND
         CustDivision."Free Freight"
      THEN
        EXIT(0);

      // Return free freight minimum order amount for customer and division
      IF CustDivision.GET(SalesHeader."Sell-to Customer No.",SalesHeader."Shortcut Dimension 5 Code") AND
         (CustDivision."Free Freight Min. Order Amount" <> 0)
      THEN
        EXIT(CustDivision."Free Freight Min. Order Amount");

      // Return free freight minimum order amount for specific shipping agent (LOCAL)
      FreeFreightMinOrderAmt.RESET;
      FreeFreightMinOrderAmt.SETRANGE("Division Code",'');
      FreeFreightMinOrderAmt.SETRANGE("Shipping Agent Code",SalesHeader."Shipping Agent Code");
      IF FreeFreightMinOrderAmt.FINDFIRST THEN
        EXIT(FreeFreightMinOrderAmt."Min. Order Amount");

      // Return free freight minimum order amount for specific division (I) and location (1|25|50)
      FreeFreightMinOrderAmt.RESET;
      FreeFreightMinOrderAmt.SETRANGE("Division Code",SalesHeader."Shortcut Dimension 5 Code");
      FreeFreightMinOrderAmt.SETRANGE("Shipping Agent Code",SalesHeader."Shipping Agent Code");
      FreeFreightMinOrderAmt.SETRANGE("Location Code",SalesHeader."Location Code");
      IF FreeFreightMinOrderAmt.FINDFIRST THEN
        EXIT(0);

      // Return free freight minimum order amount for specific division (E or L)
      FreeFreightMinOrderAmt.RESET;
      FreeFreightMinOrderAmt.SETRANGE("Division Code",SalesHeader."Shortcut Dimension 5 Code");
      FreeFreightMinOrderAmt.SETRANGE("Location Code",SalesHeader."Location Code");
      FreeFreightMinOrderAmt.SETRANGE(Oversize,Oversize);
      IF FreeFreightMinOrderAmt.FINDFIRST THEN
        EXIT(FreeFreightMinOrderAmt."Min. Order Amount");

      // Return free freight minimum order amount in other cases (for all other divisions and locations)
      FreeFreightMinOrderAmt.RESET;
      FreeFreightMinOrderAmt.SETRANGE("Division Code",'');
      FreeFreightMinOrderAmt.SETRANGE("Location Code",'');
      FreeFreightMinOrderAmt.SETRANGE(Oversize,Oversize);
      IF FreeFreightMinOrderAmt.FINDFIRST THEN
        EXIT(FreeFreightMinOrderAmt."Min. Order Amount");

      // Return zero free freight minimum order amount if no conditions for free freight found
      EXIT(0);
    END;

    BEGIN
    {
      2015-05-29 TPZ573 VCHERNYA
        Codeunit has been created
      2015-07-10 TPZ844 VCHERNYA
        Free Freight Reason Code handling has been added
      2015-09-04 TPZ874 VCHERNYA
        No Free Freight on E-Ship Agent Service handling has been added
      2016-02-17 TOP8717 TAKHMETO
        IsFreeFreight function has been modified
      2016-05-02 TPZ1548 EBAGIM
        Validating Free Fregiht on Split Orders
      2016-07-09 TPZ1608 EBAGIM
        Free Freight Override
      2016-08-10 TPZ1632 EBAGIM
        Fix order amount to include only items
      2016-10-10 TPZ1711 EBAGIM
        Free Freight Override - Remove Premium Service Restriction
      2017-10-22 TPZ1999 EBAGIM
        Free Freight Override - change to Fredight Override to lock free freight flag
    }
    END.
  }
}

