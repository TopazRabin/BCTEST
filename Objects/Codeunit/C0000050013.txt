OBJECT Codeunit 50013 Back Order Delete Job
{
  OBJECT-PROPERTIES
  {
    Date=07/01/20;
    Time=12:00:00 AM;
    Modified=Yes;
    Version List=TPZ000.00.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            Counter@1000000000 : Integer;
          BEGIN
            Counter := 0;
            SalesHeader.SETRANGE("Document Type", SalesHeader."Document Type"::Order);
            SalesHeader.SETRANGE("Shipping Advice", SalesHeader."Shipping Advice"::"No Back Order");
            SalesHeader.SETRANGE("Back Order", TRUE);
            //SalesHeader.SETRANGE("No.", 'SO1000211');
            IF SalesHeader.FINDSET THEN
            REPEAT
              IF DeleteBackOrder(SalesHeader) THEN
                Counter += 1;
            UNTIL SalesHeader.NEXT = 0;
            //MESSAGE('done. %1', Counter);
          END;

  }
  CODE
  {
    VAR
      SalesHeader@1000000000 : Record 36;

    PROCEDURE DeleteBackOrder@1000000000(VAR SalesHeader@1000000000 : Record 36) : Boolean;
    VAR
      SalesLine@1000000001 : Record 37;
      ArchiveManagement@1000000003 : Codeunit 5063;
      WhseShipmentHeader@1000000002 : Record 7320;
      WhseShipmentLine@1000000004 : Record 7321;
    BEGIN
      IF SalesHeader."Document Type" <> SalesHeader."Document Type"::Order THEN
        EXIT(FALSE);

      IF SalesHeader."No." = '' THEN
        EXIT(FALSE);


      IF SalesHeader."Shipping Advice" <> SalesHeader."Shipping Advice"::"No Back Order" THEN
        EXIT(FALSE);

      IF SalesHeader."Back Order" = FALSE THEN
        EXIT(FALSE);

      //check if order can be deleted
      SalesLine.SETRANGE("Document Type", SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.", SalesHeader."No.");
      SalesLine.SETFILTER(Quantity, '<>0');
      IF SalesLine.FINDSET THEN
      REPEAT
        IF CheckSalesLine(SalesLine) = FALSE THEN
          EXIT(FALSE);
      UNTIL SalesLine.NEXT = 0;

      WhseShipmentLine.RESET;
      WhseShipmentLine.SETRANGE("Source Type", DATABASE::"Sales Line");
      WhseShipmentLine.SETRANGE("Source Subtype", SalesHeader."Document Type");
      WhseShipmentLine.SETRANGE("Source No.", SalesHeader."No.");
      IF WhseShipmentLine.FINDSET THEN
      REPEAT
        WhseShipmentLine.CALCFIELDS("Pick Qty.");
        IF WhseShipmentLine."Pick Qty." <> 0 THEN
          EXIT(FALSE);

        IF WhseShipmentLine."Qty. Shipped" < WhseShipmentLine."Qty. Picked" THEN
          EXIT(FALSE);
      UNTIL WhseShipmentLine.NEXT = 0;

      //archive order.
      ArchiveManagement.StoreSalesDocument(SalesHeader,FALSE);

      //-------------------DELETE RECORDS----------------------
      //delete warehouse shipment and sales order
      WhseShipmentLine.RESET;
      WhseShipmentLine.SETRANGE("Source Type", DATABASE::"Sales Line");
      WhseShipmentLine.SETRANGE("Source Subtype", SalesHeader."Document Type");
      WhseShipmentLine.SETRANGE("Source No.", SalesHeader."No.");
      IF WhseShipmentLine.FINDSET THEN BEGIN
        WhseShipmentHeader.GET(WhseShipmentLine."No.");
        IF WhseShipmentHeader.Status <> WhseShipmentHeader.Status::Open THEN BEGIN
          WhseShipmentHeader.Status := WhseShipmentHeader.Status::Open;
          WhseShipmentHeader.MODIFY;
        END;
        REPEAT
          WhseShipmentLine.DELETE(TRUE);
        UNTIL WhseShipmentLine.NEXT = 0;
      END;

      //delete order
      IF SalesHeader.Status <> SalesHeader.Status::Open THEN BEGIN
        SalesHeader.Status := SalesHeader.Status::Open;
        SalesHeader.MODIFY;
      END;

      SalesLine.RESET;
      SalesLine.SETRANGE("Document Type", SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.", SalesHeader."No.");
      IF SalesLine.FINDSET THEN
      REPEAT
        SalesLine.DELETE(TRUE);
      UNTIL SalesLine.NEXT = 0;
      SalesHeader.DELETE(TRUE);

      EXIT(TRUE);
    END;

    PROCEDURE CheckSalesLine@1000000006(VAR SalesLine@1000000000 : Record 37) : Boolean;
    VAR
      WhseActivLine@1000000001 : Record 5767;
    BEGIN
      IF SalesLine."Quantity Shipped" <> SalesLine."Quantity Invoiced" THEN
        EXIT(FALSE);

      SalesLine.CALCFIELDS("Reserved Qty. (Base)");
      IF SalesLine."Reserved Qty. (Base)" <> 0 THEN
        EXIT(FALSE);

      IF SalesLine."Shipment No." = '' THEN
        IF SalesLine."Qty. Shipped Not Invoiced" <> 0 THEN
          EXIT(FALSE);

      IF SalesLine."Return Receipt No." = '' THEN
        IF SalesLine."Return Qty. Rcd. Not Invd." <> 0 THEN
          EXIT(FALSE);

      IF SalesLine.Quantity <> SalesLine."Quantity Invoiced" THEN
        IF SalesLine."Prepmt. Amt. Inv." <> SalesLine."Prepmt Amt Deducted" THEN
          EXIT(FALSE);

      WhseActivLine.SETCURRENTKEY(
        "Source Type","Source Subtype","Source No.","Source Line No.","Source Subline No.");
      WhseActivLine.SETRANGE("Source Type",DATABASE::"Sales Line");
      WhseActivLine.SETRANGE("Source Subtype",SalesLine."Document Type");
      WhseActivLine.SETRANGE("Source No.",SalesLine."Document No.");
      WhseActivLine.SETRANGE("Source Line No.", SalesLine."Line No.");
      WhseActivLine.SETRANGE("Source Subline No.",0);
      IF NOT WhseActivLine.ISEMPTY THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

