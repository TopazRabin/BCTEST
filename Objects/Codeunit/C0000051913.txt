OBJECT Codeunit 51913 Codeunit5804EventSubscriber
{
  OBJECT-PROPERTIES
  {
    Date=07/01/20;
    Time=12:00:00 AM;
    Modified=Yes;
    Version List=NAVEVENT;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      InvtSetup@1000000000 : Record 313;
      InvtSetupRead@1000000002 : Boolean;
      GLSetupRead@1000000001 : Boolean;
      GLSetup@1000000003 : Record 98;

    PROCEDURE FindPrimVendDirUnitCost@1000000000(ItemNo@1000000000 : Code[20];LocationCode@1000000001 : Code[10]) : Decimal;
    VAR
      Item@1000000003 : Record 27;
      PurchPrice@1000000002 : Record 7012;
      ProcurmentUnit@1000000004 : Record 14000555;
    BEGIN
      // <TPZ1175>

      //<VSO2468>
      IF ProcurmentUnit.GET(LocationCode,ItemNo,'') THEN;


      IF Item.GET(ItemNo) AND
         (ProcurmentUnit."Replenishment Source Code"<> '') AND
         PurchPrice.GET(Item."No.",ProcurmentUnit."Replenishment Source Code",0D,'','',Item."Base Unit of Measure")
      THEN
        EXIT(PurchPrice."Direct Unit Cost");

      EXIT(0);
      //</VSO2468>
      // </TPZ1175>
    END;

    PROCEDURE FindPrimVendInDirUnitCost@1000000001(ItemNo@1000000002 : Code[20];LocationCode@1000000003 : Code[10]) CostPerUnit : Decimal;
    VAR
      Item@1000000000 : Record 27;
      ValueEntry@1000000001 : Record 5802;
      EntryNo@1000000004 : Integer;
      ProcurmentUnit@1000000005 : Record 14000555;
    BEGIN
      // <TPZ1175>
      CostPerUnit := 0;
      EntryNo := 0;
      //<VSO2468>
      IF ProcurmentUnit.GET(LocationCode,ItemNo,'') THEN;

      IF Item.GET(ItemNo) AND (ProcurmentUnit."Replenishment Source Code"<> '') THEN BEGIN
        EntryNo := GetEntryNo(Item."No.",ProcurmentUnit."Replenishment Source Code");
      //<VSO2468>
        IF EntryNo <> 0 THEN BEGIN
          ValueEntry.RESET;
          ValueEntry.SETRANGE("Item Ledger Entry Type",ValueEntry."Item Ledger Entry Type"::Purchase);
          ValueEntry.SETRANGE("Item No.",Item."No.");
          ValueEntry.SETRANGE("Item Ledger Entry No.",EntryNo);
          ValueEntry.SETFILTER("Item Charge No.",'<>%1','');
          IF ValueEntry.FINDSET THEN
            REPEAT
              CostPerUnit := CostPerUnit + ValueEntry."Cost per Unit";
            UNTIL ValueEntry.NEXT = 0;
        END;
      END;

      EXIT(CostPerUnit);
      // </TPZ1175>
    END;

    PROCEDURE GetEntryNo@1000000002(ItemNo@1000000001 : Code[20];VendorNo@1000000002 : Code[20]) : Decimal;
    VAR
      ItemLedgEntry@1000000000 : Record 32;
      ValueEntry@1000000003 : Record 5802;
    BEGIN
      // <TPZ1175>
      ItemLedgEntry.RESET;
      ItemLedgEntry.SETCURRENTKEY("Item No.","Posting Date");
      ItemLedgEntry.ASCENDING(FALSE);
      ItemLedgEntry.SETRANGE("Item No.",ItemNo);
      ItemLedgEntry.SETRANGE("Entry Type",ItemLedgEntry."Entry Type"::Purchase);
      ItemLedgEntry.SETRANGE("Source Type",ItemLedgEntry."Source Type"::Vendor);
      ItemLedgEntry.SETRANGE("Source No.",VendorNo);
      IF ItemLedgEntry.FIND('-') THEN
        REPEAT
          ValueEntry.RESET;
          ValueEntry.SETRANGE("Item Ledger Entry No.",ItemLedgEntry."Entry No.");
          ValueEntry.SETRANGE("Item No.",ItemNo);
          ValueEntry.SETRANGE("Item Ledger Entry Type",ValueEntry."Item Ledger Entry Type"::Purchase);
          ValueEntry.SETFILTER("Item Charge No.",'<>%1','');
          IF ValueEntry.FINDFIRST THEN
            EXIT(ItemLedgEntry."Entry No.");
        UNTIL ItemLedgEntry.NEXT = 0;

      EXIT(0);
      // </TPZ1175>
    END;

    PROCEDURE SumSalesLine@1000000003(SalesHeader@1000000000 : Record 36;VAR EstFutureTotalLandedCost@1000000002 : Decimal);
    VAR
      SalesLine@1000000001 : Record 37;
      DirectCost@1000000005 : Decimal;
      IndirectCost@1000000006 : Decimal;
    BEGIN
      // <TPZ1175> //<VSO2468>
      EstFutureTotalLandedCost := 0;

      WITH SalesHeader DO BEGIN
        SalesLine.RESET;
        SalesLine.SETRANGE("Document Type","Document Type");
        SalesLine.SETRANGE("Document No.","No.");
        IF SalesLine.FINDSET THEN
          REPEAT
            EstFutureTotalLandedCost :=
              EstFutureTotalLandedCost +
              ((FindPrimVendDirUnitCost(SalesLine."No.",SalesLine."Location Code") + FindPrimVendInDirUnitCost(SalesLine."No.",SalesLine."Location Code")) *
              SalesLine.Quantity);
          UNTIL SalesLine.NEXT = 0;
      END;
      // </TPZ1175></VSO2468>
    END;

    LOCAL PROCEDURE GetInvtSetup@8();
    BEGIN
      IF NOT InvtSetupRead THEN
        InvtSetup.GET;
      InvtSetupRead := TRUE;
    END;

    LOCAL PROCEDURE GetGLSetup@13();
    BEGIN
      IF NOT GLSetupRead THEN
        GLSetup.GET;
      GLSetupRead := TRUE;
    END;

    BEGIN
    {
      2020-05-26  TPZ2830  PSHUKLA
    }
    END.
  }
}

