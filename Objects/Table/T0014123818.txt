OBJECT Table 14123818 ES Dynamic Filter
{
  OBJECT-PROPERTIES
  {
    Date=08/16/17;
    Time=12:00:00 PM;
    Version List=ES1.41.38;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ESFLADSSetup.GET;
               ESFLADSSetup.TESTFIELD("Copy From Setup Company",FALSE);

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit Setup (FLADS)");
               END;

               "Filter Value" := '';
               LockedCode := Code;
             END;

    OnModify=BEGIN
               ESFLADSSetup.GET;
               ESFLADSSetup.TESTFIELD("Copy From Setup Company",FALSE);

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit Setup (FLADS)");
               END;

               "Filter Value" := '';
               LockedCode := Code;
             END;

    OnDelete=BEGIN
               ESFLADSSetup.GET;
               ESFLADSSetup.TESTFIELD("Copy From Setup Company",FALSE);

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit Setup (FLADS)");
               END;
             END;

    OnRename=BEGIN
               ERROR(Text016);
             END;

    CaptionML=ENU=Dynamic Filter;
    LookupPageID=Page14123832;
    DrillDownPageID=Page14123832;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 11  ;   ;Calculation Code    ;Code10        ;OnValidate=BEGIN
                                                                IF "Calculation Code" <> '' THEN
                                                                  TESTFIELD("Fixed Filter Value",'');
                                                              END;

                                                   CaptionML=ENU=Calculation Code }
    { 12  ;   ;Calculation Codeunit ID;Integer    ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   OnValidate=BEGIN
                                                                IF "Calculation Codeunit ID" <> 0 THEN
                                                                  TESTFIELD("Fixed Filter Value",'');
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Calculation Codeunit ID;
                                                   BlankZero=Yes }
    { 13  ;   ;Description         ;Text250       ;CaptionML=ENU=Description }
    { 14  ;   ;Fixed Filter Value  ;Text250       ;OnValidate=BEGIN
                                                                IF "Fixed Filter Value" <> '' THEN BEGIN
                                                                  TESTFIELD("Calculation Code",'');
                                                                  TESTFIELD("Calculation Codeunit ID",0);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Fixed Filter Value }
    { 21  ;   ;Filter Value        ;Text250       ;CaptionML=ENU=Filter Value }
    { 22  ;   ;Source Table ID     ;Integer       ;TableRelation="ES Source Table Setup";
                                                   CaptionML=ENU=Source Table ID;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 23  ;   ;Data Security Code  ;Code20        ;TableRelation="ES Data Security Code".Code WHERE (Source Table ID=FIELD(Source Table ID));
                                                   CaptionML=ENU=Data Security Code;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 24  ;   ;Object Type         ;Option        ;CaptionML=ENU=Object Type;
                                                   OptionCaptionML=ENU=" ,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System";
                                                   OptionString=[ ,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 25  ;   ;Object ID           ;Integer       ;TableRelation="ES FLADS Relation"."Object ID" WHERE (Object Type=FIELD(Object Type),
                                                                                                        Type=FILTER(Source Table|Data Item),
                                                                                                        Relation Object Type=CONST(Table),
                                                                                                        Relation Object ID=FIELD(Source Table ID));
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Object ID;
                                                   Editable=No }
    { 26  ;   ;Field ID            ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table ID));
                                                   CaptionML=ENU=Field ID;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 27  ;   ;From Object Type    ;Option        ;CaptionML=ENU=From Object Type;
                                                   OptionCaptionML=ENU=" ,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System";
                                                   OptionString=[ ,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 28  ;   ;From Object ID      ;Integer       ;TableRelation="ES FLADS Relation"."Object ID" WHERE (Object Type=FIELD(Object Type),
                                                                                                        Type=FILTER(Source Table|Data Item),
                                                                                                        Relation Object Type=CONST(Table),
                                                                                                        Relation Object ID=FIELD(Source Table ID));
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=From Object ID;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Unknown %1 %2 with %3 %4.';
      ESFLADSSetup@1240520001 : Record 14123801;
      Text016@1240520002 : TextConst 'ENU=Rename not allowed.';
      ESLoginSetup@1240520003 : Record 14123540;
      LockedCode@1000 : Code[20];

    PROCEDURE CalculateFilterValue@1240520001(GiveError@1240520001 : Boolean) : Text[250];
    VAR
      ESDynamicFilter@1240520002 : Record 14123818;
    BEGIN
      IF (LockedCode <> '') AND (LockedCode = Code) THEN
        EXIT('')
      ELSE
        LockedCode := '';

      ESDynamicFilter := Rec;

      IF "Fixed Filter Value" <> '' THEN
        EXIT("Fixed Filter Value");

      IF GiveError THEN BEGIN
        IF ESDynamicFilter."Calculation Codeunit ID" = 0 THEN
          CODEUNIT.RUN(CODEUNIT::"ES Dynamic Filter Management",ESDynamicFilter)
        ELSE
          CODEUNIT.RUN(ESDynamicFilter."Calculation Codeunit ID",ESDynamicFilter);
      END ELSE BEGIN
        ESDynamicFilter."Filter Value" := '';
        CLEARLASTERROR;
        IF ESDynamicFilter."Calculation Codeunit ID" = 0 THEN BEGIN
          IF NOT CODEUNIT.RUN(CODEUNIT::"ES Dynamic Filter Management",ESDynamicFilter) THEN
            ESDynamicFilter."Filter Value" := GETLASTERRORTEXT;
        END ELSE
          IF NOT CODEUNIT.RUN(ESDynamicFilter."Calculation Codeunit ID",ESDynamicFilter) THEN
            ESDynamicFilter."Filter Value" := GETLASTERRORTEXT;
      END;

      EXIT(ESDynamicFilter."Filter Value");
    END;

    PROCEDURE ShowCalculationError@1240520002();
    BEGIN
      ERROR(
        Text001,
        TABLECAPTION,Code,FIELDCAPTION("Calculation Code"),"Calculation Code");
    END;

    PROCEDURE InsertStandardFilters@1240520003(SelectCodeunit@1240520002 : Boolean);
    VAR
      ESDynamicFilter@1240520001 : Record 14123818;
      Object@1240520003 : Record 2000000001;
    BEGIN
      CLEAR(ESDynamicFilter);

      IF SelectCodeunit THEN BEGIN
        Object.RESET;
        Object.SETRANGE(Type,Object.Type::Codeunit);
        IF PAGE.RUNMODAL(PAGE::"ES Live Objects",Object) <> ACTION::LookupOK THEN
          EXIT;
        ESDynamicFilter."Calculation Codeunit ID" := Object.ID;
      END ELSE
        ESDynamicFilter."Calculation Codeunit ID" := "Calculation Codeunit ID";

      IF ESDynamicFilter."Calculation Codeunit ID" = 0 THEN
        CODEUNIT.RUN(CODEUNIT::"ES Dynamic Filter Management",ESDynamicFilter)
      ELSE
        CODEUNIT.RUN(ESDynamicFilter."Calculation Codeunit ID",ESDynamicFilter);
    END;

    PROCEDURE InsertDynamicFilter@1240520004(NewDynamicFilterCode@1240520001 : Code[10];NewCalculationCodeunitID@1240520004 : Integer;NewDescription@1240520003 : Text[100]);
    VAR
      ESDynamicFilter@1240520002 : Record 14123818;
    BEGIN
      ESDynamicFilter.INIT;
      ESDynamicFilter.Code := NewDynamicFilterCode;
      ESDynamicFilter."Calculation Code" := ESDynamicFilter.Code;
      ESDynamicFilter."Calculation Codeunit ID" := NewCalculationCodeunitID;
      ESDynamicFilter.Description := NewDescription;
      IF NOT ESDynamicFilter.INSERT(TRUE) THEN
        ;
    END;

    BEGIN
    END.
  }
}

