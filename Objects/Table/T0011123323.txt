OBJECT Table 11123323 SC - Sales Document Header
{
  OBJECT-PROPERTIES
  {
    Date=07/01/20;
    Time=12:00:00 AM;
    Modified=Yes;
    Version List=SCW19.2.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Sales Document Header;
  }
  FIELDS
  {
    { 1   ;   ;Document No.        ;Code20        ;CaptionML=ENU=Document No.;
                                                   Editable=No }
    { 2   ;   ;Document Type       ;Option        ;CaptionML=ENU=Document Type;
                                                   OptionCaptionML=ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order;
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order;
                                                   Editable=No }
    { 5   ;   ;Payment Method Name ;Text50        ;CaptionML=ENU=Payment Method Name;
                                                   Editable=No }
    { 6   ;   ;Payment Method Status;Text50       ;CaptionML=ENU=Payment Method Status;
                                                   Editable=No }
    { 7   ;   ;Date Received       ;Date          ;CaptionML=ENU=Date Received;
                                                   Editable=No }
    { 8   ;   ;Time Received       ;Time          ;CaptionML=ENU=Time Received;
                                                   Editable=No }
    { 9   ;   ;Sana Order No.      ;Text50        ;CaptionML=ENU=Sana Order No.;
                                                   Editable=No }
    { 10  ;   ;Sana Customer No.   ;Text50        ;CaptionML=ENU=Sana Customer No.;
                                                   Editable=No }
    { 11  ;   ;Promotion Code      ;Text50        ;OnValidate=BEGIN
                                                                TestSalesHeader;

                                                                IF "Promotion Code" = '' THEN BEGIN
                                                                  "Promotion Title" := '';
                                                                  "Promotion Discount Type" := "Promotion Discount Type"::" ";
                                                                  "Promotion Discount Value" := 0;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Code }
    { 12  ;   ;Promotion Title     ;Text80        ;OnValidate=BEGIN
                                                                TESTFIELD("Promotion Code");
                                                              END;

                                                   CaptionML=ENU=Title }
    { 13  ;   ;Promotion Discount Type;Option     ;OnValidate=BEGIN
                                                                IF ("Promotion Discount Type" <> "Promotion Discount Type"::" ") THEN
                                                                  TESTFIELD("Promotion Code");

                                                                IF ("Promotion Discount Type" <> xRec."Promotion Discount Type") THEN
                                                                  "Promotion Discount Value" := 0;
                                                              END;

                                                   CaptionML=ENU=Discount Type;
                                                   OptionCaptionML=ENU=" ,Fixed Amount,Percentage";
                                                   OptionString=[ ,Fixed Amount,Percentage] }
    { 14  ;   ;Promotion Discount Value;Decimal   ;OnValidate=BEGIN
                                                                IF ("Promotion Discount Value" <> 0) THEN BEGIN
                                                                  TESTFIELD("Promotion Code");
                                                                  TESTFIELD("Promotion Discount Type");

                                                                  IF ("Promotion Discount Type" = "Promotion Discount Type"::Percentage) AND
                                                                     ("Promotion Discount Value" > 100)
                                                                  THEN
                                                                    FIELDERROR("Promotion Discount Value");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Discount Value;
                                                   MinValue=0 }
    { 15  ;   ;Promotion Discount Amount;Decimal  ;CaptionML=ENU=Discount Amount;
                                                   Editable=No }
    { 16  ;   ;Shipment Method Name;Text80        ;CaptionML=ENU=Shipment Method Name;
                                                   Editable=No }
    { 17  ;   ;Shop Account E-Mail ;Text80        ;OnValidate=BEGIN
                                                                TestSalesHeader;
                                                              END;

                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=ENU=Shop Account E-Mail;
                                                   Editable=No }
    { 18  ;   ;Payment Transaction Id;Text30      ;CaptionML=ENU=Payment Transaction Id;
                                                   Editable=No }
    { 19  ;   ;Promotion Type      ;Option        ;CaptionML=ENU=Type;
                                                   OptionString=Promotion,Coupon;
                                                   Editable=No }
    { 20  ;   ;Webshop ID          ;Text50        ;CaptionML=ENU=Webshop ID;
                                                   Editable=No }
    { 21  ;   ;Document State      ;Option        ;CaptionML=ENU=Document State;
                                                   OptionCaptionML=ENU=Normal,Basket,Wish List,Edit Order;
                                                   OptionString=Normal,Basket,Wish List,Edit Order;
                                                   Editable=No }
    { 22  ;   ;Quote Promotable    ;Boolean       ;OnValidate=BEGIN
                                                                SetUniqueWebshopDocId;
                                                                SetUniqueWebshopDocId36;
                                                              END;
                                                               }
    { 23  ;   ;Authorization Status;Text50        ;Editable=No }
    { 99  ;   ;Unique Webshop Document Id;GUID    ;CaptionML=ENU=Unique Webshop Document Id;
                                                   Editable=No }
    { 100 ;   ;Origin. Document No.;Code20        ;CaptionML=ENU=Origin. Document No.;
                                                   Editable=No }
    { 101 ;   ;Origin. Document Type;Option       ;CaptionML=ENU=Origin. Document Type;
                                                   OptionCaptionML=ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order;
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order;
                                                   Editable=No }
    { 102 ;   ;Sell-to Customer No.;Code20        ;TableRelation=Customer;
                                                   CaptionML=ENU=Sell-to Customer No.;
                                                   Editable=No }
    { 103 ;   ;Sell-to Contact No. ;Code20        ;TableRelation=Contact;
                                                   CaptionML=ENU=Sell-to Contact No.;
                                                   Editable=No }
    { 501 ;   ;Status              ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Sales Header".Status WHERE (Document Type=FIELD(Document Type),
                                                                                                   No.=FIELD(Document No.)));
                                                   OptionCaptionML=ENU=Open,Released,Pending Approval,Pending Prepayment;
                                                   OptionString=Open,Released,Pending Approval,Pending Prepayment;
                                                   Editable=No }
    { 502 ;   ;Ship                ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Sales Header".Ship WHERE (Document Type=FIELD(Document Type),
                                                                                                 No.=FIELD(Document No.)));
                                                   CaptionML=ENU=Ship;
                                                   Editable=No }
    { 503 ;   ;Shipment Date       ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Sales Header"."Shipment Date" WHERE (Document Type=FIELD(Document Type),
                                                                                                            No.=FIELD(Document No.)));
                                                   CaptionML=ENU=Shipment Date;
                                                   Editable=No }
    { 504 ;   ;Completely Shipped  ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Min("Sales Line"."Completely Shipped" WHERE (Document Type=FIELD(Document Type),
                                                                                                            Document No.=FIELD(Document No.),
                                                                                                            Type=FILTER(<>' '),
                                                                                                            Location Code=FIELD(Location Filter)));
                                                   CaptionML=ENU=Completely Shipped;
                                                   Editable=No }
    { 505 ;   ;Location Filter     ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Location;
                                                   CaptionML=ENU=Location Filter;
                                                   Editable=No }
    { 506 ;   ;Currency Code       ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Sales Header"."Currency Code" WHERE (Document Type=FIELD(Document Type),
                                                                                                            No.=FIELD(Document No.)));
                                                   Editable=No }
    { 507 ;   ;Sales Header Exists ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Sales Header" WHERE (Document Type=FIELD(Document Type),
                                                                                           No.=FIELD(Document No.)));
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Document No.,Document Type              ;Clustered=Yes }
    {    ;Document Type,Sana Order No.             }
    {    ;Origin. Document No.                     }
    {    ;Webshop ID,Document State                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text11123302@11123302 : TextConst 'ENU=You cannot edit the fields because initial sales document does not exist.';
      BasketCalculationGlobal@11123303 : Boolean;

    PROCEDURE TestSalesHeader@11123303();
    VAR
      SalesHeader@11123302 : Record 36;
    BEGIN
      IF BasketCalculationGlobal THEN
        EXIT;

      IF NOT SalesHeader.GET("Document Type","Document No.") THEN
        ERROR(Text11123302);
    END;

    PROCEDURE BasketCalculation@11123305(NewBasketCalculation@11123302 : Boolean);
    BEGIN
      BasketCalculationGlobal := NewBasketCalculation;
    END;

    PROCEDURE DeleteRelatedData@11123304(SalesHeader@11123305 : Record 36);
    VAR
      SalesDocHeader@11123302 : Record 11123323;
      SalesHeaderBlob@11123303 : Record 11123324;
    BEGIN
      IF SalesDocHeader.GET(SalesHeader."No.",SalesHeader."Document Type") THEN
        SalesDocHeader.DELETE(TRUE);

      IF SalesHeaderBlob.GET(SalesHeader."Document Type",SalesHeader."No.") THEN
        SalesHeaderBlob.DELETE(TRUE);
    END;

    PROCEDURE SanaRenameRelatedData@11123302(OldSalesHeader@11123302 : Record 36;NewSalesHeader@11123303 : Record 36);
    VAR
      SalesDocHeader@11123307 : Record 11123323;
      SalesHeaderBlob@11123306 : Record 11123324;
    BEGIN
      IF SalesDocHeader.GET(OldSalesHeader."No.",OldSalesHeader."Document Type") THEN BEGIN
        SalesDocHeader.RENAME(NewSalesHeader."No.",NewSalesHeader."Document Type");
        IF OldSalesHeader."Document Type" <> NewSalesHeader."Document Type" THEN BEGIN
          SalesDocHeader."Origin. Document No." := OldSalesHeader."No.";
          SalesDocHeader."Origin. Document Type" := OldSalesHeader."Document Type";
          SalesDocHeader.MODIFY(TRUE);
        END;
      END;

      IF SalesHeaderBlob.GET(OldSalesHeader."Document Type",OldSalesHeader."No.") THEN
        SalesHeaderBlob.RENAME(NewSalesHeader."Document Type",NewSalesHeader."No.");
    END;

    PROCEDURE SetUniqueWebshopDocId@11123306();
    BEGIN
      IF "Quote Promotable" AND ISNULLGUID("Unique Webshop Document Id") THEN
        "Unique Webshop Document Id" := CREATEGUID;
    END;

    PROCEDURE SetUniqueWebshopDocId36@11123307();
    VAR
      SalesHeader@11123302 : Record 36;
    BEGIN
      IF "Quote Promotable" THEN
        IF SalesHeader.GET("Document Type","Document No.") THEN
          IF SalesHeader."Unique Webshop Document Id" <> "Unique Webshop Document Id" THEN BEGIN
            SalesHeader."Unique Webshop Document Id" := "Unique Webshop Document Id";
            SalesHeader.MODIFY(TRUE);
          END;
    END;

    BEGIN
    END.
  }
}

