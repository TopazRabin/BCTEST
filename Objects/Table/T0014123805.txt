OBJECT Table 14123805 ES User Security Setup
{
  OBJECT-PROPERTIES
  {
    Date=10/05/18;
    Time=12:00:00 PM;
    Version List=ES1.41.55;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit User Security Setup");
               END;

               ESSourceTableSetup.GET("Source Table ID");

               IF ("Security as User ID" <> '') AND ("Source Table ID" <> 0) THEN BEGIN
                 ESUserSecuritySetup.GET("Security as User ID","Source Table ID");
                 ESUserSecuritySetup.TESTFIELD("Security as User ID",'');
                 "Field Level Security Code" := ESUserSecuritySetup."Field Level Security Code";
                 "Data Security Code" := ESUserSecuritySetup."Data Security Code";
               END ELSE BEGIN
                 IF NOT NoDefault THEN BEGIN
                   IF "Field Level Security Code" = '' THEN BEGIN
                     ESFieldLevelSecurityCode.RESET;
                     ESFieldLevelSecurityCode.SETRANGE("Source Table ID","Source Table ID");
                     ESFieldLevelSecurityCode.SETRANGE(Default,TRUE);
                     IF ESFieldLevelSecurityCode.FIND('-') THEN
                       "Field Level Security Code" := ESFieldLevelSecurityCode.Code;
                   END;

                   IF "Data Security Code" = '' THEN BEGIN
                     ESDataSecurityCode.RESET;
                     ESDataSecurityCode.SETRANGE("Source Table ID","Source Table ID");
                     ESDataSecurityCode.SETRANGE(Default,TRUE);
                     IF ESDataSecurityCode.FIND('-') THEN
                       "Data Security Code" := ESDataSecurityCode.Code;
                   END;
                 END;
               END;

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnModify=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit User Security Setup");
               END;

               ESSourceTableSetup.GET("Source Table ID");

               IF ("Security as User ID" <> '') AND ("Source Table ID" <> 0) THEN BEGIN
                 ESUserSecuritySetup.GET("Security as User ID","Source Table ID");
                 ESUserSecuritySetup.TESTFIELD("Security as User ID",'');
                 "Field Level Security Code" := ESUserSecuritySetup."Field Level Security Code";
                 "Data Security Code" := ESUserSecuritySetup."Data Security Code";
               END ELSE
                 IF "User ID" <> '' THEN BEGIN
                   ESUserSecuritySetup.RESET;
                   ESUserSecuritySetup.SETRANGE("Source Table ID","Source Table ID");
                   ESUserSecuritySetup.SETRANGE("Security as User ID","User ID");
                   IF ESUserSecuritySetup.FIND('-') THEN
                     REPEAT
                       ESUserSecuritySetup2 := ESUserSecuritySetup;
                       ESUserSecuritySetup2."Field Level Security Code" := "Field Level Security Code";
                       ESUserSecuritySetup2."Data Security Code" := "Data Security Code";
                       ESUserSecuritySetup2.MODIFY;
                     UNTIL ESUserSecuritySetup.NEXT = 0;
                 END;

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnDelete=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit User Security Setup");
               END;

               ESSourceTableSetup.GET("Source Table ID");

               ESUserSecuritySetup.RESET;
               ESUserSecuritySetup.SETRANGE("Source Table ID","Source Table ID");
               ESUserSecuritySetup.SETRANGE("Security as User ID","User ID");
               IF ESUserSecuritySetup.FIND('-') THEN
                 ESUserSecuritySetup.TESTFIELD("Security as User ID",'');

               IF ESFLADSCopySetup.GET(DATABASE::"ES User Security Setup","Source Table ID","User ID") THEN
                 ESFLADSCopySetup.DELETE;

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnRename=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);
               TESTFIELD("User ID");
               xRec.TESTFIELD("User ID");

               ESFLADSSetup.GET('');

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit User Security Setup");
               END;

               ESSourceTableSetup.GET("Source Table ID");

               ESFLADSUpdateNo.UpdateNo;
             END;

    CaptionML=ENU=User Security Setup;
    LookupPageID=Page14123805;
    DrillDownPageID=Page14123805;
  }
  FIELDS
  {
    { 1   ;   ;User ID             ;Code50        ;OnValidate=BEGIN
                                                                LoginMgt.ValidateUserID("User ID");
                                                              END;

                                                   OnLookup=BEGIN
                                                              LoginMgt.LookupUserID("User ID");
                                                            END;

                                                   CaptionML=ENU=User ID }
    { 2   ;   ;Source Table ID     ;Integer       ;TableRelation="ES Source Table Setup";
                                                   OnValidate=BEGIN
                                                                ESSourceTableSetup.GET("Source Table ID");
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Source Table ID }
    { 11  ;   ;Field Level Security Code;Code20   ;TableRelation="ES Field Level Security Code".Code WHERE (Source Table ID=FIELD(Source Table ID));
                                                   OnValidate=BEGIN
                                                                IF "Field Level Security Code" <> '' THEN BEGIN
                                                                  TESTFIELD("Source Table ID");
                                                                  TESTFIELD("Security as User ID",'');
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Field Level Security Code }
    { 12  ;   ;Data Security Code  ;Code20        ;TableRelation="ES Data Security Code".Code WHERE (Source Table ID=FIELD(Source Table ID));
                                                   OnValidate=BEGIN
                                                                IF "Data Security Code" <> '' THEN BEGIN
                                                                  TESTFIELD("Source Table ID");
                                                                  TESTFIELD("Security as User ID",'');
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Data Security Code }
    { 13  ;   ;Source Table Caption;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Source Table ID)));
                                                   CaptionML=ENU=Source Table Caption;
                                                   Editable=No }
    { 14  ;   ;Field Level Security Descr.;Text250;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Field Level Security Code".Description WHERE (Source Table ID=FIELD(Source Table ID),
                                                                                                                        Code=FIELD(Field Level Security Code)));
                                                   CaptionML=ENU=Field Level Security Descr.;
                                                   Editable=No }
    { 15  ;   ;Data Security Description;Text250  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Data Security Code".Description WHERE (Source Table ID=FIELD(Source Table ID),
                                                                                                                 Code=FIELD(Data Security Code)));
                                                   CaptionML=ENU=Data Security Description;
                                                   Editable=No }
    { 17  ;   ;Security as User ID ;Code50        ;TableRelation="ES User Security Setup"."User ID";
                                                   OnValidate=BEGIN
                                                                IF "Security as User ID" <> '' THEN BEGIN
                                                                  IF "User ID" = "Security as User ID" THEN
                                                                    ERROR(Text001);

                                                                  IF "Source Table ID" <> 0 THEN BEGIN
                                                                    ESUserSecuritySetup.GET("Security as User ID","Source Table ID");
                                                                    ESUserSecuritySetup.TESTFIELD("Security as User ID",'');
                                                                    "Field Level Security Code" := ESUserSecuritySetup."Field Level Security Code";
                                                                    "Data Security Code" := ESUserSecuritySetup."Data Security Code";
                                                                  END;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              ESUserSecuritySetup.RESET;
                                                              ESUserSecuritySetup.SETFILTER("User ID",'<>%1',"User ID");
                                                              IF "Source Table ID" <> 0 THEN
                                                                ESUserSecuritySetup.SETRANGE("Source Table ID","Source Table ID");
                                                              ESUserSecuritySetup."User ID" := "Security as User ID";
                                                              ESUserSecuritySetup."Source Table ID" := "Source Table ID";
                                                              IF ESUserSecuritySetup.FIND THEN
                                                                ;

                                                              IF PAGE.RUNMODAL(0,ESUserSecuritySetup) = ACTION::LookupOK THEN
                                                                VALIDATE("Security as User ID",ESUserSecuritySetup."User ID");
                                                            END;

                                                   CaptionML=ENU=Security as User ID }
    { 21  ;   ;Field Level Security Objects;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("ES Field Level Security Object" WHERE (Source Table ID=FIELD(Source Table ID),
                                                                                                             Field Level Security Code=FIELD(Field Level Security Code)));
                                                   CaptionML=ENU=Field Level Security Objects;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 22  ;   ;Data Security Objects;Integer      ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES Data Security Object" WHERE (Source Table ID=FIELD(Source Table ID),
                                                                                                      Data Security Code=FIELD(Data Security Code)));
                                                   CaptionML=ENU=Data Security Objects;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 23  ;   ;User Security Setups;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES User Security Setup" WHERE (Source Table ID=FIELD(Source Table ID),
                                                                                                     Security as User ID=FIELD(User ID)));
                                                   CaptionML=ENU=User Security Setups;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 24  ;   ;User Security Setup Exists;Boolean ;FieldClass=FlowField;
                                                   CalcFormula=Exist("ES User Security Setup" WHERE (User ID=FIELD(User ID),
                                                                                                     Source Table ID=FIELD(Source Table ID)));
                                                   CaptionML=ENU=User Security Setup Exists;
                                                   Editable=No }
    { 25  ;   ;From Setup Company  ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES FLADS Copy Setup"."From Setup Company" WHERE (Table ID=CONST(14123805),
                                                                                                                        Source Table ID=FIELD(Source Table ID),
                                                                                                                        Code=FIELD(User ID)));
                                                   CaptionML=ENU=From Setup Company;
                                                   Editable=No }
    { 26  ;   ;Copy Setup Exists   ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("ES FLADS Copy Setup" WHERE (Table ID=CONST(14123805),
                                                                                                  Source Table ID=FIELD(Source Table ID),
                                                                                                  Code=FIELD(User ID)));
                                                   CaptionML=ENU=Copy Setup Exists;
                                                   Editable=No }
    { 27  ;   ;Direct User Security Setup;Boolean ;CaptionML=ENU=Direct User Security Setup;
                                                   Editable=No }
    { 28  ;   ;By Group User Security Setup;Boolean;
                                                   CaptionML=ENU=By Group User Security Setup;
                                                   Editable=No }
    { 29  ;   ;Default User Security Setup;Boolean;CaptionML=ENU=Default User Security Setup;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;User ID,Source Table ID                 ;Clustered=Yes }
    {    ;Source Table ID                          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ESUserSecuritySetup@1240520010 : Record 14123805;
      ESUserSecuritySetup2@1240520011 : Record 14123805;
      ESFLADSUpdateNo@1240520012 : Record 14123813;
      ESFieldLevelSecurityCode@1240520013 : Record 14123803;
      ESDataSecurityCode@1240520009 : Record 14123804;
      ESSourceTableSetup@1240520015 : Record 14123802;
      ESFLADSCopySetup@1240520001 : Record 14123816;
      ESFLADSSetup@1240520003 : Record 14123801;
      ESLoginSetup@1240520002 : Record 14123540;
      LoginMgt@1240520008 : Codeunit 418;
      Text001@1001 : TextConst 'ENU=cannot be like the same user.';
      Text003@1240520026 : TextConst 'ENU=File %1 already exists, overwrite?';
      Text004@1240520025 : TextConst 'ENU=Nothing has been exported.';
      Text005@1240520024 : TextConst 'ENU=Exporting %1 to file\';
      Text006@1240520023 : TextConst 'ENU=Process #1################################################';
      Text007@1240520022 : TextConst 'ENU=Updating Version';
      Text008@1240520021 : TextConst 'ENU=Creating File';
      Text011@1240520020 : TextConst 'ENU=Closing File';
      Text012@1240520019 : TextConst 'ENU=%1 %2 %3 exported to %4 with %5 records.';
      Text013@1240520018 : TextConst 'ENU=Nothing imported.';
      Text014@1240520017 : TextConst 'ENU=File Name must be entered.';
      Text015@1240520016 : TextConst 'ENU=Importing %1 from file\';
      Text016@1240520014 : TextConst 'ENU=Process #1################################################\';
      Text017@1240520007 : TextConst 'ENU=Opening file';
      Text018@1240520006 : TextConst 'ENU=%1 records have been imported from %2.';
      Text019@1240520005 : TextConst 'ENU=%1 %2 exported to %3 with %4 records.';
      Text002@1240520004 : TextConst 'ENU=%1 is not valid content for this filetype.';
      NoDefault@1240520027 : Boolean;
      Text101@1002 : TextConst 'ENU=Downloading File.';
      Text102@1000 : TextConst 'ENU=Uploading file.';

    PROCEDURE ExportMultiUserSecuritySetup@1240520002(VAR ESUserSecuritySetup@1240520004 : Record 14123805;ShowStatus@1240520021 : Boolean;ExportFileName@1240520001 : Text[250]);
    VAR
      ESEnterFileName@1240520002 : Page 14123560;
      FileMgt@1001 : Codeunit 419;
      TempServerFile@1000 : File;
      Window@1240520005 : Dialog;
      TempServerFileName@1002 : Text[1000];
      TotalRecords@1240520008 : Integer;
      TotalCodes@1240520003 : Integer;
    BEGIN
      IF ExportFileName = '' THEN BEGIN
        CLEAR(ESEnterFileName);
        ESEnterFileName.LOOKUPMODE(TRUE);
        ESEnterFileName.SelectNewFile(TRUE);
        IF ESEnterFileName.RUNMODAL <> ACTION::LookupOK THEN
          ERROR(Text004);
        ExportFileName := ESEnterFileName.GetFileName;
      END;

      TempServerFile.WRITEMODE(TRUE);
      TempServerFile.TEXTMODE(TRUE);
      TempServerFile.CREATETEMPFILE;
      TempServerFileName := TempServerFile.NAME;
      TempServerFile.CLOSE;

      IF GUIALLOWED THEN
        Window.OPEN(Text006);

      IF ESUserSecuritySetup.FIND('-') THEN
        REPEAT
          IF GUIALLOWED THEN
            Window.UPDATE(
              1,
              STRSUBSTNO(
              '%1 %2',ESUserSecuritySetup."User ID",ESUserSecuritySetup."Source Table ID"));

          TotalCodes := TotalCodes + 1;

          TotalRecords :=
            TotalRecords +
            ESUserSecuritySetup.ExportUserSecuritySetup(FALSE,TRUE,TRUE,TempServerFileName,FALSE);
        UNTIL ESUserSecuritySetup.NEXT = 0;

      IF GUIALLOWED THEN
        Window.UPDATE(1,Text101);
      FileMgt.DownloadToFile(TempServerFileName,ExportFileName);

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(
          Text019,
          TotalCodes,TABLECAPTION,ExportFileName,TotalRecords);
    END;

    PROCEDURE ExportUserSecuritySetup@1240520014(ShowStatus@1240520021 : Boolean;ConfirmOverwrite@1240520020 : Boolean;AppendToFile@1240520004 : Boolean;ExportFileName@1240520001 : Text[250];ShowProgress@1240520005 : Boolean) : Integer;
    VAR
      ESEnterFileName@1240520002 : Page 14123560;
      ESFileMgt@1240520003 : Codeunit 14123502;
      FileMgt@1001 : Codeunit 419;
      Window@1240520009 : Dialog;
      OutputFile@1240520019 : File;
      Value@1240520022 : ARRAY [100] OF Text[1000];
      LocalFileName@1000 : Text[1000];
      TotalRecords@1240520008 : Integer;
    BEGIN
      IF NOT AppendToFile THEN BEGIN
        IF ExportFileName = '' THEN BEGIN
          CLEAR(ESEnterFileName);
          ESEnterFileName.LOOKUPMODE(TRUE);
          ESEnterFileName.SelectNewFile(TRUE);
          IF ESEnterFileName.RUNMODAL <> ACTION::LookupOK THEN
            ERROR(Text004);
          ExportFileName := ESEnterFileName.GetFileName;
        END;

        IF ConfirmOverwrite THEN
          IF EXISTS(ExportFileName) THEN
            IF NOT CONFIRM(Text003,FALSE,ExportFileName) THEN
              ERROR(Text004);

        LocalFileName := ExportFileName;
        OutputFile.WRITEMODE(TRUE);
        OutputFile.TEXTMODE(TRUE);
        OutputFile.CREATETEMPFILE;
        ExportFileName := OutputFile.NAME;
        OutputFile.CLOSE;
      END;

      IF GUIALLOWED AND ShowProgress THEN
        Window.OPEN(STRSUBSTNO(ESFLADSSetup.ExtendProgressText(Text005),TABLECAPTION) + Text006);

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,Text008);
      OutputFile.WRITEMODE(TRUE);
      OutputFile.TEXTMODE(TRUE);
      IF EXISTS(ExportFileName) AND AppendToFile THEN BEGIN
        OutputFile.OPEN(ExportFileName);
        OutputFile.SEEK(OutputFile.LEN);
      END ELSE
        OutputFile.CREATE(ExportFileName);

      CLEAR(ESFileMgt);
      ESFileMgt.SetDelimiters('~"','"~',',');

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,'HEADER');
      Value[1] := ESFileMgt.FormatInteger(0) +'V1';
      Value[2] := 'HEADER';
      Value[3] := "User ID" + ' ' + ESFileMgt.FormatInteger("Source Table ID");
      Value[4] := '';
      Value[5] := USERID;
      Value[6] := ESFileMgt.FormatDate(TODAY);
      Value[7] := ESFileMgt.FormatTime(TIME);
      OutputFile.WRITE(ESFileMgt.FormatValues(Value,7));

      Value[1] := ESFileMgt.FormatInteger(DATABASE::"ES User Security Setup") + 'V1';
      Value[2] := "User ID";
      Value[3] := ESFileMgt.FormatInteger("Source Table ID");
      Value[4] := "Field Level Security Code";
      Value[5] := "Data Security Code";
      Value[6] := "Security as User ID";

      OutputFile.WRITE(ESFileMgt.FormatValues(Value,6));

      TotalRecords := TotalRecords + 1;

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,'TOTALS');
      Value[1] := ESFileMgt.FormatInteger(0) + 'V1';
      Value[2] := 'TOTALS';
      Value[3] := "User ID" + ' ' + ESFileMgt.FormatInteger("Source Table ID");
      Value[4] := '';
      Value[5] := USERID;
      Value[6] := ESFileMgt.FormatDate(TODAY);
      Value[7] := ESFileMgt.FormatTime(TIME);
      Value[8] := ESFileMgt.FormatInteger(TotalRecords);
      OutputFile.WRITE(ESFileMgt.FormatValues(Value,8));

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,Text011);
      OutputFile.CLOSE;

      IF NOT AppendToFile THEN BEGIN
        IF GUIALLOWED AND ShowProgress THEN
          Window.UPDATE(1,Text101);
        FileMgt.DownloadToFile(ExportFileName,LocalFileName);
      END;

      IF GUIALLOWED AND ShowProgress THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(
          Text012,
          TABLECAPTION,"User ID","Source Table ID",LocalFileName,TotalRecords);

      EXIT(TotalRecords);
    END;

    PROCEDURE ImportUserSecuritySetup@1240520013(ShowStatus@1240520011 : Boolean;ImportFileName@1240520018 : Text[250];OverWriteExisting@1240520001 : Boolean);
    VAR
      ESUserSecuritySetup@1240520002 : Record 14123805;
      ESUserSecuritySetup2@1240520006 : Record 14123805;
      ESEnterFileName@1240520019 : Page 14123560;
      ESFileMgt@1240520005 : Codeunit 14123502;
      FileMgt@1001 : Codeunit 419;
      InputFile@1240520009 : File;
      Window@1240520012 : Dialog;
      TempText@1240520010 : Text[1000];
      Value@1240520008 : ARRAY [100] OF Text[1000];
      ServerFileName@1000 : Text[1000];
      NextUpdatePos@1240520022 : Integer;
      TotalRecords@1240520023 : Integer;
    BEGIN
      IF ImportFileName = '' THEN BEGIN
        CLEAR(ESEnterFileName);
        ESEnterFileName.LOOKUPMODE(TRUE);
        IF ESEnterFileName.RUNMODAL <> ACTION::LookupOK THEN
          ERROR(Text013);
        ImportFileName := ESEnterFileName.GetFileName;
      END;

      IF ImportFileName = '' THEN
        ERROR(Text014);

      IF GUIALLOWED THEN
        Window.OPEN(
          STRSUBSTNO(ESFLADSSetup.ExtendProgressText(Text015),TABLECAPTION) +
          Text016 + '        @2@@@@@@@@@@@@@@@@@@@@@@@@@@@@');

      CLEAR(ESFileMgt);
      ESFileMgt.SetDelimiters('~"','"~',',');

      IF GUIALLOWED THEN
        Window.UPDATE(1,Text017);
      InputFile.WRITEMODE(FALSE);
      InputFile.TEXTMODE(TRUE);
      IF NOT InputFile.OPEN(ImportFileName) THEN BEGIN
        IF GUIALLOWED THEN
          Window.UPDATE(1,Text102);
        ServerFileName := FileMgt.UploadFileSilent(ImportFileName);
        IF GUIALLOWED THEN
          Window.UPDATE(1,Text017);

        IF NOT InputFile.OPEN(ServerFileName) THEN
          InputFile.OPEN(ImportFileName);
      END;
      WHILE InputFile.POS < InputFile.LEN DO BEGIN
        IF NextUpdatePos < InputFile.POS THEN BEGIN
          IF GUIALLOWED THEN
            Window.UPDATE(2,ROUND(10000 * (InputFile.POS / InputFile.LEN),1));
          NextUpdatePos := ROUND(InputFile.POS + InputFile.LEN / 500,1);
        END;

        InputFile.READ(TempText);
        ESFileMgt.SplitInValues(Value,TempText);

        CASE Value[1] OF
          '','0V1':
            ;
          '14123805V1':
            BEGIN
              IF GUIALLOWED THEN
                Window.UPDATE(1,STRSUBSTNO('%1 %2',Value[2],Value[3]));

              ESUserSecuritySetup.INIT;
              ESUserSecuritySetup."User ID" := Value[2];
              ESUserSecuritySetup."Source Table ID" := ESFileMgt.EvaluateInteger(Value[3]);
              ESUserSecuritySetup."Field Level Security Code" := Value[4];
              ESUserSecuritySetup."Data Security Code" := Value[5];
              ESUserSecuritySetup."Security as User ID" := Value[6];

              IF OverWriteExisting AND
                 ESUserSecuritySetup2.GET(
                   ESUserSecuritySetup."User ID",ESUserSecuritySetup."Source Table ID")
              THEN
                ESUserSecuritySetup2.DELETE(TRUE);

              ESUserSecuritySetup.SetNoDefault(TRUE);
              ESUserSecuritySetup.INSERT(TRUE);

              TotalRecords := TotalRecords + 1;
            END;
          ELSE
            ERROR(Text002,Value[1]);
        END;
      END;

      InputFile.CLOSE;
      IF ServerFileName <> '' THEN
        ERASE(ServerFileName);

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(Text018,TotalRecords,ImportFileName);
    END;

    PROCEDURE SetNoDefault@1240520001(NewValue@1240520001 : Boolean);
    BEGIN
      NoDefault := NewValue;
    END;

    BEGIN
    END.
  }
}

