OBJECT Table 14000909 E-Mail Attachment
{
  OBJECT-PROPERTIES
  {
    Date=09/24/20;
    Time=[ 5:45:16 AM];
    Modified=Yes;
    Version List=SE0.50.32;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               EMailElement.RESET;
               EMailElement.SETRANGE("E-Mail Code","E-Mail Code");
               EMailElement.SETRANGE("Line No.",-"Line No.");
               EMailElement.DELETEALL;
             END;

    CaptionML=ENU=E-Mail Attachment;
  }
  FIELDS
  {
    { 1   ;   ;E-Mail Code         ;Code10        ;TableRelation="E-Mail Header";
                                                   CaptionML=ENU=E-Mail Code }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 11  ;   ;Description         ;Text30        ;CaptionML=ENU=Description }
    { 12  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Type <> xRec.Type THEN BEGIN
                                                                  "Attachment Report ID" := 0;
                                                                  CALCFIELDS("Attachment Report Name");
                                                                  "Fixed File Name" := '';
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Report,Fixed File,Processing Report;
                                                   OptionString=Report,Fixed File,Processing Report }
    { 13  ;   ;Use Attachment File Name;Text250   ;CaptionML=ENU=Use Attachment File Name;
                                                   Editable=No }
    { 14  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("E-Mail Comment Line" WHERE (Table ID=CONST(14000902),
                                                                                                  Code=FIELD(E-Mail Code),
                                                                                                  Integer 1=FIELD(Line No.)));
                                                   CaptionML=ENU=Comment;
                                                   Editable=No }
    { 21  ;   ;Attachment Report ID;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Report));
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::Report);

                                                                CALCFIELDS("Attachment Report Name");
                                                              END;

                                                   CaptionML=ENU=Attachment Report ID }
    { 22  ;   ;Attachment Report Name;Text30      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObj."Object Name" WHERE (Object Type=CONST(Report),
                                                                                                  Object ID=FIELD(Attachment Report ID)));
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::Report);
                                                              END;

                                                   CaptionML=ENU=Attachment Report Name;
                                                   Editable=No }
    { 23  ;   ;Attachment Name     ;Text30        ;OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::Report);
                                                              END;

                                                   CaptionML=ENU=Attachment Name }
    { 31  ;   ;Fixed File Name     ;Text250       ;OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::"Fixed File");
                                                              END;

                                                   CaptionML=ENU=Fixed File Name }
    { 51700;  ;Processing Report ID;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Report));
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::"Processing Report");

                                                                CALCFIELDS("Attachment Report Name");
                                                              END;

                                                   CaptionML=ENU=Processing Report ID }
    { 51701;  ;Processing Report Name;Text30      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObj."Object Name" WHERE (Object Type=CONST(Report),
                                                                                                  Object ID=FIELD(Processing Report ID)));
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::"Processing Report");
                                                              END;

                                                   CaptionML=ENU=Processing Report Name;
                                                   Editable=No }
    { 51702;  ;File Format         ;Option        ;CaptionML=ENU=File Format;
                                                   OptionCaptionML=ENU=" ,Excel";
                                                   OptionString=[ ,Excel] }
  }
  KEYS
  {
    {    ;E-Mail Code,Line No.                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EMailHeader@1000000000 : Record 14000901;
      EMailElement@1000000001 : Record 14000903;
      EMailCommentLine@1000000004 : Record 14000910;
      EMailComment@1000000005 : Page 14000921;
      LevelLookupString@1000000002 : Text[250];
      MaxLevels@1000000003 : Integer;

    LOCAL PROCEDURE GetHeader@1();
    VAR
      SalesOrderEMail@1000000000 : Report 14000901;
      SalesShipmentEMail@1000000001 : Report 14000902;
      SalesInvoiceEMail@1000000002 : Report 14000903;
      BillOfLadingEMail@1000000003 : Report 14000908;
      CustomerStatementEMail@1000000004 : Report 14000910;
      EMailListEntryEMail@1000000005 : Report 14000913;
      ContactEMail@1000000006 : Report 14000912;
      PurchaseEMail@1000000009 : Report 14000914;
      IssuedReminderEMail@1000000010 : Report 14000916;
      IssuedFinChargeMemoEMail@1000000011 : Report 14000918;
      PurchaseReceiptEMail@1240030000 : Report 14000920;
      PurchaseInvoiceEMail@1240030001 : Report 14000921;
      MfrRepStatementEmail@1000000012 : Report 51706;
      LocHdlgStatementEMail@1000000013 : Report 51707;
      SalesCrMemoEMail@1000000014 : Report 51020;
      i@1000000007 : Integer;
      LevelText@1000000008 : Text[30];
    BEGIN
      IF EMailHeader.Code <> "E-Mail Code" THEN
        IF "E-Mail Code" = '' THEN BEGIN
          CLEAR(EMailHeader);
          LevelLookupString := '';
          MaxLevels := 0;
        END ELSE BEGIN
          EMailHeader.GET("E-Mail Code");

          LevelLookupString := '';
          MaxLevels := 0;
          REPEAT
            MaxLevels := MaxLevels + 1;

            CASE EMailHeader."E-Mail Usage" OF
              EMailHeader."E-Mail Usage"::"Sales Order":
                LevelText := SalesOrderEMail.GetLevelName(MaxLevels);
              EMailHeader."E-Mail Usage"::"Sales Shipment":
                LevelText := SalesShipmentEMail.GetLevelName(MaxLevels);
              EMailHeader."E-Mail Usage"::"Sales Invoice":
                LevelText := SalesInvoiceEMail.GetLevelName(MaxLevels);
              EMailHeader."E-Mail Usage"::"Bill of Lading":
                LevelText := BillOfLadingEMail.GetLevelName(MaxLevels);
              EMailHeader."E-Mail Usage"::"Customer Statement":
                LevelText := CustomerStatementEMail.GetLevelName(MaxLevels);
              EMailHeader."E-Mail Usage"::"E-Mail List Mailing":
                LevelText := EMailListEntryEMail.GetLevelName(MaxLevels);
              EMailHeader."E-Mail Usage"::"Contact Mailing":
                LevelText := ContactEMail.GetLevelName(MaxLevels);
              EMailHeader."E-Mail Usage"::Purchase:
                LevelText := PurchaseEMail.GetLevelName(MaxLevels);
              EMailHeader."E-Mail Usage"::"Issued Reminder":
                LevelText := IssuedReminderEMail.GetLevelName(MaxLevels);
              EMailHeader."E-Mail Usage"::"Issued Fin. Charge Memo":
                LevelText := IssuedFinChargeMemoEMail.GetLevelName(MaxLevels);
              EMailHeader."E-Mail Usage"::"Purchase Receipt":
                LevelText := PurchaseReceiptEMail.GetLevelName(MaxLevels);
              EMailHeader."E-Mail Usage"::"Purchase Invoice":
                LevelText := PurchaseInvoiceEMail.GetLevelName(MaxLevels);
              // <TPZ92, TPZ143>
              EMailHeader."E-Mail Usage"::"Mfr. Rep. Statement":
                LevelText := MfrRepStatementEmail.GetLevelName(MaxLevels);
              EMailHeader."E-Mail Usage"::"Location Hdlg. Statement":
                LevelText := LocHdlgStatementEMail.GetLevelName(MaxLevels);
              EMailHeader."E-Mail Usage"::"Sales Credit Memo":
                LevelText := SalesCrMemoEMail.GetLevelName(MaxLevels);
              // </TPZ92, TPZ143>
              ELSE
                LevelText := '';
            END;

            IF LevelText <> '' THEN
              IF LevelLookupString <> '' THEN
                LevelLookupString := LevelLookupString + ',' + LevelText
              ELSE
                LevelLookupString := LevelText;
          UNTIL LevelText = '';
          MaxLevels := MaxLevels - 1;
        END;
    END;

    PROCEDURE LookupComment@4();
    BEGIN
      EMailCommentLine.RESET;
      EMailCommentLine.SETRANGE("Table ID",DATABASE::"E-Mail Line");
      EMailCommentLine.SETRANGE(Code,"E-Mail Code");
      EMailCommentLine.SETRANGE("Integer 1","Line No.");
      CLEAR(EMailComment);
      EMailComment.SETTABLEVIEW(EMailCommentLine);
      EMailComment.RUNMODAL;
    END;

    BEGIN
    {
      2015-06-12 TPZ92 TAKHMETO
        GetHeader function has been modified, Processing Report ID, Processing Report Name, File Format fields have been added
      2015-08-25 TPZ143 TAKHMETO
        GetHeader function has been modified
    }
    END.
  }
}

