OBJECT Table 1877 VAT Setup Posting Groups
{
  OBJECT-PROPERTIES
  {
    Date=02/01/20;
    Time=12:00:00 PM;
    Version List=NAVW111.00.00.40505;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Tax Setup Posting Groups;
               ESM=Grupos contables de configuraci¢n del IVA;
               FRC=Groupes de report de configuration TVA;
               ENC=Tax Setup Posting Groups];
  }
  FIELDS
  {
    { 2   ;   ;VAT Prod. Posting Group;Code20     ;CaptionML=[ENU=VAT Prod. Posting Group;
                                                              ESM=Grupo registro IVA prod.;
                                                              FRC=Groupe de report produit TVA;
                                                              ENC=VAT Prod. Posting Group];
                                                   NotBlank=Yes }
    { 4   ;   ;VAT %               ;Decimal       ;CaptionML=[ENU=Tax %;
                                                              ESM=% IVA;
                                                              FRC=% TVA;
                                                              ENC=Tax %];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 7   ;   ;Sales VAT Account   ;Code20        ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Sales Tax Account;
                                                              ESM=Cta. IVA por pagar;
                                                              FRC=Compte TVA vente;
                                                              ENC=Sales Tax Account] }
    { 9   ;   ;Purchase VAT Account;Code20        ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Purchase Tax Account;
                                                              ESM=Cta. IVA acreditable;
                                                              FRC=Compte TVA achat;
                                                              ENC=Purchase Tax Account] }
    { 11  ;   ;Reverse Chrg. VAT Acc.;Code20      ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Reverse Chrg. Tax Acc.;
                                                              ESM=Cta. reversi¢n IVA;
                                                              FRC=Compte TVA due intracomm.;
                                                              ENC=Reverse Chrg. Tax Acc.] }
    { 18  ;   ;VAT Prod. Posting Grp Desc.;Text50 ;CaptionML=[ENU=Tax Prod. Posting Grp Desc.;
                                                              ESM=Desc. grupo reg. IVA prod.;
                                                              FRC=Desc. gpe report produit TVA;
                                                              ENC=Tax Prod. Posting Grp Desc.] }
    { 19  ;   ;VAT Clause Desc     ;Text250       ;CaptionML=[ENU=Tax Clause Desc;
                                                              ESM=Desc. cl usula de IVA;
                                                              FRC=Desc clause TVA;
                                                              ENC=Tax Clause Desc] }
    { 22  ;   ;Selected            ;Boolean       ;FieldClass=Normal;
                                                   CaptionML=[ENU=Selected;
                                                              ESM=Seleccionado;
                                                              FRC=S‚lectionn‚;
                                                              ENC=Selected] }
    { 23  ;   ;Application Type    ;Option        ;CaptionML=[ENU=Application Type;
                                                              ESM=Tipo de aplicaci¢n;
                                                              FRC=Type d'affectation;
                                                              ENC=Application Type];
                                                   OptionCaptionML=[ENU=,Items,Services;
                                                                    ESM=,Productos,Servicios;
                                                                    FRC=,Articles,Services;
                                                                    ENC=,Items,Services];
                                                   OptionString=,Items,Services }
  }
  KEYS
  {
    {    ;VAT Prod. Posting Group                 ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      FULLNORMTok@1601 : TextConst '@@@={Locked};ENU=FULL NORM;ESM=FULL NORM;FRC=FULL NORM;ENC=FULL NORM';
      FULLREDTok@1602 : TextConst '@@@={Locked};ENU=FULL RED;ESM=FULL RED;FRC=FULL RED;ENC=FULL RED';
      REDUCEDTok@1603 : TextConst '@@@={Locked};ENU=REDUCED;ESM=REDUCED;FRC=REDUCED;ENC=REDUCED';
      SERVNORMTok@1604 : TextConst '@@@={Locked};ENU=SERV NORM;ESM=SERV NORM;FRC=SERV NORM;ENC=SERV NORM';
      SERVREDTok@1605 : TextConst '@@@={Locked};ENU=SERV RED;ESM=SERV RED;FRC=SERV RED;ENC=SERV RED';
      STANDARDTok@1606 : TextConst '@@@={Locked};ENU=STANDARD;ESM=STANDARD;FRC=STANDARD;ENC=STANDARD';
      ZEROTok@1607 : TextConst '@@@={Locked};ENU=ZERO;ESM=ZERO;FRC=ZERO;ENC=ZERO';
      FULLNORMTxt@1608 : TextConst 'ENU=Tax Only Invoices 25%;ESM=Facturas de solo IVA (25ÿ%);FRC=Factures TVA uniquement 25ÿ%;ENC=Tax Only Invoices 25%';
      FULLREDTxt@1609 : TextConst 'ENU=Tax Only Invoices 10%;ESM=Facturas de solo IVA (10ÿ%);FRC=Factures TVA uniquement 10ÿ%;ENC=Tax Only Invoices 10%';
      REDUCEDTxt@1610 : TextConst 'ENU=Reduced Tax (10%);ESM=IVA reducido (10ÿ%);FRC=TVA r‚duite (10ÿ%);ENC=Reduced Tax (10%)';
      SERVNORMTxt@1611 : TextConst 'ENU=Miscellaneous 25% Tax;ESM=Varios con IVA 25ÿ%;FRC=TVA diversÿ25ÿ%;ENC=Miscellaneous 25% Tax';
      SERVREDTxt@1612 : TextConst 'ENU=Miscellaneous 10% Tax;ESM=Varios con IVA 10ÿ%;FRC=TVA diversÿ10ÿ%;ENC=Miscellaneous 10% Tax';
      STANDARDTxt@1613 : TextConst 'ENU=Standard Tax (25%);ESM=IVA est ndar (25ÿ%);FRC=TVA standard (25ÿ%);ENC=Standard Tax (25%)';
      ZEROTxt@1614 : TextConst 'ENU=No Tax;ESM=N.§ IVA;FRC=Pas de TVA;ENC=No Tax';
      InvalidGLAccountsTxt@1000 : TextConst '@@@=%1 is placeholder for the invalid gl account code;ENU=%1 is not valid G/L Account.;ESM=%1 no es una cuenta de C/G v lida.;FRC=%1 n''est pas un compte du grand livre valide.;ENC=%1 is not valid G/L Account.';

      VATAssistedAccountsMsg@1001 : TextConst
        'ENU=You have not assigned general ledger accounts for sales and purchases for all Tax amounts. You won''t be able to calculate and post Tax for the missing accounts. If you''re skipping this step on purpose, you can manually assign accounts later in the Tax Posting Setup page.',
        'ESM=No ha asignado las cuentas de contabilidad general de ventas y compras a todos los importes de IVA. No podr  calcular ni registrar el IVA de las cuentas que faltan. Si omite este paso a prop¢sito, podr  asignar manualmente las cuentas m s adelante en la p gina de configuraci¢n de registro de IVA.',
        'FRC=Vous n''avez pas affect‚ de comptes GL pour les ventes et les achats pour tous les montants de TVA. Vous ne pourrez pas calculer ni reporter la TVA pour les comptes manquants. Si vous ignorez d‚lib‚r‚ment cette ‚tape, vous pourrez affecter manuellement des comptes ult‚rieurement dans la page Configuration report de taxe.',
        'ENC=You have not assigned general ledger accounts for sales and purchases for all Tax amounts. You won''t be able to calculate and post Tax for the missing accounts. If you''re skipping this step on purpose, you can manually assign accounts later in the Tax Posting Setup page.';

    PROCEDURE ValidateVATRates@1() : Boolean;
    VAR
      VATSetupPostingGroups@1000 : Record 1877;
    BEGIN
      VATSetupPostingGroups.RESET;
      VATSetupPostingGroups.SETRANGE(Selected,TRUE);
      EXIT(NOT VATSetupPostingGroups.ISEMPTY);
    END;

    PROCEDURE PopulateVATProdGroups@37();
    BEGIN
      DELETEALL;
      InsertProdPostingGrp(FULLNORMTok,FULLNORMTxt,100,'','',FALSE);
      InsertProdPostingGrp(FULLREDTok,FULLREDTxt,100,'','',FALSE);
      InsertProdPostingGrp(REDUCEDTok,REDUCEDTxt,10,'5611','5631',FALSE);
      InsertProdPostingGrp(SERVNORMTok,SERVNORMTxt,25,'5611','5631',TRUE);
      InsertProdPostingGrp(SERVREDTok,SERVREDTxt,10,'5611','5631',TRUE);
      InsertProdPostingGrp(STANDARDTok,STANDARDTxt,25,'5610','5630',FALSE);
      InsertProdPostingGrp(ZEROTok,ZEROTxt,0,'5610','5630',FALSE);
    END;

    LOCAL PROCEDURE InsertProdPostingGrp@2(ProdGrpCode@1001 : Code[10];GrpDesc@1002 : Text[50];VatRate@1003 : Integer;SalesAccount@1004 : Code[20];PurchAccount@1005 : Code[20];IsService@1006 : Boolean);
    VAR
      GLAccount@1000 : Record 15;
    BEGIN
      INIT;
      "VAT Prod. Posting Group" := ProdGrpCode;
      "VAT Prod. Posting Grp Desc." := GrpDesc;
      "VAT %" := VatRate;
      IF GLAccount.GET(SalesAccount) THEN
        "Sales VAT Account" := SalesAccount;
      IF GLAccount.GET(PurchAccount) THEN
        "Purchase VAT Account" := PurchAccount;
      "Application Type" := "Application Type"::Items;
      IF IsService THEN
        "Application Type" := "Application Type"::Services;
      Selected := TRUE;
      INSERT;
    END;

    PROCEDURE CheckExistingItemAndServiceWithVAT@20(VATProdPostingGroupCode@1002 : Code[20];IsService@1001 : Boolean) : Boolean;
    VAR
      Item@1003 : Record 27;
      ServiceLine@1000 : Record 5902;
    BEGIN
      IF IsService THEN BEGIN
        ServiceLine.SETRANGE("VAT Prod. Posting Group",VATProdPostingGroupCode);
        EXIT(NOT ServiceLine.ISEMPTY);
      END;
      Item.SETRANGE("VAT Prod. Posting Group",VATProdPostingGroupCode);
      EXIT(NOT Item.ISEMPTY);
    END;

    PROCEDURE ValidateGLAccountsExist@3(VAR ErrorMessage@1000 : Text) : Boolean;
    VAR
      VATSetupPostingGroups@1001 : Record 1877;
      GLAccount@1002 : Record 15;
    BEGIN
      IF ValidateVATRates = FALSE THEN
        EXIT(FALSE);
      VATSetupPostingGroups.SETRANGE(Selected,TRUE);
      IF NOT VATSetupPostingGroups.FINDSET THEN
        EXIT;

      REPEAT
        IF
           (DELCHR(VATSetupPostingGroups."Sales VAT Account",'<>') = '') OR
           (DELCHR(VATSetupPostingGroups."Purchase VAT Account",'<>') = '')
        THEN BEGIN
          ErrorMessage := VATAssistedAccountsMsg;
          EXIT(FALSE);
        END;

        IF NOT GLAccount.GET(VATSetupPostingGroups."Sales VAT Account") THEN BEGIN
          ErrorMessage := STRSUBSTNO(InvalidGLAccountsTxt,VATSetupPostingGroups."Sales VAT Account");
          EXIT(FALSE);
        END;
        IF NOT GLAccount.GET(VATSetupPostingGroups."Purchase VAT Account") THEN BEGIN
          ErrorMessage := STRSUBSTNO(InvalidGLAccountsTxt,VATSetupPostingGroups."Purchase VAT Account");
          EXIT(FALSE);
        END;
      UNTIL VATSetupPostingGroups.NEXT = 0;
      ErrorMessage := '';
      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

