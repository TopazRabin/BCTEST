OBJECT Table 737 VAT Return Period
{
  OBJECT-PROPERTIES
  {
    Date=07/01/19;
    Time=12:00:00 PM;
    Version List=NAVW111.00.00.33812;
  }
  PROPERTIES
  {
    OnInsert=VAR
               NoSeriesMgt@1001 : Codeunit 396;
             BEGIN
               IF "No." = '' THEN
                 NoSeriesMgt.InitSeries(GetNoSeriesCode,xRec."No. Series",WORKDATE,"No.","No. Series");
             END;

    OnDelete=VAR
               VATReportHeader@1000 : Record 740;
             BEGIN
               IF VATReportHeader.GET(VATReportHeader."VAT Report Config. Code"::"VAT Return","VAT Return No.") THEN
                 ERROR(DeleteExistingVATRetErr);
             END;

    CaptionML=[ENU=Tax Return Period;
               ESM=Per°odo de devoluci¢n de IVA;
               FRC=PÇriode de retour TVA;
               ENC=Tax Return Period];
    LookupPageID=Page737;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;CaptionML=[ENU=No.;
                                                              ESM=N.ß;
                                                              FRC=N¯;
                                                              ENC=No.] }
    { 2   ;   ;No. Series          ;Code20        ;CaptionML=[ENU=No. Series;
                                                              ESM=N.ß serie;
                                                              FRC=Souches de n¯;
                                                              ENC=No. Series] }
    { 3   ;   ;Period Key          ;Code10        ;CaptionML=[ENU=Period Key;
                                                              ESM=Clave de per°odo;
                                                              FRC=ClÇ de pÇriode;
                                                              ENC=Period Key] }
    { 4   ;   ;Start Date          ;Date          ;CaptionML=[ENU=Start Date;
                                                              ESM=Fecha de inicio;
                                                              FRC=Date de dÇbut;
                                                              ENC=Start Date] }
    { 5   ;   ;End Date            ;Date          ;CaptionML=[ENU=End Date;
                                                              ESM=Fecha de finalizaci¢n;
                                                              FRC=Date de fin;
                                                              ENC=End Date] }
    { 6   ;   ;Due Date            ;Date          ;CaptionML=[ENU=Due Date;
                                                              ESM=Fecha de vencimiento;
                                                              FRC=Date d'ÇchÇance;
                                                              ENC=Due Date] }
    { 7   ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              ESM=Estado;
                                                              FRC=êtat;
                                                              ENC=Status];
                                                   OptionCaptionML=[ENU=Open,Closed;
                                                                    ESM=Pendiente,Cerrado;
                                                                    FRC=Ouvert,FermÇ;
                                                                    ENC=Open,Closed];
                                                   OptionString=Open,Closed }
    { 8   ;   ;Received Date       ;Date          ;CaptionML=[ENU=Received Date;
                                                              ESM=Fecha de recepci¢n;
                                                              FRC=Date de rÇception;
                                                              ENC=Received Date] }
    { 20  ;   ;VAT Return No.      ;Code20        ;TableRelation=IF (VAT Return No.=FILTER(<>'')) "VAT Report Header".No. WHERE (VAT Report Config. Code=CONST(VAT Return),
                                                                                                                                 No.=FIELD(VAT Return No.));
                                                   OnLookup=BEGIN
                                                              DrillDownVATReturn;
                                                            END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Tax Return No.;
                                                              ESM=N.ß devoluci¢n IVA;
                                                              FRC=N¯ retour TVA;
                                                              ENC=Tax Return No.];
                                                   Editable=No }
    { 21  ;   ;VAT Return Status   ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("VAT Report Header".Status WHERE (VAT Report Config. Code=CONST(VAT Return),
                                                                                                        No.=FIELD(VAT Return No.)));
                                                   CaptionML=[ENU=Tax Return Status;
                                                              ESM=Estado de devoluci¢n de IVA;
                                                              FRC=êtat du retour TVA;
                                                              ENC=Tax Return Status];
                                                   OptionCaptionML=[ENU=Open,Released,Submitted,Accepted,Closed,Rejected,Canceled;
                                                                    ESM=Abierto,Lanzado,Enviado,Aceptado,Cerrado,Rechazado,Cancelado;
                                                                    FRC=Ouvert,LibÇrÇ,Soumis,AcceptÇ,FermÇ,RejetÇ,AnnulÇ;
                                                                    ENC=Open,Released,Submitted,Accepted,Closed,Rejected,Cancelled];
                                                   OptionString=Open,Released,Submitted,Accepted,Closed,Rejected,Canceled;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DeleteExistingVATRetErr@1000 : TextConst 'ENU=You cannot delete a Tax return period that has a linked Tax return.;ESM=No puede eliminar un per°odo de devoluci¢n de IVA que tenga una devoluci¢n de IVA vinculada.;FRC=Vous ne pouvez pas supprimer une pÇriode de retour TVA avec un retour TVA liÇ.;ENC=You cannot delete a Tax return period that has a linked Tax return.';
      OverdueTxt@1002 : TextConst '@@@="%1 - date; %2 - days count";ENU=Your Tax return is overdue since %1 (%2 days);ESM=La devoluci¢n de IVA vence desde %1 (%2 d°as);FRC=Votre retour TVA est Çchu depuis %1 (%2 jours);ENC=Your Tax return is overdue since %1 (%2 days)';
      OpenTxt@1001 : TextConst '@@@="%1 - date; %2 - days count";ENU=Your Tax return is due %1 (in %2 days);ESM=La devoluci¢n de IVA vence %1 (en %2 d°as);FRC=Votre retour TVA est dñ le %1 (dans %2 jours);ENC=Your Tax return is due %1 (in %2 days)';
      VATReportSetup@1003 : Record 743;
      VATReportSetupGot@1004 : Boolean;

    LOCAL PROCEDURE GetNoSeriesCode@4() : Code[20];
    VAR
      VATReportSetup@1000 : Record 743;
    BEGIN
      VATReportSetup.GET;
      VATReportSetup.TESTFIELD("VAT Return Period No. Series");
      EXIT(VATReportSetup."VAT Return Period No. Series");
    END;

    PROCEDURE CopyToVATReturn@1(VAR VATReportHeader@1000 : Record 740);
    BEGIN
      TESTFIELD("Start Date");
      TESTFIELD("End Date");
      VATReportHeader."Return Period No." := "No.";
      VATReportHeader."Period Year" := DATE2DMY("End Date",3);
      VATReportHeader."Start Date" := "Start Date";
      VATReportHeader."End Date" := "End Date";
      ParseDatePeriod(VATReportHeader);
    END;

    LOCAL PROCEDURE ParseDatePeriod@2(VAR VATReportHeader@1000 : Record 740);
    VAR
      Date@1001 : Record 2000000007;
    BEGIN
      Date.SETRANGE("Period Start",VATReportHeader."Start Date");
      Date.SETRANGE("Period End",CLOSINGDATE(VATReportHeader."End Date"));
      IF Date.FINDFIRST THEN BEGIN
        CASE Date."Period Type" OF
          Date."Period Type"::Month:
            VATReportHeader."Period Type" := VATReportHeader."Period Type"::Month;
          Date."Period Type"::Quarter:
            VATReportHeader."Period Type" := VATReportHeader."Period Type"::Quarter;
          Date."Period Type"::Year:
            VATReportHeader."Period Type" := VATReportHeader."Period Type"::Year;
        END;
        VATReportHeader."Period No." := Date."Period No.";
      END ELSE BEGIN
        VATReportHeader."Period Type" := VATReportHeader."Period Type"::" ";
        VATReportHeader."Period No." := 0;
      END;
    END;

    PROCEDURE FindVATReturnPeriod@3(VAR VATReturnPeriod@1000 : Record 737;StartDate@1001 : Date;EndDate@1002 : Date) : Boolean;
    BEGIN
      VATReturnPeriod.SETRANGE("Start Date",StartDate);
      VATReturnPeriod.SETRANGE("End Date",EndDate);
      EXIT(VATReturnPeriod.FINDFIRST);
    END;

    PROCEDURE DiffersFromVATReturnPeriod@5(VAR VATReturnPeriod@1000 : Record 737) : Boolean;
    BEGIN
      EXIT(
        ("Due Date" <> VATReturnPeriod."Due Date") OR
        (Status <> VATReturnPeriod.Status) OR
        ("Received Date" <> VATReturnPeriod."Received Date") OR
        ("Period Key" <> VATReturnPeriod."Period Key"));
    END;

    PROCEDURE DrillDownVATReturn@6();
    VAR
      VATReportMgt@1000 : Codeunit 737;
    BEGIN
      IF (Status = Status::Open) OR ("VAT Return No." <> '') THEN
        VATReportMgt.OpenVATReturnCardFromVATPeriod(Rec);
    END;

    LOCAL PROCEDURE GetVATReportSetup@8();
    BEGIN
      IF VATReportSetupGot THEN
        EXIT;

      VATReportSetup.GET;
      VATReportSetupGot := TRUE;
    END;

    PROCEDURE CheckOpenOrOverdue@7() : Text;
    BEGIN
      GetVATReportSetup;
      IF (Status = Status::Open) AND ("Due Date" <> 0D) THEN
        CASE TRUE OF
          // Overdue
          ("Due Date" < WORKDATE):
            EXIT(STRSUBSTNO(OverdueTxt,"Due Date",WORKDATE - "Due Date"));
          // Open
          VATReportSetup.IsPeriodReminderCalculation AND
          ("Due Date" >= WORKDATE) AND
          ("Due Date" <= CALCDATE(VATReportSetup."Period Reminder Calculation",WORKDATE)):
            EXIT(STRSUBSTNO(OpenTxt,"Due Date","Due Date" - WORKDATE));
        END;
    END;

    BEGIN
    END.
  }
}

