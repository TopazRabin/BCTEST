OBJECT Table 14123814 ES FLADS Control
{
  OBJECT-PROPERTIES
  {
    Date=04/23/16;
    Time=12:00:00 PM;
    Version List=ES1.41.16;
  }
  PROPERTIES
  {
    CaptionML=ENU=Field Level and Data Security Control;
    LookupPageID=Page14123842;
    DrillDownPageID=Page14123842;
  }
  FIELDS
  {
    { 1   ;   ;Object Type         ;Option        ;CaptionML=ENU=Object Type;
                                                   OptionCaptionML=ENU=TableData,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System;
                                                   OptionString=TableData,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System }
    { 3   ;   ;Object ID           ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=FIELD(Object Type));
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Object ID;
                                                   NotBlank=Yes }
    { 4   ;   ;Control ID          ;Integer       ;CaptionML=ENU=Control ID;
                                                   NotBlank=Yes }
    { 5   ;   ;Index               ;Integer       ;CaptionML=ENU=Index }
    { 11  ;   ;Object Caption      ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FIELD(Object Type),
                                                                                                                Object ID=FIELD(Object ID)));
                                                   CaptionML=ENU=Object Caption;
                                                   Editable=No }
    { 12  ;   ;Control Type Text   ;Text30        ;CaptionML=ENU=Control Type Text }
    { 13  ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=" ,TextBox,Command Button,Menu Button,Menu Item";
                                                   OptionString=[ ,TextBox,Command Button,Menu Button,Menu Item] }
    { 14  ;   ;Control Name        ;Text100       ;CaptionML=ENU=Control Name }
    { 15  ;   ;Field ID            ;Integer       ;CaptionML=ENU=Field ID;
                                                   BlankZero=Yes }
    { 16  ;   ;Field Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Source Table No.),
                                                                                                   No.=FIELD(Field ID)));
                                                   CaptionML=ENU=Field Caption;
                                                   Editable=No }
    { 17  ;   ;Source Table No.    ;Integer       ;CaptionML=ENU=Source Table No.;
                                                   BlankZero=Yes }
    { 18  ;   ;Source Table Caption;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Source Table No.)));
                                                   CaptionML=ENU=Source Table Caption;
                                                   Editable=No }
    { 19  ;   ;Editable            ;Boolean       ;CaptionML=ENU=Editable }
    { 20  ;   ;Visible             ;Boolean       ;CaptionML=ENU=Visible }
    { 21  ;   ;Level               ;Integer       ;CaptionML=ENU=Level }
    { 22  ;   ;Visible Code        ;Text50        ;CaptionML=ENU=Visible Code }
    { 23  ;   ;Editable Code       ;Text50        ;CaptionML=ENU=Editable Code }
    { 24  ;   ;HideValue Code      ;Text50        ;CaptionML=ENU=HideValue Code }
    { 25  ;   ;SourceExpr Code     ;Text50        ;CaptionML=ENU=SourceExpr Code }
    { 26  ;   ;RunObject Code      ;Text50        ;CaptionML=ENU=RunObject Code }
    { 27  ;   ;Parent Control ID   ;Integer       ;CaptionML=ENU=Parent Control ID;
                                                   NotBlank=Yes;
                                                   BlankZero=Yes }
    { 28  ;   ;PageType Code       ;Text30        ;CaptionML=ENU=PageType Code }
    { 29  ;   ;Control Number in Code;Integer     ;CaptionML=ENU=Control Number in Code }
    { 30  ;   ;Field Name          ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Source Table No.),
                                                                                             No.=FIELD(Field ID)));
                                                   CaptionML=ENU=Field Name;
                                                   Editable=No }
    { 31  ;   ;Enabled Code        ;Text50        ;CaptionML=ENU=Enabled Code }
    { 32  ;   ;Run Object Type     ;Option        ;CaptionML=ENU=Run Object Type;
                                                   OptionCaptionML=ENU=TableData,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System;
                                                   OptionString=TableData,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System }
    { 33  ;   ;Run Object ID       ;Integer       ;CaptionML=ENU=Run Object ID }
    { 34  ;   ;Run Object Caption  ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FIELD(Run Object Type),
                                                                                                                Object ID=FIELD(Run Object ID)));
                                                   CaptionML=ENU=Run Object Caption;
                                                   Editable=No }
    { 35  ;   ;First Variable ID   ;Integer       ;CaptionML=ENU=First Variable ID }
    { 36  ;   ;Control Name Blank  ;Boolean       ;CaptionML=ENU=Control Name Blank }
    { 37  ;   ;Control Caption     ;Text30        ;CaptionML=ENU=Control Caption }
    { 38  ;   ;Control Type        ;Option        ;CaptionML=ENU=Control Type;
                                                   OptionCaptionML=ENU=" ,MenuButton,CommandButton,Action,TableBox,Field,TextBox,CheckBox,PictureBox,SubForm,Part,Group,TabControl,Separator,Frame,OptionButton,Indicator,Image,MatrixBox,ActionGroup,Container,ActionContainer,Label";
                                                   OptionString=[ ,MenuButton,CommandButton,Action,TableBox,Field,TextBox,CheckBox,PictureBox,SubForm,Part,Group,TabControl,Separator,Frame,OptionButton,Indicator,Image,MatrixBox,ActionGroup,Container,ActionContainer,Label] }
    { 39  ;   ;ENU Control Caption ;Text30        ;CaptionML=ENU=ENU Control Caption }
    { 40  ;   ;Field Class         ;Option        ;CaptionML=ENU=Field Class;
                                                   OptionCaptionML=ENU=Normal,FlowField,Flowfilter;
                                                   OptionString=Normal,FlowField,Flowfilter }
    { 51  ;   ;Visible Variable Name;Text30       ;CaptionML=ENU=Visible Variable Name }
    { 52  ;   ;Editable Variable Name;Text30      ;CaptionML=ENU=Editable Variable Name }
    { 53  ;   ;HideValue Variable Name;Text30     ;CaptionML=ENU=HideValue Variable Name }
    { 54  ;   ;Enabled Variable Name;Text30       ;CaptionML=ENU=Enabled Variable Name }
    { 55  ;   ;Control Used        ;Boolean       ;CaptionML=ENU=Control Used }
    { 56  ;   ;Conflicting Code    ;Boolean       ;CaptionML=ENU=Conflicting Code }
  }
  KEYS
  {
    {    ;Object Type,Object ID,Control ID,Index  ;Clustered=Yes }
    {    ;Object Type,Object ID,Control Number in Code }
    {    ;Object Type,Object ID,Field ID,Control ID }
    {    ;Object Type,Object ID,First Variable ID  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=%1 must be positive.';
      Text002@1002 : TextConst 'ENU=%1 records inserted and %2 records modified.';
      Text003@1003 : TextConst 'ENU=%1 must be negative.';

    PROCEDURE UpdateValues@1240520008();
    VAR
      ESFLADSControl@1240520010 : Record 14123814;
      ESFLADSSetup@1240520001 : Record 14123801;
      ESFLADSRelation@1240520002 : Record 14123810;
      TempText@1240520009 : Text[30];
      Pos@1240520008 : Integer;
    BEGIN
      ESFLADSSetup.GET;

      CASE "Control Type Text" OF
        'MenuButton':
          "Control Type" := "Control Type"::MenuButton;
        'CommandButton':
          "Control Type" := "Control Type"::CommandButton;
        'Action':
          "Control Type" := "Control Type"::Action;
        'TableBox':
          "Control Type" := "Control Type"::TableBox;
        'Field':
          "Control Type" := "Control Type"::Field;
        'TextBox':
          "Control Type" := "Control Type"::TextBox;
        'CheckBox':
          "Control Type" := "Control Type"::CheckBox;
        'PictureBox':
          "Control Type" := "Control Type"::PictureBox;
        'SubForm':
          "Control Type" := "Control Type"::SubForm;
        'Part':
          "Control Type" := "Control Type"::Part;
        'Group':
          "Control Type" := "Control Type"::Group;
        'TabControl':
          "Control Type" := "Control Type"::TabControl;
        'Separator':
          "Control Type" := "Control Type"::Separator;
        'Frame':
          "Control Type" := "Control Type"::Frame;
        'OptionButton':
          "Control Type" := "Control Type"::OptionButton;
        'Indicator':
          "Control Type" := "Control Type"::Indicator;
        'Image':
          "Control Type" := "Control Type"::Image;
        'MatrixBox':
          "Control Type" := "Control Type"::MatrixBox;
        'ActionGroup':
          "Control Type" := "Control Type"::ActionGroup;
        'Container':
          "Control Type" := "Control Type"::Container;
        'ActionContainer':
          "Control Type" := "Control Type"::ActionContainer;
        'Label':
          "Control Type" := "Control Type"::Label;
      END;

      "Conflicting Code" := FALSE;
      CASE "Object Type" OF
        "Object Type"::"2":
          IF "Field ID" <> 0 THEN BEGIN
            IF NOT ESFLADSSetup."No Field Code on Forms" THEN BEGIN
              "Visible Variable Name" :=
                STRSUBSTNO('ESACC_F%1_Visible',DELCHR(FORMAT("Field ID"),'=',',.'));
              "Editable Variable Name" :=
                STRSUBSTNO('ESACC_F%1_Editable',DELCHR(FORMAT("Field ID"),'=',',.'));
              IF ESFLADSSetup."Disable Coded OnLookup" THEN BEGIN
                ESFLADSRelation.RESET;
                ESFLADSRelation.SETRANGE("Object Type",ESFLADSRelation."Object Type"::Table);
                ESFLADSRelation.SETRANGE("Object ID","Source Table No.");
                ESFLADSRelation.SETRANGE("Field ID","Field ID");
                ESFLADSRelation.SETRANGE(Type,ESFLADSRelation.Type::OnLookup);
                IF ESFLADSRelation.FIND('-') THEN
                  "Enabled Variable Name" := "Editable Variable Name";
              END;
            END;
          END ELSE
            IF ("Control Type" IN ["Control Type"::CommandButton,"Control Type"::MenuButton]) AND
               ("Control Name" <> '')
            THEN BEGIN
              IF NOT ESFLADSSetup."No Button Code on Forms" THEN BEGIN
                "Visible Variable Name" :=
                  STRSUBSTNO('ESACC_C%1_Visible',DELCHR(FORMAT("Control ID"),'=',',.'));
                "Enabled Variable Name" :=
                  STRSUBSTNO('ESACC_C%1_Enabled',DELCHR(FORMAT("Control ID"),'=',',.'));
              END;
            END;
        "Object Type"::Page:
          IF ("Control Type" = "Control Type"::Field) AND ("Field ID" <> 0) THEN BEGIN
            IF NOT ESFLADSSetup."No Field Code on Pages" AND
               NOT ESFLADSControl.GET("Object Type",-"Object ID","Control ID",Index)
            THEN BEGIN
              "Visible Variable Name" :=
                STRSUBSTNO('ESACC_F%1_Visible',DELCHR(FORMAT("Field ID"),'=',',.'));
              "HideValue Variable Name" :=
                STRSUBSTNO('ESACC_F%1_HideValue',DELCHR(FORMAT("Field ID"),'=',',.'));

              IF ("Visible Code" <> '') AND ("HideValue Code" <> '') THEN
                IF (COPYSTR("Visible Code",1,7) <> 'ESACC_F') AND
                   (COPYSTR("HideValue Code",1,7) <> 'ESACC_F')
                THEN
                  "Conflicting Code" := TRUE;

              IF "Field Class" = "Field Class"::Normal THEN BEGIN
                "Editable Variable Name" :=
                  STRSUBSTNO('ESACC_F%1_Editable',DELCHR(FORMAT("Field ID"),'=',',.'));

                IF "Editable Code" <> '' THEN
                  IF (COPYSTR("Editable Code",1,7) <> 'ESACC_F') AND
                     (UPPERCASE("Editable Code") <> 'FALSE')
                  THEN
                    "Conflicting Code" := TRUE;

                IF ESFLADSSetup."Disable Coded OnLookup" THEN BEGIN
                  ESFLADSRelation.RESET;
                  ESFLADSRelation.SETRANGE("Object Type",ESFLADSRelation."Object Type"::Table);
                  ESFLADSRelation.SETRANGE("Object ID","Source Table No.");
                  ESFLADSRelation.SETRANGE("Field ID","Field ID");
                  ESFLADSRelation.SETRANGE(Type,ESFLADSRelation.Type::OnLookup);
                  IF ESFLADSRelation.FIND('-') THEN BEGIN
                    "Enabled Variable Name" := "Editable Variable Name";

                    IF "Enabled Code" <> '' THEN
                      IF COPYSTR("Enabled Code",1,7) <> 'ESACC_F' THEN
                        "Conflicting Code" := TRUE;
                  END;
                END;
              END;
            END ELSE BEGIN
              IF "Visible Variable Name" <> '' THEN
                IF COPYSTR("Visible Variable Name",1,7) = 'ESACC_F' THEN
                  "Visible Variable Name" := '';
              IF "Editable Variable Name" <> '' THEN
                IF COPYSTR("Editable Variable Name",1,7) = 'ESACC_F' THEN
                  "Editable Variable Name" := '';
              IF "Enabled Variable Name" <> '' THEN
                IF COPYSTR("Enabled Variable Name",1,7) = 'ESACC_F' THEN
                  "Enabled Variable Name" := '';
              IF "HideValue Variable Name" <> '' THEN
                IF COPYSTR("HideValue Variable Name",1,7) = 'ESACC_F' THEN
                  "HideValue Variable Name" := '';
            END;
          END ELSE BEGIN
            IF ("Control Type" = "Control Type"::Action) THEN BEGIN
              IF NOT ESFLADSSetup."No Action Code on Pages" AND
                 NOT ESFLADSControl.GET("Object Type",-"Object ID","Control ID",Index)
              THEN BEGIN
                "Visible Variable Name" :=
                  STRSUBSTNO('ESACC_C%1_Visible',DELCHR(FORMAT("Control ID"),'=',',.'));

                IF "Visible Code" <> '' THEN
                  IF COPYSTR("Visible Code",1,7) <> 'ESACC_C' THEN
                    "Conflicting Code" := TRUE;

                "Enabled Variable Name" :=
                  STRSUBSTNO('ESACC_C%1_Enabled',DELCHR(FORMAT("Control ID"),'=',',.'));

                IF "Enabled Code" <> '' THEN
                  IF COPYSTR("Enabled Code",1,7) <> 'ESACC_C' THEN
                    "Conflicting Code" := TRUE;
              END ELSE BEGIN
                IF "Visible Variable Name" <> '' THEN
                  IF COPYSTR("Visible Variable Name",1,7) = 'ESACC_C' THEN
                    "Visible Variable Name" := '';
                IF "Enabled Variable Name" <> '' THEN
                  IF COPYSTR("Enabled Variable Name",1,7) = 'ESACC_C' THEN
                    "Enabled Variable Name" := '';
              END;
            END;
          END;
      END;

      IF Index > 1 THEN BEGIN
        IF "Visible Variable Name" <> '' THEN
          "Visible Variable Name" :=
            "Visible Variable Name" + '_' + DELCHR(FORMAT(Index),'=',',.');

        IF "Editable Variable Name" <> '' THEN
          "Editable Variable Name" :=
            "Editable Variable Name" + '_' + DELCHR(FORMAT(Index),'=',',.');

        IF "HideValue Variable Name" <> '' THEN
          "HideValue Variable Name" :=
            "HideValue Variable Name" + '_' + DELCHR(FORMAT(Index),'=',',.');

        IF "Enabled Variable Name" <> '' THEN
          "Enabled Variable Name" :=
            "Enabled Variable Name" + '_' + DELCHR(FORMAT(Index),'=',',.');
      END;

      IF "RunObject Code" <> '' THEN BEGIN
        Pos := STRPOS("RunObject Code",' ');
        IF (Pos > 1) AND (Pos < STRLEN("RunObject Code")) THEN BEGIN
          TempText := COPYSTR("RunObject Code",Pos + 1);
          CASE UPPERCASE(COPYSTR("RunObject Code",1,Pos - 1)) OF
            'TABLE':
              "Run Object Type" := "Run Object Type"::Table;
            'FORM':
              "Run Object Type" := "Run Object Type"::"2";
            'REPORT':
              "Run Object Type" := "Run Object Type"::Report;
            'DATAPORT':
              "Run Object Type" := "Run Object Type"::"4";
            'CODEUNIT':
              "Run Object Type" := "Run Object Type"::Codeunit;
            'XMLPORT':
              "Run Object Type" := "Run Object Type"::XMLport;
            'PAGE':
              "Run Object Type" := "Run Object Type"::Page;
            'QUERY':
              "Run Object Type" := "Run Object Type"::Query;
          END;

          IF "Run Object Type" > 0 THEN
            IF EVALUATE("Run Object ID",TempText) THEN
              ;
        END;
      END;

      IF "Field ID" <> 0 THEN
        "First Variable ID" := 900000000 + 10 * ("Field ID" MOD 9000000)
      ELSE
        "First Variable ID" := 900000000 + 10 * (("Control ID" + 500000) MOD 9000000 + (Index - 1));

      ESFLADSControl.RESET;
      ESFLADSControl.SETCURRENTKEY("Object Type","Object ID","First Variable ID");
      ESFLADSControl.SETRANGE("Object Type","Object Type");
      ESFLADSControl.SETRANGE("Object ID","Object ID");
      ESFLADSControl.SETFILTER("Control ID",'<>%1',"Control ID");
      ESFLADSControl.SETRANGE("First Variable ID","First Variable ID");
      WHILE ESFLADSControl.FIND('-') DO BEGIN
        "First Variable ID" := "First Variable ID" + 10;
        ESFLADSControl.SETRANGE("First Variable ID","First Variable ID");
      END;

      ESFLADSControl.RESET;
      ESFLADSControl.SETCURRENTKEY("Object Type","Object ID","First Variable ID");
      ESFLADSControl.SETRANGE("Object Type","Object Type");
      ESFLADSControl.SETRANGE("Object ID","Object ID");
      ESFLADSControl.SETRANGE("Control ID","Control ID");
      ESFLADSControl.SETFILTER(Index,'<>%1',Index);
      ESFLADSControl.SETRANGE("First Variable ID","First Variable ID");
      WHILE ESFLADSControl.FIND('-') DO BEGIN
        "First Variable ID" := "First Variable ID" + 10;
        ESFLADSControl.SETRANGE("First Variable ID","First Variable ID");
      END;
    END;

    PROCEDURE GetCaption@1240520012() : Text[250];
    VAR
      TempText@1240520009 : Text[50];
      Pos@1240520010 : Integer;
    BEGIN
      IF "Control Caption" <> '' THEN
        EXIT("Control Caption");

      IF "Control Name" <> '' THEN
        IF COPYSTR("Control Name",1,7) = 'ESACC_C' THEN BEGIN
          TempText := COPYSTR("Control Name",7);
          Pos := STRPOS(TempText,'_');
          IF (Pos > 0) AND (Pos < STRLEN(TempText)) THEN
            EXIT(COPYSTR(TempText,Pos + 1))
          ELSE
            EXIT("Control Name");
        END ELSE
          EXIT("Control Name");

      IF "Field ID" <> 0 THEN BEGIN
        CALCFIELDS("Field Caption");
        EXIT("Field Caption");
      END;

      IF "Run Object ID" <> 0 THEN BEGIN
        CALCFIELDS("Run Object Caption");
        EXIT(STRSUBSTNO('%1 %2 %3',"Run Object Type","Run Object ID","Run Object Caption"));
      END;

      IF "RunObject Code" <> '' THEN
        EXIT("RunObject Code");

      EXIT(STRSUBSTNO('%1 %2',"Control Type","Control ID"));
    END;

    PROCEDURE AddNoCodeImplemented@1240520001(VAR ESFLADSControl@1240520001 : Record 14123814);
    VAR
      ESFLADSControl2@1240520002 : Record 14123814;
      InsertedRecords@1240520003 : Integer;
      ExistingRecords@1240520004 : Integer;
    BEGIN
      IF ESFLADSControl.FIND('-') THEN
        REPEAT
          ESFLADSControl.TESTFIELD("Object Type",ESFLADSControl."Object Type"::Page);
          IF ESFLADSControl."Object ID" < 0 THEN
            ERROR(Text001,ESFLADSControl2.FIELDCAPTION("Object ID"));

          IF NOT ESFLADSControl2.GET(
                   ESFLADSControl."Object Type",-ESFLADSControl."Object ID",
                   ESFLADSControl."Control ID",ESFLADSControl.Index)
          THEN BEGIN
            ESFLADSControl2.INIT;
            ESFLADSControl2."Object Type" := ESFLADSControl."Object Type";
            ESFLADSControl2."Object ID" := -ESFLADSControl."Object ID";
            ESFLADSControl2."Control ID" := ESFLADSControl."Control ID";
            ESFLADSControl2.Index := ESFLADSControl.Index;
            ESFLADSControl2."Source Table No." := ESFLADSControl."Source Table No.";
            ESFLADSControl2."Field ID" := ESFLADSControl."Field ID";
            ESFLADSControl2."Control Type" := ESFLADSControl."Control Type";
            ESFLADSControl2."Control Caption" := ESFLADSControl."Control Caption";
            ESFLADSControl2.INSERT;

            InsertedRecords := InsertedRecords + 1;
          END ELSE BEGIN
            ESFLADSControl2."Source Table No." := ESFLADSControl."Source Table No.";
            ESFLADSControl2."Field ID" := ESFLADSControl."Field ID";
            ESFLADSControl2."Control Type" := ESFLADSControl."Control Type";
            ESFLADSControl2."Control Caption" := ESFLADSControl."Control Caption";
            ESFLADSControl2.MODIFY;

            ExistingRecords := ExistingRecords + 1;
          END;
        UNTIL ESFLADSControl.NEXT = 0;

      MESSAGE(Text002,InsertedRecords,ExistingRecords);
    END;

    PROCEDURE DeleteControls@1240520002(VAR ESFLADSControl@1240520001 : Record 14123814);
    VAR
      ESFLADSControl2@1240520002 : Record 14123814;
    BEGIN
      IF ESFLADSControl.FIND('-') THEN
        REPEAT
          IF ESFLADSControl2.GET(
               ESFLADSControl."Object Type",ESFLADSControl."Object ID",
               ESFLADSControl."Control ID",ESFLADSControl.Index)
          THEN BEGIN
            IF ESFLADSControl2."Object ID" > 0 THEN
              ERROR(Text003,ESFLADSControl2.FIELDCAPTION("Object ID"));

            ESFLADSControl2.DELETE;
          END;
        UNTIL ESFLADSControl.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

