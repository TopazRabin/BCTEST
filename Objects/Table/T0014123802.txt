OBJECT Table 14123802 ES Source Table Setup
{
  OBJECT-PROPERTIES
  {
    Date=08/25/17;
    Time=12:00:00 PM;
    Version List=ES1.41.39;
  }
  PROPERTIES
  {
    DataCaptionFields=Source Table ID,Source Table Caption;
    OnInsert=BEGIN
               ESFLADSSetup.GET;
               ESFLADSSetup.TESTFIELD("Install Allowed");

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit Setup (FLADS)");
               END;

               IF ESFLADSSetup."Create Read-Only Automatically" THEN
                 CreateReadOnlySecurityCode(TRUE,FALSE);

               ESFLADSUpdateNo.UpdateNo;

               "Created By User" := USERID;
               "Created Date Time" := CURRENTDATETIME;
             END;

    OnModify=BEGIN
               ESFLADSSetup.GET;
               ESFLADSSetup.TESTFIELD("Install Allowed");

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit Setup (FLADS)");
               END;

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnDelete=BEGIN
               ESFLADSSetup.GET;
               ESFLADSSetup.TESTFIELD("Install Allowed");

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit Setup (FLADS)");
               END;

               IF ESFLADSSetup."Install Allowed" THEN
                 IF NOT CONFIRM(Text003,FALSE,TABLECAPTION) THEN
                   ERROR(Text004);

               ESFieldLevelSecurityCode.RESET;
               ESFieldLevelSecurityCode.SETRANGE("Source Table ID","Source Table ID");
               IF ESFieldLevelSecurityCode.FIND('-') THEN BEGIN
                 IF ESFLADSSetup."Create Read-Only Automatically" AND
                    (ESFLADSSetup."Read-Only Field Level Sec.Code" <> '')
                 THEN BEGIN
                   ESFieldLevelSecurityCode.SETFILTER(Code,'<>%1',ESFLADSSetup."Read-Only Field Level Sec.Code");
                   IF NOT ESFieldLevelSecurityCode.FIND('-') THEN BEGIN
                     ESFieldLevelSecurityCode.SETRANGE(Code);
                     ESFieldLevelSecurityCode.DELETE(TRUE);
                   END ELSE
                     ESFieldLevelSecurityCode.TESTFIELD("Source Table ID",0);
                 END ELSE
                   ESFieldLevelSecurityCode.TESTFIELD("Source Table ID",0);
               END;

               ESDataSecurityCode.RESET;
               ESDataSecurityCode.SETRANGE("Source Table ID","Source Table ID");
               IF ESDataSecurityCode.FIND('-') THEN
                 ESDataSecurityCode.TESTFIELD("Source Table ID",0);

               ESUserSecuritySetup.RESET;
               ESUserSecuritySetup.SETRANGE("Source Table ID","Source Table ID");
               IF ESUserSecuritySetup.FIND('-') THEN
                 ESUserSecuritySetup.TESTFIELD("Source Table ID",0);

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnRename=BEGIN
               ERROR(Text001);
             END;

    CaptionML=ENU=Source Table Setup;
    LookupPageID=Page14123802;
    DrillDownPageID=Page14123802;
  }
  FIELDS
  {
    { 1   ;   ;Source Table ID     ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table),
                                                                                                      Object ID=FILTER(1..1999999999));
                                                   OnValidate=BEGIN
                                                                IF "Source Table ID" > 1999999999 THEN
                                                                  ERROR(Text002);

                                                                CALCFIELDS("Source Table Name","Source Table Caption",Pages);
                                                              END;

                                                   OnLookup=BEGIN
                                                              AllObjWithCaption.RESET;
                                                              AllObjWithCaption.FILTERGROUP(2);
                                                              AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::Table);
                                                              AllObjWithCaption.SETRANGE("Object ID",1,1999999999);
                                                              AllObjWithCaption.FILTERGROUP(0);
                                                              IF PAGE.RUNMODAL(PAGE::"ES Live Object Informations",AllObjWithCaption) = ACTION::LookupOK THEN
                                                                VALIDATE("Source Table ID",AllObjWithCaption."Object ID");
                                                            END;

                                                   CaptionML=ENU=Source Table ID;
                                                   NotBlank=Yes }
    { 11  ;   ;Source Table Name   ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                             Object ID=FIELD(Source Table ID)));
                                                   CaptionML=ENU=Source Table Name;
                                                   Editable=No }
    { 12  ;   ;Source Table Caption;Text249       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Source Table ID)));
                                                   CaptionML=ENU=Source Table Caption;
                                                   Editable=No }
    { 19  ;   ;Do Not Modify Objects;Boolean      ;OnValidate=BEGIN
                                                                IF "Do Not Modify Objects" THEN BEGIN
                                                                  ESFLADSSetup.GET;
                                                                  ESFLADSSetup.TESTFIELD("Install Allowed");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Do Not Modify Objects }
    { 20  ;   ;No HideValue in Page Parts;Boolean ;CaptionML=ENU=No HideValue in Page Parts }
    { 21  ;   ;Field Level Security Codes;Integer ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES Field Level Security Code" WHERE (Source Table ID=FIELD(Source Table ID)));
                                                   CaptionML=ENU=Field Level Security Codes;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 22  ;   ;Data Security Codes ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES Data Security Code" WHERE (Source Table ID=FIELD(Source Table ID)));
                                                   CaptionML=ENU=Data Security Codes;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 24  ;   ;Pages               ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES FLADS Relation" WHERE (Object Type=CONST(Page),
                                                                                                Relation Object Type=CONST(Table),
                                                                                                Relation Object ID=FIELD(Source Table ID)));
                                                   CaptionML=ENU=Pages;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 25  ;   ;Def Fld Level Sec Code Exists;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Field Level Security Code" WHERE (Source Table ID=FIELD(Source Table ID),
                                                                                                           Default=CONST(Yes)));
                                                   CaptionML=ENU=Default Field Level Security Code Exists;
                                                   Editable=No }
    { 26  ;   ;Default Data Sec. Code Exists;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Data Security Code" WHERE (Source Table ID=FIELD(Source Table ID),
                                                                                                    Default=CONST(Yes)));
                                                   CaptionML=ENU=Default Data Security Code Exists;
                                                   Editable=No }
    { 27  ;   ;No Page Action Changes;Boolean     ;CaptionML=ENU=No Page Action Changes }
    { 28  ;   ;No OnInitPage Changes;Boolean      ;InitValue=Yes;
                                                   CaptionML=ENU=No OnInitPage Changes }
    { 29  ;   ;Created By User     ;Code50        ;CaptionML=ENU=Created By User;
                                                   Editable=No }
    { 30  ;   ;Created Date Time   ;DateTime      ;CaptionML=ENU=Created Date Time;
                                                   Editable=No }
    { 31  ;   ;Filter Codeunit Number;Integer     ;OnValidate=VAR
                                                                Object@1240520001 : Record 2000000001;
                                                              BEGIN
                                                                CASE "Filter Codeunit Number" OF
                                                                  0: Object.GET(Object.Type::Codeunit,'',CODEUNIT::"ES FLADS Set Filters");
                                                                  1: Object.GET(Object.Type::Codeunit,'',CODEUNIT::"ES FLADS Set Filters 1");
                                                                  2: Object.GET(Object.Type::Codeunit,'',CODEUNIT::"ES FLADS Set Filters 2");
                                                                  3: Object.GET(Object.Type::Codeunit,'',CODEUNIT::"ES FLADS Set Filters 3");
                                                                  4: Object.GET(Object.Type::Codeunit,'',CODEUNIT::Codeunit14123821);
                                                                  5: Object.GET(Object.Type::Codeunit,'',CODEUNIT::Codeunit14123823);
                                                                  6: Object.GET(Object.Type::Codeunit,'',CODEUNIT::Codeunit14123825);
                                                                  7: Object.GET(Object.Type::Codeunit,'',CODEUNIT::Codeunit14123827);
                                                                  8: Object.GET(Object.Type::Codeunit,'',CODEUNIT::Codeunit14123829);
                                                                  ELSE ERROR(Text010);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Filter Codeunit Number }
    { 32  ;   ;Lookup Codeunit Number;Integer     ;OnValidate=VAR
                                                                Object@1240520001 : Record 2000000001;
                                                              BEGIN
                                                                CASE "Lookup Codeunit Number" OF
                                                                  0: Object.GET(Object.Type::Codeunit,'',CODEUNIT::"ES FLADS Lookup Data");
                                                                  1: Object.GET(Object.Type::Codeunit,'',CODEUNIT::"ES FLADS Lookup Data 1");
                                                                  2: Object.GET(Object.Type::Codeunit,'',CODEUNIT::"ES FLADS Lookup Data 2");
                                                                  3: Object.GET(Object.Type::Codeunit,'',CODEUNIT::"ES FLADS Lookup Data 3");
                                                                  4: Object.GET(Object.Type::Codeunit,'',CODEUNIT::Codeunit14123822);
                                                                  5: Object.GET(Object.Type::Codeunit,'',CODEUNIT::Codeunit14123824);
                                                                  6: Object.GET(Object.Type::Codeunit,'',CODEUNIT::Codeunit14123826);
                                                                  7: Object.GET(Object.Type::Codeunit,'',CODEUNIT::Codeunit14123828);
                                                                  8: Object.GET(Object.Type::Codeunit,'',CODEUNIT::Codeunit14123830);
                                                                  ELSE ERROR(Text010);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Lookup Codeunit Number }
  }
  KEYS
  {
    {    ;Source Table ID                         ;Clustered=Yes }
    {    ;Filter Codeunit Number                   }
    {    ;Lookup Codeunit Number                   }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Source Table ID,Source Table Caption,Field23,Pages }
  }
  CODE
  {
    VAR
      ESFLADSSetup@1240520013 : Record 14123801;
      ESFieldLevelSecurityCode@1240520008 : Record 14123803;
      ESDataSecurityCode@1240520009 : Record 14123804;
      ESUserSecuritySetup@1240520011 : Record 14123805;
      ESFLADSUpdateNo@1240520010 : Record 14123813;
      Text001@1001 : TextConst 'ENU=Rename not allowed.';
      Text003@1003 : TextConst 'ENU=Deleting %1 without deleting code changes can cause compile issues with forms and pages. Continue?';
      Text004@1004 : TextConst 'ENU=Nothing changed.';
      Text005@1005 : TextConst 'ENU=Read-Only Access';
      Text006@1006 : TextConst 'ENU=Delete all related data to %1 %2?';
      Text007@1007 : TextConst 'ENU=Nothing changed.';
      ESLoginSetup@1240520001 : Record 14123540;
      AllObjWithCaption@1000000000 : Record 2000000058;
      Text002@1002 : TextConst 'ENU=System Tables cannot have Field Level and Data Security.';
      Text008@1008 : TextConst 'ENU=Needed for Implement Changes (All),Needed for Implement Changes (Licensed)';
      Text009@1009 : TextConst 'ENU=Set DBM Table No.';
      Text010@1240520002 : TextConst 'ENU=Too many Codeunits.';

    PROCEDURE ObjectWhereUsed@1240520010();
    VAR
      ESObjectWhereUsed@1240520008 : Page 14123562;
    BEGIN
      CLEAR(ESObjectWhereUsed);
      ESObjectWhereUsed.InitObject(1,"Source Table ID");
      ESObjectWhereUsed.RUN;
    END;

    PROCEDURE CreateReadOnlySecurityCode@1240520012(DuringInsert@1240520001 : Boolean;GiveError@1240520011 : Boolean);
    VAR
      ESFieldLevelSecurityCode@1240520008 : Record 14123803;
      ESFieldLevelSecurityObject@1240520009 : Record 14123806;
      ESFieldLevelSecControl@1240520010 : Record 14123807;
    BEGIN
      IF "Source Table ID" = 0 THEN
        EXIT;

      ESFLADSSetup.GET;
      ESFLADSSetup.TESTFIELD("Read-Only Field Level Sec.Code");

      IF NOT GiveError THEN
        IF ESFieldLevelSecurityCode.GET("Source Table ID",ESFLADSSetup."Read-Only Field Level Sec.Code") THEN
          EXIT;

      ESFieldLevelSecurityCode.INIT;
      IF DuringInsert THEN BEGIN
        ESFieldLevelSecurityCode."Source Table ID" := "Source Table ID";
        ESFieldLevelSecurityCode.Code := ESFLADSSetup."Read-Only Field Level Sec.Code";
      END ELSE BEGIN
        ESFieldLevelSecurityCode.VALIDATE("Source Table ID","Source Table ID");
        ESFieldLevelSecurityCode.VALIDATE(Code,ESFLADSSetup."Read-Only Field Level Sec.Code");
      END;
      ESFieldLevelSecurityCode.Description := Text005;
      ESFieldLevelSecurityCode.Default := ESFLADSSetup."Set Read-Only to Default";
      ESFieldLevelSecurityCode.INSERT(NOT DuringInsert);

      IF ESFLADSSetup."Modify Forms" THEN BEGIN
        ESFieldLevelSecurityObject.INIT;
        ESFieldLevelSecurityObject."Source Table ID" := ESFieldLevelSecurityCode."Source Table ID";
        ESFieldLevelSecurityObject."Field Level Security Code" := ESFieldLevelSecurityCode.Code;
        ESFieldLevelSecurityObject."Object Type" := ESFieldLevelSecurityObject."Object Type"::"2";
        ESFieldLevelSecurityObject."General Permission" :=
          ESFieldLevelSecurityObject."General Permission"::View;
        ESFieldLevelSecurityObject.INSERT(NOT DuringInsert);

        ESFieldLevelSecControl.INIT;
        ESFieldLevelSecControl."Source Table ID" := ESFieldLevelSecurityObject."Source Table ID";
        ESFieldLevelSecControl."Field Level Security Code" :=
          ESFieldLevelSecurityObject."Field Level Security Code";
        ESFieldLevelSecControl."Object Type" := ESFieldLevelSecurityObject."Object Type";
        ESFieldLevelSecControl."Object ID" := ESFieldLevelSecurityObject."Object ID";
        ESFieldLevelSecControl.Type := ESFieldLevelSecControl.Type::Control;
        ESFieldLevelSecControl.Permission := ESFieldLevelSecControl.Permission::View;
        ESFieldLevelSecControl.INSERT(NOT DuringInsert);
      END;

      IF ESFLADSSetup."Modify Pages" THEN BEGIN
        ESFieldLevelSecurityObject.INIT;
        ESFieldLevelSecurityObject."Source Table ID" := ESFieldLevelSecurityCode."Source Table ID";
        ESFieldLevelSecurityObject."Field Level Security Code" := ESFieldLevelSecurityCode.Code;
        ESFieldLevelSecurityObject."Object Type" := ESFieldLevelSecurityObject."Object Type"::Page;
        ESFieldLevelSecurityObject."General Permission" :=
          ESFieldLevelSecurityObject."General Permission"::View;
        ESFieldLevelSecurityObject.INSERT(NOT DuringInsert);

        ESFieldLevelSecControl.INIT;
        ESFieldLevelSecControl."Source Table ID" := ESFieldLevelSecurityObject."Source Table ID";
        ESFieldLevelSecControl."Field Level Security Code" :=
          ESFieldLevelSecurityObject."Field Level Security Code";
        ESFieldLevelSecControl."Object Type" := ESFieldLevelSecurityObject."Object Type";
        ESFieldLevelSecControl."Object ID" := ESFieldLevelSecurityObject."Object ID";
        ESFieldLevelSecControl.Type := ESFieldLevelSecControl.Type::Control;
        ESFieldLevelSecControl.Permission := ESFieldLevelSecControl.Permission::View;
        ESFieldLevelSecControl.INSERT(NOT DuringInsert);
      END;
    END;

    PROCEDURE DeleteRelatedData@1240520013(RequireConfirm@1240520012 : Boolean);
    VAR
      ESFieldLevelSecurityObject@1240520009 : Record 14123806;
      ESFieldLevelSecControl@1240520008 : Record 14123807;
      ESDataSecurityObject@1240520010 : Record 14123808;
      ESDataSecurityField@1240520011 : Record 14123809;
    BEGIN
      IF RequireConfirm THEN
        IF NOT CONFIRM(Text006,FALSE,TABLECAPTION,"Source Table ID") THEN
          ERROR(Text007);

      ESUserSecuritySetup.RESET;
      ESUserSecuritySetup.SETRANGE("Source Table ID","Source Table ID");
      ESUserSecuritySetup.DELETEALL;

      ESFieldLevelSecurityCode.RESET;
      ESFieldLevelSecurityCode.SETRANGE("Source Table ID","Source Table ID");
      ESFieldLevelSecurityCode.DELETEALL;

      ESFieldLevelSecurityObject.RESET;
      ESFieldLevelSecurityObject.SETRANGE("Source Table ID","Source Table ID");
      ESFieldLevelSecurityObject.DELETEALL;

      ESFieldLevelSecControl.RESET;
      ESFieldLevelSecControl.SETRANGE("Source Table ID","Source Table ID");
      ESFieldLevelSecControl.DELETEALL;

      ESDataSecurityCode.RESET;
      ESDataSecurityCode.SETRANGE("Source Table ID","Source Table ID");
      ESDataSecurityCode.DELETEALL;

      ESDataSecurityObject.RESET;
      ESDataSecurityObject.SETRANGE("Source Table ID","Source Table ID");
      ESDataSecurityObject.DELETEALL;

      ESDataSecurityField.RESET;
      ESDataSecurityField.SETRANGE("Source Table ID","Source Table ID");
      ESDataSecurityField.DELETEALL;

      ESFLADSUpdateNo.UpdateNo;
    END;

    PROCEDURE ExportNeededObjects@1240520009(OnlyObjectsForInstall@1240520014 : Boolean;ShowStatus@1240520013 : Boolean;SetDBMTableNo@1240520015 : Boolean;OnlyLicensedObjects@1240520001 : Boolean);
    VAR
      ESObjectExplorer@1240520002 : Record 14123901;
    BEGIN
      ESFLADSSetup.GET;
      ESFLADSSetup.TESTFIELD("Install Allowed");
      IF NOT SetDBMTableNo THEN
        ESFLADSSetup.TESTFIELD("Source Code File Name");

      ESObjectExplorer.ExportNeededObjects(
        ESFLADSSetup."Source Code File Name",TRUE,FALSE,FALSE,OnlyObjectsForInstall,
        ESFLADSSetup."Modify Forms",ESFLADSSetup."Modify Pages",
        ShowStatus,SetDBMTableNo,OnlyLicensedObjects,"Source Table ID");
    END;

    PROCEDURE SetDBMTableNo@1240520023(ShowStatus@1240520002 : Boolean);
    VAR
      Selection@1240520001 : Integer;
    BEGIN
      Selection :=
        STRMENU(
          Text008,1,
          Text009);

      CASE Selection OF
        1:
          ExportNeededObjects(TRUE,ShowStatus,TRUE,FALSE);
        2:
          ExportNeededObjects(TRUE,ShowStatus,TRUE,TRUE);
        ELSE
          EXIT;
      END;
    END;

    BEGIN
    END.
  }
}

