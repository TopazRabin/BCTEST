OBJECT Table 14123809 ES Data Security Field
{
  OBJECT-PROPERTIES
  {
    Date=11/01/16;
    Time=12:00:00 PM;
    Version List=ES1.41.22;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit Data Security Codes");
               END;

               ESDataSecurityCode.GET("Source Table ID","Data Security Code");

               ESSourceTableSetup.GET("Source Table ID");

               ESDataSecurityObject.GET("Source Table ID","Data Security Code","Object Type","Object ID");

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnModify=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit Data Security Codes");
               END;

               ESDataSecurityCode.GET("Source Table ID","Data Security Code");

               ESSourceTableSetup.GET("Source Table ID");

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnDelete=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit Data Security Codes");
               END;

               ESDataSecurityCode.GET("Source Table ID","Data Security Code");

               ESSourceTableSetup.GET("Source Table ID");

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnRename=BEGIN
               xRec.CALCFIELDS("From Setup Company");
               xRec.TESTFIELD("From Setup Company",FALSE);

               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESDataSecurityCode.GET("Source Table ID","Data Security Code");

               ESSourceTableSetup.GET("Source Table ID");

               ESFLADSUpdateNo.UpdateNo;
             END;

    CaptionML=ENU=Data Security Field;
    LookupPageID=Page14123809;
    DrillDownPageID=Page14123809;
  }
  FIELDS
  {
    { 1   ;   ;Source Table ID     ;Integer       ;TableRelation="ES Source Table Setup";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Source Table Caption");
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupObjectID;
                                                            END;

                                                   CaptionML=ENU=Source Table ID;
                                                   NotBlank=Yes }
    { 2   ;   ;Data Security Code  ;Code20        ;TableRelation="ES Data Security Code".Code WHERE (Source Table ID=FIELD(Source Table ID));
                                                   OnLookup=BEGIN
                                                              LookupObjectID;
                                                            END;

                                                   CaptionML=ENU=Data Security Code;
                                                   NotBlank=Yes }
    { 3   ;   ;Object Type         ;Option        ;OnLookup=BEGIN
                                                              LookupObjectID;
                                                            END;

                                                   CaptionML=ENU=Object Type;
                                                   OptionCaptionML=ENU=" ,,,,,,,,Page";
                                                   OptionString=[ ,,,,,,,,Page];
                                                   NotBlank=Yes }
    { 4   ;   ;Object ID           ;Integer       ;TableRelation="ES FLADS Relation"."Object ID" WHERE (Object Type=FIELD(Object Type),
                                                                                                        Type=FILTER(Source Table|Data Item),
                                                                                                        Relation Object Type=CONST(Table),
                                                                                                        Relation Object ID=FIELD(Source Table ID));
                                                   OnValidate=BEGIN
                                                                IF "Object ID" <> 0 THEN BEGIN
                                                                  ESFLADSSetup.GET;

                                                                  ESFLADSRelation.RESET;
                                                                  IF ESFLADSSetup."Use Form Setup for Pages" OR ESFLADSSetup."Use Page Setup for Forms" THEN
                                                                    ESFLADSRelation.SETFILTER("Object Type",'%1|%2',"Object Type"::"2","Object Type"::Page)
                                                                  ELSE
                                                                    ESFLADSRelation.SETRANGE("Object Type","Object Type");
                                                                  ESFLADSRelation.SETRANGE("Object ID","Object ID");
                                                                  ESFLADSRelation.SETRANGE(Type,ESFLADSRelation.Type::"Source Table");
                                                                  ESFLADSRelation.SETRANGE("Relation Object Type",ESFLADSRelation."Relation Object Type"::Table);
                                                                  ESFLADSRelation.SETRANGE("Relation Object ID","Source Table ID");
                                                                  ESFLADSRelation.FIND('-');
                                                                END;

                                                                CALCFIELDS("Object Caption");
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupObjectID;
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Object ID }
    { 5   ;   ;Field ID            ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table ID));
                                                   OnLookup=BEGIN
                                                              TESTFIELD("Source Table ID");

                                                              Field.FILTERGROUP(2);
                                                              Field.RESET;
                                                              Field.SETRANGE(TableNo,"Source Table ID");
                                                              Field.FILTERGROUP(0);

                                                              Field.TableNo := "Source Table ID";
                                                              Field."No." := "Field ID";
                                                              IF Field.FIND('=><') THEN
                                                                ;
                                                              IF PAGE.RUNMODAL(PAGE::"ES Fields",Field) = ACTION::LookupOK THEN BEGIN
                                                                Field.TESTFIELD(TableNo,"Source Table ID");
                                                                "Field ID" := Field."No.";
                                                              END;

                                                              CALCFIELDS("Field Caption");
                                                              CALCFIELDS("Type Name");
                                                            END;

                                                   CaptionML=ENU=Field ID;
                                                   NotBlank=Yes }
    { 11  ;   ;Comment             ;Text250       ;CaptionML=ENU=Comment }
    { 12  ;   ;Source Table Caption;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Source Table ID)));
                                                   CaptionML=ENU=Source Table Caption;
                                                   Editable=No }
    { 13  ;   ;Object Caption      ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FIELD(Object Type),
                                                                                                                Object ID=FIELD(Object ID)));
                                                   CaptionML=ENU=Object Caption;
                                                   Editable=No }
    { 14  ;   ;Data Security Description;Text250  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Data Security Code".Description WHERE (Source Table ID=FIELD(Source Table ID),
                                                                                                                 Code=FIELD(Data Security Code)));
                                                   CaptionML=ENU=Data Security Description;
                                                   Editable=No }
    { 15  ;   ;Field Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Source Table ID),
                                                                                                   No.=FIELD(Field ID)));
                                                   CaptionML=ENU=Field Caption;
                                                   Editable=No }
    { 16  ;   ;Filter Type         ;Option        ;CaptionML=ENU=Filter Type;
                                                   OptionCaptionML=ENU=Hidden,Editable;
                                                   OptionString=Hidden,Editable }
    { 17  ;   ;Filter Value        ;Text250       ;CaptionML=ENU=Filter Value }
    { 18  ;   ;Substitute Filter Value;Boolean    ;OnValidate=BEGIN
                                                                IF "Substitute Filter Value" <> xRec."Substitute Filter Value" THEN
                                                                  "Always Substitute Filter Value" := "Substitute Filter Value";
                                                              END;

                                                   CaptionML=ENU=Substitute Filter Value }
    { 19  ;   ;Original Filter Value;Text250      ;CaptionML=ENU=Original Filter Value;
                                                   Editable=No }
    { 20  ;   ;Always Substitute Filter Value;Boolean;
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Substitute Filter Value");
                                                              END;

                                                   CaptionML=ENU=Always Substitute Filter Value }
    { 21  ;   ;From Setup Company  ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES FLADS Copy Setup"."From Setup Company" WHERE (Table ID=CONST(14123804),
                                                                                                                        Source Table ID=FIELD(Source Table ID),
                                                                                                                        Code=FIELD(Data Security Code)));
                                                   CaptionML=ENU=From Setup Company;
                                                   Editable=No }
    { 22  ;   ;Type Name           ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Type Name" WHERE (TableNo=FIELD(Source Table ID),
                                                                                               No.=FIELD(Field ID)));
                                                   CaptionML=ENU=Type Name;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Source Table ID,Data Security Code,Object Type,Object ID,Field ID;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ESDataSecurityCode@1240520013 : Record 14123804;
      ESDataSecurityObject@1240520009 : Record 14123808;
      Field@1240520010 : Record 2000000041;
      ESFLADSUpdateNo@1240520011 : Record 14123813;
      ESFLADSRelation@1240520003 : Record 14123810;
      ESSourceTableSetup@1240520014 : Record 14123802;
      ESFLADSSetup@1240520002 : Record 14123801;
      ESLoginSetup@1240520001 : Record 14123540;
      Text001@1001 : TextConst 'ENU=No new Fields selected.';
      Text002@1002 : TextConst 'ENU=Add %1 of %2 selected Fields.';

    PROCEDURE AddFields@1240520002();
    VAR
      Field@1240520009 : Record 2000000041;
      Field2@1240520002 : Record 2000000041;
      ESDataSecurityField@1240520005 : Record 14123809;
      ESFields@1240520001 : Page 14123813;
      NewFields@1240520003 : Integer;
      SelectedFields@1240520004 : Integer;
    BEGIN
      TESTFIELD("Source Table ID");

      Field.FILTERGROUP(2);
      Field.RESET;
      Field.SETRANGE(TableNo,"Source Table ID");
      Field.FILTERGROUP(0);

      Field.TableNo := "Source Table ID";
      Field."No." := "Field ID";
      IF Field.FIND('=><') THEN
        ;

      CLEAR(ESFields);
      ESFields.LOOKUPMODE(TRUE);
      Field.FILTERGROUP(2);
      ESFields.SETTABLEVIEW(Field);
      Field.FILTERGROUP(0);
      IF ESFields.RUNMODAL = ACTION::LookupOK THEN BEGIN
        ESFields.GetSelectionFilter(Field2);
        Field2.FIND('-');
        REPEAT
          Field2.TESTFIELD(TableNo,"Source Table ID");

          IF NOT ESDataSecurityField.GET(
                   "Source Table ID","Data Security Code","Object Type","Object ID",Field2."No.")
          THEN
            NewFields := NewFields + 1;
          SelectedFields := SelectedFields + 1;
        UNTIL Field2.NEXT = 0;

        IF NewFields = 0 THEN
          ERROR(Text001);

        IF CONFIRM(Text002,TRUE,NewFields,SelectedFields) THEN BEGIN
          Field2.FIND('-');
          REPEAT
            IF NOT ESDataSecurityField.GET(
                     "Source Table ID","Data Security Code","Object Type","Object ID",Field2."No.")
            THEN BEGIN
              ESDataSecurityField.INIT;
              ESDataSecurityField."Source Table ID" := "Source Table ID";
              ESDataSecurityField."Data Security Code" := "Data Security Code";
              ESDataSecurityField."Object Type" := "Object Type";
              ESDataSecurityField."Object ID" := "Object ID";
              ESDataSecurityField.VALIDATE("Field ID",Field2."No.");
              ESDataSecurityField.INSERT(TRUE);
            END;
          UNTIL Field2.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE LookupObjectID@1240520008();
    BEGIN
      ESDataSecurityObject.RESET;
      IF "Source Table ID" <> 0 THEN
        ESDataSecurityObject.SETRANGE("Source Table ID","Source Table ID");
      IF "Data Security Code" <> '' THEN
        ESDataSecurityObject.SETRANGE("Data Security Code","Data Security Code");
      IF "Object Type" <> 0 THEN
        ESDataSecurityObject.SETRANGE("Object Type","Object Type");

      ESDataSecurityObject."Source Table ID" := "Source Table ID";
      ESDataSecurityObject."Data Security Code" := "Data Security Code";
      ESDataSecurityObject."Object Type" := "Object Type";
      ESDataSecurityObject."Object ID" := "Object ID";
      IF ESDataSecurityObject.FIND('=><') THEN
        ;

      IF PAGE.RUNMODAL(0,ESDataSecurityObject) = ACTION::LookupOK THEN BEGIN
        "Source Table ID" := ESDataSecurityObject."Source Table ID";
        "Data Security Code" := ESDataSecurityObject."Data Security Code";
        "Object Type" := ESDataSecurityObject."Object Type";
        "Object ID" := ESDataSecurityObject."Object ID";
      END;
    END;

    PROCEDURE LookupFilterValue@1240520001();
    VAR
      ESDynamicFilter@1240520001 : Record 14123818;
    BEGIN
      TESTFIELD("Substitute Filter Value");
      IF PAGE.RUNMODAL(0,ESDynamicFilter) = ACTION::LookupOK THEN
        VALIDATE("Filter Value","Filter Value" + '%' + ESDynamicFilter.Code + '%');
    END;

    BEGIN
    END.
  }
}

