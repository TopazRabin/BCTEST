OBJECT Table 14123654 ES Role Recording
{
  OBJECT-PROPERTIES
  {
    Date=05/04/16;
    Time=12:00:00 PM;
    Version List=ES1.41.18;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ESRole.GET("Role ID");
               ESRole.Comment(1,STRSUBSTNO(Text001,TABLECAPTION,"Recording Code"));

               ESRoleDetail.GET("Role ID");
               ESRoleDetail.TESTFIELD("Role Group ID",'');

               ESRecording.GET("Recording Code");
               IF NOT DoNotSetDefault THEN BEGIN
                 "Extend Insert Permissions" := ESRecording."Extend Insert Permissions";
                 "Add Related Permissions" := ESRecording."Add Related Permissions";
                 "Role for Both Clients" := ESRecording."Role for Both Clients";
               END;

               "Created By User" := USERID;
               "Created Date Time" := CURRENTDATETIME;
             END;

    OnModify=BEGIN
               ESRole.GET("Role ID");
               ESRole.Comment(1,STRSUBSTNO(Text002,TABLECAPTION,"Recording Code"));
             END;

    OnDelete=BEGIN
               ESRole.GET("Role ID");
               ESRole.Comment(1,STRSUBSTNO(Text003,TABLECAPTION,"Recording Code"));

               Comment(4,'');
             END;

    OnRename=BEGIN
               ERROR(Text004);
             END;

    CaptionML=ENU=Permission Set Recording;
    LookupPageID=Page14123670;
    DrillDownPageID=Page14123670;
  }
  FIELDS
  {
    { 1   ;   ;Role ID             ;Code20        ;TableRelation="ES Role"."Role ID";
                                                   CaptionML=ENU=Permission Set ID;
                                                   NotBlank=Yes }
    { 2   ;   ;Recording Code      ;Code30        ;TableRelation="ES Recording";
                                                   CaptionML=ENU=Recording Code;
                                                   NotBlank=Yes }
    { 3   ;   ;Extend Insert Permissions;Boolean  ;CaptionML=ENU=Extend Insert Permissions }
    { 4   ;   ;Add Related Permissions;Boolean    ;CaptionML=ENU=Add Related Permissions }
    { 5   ;   ;Role for Both Clients;Boolean      ;CaptionML=ENU=Permission Set for Both Clients }
    { 6   ;   ;Recording Description;Text250      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Recording".Description WHERE (Code=FIELD(Recording Code)));
                                                   CaptionML=ENU=Recording Description;
                                                   Editable=No }
    { 7   ;   ;Created By User     ;Code50        ;CaptionML=ENU=Created By User;
                                                   Editable=No }
    { 8   ;   ;Created Date Time   ;DateTime      ;CaptionML=ENU=Created Date Time;
                                                   Editable=No }
    { 9   ;   ;Role Name           ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Role".Name WHERE (Role ID=FIELD(Role ID)));
                                                   CaptionML=ENU=Permission Set Name;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Role ID,Recording Code                  ;Clustered=Yes }
    {    ;Recording Code                           }
    {    ;Created Date Time                        }
    {    ;Created By User,Created Date Time        }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ESRole@1240520008 : Record 14123505;
      ESRecording@1240520009 : Record 14123651;
      Text001@1001 : TextConst 'ENU=Inserted %1 %2';
      Text002@1002 : TextConst 'ENU=Modified %1 %2';
      Text003@1003 : TextConst 'ENU=Deleted %1 %2';
      Text004@1004 : TextConst 'ENU=Rename not allowed.';
      ESRoleDetail@1240520010 : Record 14123653;
      DoNotSetDefault@1240520001 : Boolean;

    PROCEDURE ChangeDoNotSetDefault@1240520001(NewValue@1240520001 : Boolean);
    BEGIN
      DoNotSetDefault := NewValue;
    END;

    PROCEDURE FormatAsTextLine@1240520065(VAR ESTextLine@1240520011 : Record 14123520;NoOfRelatedKeyFields@1240520009 : Integer;CalcFlowFields@1240520012 : Boolean;UserDateTimeDetails@1240520013 : Boolean) : Boolean;
    VAR
      ESFormatMgt@1240520014 : Codeunit 14123505;
      RecRef@1240520010 : RecordRef;
    BEGIN
      ESFormatMgt.NewRecord(
        TABLECAPTION,"Role ID","Recording Code",'','','','','','','','',2,
        NoOfRelatedKeyFields);
      IF "Extend Insert Permissions" THEN
        ESFormatMgt.AddField(
          FIELDCAPTION("Extend Insert Permissions"),FORMAT("Extend Insert Permissions"));
      IF "Add Related Permissions" THEN
        ESFormatMgt.AddField(FIELDCAPTION("Add Related Permissions"),FORMAT("Add Related Permissions"));
      IF "Role for Both Clients" THEN
        ESFormatMgt.AddField(FIELDCAPTION("Role for Both Clients"),FORMAT("Role for Both Clients"));

      IF UserDateTimeDetails THEN BEGIN
        ESFormatMgt.AddField(FIELDCAPTION("Created By User"),FORMAT("Created By User"));
        IF "Created Date Time" <> 0DT THEN
          ESFormatMgt.AddField(FIELDCAPTION("Created Date Time"),FORMAT("Created Date Time"));
      END;

      IF CalcFlowFields THEN BEGIN
        CALCFIELDS("Recording Description","Role Name");

        ESFormatMgt.AddFlowField(FIELDCAPTION("Recording Description"),FORMAT("Recording Description"));
        ESFormatMgt.AddFlowField(FIELDCAPTION("Role Name"),FORMAT("Role Name"));
      END;

      RecRef.OPEN(DATABASE::"ES Role Recording");
      RecRef.GETTABLE(Rec);
      ESFormatMgt.FormatNewESTextLine(ESTextLine,DATABASE::"ES Role Recording",RecRef.RECORDID);

      EXIT(ESFormatMgt.NoTextOverflow);
    END;

    PROCEDURE Comment@1240520020(FunctionNo@1240520008 : ' ,Insert,Show,Exist,DeleteAll';NewText@1240520010 : Text[250]) : Boolean;
    VAR
      ESComment@1240520009 : Record 14123524;
    BEGIN
      ESComment.RESET;
      ESComment.SETRANGE("Table ID",DATABASE::"ES Role Recording");
      ESComment.SETRANGE("Role ID","Role ID");
      ESComment.SETRANGE(Code,"Recording Code");
      EXIT(ESComment.Functions(FunctionNo,NewText));
    END;

    BEGIN
    END.
  }
}

