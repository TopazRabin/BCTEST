OBJECT Table 14000589 AFP Promotion Header
{
  OBJECT-PROPERTIES
  {
    Date=03/27/19;
    Time=12:00:00 PM;
    Version List=UBP3.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD(Released,FALSE);
               "Date Changed" := TODAY;
               "Changed By" := USERID;
             END;

    OnModify=BEGIN
               TESTFIELD(Released,FALSE);
               "Date Changed" := TODAY;
               "Changed By" := USERID;
             END;

    OnDelete=VAR
               PromotionLine@1240020000 : Record 14000590;
             BEGIN
               PromotionLine.SETRANGE("Promotion Code",Code);
               PromotionLine.DELETEALL(TRUE);
             END;

    OnRename=VAR
               PromotionLine@1240020000 : Record 14000590;
             BEGIN
               TESTFIELD(Released,FALSE);
               PromotionLine.SETRANGE("Promotion Code",xRec.Code);
               IF NOT PromotionLine.ISEMPTY THEN
                 ERROR(Text004);
             END;

    CaptionML=ENU=Promotion Header;
    LookupPageID=Page14000541;
    DrillDownPageID=Page14000541;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=ENU=Code }
    { 11  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 12  ;   ;Starting Date       ;Date          ;OnValidate=VAR
                                                                ForecastInvPeriod@1240020000 : Record 14000552;
                                                              BEGIN
                                                                IF "Starting Date" <> 0D THEN BEGIN
                                                                  ForecastInvPeriod.SETRANGE(Type,ForecastInvPeriod.Type::Calculate);
                                                                  ForecastInvPeriod.SETRANGE("Starting Date","Starting Date");
                                                                  IF NOT ForecastInvPeriod.FIND('-') THEN
                                                                    ERROR(Text002);
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ForecastInvPeriod@1240020000 : Record 14000552;
                                                            BEGIN
                                                              ForecastInvPeriod.SETRANGE(Type,ForecastInvPeriod.Type::Calculate);
                                                              IF PAGE.RUNMODAL(PAGE::"Forecast Inventory Periods",ForecastInvPeriod) = ACTION::LookupOK THEN BEGIN
                                                                IF "Ending Date" <> 0D THEN
                                                                  IF "Ending Date" < ForecastInvPeriod."Starting Date" THEN
                                                                    ERROR(Text001,ForecastInvPeriod."Starting Date","Ending Date");
                                                                "Starting Date" := ForecastInvPeriod."Starting Date";
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Starting Date }
    { 13  ;   ;Ending Date         ;Date          ;OnValidate=VAR
                                                                ForecastInvPeriod@1240020000 : Record 14000552;
                                                              BEGIN
                                                                IF "Ending Date" <> 0D THEN BEGIN
                                                                  ForecastInvPeriod.RESET;
                                                                  ForecastInvPeriod.SETRANGE(Type,ForecastInvPeriod.Type::Calculate);
                                                                  ForecastInvPeriod.SETRANGE("Ending Date","Ending Date");
                                                                  IF NOT ForecastInvPeriod.FIND('-') THEN
                                                                    ERROR(Text003);
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ForecastInvPeriod@1240020000 : Record 14000552;
                                                            BEGIN
                                                              ForecastInvPeriod.SETRANGE(Type,ForecastInvPeriod.Type::Calculate);
                                                              IF PAGE.RUNMODAL(PAGE::"Forecast Inventory Periods",ForecastInvPeriod) = ACTION::LookupOK THEN BEGIN
                                                                IF "Starting Date" <> 0D THEN
                                                                  IF "Starting Date" > ForecastInvPeriod."Ending Date" THEN
                                                                    ERROR(Text001,"Starting Date",ForecastInvPeriod."Ending Date");
                                                                "Ending Date" := ForecastInvPeriod."Ending Date";
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Ending Date }
    { 15  ;   ;Date Changed        ;Date          ;CaptionML=ENU=Date Changed;
                                                   Editable=No }
    { 16  ;   ;Changed By          ;Code60        ;CaptionML=ENU=Changed By;
                                                   Editable=No }
    { 17  ;   ;Released            ;Boolean       ;CaptionML=ENU=Released;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1240030000 : TextConst 'ENU=Starting Date %1 is greater than Ending Date %2';
      Text002@1240030002 : TextConst 'ENU=Date entered must be a Forecast Inventory Period Starting Date.';
      Text003@1240030003 : TextConst 'ENU=Date entered must be a Forecast Inventory Period Ending Date.';
      Text004@1240030005 : TextConst 'ENU=Unable to rename a promotion with lines.';

    PROCEDURE AutoFillAdj@1240030000();
    VAR
      PromotionLine@1240020000 : Record 14000590;
    BEGIN
      PromotionLine.SETRANGE("Promotion Code",Code);
      IF PromotionLine.FINDSET THEN
        REPEAT
          IF (NOT PromotionLine."Historical Data Adjusted") AND
             (PromotionLine."Usage Adjust Quantity" = 0)
          THEN BEGIN
            PromotionLine.VALIDATE("Usage Adjust Quantity",PromotionLine."Promotion Quantity");
            PromotionLine.MODIFY(TRUE);
          END;
        UNTIL PromotionLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

