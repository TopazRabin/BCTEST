OBJECT Table 14123652 ES Recording Permission
{
  OBJECT-PROPERTIES
  {
    Date=03/05/18;
    Time=12:00:00 PM;
    Version List=ES1.41.46;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF NOT AllowZeroNoQuestion THEN BEGIN
                 ESSecuritySetup.GET;
                 IF ("Object ID" = 0) AND NOT ESSecuritySetup."Allow 0 Permission" THEN
                   IF NOT CONFIRM(Text001,FALSE) THEN
                     ERROR(Text002,FIELDCAPTION("Object ID"));
               END;

               IF NOT ChangeWithNoComment THEN BEGIN
                 ESRecording.GET("Recording Code");
                 ESRecording.Comment(1,STRSUBSTNO(Text012,TABLECAPTION,"Object Type","Object ID"));
               END;

               "Created By User" := USERID;
               "Created Date Time" := CURRENTDATETIME;
             END;

    OnModify=BEGIN
               IF NOT ChangeWithNoComment THEN BEGIN
                 ESRecording.GET("Recording Code");
                 ESRecording.Comment(1,STRSUBSTNO(Text013,TABLECAPTION,"Object Type","Object ID"));
               END;
             END;

    OnDelete=BEGIN
               IF NOT ChangeWithNoComment THEN BEGIN
                 ESRecording.GET("Recording Code");
                 ESRecording.Comment(1,STRSUBSTNO(Text014,TABLECAPTION,"Object Type","Object ID"));
               END;

               Comment(4,'');
             END;

    OnRename=BEGIN
               ERROR(Text003);
             END;

    CaptionML=ENU=Recording Permission;
    LookupPageID=Page14123669;
    DrillDownPageID=Page14123669;
  }
  FIELDS
  {
    { 1   ;   ;Recording Code      ;Code30        ;TableRelation="ES Recording";
                                                   CaptionML=ENU=Recording Code;
                                                   NotBlank=Yes }
    { 3   ;   ;Object Type         ;Option        ;CaptionML=ENU=Object Type;
                                                   OptionCaptionML=ENU=TableData,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System;
                                                   OptionString=TableData,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System }
    { 4   ;   ;Object ID           ;Integer       ;TableRelation="ES Object Information"."Object ID" WHERE (Object Type=FIELD(Object Type));
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Object ID }
    { 5   ;   ;Object Caption      ;Text249       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Object Information"."Object Caption" WHERE (Object Type=FIELD(Object Type),
                                                                                                                      Object ID=FIELD(Object ID)));
                                                   CaptionML=ENU=Object Caption;
                                                   Editable=No }
    { 6   ;   ;Read Required       ;Boolean       ;CaptionML=ENU=Read Required }
    { 7   ;   ;Insert Required     ;Boolean       ;CaptionML=ENU=Insert Required }
    { 8   ;   ;Modify Required     ;Boolean       ;CaptionML=ENU=Modify Required }
    { 9   ;   ;Delete Required     ;Boolean       ;CaptionML=ENU=Delete Required }
    { 10  ;   ;Execute Required    ;Boolean       ;CaptionML=ENU=Execute Required }
    { 11  ;   ;Origin              ;Option        ;CaptionML=ENU=Origin;
                                                   OptionCaptionML=ENU=" ,Recorded,Additional,Object Information,Relation,Copied,History Table,Security Table";
                                                   OptionString=[ ,Recorded,Additional,Object Information,Relation,Copied,History Table,Security Table] }
    { 12  ;   ;Do not include Additional;Boolean  ;CaptionML=ENU=Do not include Additional }
    { 13  ;   ;Source/DataItem Table ID;Integer   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Relation"."Relation Object ID" WHERE (Object Type=FIELD(Object Type),
                                                                                                                Object ID=FIELD(Object ID),
                                                                                                                Type=FILTER(Source Table|Data Item)));
                                                   CaptionML=ENU=Source/DataItem Table ID;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 14  ;   ;Limited Access Object;Boolean      ;FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Object Property" WHERE (Object Type=FIELD(Object Type),
                                                                                                 Object ID=FIELD(Object ID),
                                                                                                 Object Level Permissions=CONST(Yes)));
                                                   CaptionML=ENU=Limited Access Object;
                                                   Editable=No }
    { 15  ;   ;Limited License Object;Boolean     ;FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Object Property" WHERE (Object Type=FIELD(Object Type),
                                                                                                 Object ID=FIELD(Object ID),
                                                                                                 Limited License Object=CONST(Yes)));
                                                   CaptionML=ENU=Limited License Object;
                                                   Editable=No }
    { 16  ;   ;Created By User     ;Code50        ;CaptionML=ENU=Created By User;
                                                   Editable=No }
    { 17  ;   ;Created Date Time   ;DateTime      ;CaptionML=ENU=Created Date Time;
                                                   Editable=No }
    { 18  ;   ;Free Access Object  ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Object Property" WHERE (Object Type=FIELD(Object Type),
                                                                                                 Object ID=FIELD(Object ID),
                                                                                                 Object Level Permissions=CONST(No),
                                                                                                 Limited License Object=CONST(No)));
                                                   CaptionML=ENU=Free Access Object;
                                                   Editable=No }
    { 19  ;   ;Calculate Flowfield Required;Boolean;
                                                   CaptionML=ENU=Calculate Flowfield Required }
    { 20  ;   ;Object Information Exists;Boolean  ;FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Object Information" WHERE (Object Type=FIELD(Object Type),
                                                                                                    Object ID=FIELD(Object ID)));
                                                   CaptionML=ENU=Object Information Exists;
                                                   Editable=No }
    { 21  ;   ;Security Filter Object;Boolean     ;FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Object Property" WHERE (Object Type=FIELD(Object Type),
                                                                                                 Object ID=FIELD(Object ID),
                                                                                                 Security Filter Table=CONST(Yes)));
                                                   CaptionML=ENU=Security Filter Object;
                                                   Editable=No }
    { 22  ;   ;Limited User Included Object;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Object Property" WHERE (Object Type=FIELD(Object Type),
                                                                                                 Object ID=FIELD(Object ID),
                                                                                                 Limited User Table Access=CONST(Included)));
                                                   CaptionML=ENU=Limited User Included Object;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Recording Code,Object Type,Object ID    ;Clustered=Yes }
    {    ;Object Type,Object ID                    }
    {    ;Created Date Time                        }
    {    ;Created By User,Created Date Time        }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ESSecuritySetup@1240520016 : Record 14123521;
      ESRecording@1240520001 : Record 14123651;
      AllowZeroNoQuestion@1240520008 : Boolean;
      Text001@1001 : TextConst 'ENU=Insert Permission for 0?';
      Text002@1002 : TextConst 'ENU=%1 must be entered';
      Text003@1003 : TextConst 'ENU=Rename not allowed.';
      Text004@1240520009 : TextConst 'ENU=Permissions';
      Text005@1240520010 : TextConst 'ENU=R';
      Text006@1240520011 : TextConst 'ENU=r';
      Text007@1240520012 : TextConst 'ENU=I';
      Text008@1240520013 : TextConst 'ENU=M';
      Text009@1240520014 : TextConst 'ENU=D';
      Text010@1240520015 : TextConst 'ENU=E';
      Text011@1004 : TextConst 'ENU=%1 objects inserted.';
      Text012@1005 : TextConst 'ENU=Inserted %1 %2 %3';
      Text013@1006 : TextConst 'ENU=Modified %1 %2 %3';
      Text014@1007 : TextConst 'ENU=Deleted %1 %2 %3';
      ChangeWithNoComment@1240520002 : Boolean;

    PROCEDURE FormatPermissionString@1240520008() PermissionText : Text[30];
    BEGIN
      IF "Read Required" THEN
        PermissionText := Text005
      ELSE
        IF "Calculate Flowfield Required" THEN
          PermissionText := Text006
        ELSE
          PermissionText := '-';
      IF "Insert Required" THEN
        PermissionText := PermissionText + Text007
      ELSE
        PermissionText := PermissionText + '-';
      IF "Modify Required" THEN
        PermissionText := PermissionText + Text008
      ELSE
        PermissionText := PermissionText + '-';
      IF "Delete Required" THEN
        PermissionText := PermissionText + Text009
      ELSE
        PermissionText := PermissionText + '-';
      IF "Execute Required" THEN
        PermissionText := PermissionText + Text010
      ELSE
        PermissionText := PermissionText + '-';
      EXIT(PermissionText);
    END;

    PROCEDURE SetAllowZeroNoQuestion@1240520010(NewAllowZeroNoQuestion@1240520008 : Boolean;NewChangeWithNoComment@1240520001 : Boolean);
    BEGIN
      AllowZeroNoQuestion := NewAllowZeroNoQuestion;
      ChangeWithNoComment := NewChangeWithNoComment;
    END;

    PROCEDURE UpdateObjectCaption@1240520009();
    VAR
      AllObjWithCaption@1240520008 : Record 2000000058;
    BEGIN
      IF "Object Caption" <> '' THEN
        EXIT;

      IF AllObjWithCaption.GET("Object Type","Object ID") THEN
        "Object Caption" := AllObjWithCaption."Object Caption";
    END;

    PROCEDURE ObjectWhereUsed@1240520011();
    VAR
      ESObjectWhereUsed@1240520008 : Page 14123562;
    BEGIN
      CLEAR(ESObjectWhereUsed);
      ESObjectWhereUsed.InitObject("Object Type","Object ID");
      ESObjectWhereUsed.RUN;
    END;

    PROCEDURE CopyFormPage@1240520001(VAR ESRecordingPermission@1240520001 : Record 14123652);
    VAR
      ESRecordingPermissionTmp@1240520003 : TEMPORARY Record 14123652;
      ESRecordingPermission2@1240520002 : Record 14123652;
      InsertedRecords@1240520004 : Integer;
    BEGIN
      IF ESRecordingPermission.FIND('-') THEN
        REPEAT
          IF ESRecordingPermission."Object Type" = ESRecordingPermission."Object Type"::"2" THEN
            IF NOT ESRecordingPermission2.GET(
                     ESRecordingPermission."Recording Code",ESRecordingPermission."Object Type"::Page,
                     ESRecordingPermission."Object ID")
            THEN BEGIN
              ESRecordingPermissionTmp := ESRecordingPermission;
              ESRecordingPermissionTmp.INSERT;
            END;

          IF ESRecordingPermission."Object Type" = ESRecordingPermission."Object Type"::Page THEN
            IF NOT ESRecordingPermission2.GET(
                     ESRecordingPermission."Recording Code",ESRecordingPermission."Object Type"::"2",
                     ESRecordingPermission."Object ID")
            THEN BEGIN
              ESRecordingPermissionTmp := ESRecordingPermission;
              ESRecordingPermissionTmp.INSERT;
            END;
        UNTIL ESRecordingPermission.NEXT = 0;

      IF ESRecordingPermissionTmp.FIND('-') THEN
        REPEAT
          ESRecordingPermission2 := ESRecordingPermissionTmp;
          IF ESRecordingPermissionTmp."Object Type" = ESRecordingPermissionTmp."Object Type"::"2" THEN
            ESRecordingPermission2."Object Type" := ESRecordingPermission2."Object Type"::Page
          ELSE
            ESRecordingPermission2."Object Type" := ESRecordingPermission2."Object Type"::"2";
          ESRecordingPermission2.INSERT(TRUE);

          InsertedRecords := InsertedRecords + 1;
        UNTIL ESRecordingPermissionTmp.NEXT = 0;

      MESSAGE(Text011,InsertedRecords);
    END;

    PROCEDURE FormatAsTextLine@1240520067(ShortPermissionText@1240520009 : Boolean;VAR ESTextLine@1240520011 : Record 14123520;NoOfRelatedKeyFields@1240520012 : Integer;CalcFlowFields@1240520013 : Boolean;UserDateTimeDetails@1240520014 : Boolean;OriginDetails@1240520015 : Boolean) : Boolean;
    VAR
      ESFormatMgt@1240520016 : Codeunit 14123505;
      RecRef@1240520010 : RecordRef;
    BEGIN
      ESFormatMgt.NewRecord(
        TABLECAPTION,"Recording Code",FORMAT("Object Type"),FORMAT("Object ID"),
        '','','','','','','',3,NoOfRelatedKeyFields);
      IF NOT ShortPermissionText THEN BEGIN
        ESFormatMgt.AddField(FIELDCAPTION("Read Required"),FORMAT("Read Required"));
        ESFormatMgt.AddField(FIELDCAPTION("Insert Required"),FORMAT("Insert Required"));
        ESFormatMgt.AddField(FIELDCAPTION("Modify Required"),FORMAT("Modify Required"));
        ESFormatMgt.AddField(FIELDCAPTION("Delete Required"),FORMAT("Delete Required"));
        ESFormatMgt.AddField(FIELDCAPTION("Execute Required"),FORMAT("Execute Required"));
        ESFormatMgt.AddField(FIELDCAPTION("Calculate Flowfield Required"),FORMAT("Calculate Flowfield Required"));
      END ELSE
        ESFormatMgt.AddField(Text004,FormatPermissionString);

      IF OriginDetails THEN BEGIN
        ESFormatMgt.AddField(FIELDCAPTION(Origin),FORMAT(Origin));
        IF "Do not include Additional" THEN
          ESFormatMgt.AddField(FIELDCAPTION("Do not include Additional"),FORMAT("Do not include Additional"));
      END;

      IF UserDateTimeDetails THEN BEGIN
        ESFormatMgt.AddField(FIELDCAPTION("Created By User"),FORMAT("Created By User"));
        IF "Created Date Time" <> 0DT THEN
          ESFormatMgt.AddField(FIELDCAPTION("Created Date Time"),FORMAT("Created Date Time"));
      END;

      IF CalcFlowFields THEN BEGIN
        CALCFIELDS(
          "Object Caption","Source/DataItem Table ID","Limited Access Object",
          "Limited License Object","Free Access Object");

        ESFormatMgt.AddFlowField(FIELDCAPTION("Object Caption"),FORMAT("Object Caption"));
        IF "Source/DataItem Table ID" <> 0 THEN
          ESFormatMgt.AddFlowField(
            FIELDCAPTION("Source/DataItem Table ID"),FORMAT("Source/DataItem Table ID"));
        IF "Limited Access Object" THEN
          ESFormatMgt.AddFlowField(
            FIELDCAPTION("Limited Access Object"),FORMAT("Limited Access Object"));
        IF "Limited License Object" THEN
          ESFormatMgt.AddFlowField(
            FIELDCAPTION("Limited License Object"),FORMAT("Limited License Object"));
        IF "Free Access Object" THEN
          ESFormatMgt.AddFlowField(
            FIELDCAPTION("Free Access Object"),FORMAT("Free Access Object"));
      END;

      RecRef.OPEN(DATABASE::"ES Recording Permission");
      RecRef.GETTABLE(Rec);
      ESFormatMgt.FormatNewESTextLine(ESTextLine,DATABASE::"ES Recording Permission",RecRef.RECORDID);

      EXIT(ESFormatMgt.NoTextOverflow);
    END;

    PROCEDURE Comment@1240520020(FunctionNo@1240520008 : ' ,Insert,Show,Exist,DeleteAll';NewText@1240520010 : Text[250]) : Boolean;
    VAR
      ESComment@1240520009 : Record 14123524;
    BEGIN
      ESComment.RESET;
      ESComment.SETRANGE("Table ID",DATABASE::"ES Recording Permission");
      ESComment.SETRANGE(Code,"Recording Code");
      ESComment.SETRANGE(Type,"Object Type");
      ESComment.SETRANGE("Object ID","Object ID");
      EXIT(ESComment.Functions(FunctionNo,NewText));
    END;

    BEGIN
    END.
  }
}

