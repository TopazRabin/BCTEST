OBJECT Table 14123543 ES Summary Calculation Line
{
  OBJECT-PROPERTIES
  {
    Date=05/11/18;
    Time=12:00:00 PM;
    Version List=ES1.41.52;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ESSummaryCalculation.GET("Summary Calculation Type","Summary Calculation Code");
               ESSummaryCalculation.Comment(1,STRSUBSTNO(Text015,TABLECAPTION,"Object Type","Object ID"));
             END;

    OnModify=BEGIN
               ESSummaryCalculation.GET("Summary Calculation Type","Summary Calculation Code");
               ESSummaryCalculation.Comment(1,STRSUBSTNO(Text016,TABLECAPTION,"Object Type","Object ID"));
             END;

    OnDelete=BEGIN
               ESSummaryCalculation.GET("Summary Calculation Type","Summary Calculation Code");
               ESSummaryCalculation.Comment(1,STRSUBSTNO(Text017,TABLECAPTION,"Object Type","Object ID"));
             END;

    CaptionML=ENU=Summary Calculation Line;
  }
  FIELDS
  {
    { 1   ;   ;Summary Calculation Type;Option    ;CaptionML=ENU=Summary Calculation Type;
                                                   OptionCaptionML=ENU=" ,Violation,Ability";
                                                   OptionString=[ ,Violation,Ability] }
    { 2   ;   ;Summary Calculation Code;Code20    ;TableRelation="ES Summary Calculation".Code WHERE (Type=FIELD(Summary Calculation Type));
                                                   CaptionML=ENU=Summary Calculation Code;
                                                   NotBlank=Yes }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 11  ;   ;Description         ;Text250       ;CaptionML=ENU=Description }
    { 12  ;   ;Object Access Filter;Text250       ;OnValidate=BEGIN
                                                                TESTFIELD("Access Type","Access Type"::" ");

                                                                IF "Object Access Filter" = '' THEN
                                                                  EXIT;

                                                                IF "Object Type" = "Object Type"::Ability THEN
                                                                  ERROR(
                                                                    Text018,
                                                                    FIELDCAPTION("Object Access Filter"),FIELDCAPTION("Object Type"),"Object Type");

                                                                TESTFIELD("Ability Summ. Calculation Code",'');

                                                                ValidateObjectAccessFilter(FALSE,TRUE,TRUE);
                                                              END;

                                                   CaptionML=ENU=Object Access Filter }
    { 13  ;   ;Object Type         ;Option        ;OnValidate=BEGIN
                                                                IF xRec."Object Type" <> "Object Type" THEN BEGIN
                                                                  Description := '';
                                                                  "Object Access Filter" := '';
                                                                  "Object ID" := 0;
                                                                  "Access Type" := "Access Type"::" ";
                                                                  "Ability Summ. Calculation Code" := '';
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Object Type;
                                                   OptionCaptionML=ENU=TableData,Table,Form,Report,Dataport,Codeunit,XMLport,MenuSuite,Page,Query,System,,,,,,,,,,Ability;
                                                   OptionString=TableData,Table,Form,Report,Dataport,Codeunit,XMLport,MenuSuite,Page,Query,System,,,,,,,,,,Ability }
    { 14  ;   ;Object ID           ;Integer       ;TableRelation="ES Object Information"."Object ID" WHERE (Object Type=FIELD(Object Type));
                                                   OnValidate=VAR
                                                                ESObjectProperty@1240520002 : Record 14123522;
                                                                ObjectIDText@1240520001 : Text[30];
                                                              BEGIN
                                                                IF "Object Type" = "Object Type"::Ability THEN
                                                                  ERROR(
                                                                    Text018,
                                                                    FIELDCAPTION("Object ID"),FIELDCAPTION("Object Type"),"Object Type");

                                                                CALCFIELDS("Object Caption");

                                                                IF "Object ID" <> 0 THEN BEGIN
                                                                  TESTFIELD("Ability Summ. Calculation Code",'');

                                                                  IF NOT ESObjectProperty.GET("Object Type","Object ID") THEN BEGIN
                                                                    ESObjectProperty.INIT;
                                                                    ESObjectProperty.VALIDATE("Object Type","Object Type");
                                                                    ESObjectProperty.VALIDATE("Object ID","Object ID");
                                                                    ESObjectProperty.VALIDATE("Segregation of Duties",TRUE);
                                                                    ESObjectProperty.INSERT(TRUE);

                                                                    MESSAGE(Text001,ESObjectProperty.TABLECAPTION,"Object Type","Object ID");
                                                                  END ELSE BEGIN
                                                                    IF NOT ESObjectProperty."Segregation of Duties" THEN BEGIN
                                                                      ESObjectProperty.VALIDATE("Segregation of Duties",TRUE);
                                                                      ESObjectProperty.MODIFY(TRUE);

                                                                      MESSAGE(Text002,ESObjectProperty.TABLECAPTION,"Object Type","Object ID");
                                                                    END;
                                                                  END;
                                                                END;

                                                                IF "Object Access Filter" = '' THEN BEGIN
                                                                  ObjectIDText := DELCHR(FORMAT("Object ID"),'=',',.');

                                                                  CASE "Object Type" OF
                                                                    "Object Type"::TableData:
                                                                      BEGIN
                                                                        ESObjectProperty.GET("Object Type","Object ID");
                                                                        IF ESObjectProperty."Limited License Object" THEN
                                                                          "Object Access Filter" :=
                                                                            STRSUBSTNO('TD0 RIMD?|TD0 Rimd?|TD%1 RIMD?|TD%1 Rimd?',ObjectIDText)
                                                                        ELSE
                                                                          "Object Access Filter" :=
                                                                            STRSUBSTNO('TD0 RIMD?|TD%1 RIMD?',ObjectIDText);
                                                                      END;
                                                                    "Object Type"::Table:
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TA0 ????E|TA%1 ????E',ObjectIDText);
                                                                    "Object Type"::Form:
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('FO0 ????E|FO%1 ????E',ObjectIDText);
                                                                    "Object Type"::Report:
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('RE0 ????E|RE%1 ????E',ObjectIDText);
                                                                    "Object Type"::Dataport:
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('DA0 ????E|DA%1 ????E',ObjectIDText);
                                                                    "Object Type"::Codeunit:
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('CO0 ????E|CO%1 ????E',ObjectIDText);
                                                                    "Object Type"::XMLport:
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('XM0 ????E|XM%1 ????E',ObjectIDText);
                                                                    "Object Type"::MenuSuite:
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('ME0 ????E|ME%1 ????E',ObjectIDText);
                                                                    "Object Type"::Page:
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('PA0 ????E|PA%1 ????E',ObjectIDText);
                                                                    "Object Type"::Query:
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('QU0 ????E|QU%1 ????E',ObjectIDText);
                                                                    "Object Type"::System:
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('SY0 ????E|SY%1 ????E',ObjectIDText);
                                                                  END;
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Object ID }
    { 15  ;   ;Access Type         ;Option        ;OnValidate=VAR
                                                                ESObjectProperty@1240520002 : Record 14123522;
                                                                ObjectIDText@1240520001 : Text[30];
                                                              BEGIN
                                                                IF "Access Type" = "Access Type"::" " THEN
                                                                  EXIT;

                                                                TESTFIELD("Object Type","Object Type"::TableData);
                                                                TESTFIELD("Object ID");
                                                                TESTFIELD("Ability Summ. Calculation Code",'');

                                                                ObjectIDText := DELCHR(FORMAT("Object ID"),'=',',.');
                                                                ESObjectProperty.GET("Object Type","Object ID");
                                                                CASE "Access Type" OF
                                                                  "Access Type"::Read:
                                                                    "Object Access Filter" := STRSUBSTNO('TD0 R????|TD%1 R????',ObjectIDText);
                                                                  "Access Type"::Insert:
                                                                    IF ESObjectProperty."Limited License Object" THEN
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TD0 RI???|TD0 Ri???|TD%1 RI???|TD%1 Ri???',ObjectIDText)
                                                                    ELSE
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TD0 RI???|TD%1 RI???',ObjectIDText);
                                                                  "Access Type"::Modify:
                                                                    IF ESObjectProperty."Limited License Object" THEN
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TD0 R?M??|TD0 R?m??|TD%1 R?M??|TD%1 R?m??',ObjectIDText)
                                                                    ELSE
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TD0 R?M??|TD%1 R?M??',ObjectIDText);
                                                                  "Access Type"::Delete:
                                                                    IF ESObjectProperty."Limited License Object" THEN
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TD0 R??D?|TD0 R??d?|TD%1 R??D?|TD%1 R??d?',ObjectIDText)
                                                                    ELSE
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TD0 R??D?|TD%1 R??D?',ObjectIDText);
                                                                  "Access Type"::"Insert or Modify":
                                                                    IF ESObjectProperty."Limited License Object" THEN
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO(
                                                                          'TD0 RI???|TD0 Ri???|TD0 R?M??|TD0 R?m??|TD%1 RI???|TD%1 Ri???|TD%1 R?M??|TD%1 R?m??',
                                                                          ObjectIDText)
                                                                    ELSE
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TD0 RI???|TD0 R?M??|TD%1 RI???|TD%1 R?M??',ObjectIDText);
                                                                  "Access Type"::"Modify or Delete":
                                                                    IF ESObjectProperty."Limited License Object" THEN
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO(
                                                                          'TD0 R?M???|TD0 R?m??|TD0 R??D?|TD0 R??d?|TD%1 R?M??|TD%1 R?m??|TD%1 R??D?|TD%1 R??d?',
                                                                          ObjectIDText)
                                                                    ELSE
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TD0 R?M??|TD0 R??D?|TD%1 R?M??|TD%1 R??D?',ObjectIDText);
                                                                  "Access Type"::"Insert and Modify":
                                                                    IF ESObjectProperty."Limited License Object" THEN
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TD0 RIM??|TD0 Rim??|TD%1 RIM??|TD%1 Rim??',ObjectIDText)
                                                                    ELSE
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TD0 RIM??|TD%1 RIM??',ObjectIDText);
                                                                  "Access Type"::"Insert and Delete":
                                                                    IF ESObjectProperty."Limited License Object" THEN
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TD0 RI?D?|TD0 Ri?d?|TD%1 RI?D?|TD%1 Ri?d?',ObjectIDText)
                                                                    ELSE
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TD0 RI?D?|TD%1 RI?D?',ObjectIDText);
                                                                  "Access Type"::"Modify and Delete":
                                                                    IF ESObjectProperty."Limited License Object" THEN
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TD0 R?MD?|TD0 R?md?|TD%1 R?MD?|TD%1 R?md?',ObjectIDText)
                                                                    ELSE
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TD0 R?MD?|TD%1 R?MD?',ObjectIDText);
                                                                  "Access Type"::"Insert and Modify and Delete":
                                                                    IF ESObjectProperty."Limited License Object" THEN
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TD0 RIMD?|TD0 Rimd?|TD%1 RIMD?|TD%1 Rimd?',ObjectIDText)
                                                                    ELSE
                                                                      "Object Access Filter" :=
                                                                        STRSUBSTNO('TD0 RIMD?|TD%1 RIMD?',ObjectIDText);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Access Type;
                                                   OptionCaptionML=ENU=" ,Read,Insert,Modify,Delete,Insert or Modify,Modify or Delete,Insert and Modify,Insert and Delete,Modify and Delete,Insert and Modify and Delete";
                                                   OptionString=[ ,Read,Insert,Modify,Delete,Insert or Modify,Modify or Delete,Insert and Modify,Insert and Delete,Modify and Delete,Insert and Modify and Delete] }
    { 16  ;   ;Ability Summ. Calculation Code;Code20;
                                                   TableRelation="ES Summary Calculation".Code WHERE (Type=CONST(Ability));
                                                   OnValidate=BEGIN
                                                                IF "Ability Summ. Calculation Code" <> '' THEN BEGIN
                                                                  TESTFIELD("Object Type","Object Type"::Ability);
                                                                  TESTFIELD("Object ID",0);
                                                                  TESTFIELD("Access Type",0);
                                                                  TESTFIELD("Object Access Filter",'');
                                                                  TESTFIELD("Summary Calculation Type","Summary Calculation Type"::Violation);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Ability Summary Calculation Code }
    { 21  ;   ;Object Caption      ;Text249       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Object Information"."Object Caption" WHERE (Object Type=FIELD(Object Type),
                                                                                                                      Object ID=FIELD(Object ID)));
                                                   CaptionML=ENU=Object Caption;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Summary Calculation Type,Summary Calculation Code,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=%1 %2 %3 inserted.';
      Text002@1002 : TextConst 'ENU=%1 %2 %3 updated.';
      Text003@1003 : TextConst 'ENU=Wrong use of |';
      Text004@1004 : TextConst 'ENU=Wrong filter %1';
      Text005@1005 : TextConst 'ENU="Must contain  "';
      Text006@1006 : TextConst 'ENU=Wrong Read filter %1';
      Text007@1007 : TextConst 'ENU=Wrong Insert filter %1';
      Text008@1008 : TextConst 'ENU=Wrong Modify filter %1';
      Text009@1009 : TextConst 'ENU=Wrong Delete filter %1';
      Text010@1010 : TextConst 'ENU=Wrong Execute filter %1';
      Text011@1011 : TextConst 'ENU=Wrong object type %1';
      Text012@1012 : TextConst 'ENU=Wrong character in object ID %1';
      Text013@1013 : TextConst 'ENU=Wrong object ID %1';
      Text014@1014 : TextConst 'ENU=%1 %2 %3 does not exist.';
      Text015@1240520003 : TextConst 'ENU=Inserted %1 %2 %3';
      Text016@1240520002 : TextConst 'ENU=Modified %1 %2 %3';
      Text017@1240520001 : TextConst 'ENU=Deleted %1 %2 %3';
      ESSummaryCalculation@1240520004 : Record 14123542;
      Text018@1015 : TextConst 'ENU=%1 cannot be specified when %2 is %3.';

    PROCEDURE ValidateObjectAccessFilter@1240520001(FixObjectProperties@1240520008 : Boolean;ShowError@1240520001 : Boolean;ShowMessage@1240520009 : Boolean) : Boolean;
    VAR
      ESObjectProperty@1240520005 : Record 14123522;
      TempText@1240520002 : Text[1000];
      TempText2@1240520003 : Text[1000];
      ObjectIDText@1240520006 : Text[1000];
      Pos@1240520004 : Integer;
      ObjectID@1240520007 : Integer;
    BEGIN
      IF "Object Access Filter" = '' THEN
        EXIT(TRUE);

      TempText := "Object Access Filter";
      WHILE TempText <> '' DO BEGIN
        Pos := STRPOS(TempText,'|');
        IF (Pos = 1) OR (Pos = STRLEN(TempText)) THEN
          IF ShowError THEN
            ERROR(Text003)
          ELSE
            EXIT(FALSE);

        IF Pos > 0 THEN BEGIN
          TempText2 := COPYSTR(TempText,1,Pos - 1);
          TempText := COPYSTR(TempText,Pos + 1);
        END ELSE BEGIN
          TempText2 := TempText;
          TempText := '';
        END;

        IF (STRLEN(TempText2) < 9) OR (STRLEN(TempText2) > 18) THEN
          IF ShowError THEN
            ERROR(Text004,TempText2)
          ELSE
            EXIT(FALSE);

        IF COPYSTR(TempText2,STRLEN(TempText2) - 5,1) <> ' ' THEN
          IF ShowError THEN
            ERROR(Text005)
          ELSE
            EXIT(FALSE);

        IF STRPOS('?Rr',COPYSTR(TempText2,STRLEN(TempText2) - 4,1)) = 0 THEN
          IF ShowError THEN
            ERROR(Text006,COPYSTR(TempText2,STRLEN(TempText2) - 4,1))
          ELSE
            EXIT(FALSE);
        IF STRPOS('?Ii',COPYSTR(TempText2,STRLEN(TempText2) - 3,1)) = 0 THEN
          IF ShowError THEN
            ERROR(Text007,COPYSTR(TempText2,STRLEN(TempText2) - 3,1))
          ELSE
            EXIT(FALSE);
        IF STRPOS('?Mm',COPYSTR(TempText2,STRLEN(TempText2) - 2,1)) = 0 THEN
          IF ShowError THEN
            ERROR(Text008,COPYSTR(TempText2,STRLEN(TempText2) - 2,1))
          ELSE
            EXIT(FALSE);
        IF STRPOS('?Dd',COPYSTR(TempText2,STRLEN(TempText2) - 1,1)) = 0 THEN
          IF ShowError THEN
            ERROR(Text009,COPYSTR(TempText2,STRLEN(TempText2) - 1,1))
          ELSE
            EXIT(FALSE);
        IF STRPOS('?Ee',COPYSTR(TempText2,STRLEN(TempText2),1)) = 0 THEN
          IF ShowError THEN
            ERROR(Text010,COPYSTR(TempText2,STRLEN(TempText2),1))
          ELSE
            EXIT(FALSE);

        CASE COPYSTR(TempText2,1,2) OF
          'TD':
            ESObjectProperty."Object Type" := ESObjectProperty."Object Type"::TableData;
          'TA':
            ESObjectProperty."Object Type" := ESObjectProperty."Object Type"::Table;
          'FO':
            ESObjectProperty."Object Type" := ESObjectProperty."Object Type"::"2";
          'RE':
            ESObjectProperty."Object Type" := ESObjectProperty."Object Type"::Report;
          'DA':
            ESObjectProperty."Object Type" := ESObjectProperty."Object Type"::"4";
          'CO':
            ESObjectProperty."Object Type" := ESObjectProperty."Object Type"::Codeunit;
          'XM':
            ESObjectProperty."Object Type" := ESObjectProperty."Object Type"::XMLport;
          'ME':
            ESObjectProperty."Object Type" := ESObjectProperty."Object Type"::MenuSuite;
          'PA':
            ESObjectProperty."Object Type" := ESObjectProperty."Object Type"::Page;
          'QU':
            ESObjectProperty."Object Type" := ESObjectProperty."Object Type"::Query;
          'SY':
            ESObjectProperty."Object Type" := ESObjectProperty."Object Type"::System;
          ELSE
            IF ShowError THEN
              ERROR(Text011,COPYSTR(TempText2,1,2))
            ELSE
              EXIT(FALSE);
        END;

        ObjectIDText := COPYSTR(TempText2,3,STRLEN(TempText2) - 8);
        FOR Pos := 1 TO STRLEN(ObjectIDText) DO
          IF STRPOS('1234567890',COPYSTR(ObjectIDText,Pos,1)) = 0 THEN
            IF ShowError THEN
              ERROR(Text012,COPYSTR(ObjectIDText,Pos,1))
            ELSE
              EXIT(FALSE);

        IF NOT EVALUATE(ObjectID,ObjectIDText) THEN
            IF ShowError THEN
              ERROR(Text013,ObjectIDText)
            ELSE
              EXIT(FALSE);

        IF ObjectID <> 0 THEN BEGIN
          IF NOT ESObjectProperty.GET(ESObjectProperty."Object Type",ObjectID) THEN BEGIN
            IF FixObjectProperties THEN BEGIN
              ESObjectProperty.INIT;
              ESObjectProperty.VALIDATE("Object Type","Object Type");
              ESObjectProperty.VALIDATE("Object ID","Object ID");
              ESObjectProperty.VALIDATE("Segregation of Duties",TRUE);
              ESObjectProperty.INSERT(TRUE);

              IF ShowMessage THEN
                MESSAGE(Text001,ESObjectProperty.TABLECAPTION,"Object Type","Object ID");
            END ELSE BEGIN
              IF ShowError THEN
                ERROR(
                  Text014,ESObjectProperty.TABLECAPTION,
                  ESObjectProperty."Object Type",ObjectID)
              ELSE
                EXIT(FALSE);
            END;
          END;

          IF NOT ESObjectProperty."Segregation of Duties" THEN BEGIN
            IF FixObjectProperties THEN BEGIN
              ESObjectProperty.VALIDATE("Segregation of Duties",TRUE);
              ESObjectProperty.MODIFY(TRUE);

              IF ShowMessage THEN
                MESSAGE(Text002,ESObjectProperty.TABLECAPTION,"Object Type","Object ID");
            END ELSE BEGIN
              IF ShowError THEN
                ESObjectProperty.TESTFIELD("Segregation of Duties")
              ELSE
                EXIT(FALSE);
            END;
          END;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

