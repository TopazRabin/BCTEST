OBJECT Table 14123529 ES Windows Login Mapping
{
  OBJECT-PROPERTIES
  {
    Date=04/30/18;
    Time=12:00:00 PM;
    Version List=ES1.41.51;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Comment(1,STRSUBSTNO(Text006,TABLECAPTION));

               "Created By User" := USERID;
               "Created Date Time" := CURRENTDATETIME;
             END;

    OnModify=BEGIN
               Comment(1,STRSUBSTNO(Text007,TABLECAPTION));
             END;

    OnDelete=BEGIN
               Comment(1,STRSUBSTNO(Text008,TABLECAPTION));
             END;

    CaptionML=ENU=User Mapping;
    LookupPageID=Page14123534;
    DrillDownPageID=Page14123534;
  }
  FIELDS
  {
    { 1   ;   ;SID                 ;GUID          ;CaptionML=ENU=User Security ID }
    { 2   ;   ;ID                  ;Text132       ;OnValidate=BEGIN
                                                                "User ID" := FindUserID;
                                                              END;

                                                   CaptionML=ENU=ID }
    { 4   ;   ;User ID             ;Code50        ;CaptionML=ENU=User ID }
    { 5   ;   ;Created By User     ;Code50        ;CaptionML=ENU=Created By User;
                                                   Editable=No }
    { 6   ;   ;Created Date Time   ;DateTime      ;CaptionML=ENU=Created Date Time;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;SID                                     ;Clustered=Yes }
    {    ;Created Date Time                        }
    {    ;Created By User,Created Date Time        }
    {    ;User ID                                  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Copying Live Users\';
      Text002@1002 : TextConst 'ENU=Process #1################################################';
      Text003@1003 : TextConst 'ENU=Updating User Mappings';
      Text004@1004 : TextConst 'ENU=%1 Users inserted.';
      Text005@1005 : TextConst 'ENU=Copying Easy Security Users\';
      Text006@1006 : TextConst 'ENU=Inserted %1';
      Text007@1007 : TextConst 'ENU=Modified %1';
      Text008@1008 : TextConst 'ENU=Deleted %1';
      ESSecuritySetup@1240520001 : Record 14123521;

    PROCEDURE FindUserID@1240520008() : Code[50];
    VAR
      TempText@1240520009 : Text[1000];
      Pos@1240520008 : Integer;
    BEGIN
      IF ID = '' THEN
        EXIT('');

      TempText := ID;
      Pos := STRPOS(TempText,'\');
      WHILE Pos > 0 DO BEGIN
        TempText := COPYSTR(TempText,Pos + 1);
        Pos := STRPOS(TempText,'\');
      END;

      EXIT(TempText);
    END;

    PROCEDURE CopyWindowsLogins@1240520011(ShowStatus@1240520011 : Boolean);
    VAR
      WindowsLogin@1240520008 : Record 2000000120;
      ESWindowsLoginMapping@1240520009 : Record 14123529;
      Window@1240520012 : Dialog;
      InsertedRecords@1240520010 : Integer;
    BEGIN
      IF GUIALLOWED THEN
        Window.OPEN(ESSecuritySetup.ExtendProgressText(Text001) + Text002);

      IF GUIALLOWED THEN
        Window.UPDATE(1,Text003);
      WindowsLogin.RESET;
      IF WindowsLogin.FIND('-') THEN
        REPEAT
          IF STRPOS(WindowsLogin."User Name",'\') > 0 THEN BEGIN
            ESWindowsLoginMapping.INIT;
            ESWindowsLoginMapping.SID := WindowsLogin."User Security ID";
            ESWindowsLoginMapping.VALIDATE(ID,WindowsLogin."User Name");
            IF STRLEN(WindowsLogin."User Name") > MAXSTRLEN(ESWindowsLoginMapping.ID) THEN
              ESWindowsLoginMapping.VALIDATE(
                ID,
                COPYSTR(
                  WindowsLogin."User Name",
                  STRLEN(WindowsLogin."User Name") - MAXSTRLEN(ESWindowsLoginMapping.ID) + 1,
                  MAXSTRLEN(ESWindowsLoginMapping.ID)))
            ELSE
              ESWindowsLoginMapping.VALIDATE(ID,WindowsLogin."User Name");
            IF ESWindowsLoginMapping."User ID" <> '' THEN
              IF ESWindowsLoginMapping.INSERT(TRUE) THEN
                InsertedRecords := InsertedRecords + 1;
          END;
        UNTIL WindowsLogin.NEXT = 0;

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(Text004,InsertedRecords);
    END;

    PROCEDURE CopyESWindowsLogins@1240520010(ShowStatus@1240520011 : Boolean);
    VAR
      ESWindowsLogin@1240520008 : Record 14123503;
      ESWindowsLoginMapping@1240520009 : Record 14123529;
      ESSecuritySetup@1240520001 : Record 14123521;
      Window@1240520012 : Dialog;
      InsertedRecords@1240520010 : Integer;
    BEGIN
      IF GUIALLOWED THEN
        Window.OPEN(ESSecuritySetup.ExtendProgressText(Text005) + Text002);

      IF GUIALLOWED THEN
        Window.UPDATE(1,Text003);
      ESWindowsLogin.RESET;
      IF ESWindowsLogin.FIND('-') THEN
        REPEAT
          IF STRPOS(ESWindowsLogin.ID,'\') > 0 THEN BEGIN
            ESWindowsLoginMapping.INIT;
            ESWindowsLoginMapping.SID := ESWindowsLogin.SID;
            ESWindowsLoginMapping.VALIDATE(ID,ESWindowsLogin.ID);
            IF ESWindowsLoginMapping."User ID" <> '' THEN
              IF ESWindowsLoginMapping.INSERT(TRUE) THEN
                InsertedRecords := InsertedRecords + 1;
          END;
        UNTIL ESWindowsLogin.NEXT = 0;

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(Text004,InsertedRecords);
    END;

    PROCEDURE Comment@1240520009(FunctionNo@1240520008 : ' ,Insert,Show,Exist,DeleteAll';NewText@1240520010 : Text[250]) : Boolean;
    VAR
      ESComment@1240520009 : Record 14123524;
    BEGIN
      ESComment.RESET;
      ESComment.SETRANGE("Table ID",DATABASE::"ES Windows Login Mapping");
      ESComment.SETRANGE(SID,COPYSTR(SID,1,MAXSTRLEN(ESComment.SID)));
      EXIT(ESComment.Functions(FunctionNo,NewText));
    END;

    BEGIN
    END.
  }
}

