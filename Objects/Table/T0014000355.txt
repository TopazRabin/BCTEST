OBJECT Table 14000355 EDI Element
{
  OBJECT-PROPERTIES
  {
    Date=08/27/08;
    Time=12:00:00 PM;
    Version List=SE0.53.25;
  }
  PROPERTIES
  {
    DataPerCompany=Yes;
    OnInsert=BEGIN
               IF Origin = Origin::" " THEN
                 "Nav Data Type" := "Nav Data Type"::Text;
             END;

    OnDelete=BEGIN
               EDIConditional.RESET;
               EDIConditional.SETRANGE(EDIConditional."Trade Partner No.","Trade Partner No.");
               EDIConditional.SETRANGE(EDIConditional."Navision Document","Navision Document");
               EDIConditional.SETRANGE(EDIConditional."EDI Document No.","EDI Document No.");
               EDIConditional.SETRANGE(EDIConditional.Version,Version);
               EDIConditional.SETRANGE(EDIConditional.Type,Type);
               EDIConditional.SETRANGE(EDIConditional.Segment,Segment);
               EDIConditional.SETRANGE(EDIConditional.Element,Element);
               EDIConditional.DELETEALL;
             END;

    IncludeDataInDesc=No;
    CaptionML=ENU=EDI Element;
    LookupPageID=Page14000355;
  }
  FIELDS
  {
    { 5   ;   ;Trade Partner No.   ;Code20        ;TableRelation="EDI Segment"."Trade Partner No.";
                                                   CaptionML=ENU=Trade Partner No. }
    { 7   ;   ;Navision Document   ;Code10        ;TableRelation="EDI Segment"."Navision Document" WHERE (Trade Partner No.=FIELD(Trade Partner No.));
                                                   CaptionML=ENU=Navision Document }
    { 10  ;   ;EDI Document No.    ;Code3         ;TableRelation="EDI Segment"."EDI Document No." WHERE (Trade Partner No.=FIELD(Trade Partner No.),
                                                                                                         Navision Document=FIELD(Navision Document));
                                                   CaptionML=ENU=EDI Document No. }
    { 15  ;   ;Version             ;Code10        ;TableRelation="EDI Segment".Version WHERE (Trade Partner No.=FIELD(Trade Partner No.),
                                                                                              Navision Document=FIELD(Navision Document),
                                                                                              EDI Document No.=FIELD(EDI Document No.));
                                                   CaptionML=ENU=Version }
    { 17  ;   ;Type                ;Option        ;TableRelation="EDI Segment".Type WHERE (Trade Partner No.=FIELD(Trade Partner No.),
                                                                                           Navision Document=FIELD(Navision Document),
                                                                                           EDI Document No.=FIELD(EDI Document No.),
                                                                                           Version=FIELD(Version));
                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=" ,Import,Export";
                                                   OptionString=[ ,Import,Export] }
    { 20  ;   ;Segment             ;Code15        ;TableRelation="EDI Segment".Segment WHERE (Trade Partner No.=FIELD(Trade Partner No.),
                                                                                              Navision Document=FIELD(Navision Document),
                                                                                              EDI Document No.=FIELD(EDI Document No.),
                                                                                              Version=FIELD(Version),
                                                                                              Type=FIELD(Type));
                                                   CaptionML=ENU=Segment }
    { 25  ;   ;Element             ;Code2         ;CaptionML=ENU=Element }
    { 30  ;   ;NAV Table No.       ;Integer       ;TableRelation="EDI NAV Tables"."NAV Table No.";
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=NAV Table No. }
    { 31  ;   ;NAV Table Name      ;Text40        ;TableRelation=IF (Origin=FILTER(Navision)) "EDI NAV Tables"."NAV Table Name";
                                                   OnValidate=BEGIN
                                                                IF "NAV Table Name" = '' THEN
                                                                  "NAV Table No." := 0;
                                                                EDINAVTables.SETRANGE(EDINAVTables."NAV Table Name","NAV Table Name");
                                                                IF EDINAVTables.FIND('-') THEN
                                                                  VALIDATE("NAV Table No.",EDINAVTables."NAV Table No.");

                                                                IF xRec."NAV Table Name" <> "NAV Table Name" THEN BEGIN
                                                                  xRec.Origin := Origin;
                                                                  xRec."NAV Table No." := "NAV Table No.";
                                                                  xRec."NAV Table Name" := "NAV Table Name";
                                                                  xRec.Element := Element;
                                                                  Rec.INIT;
                                                                  Origin := xRec.Origin;
                                                                  "NAV Table No." := xRec."NAV Table No.";
                                                                  "NAV Table Name" := xRec."NAV Table Name";
                                                                  Element := xRec.Element;
                                                                END;
                                                              END;

                                                   TestTableRelation=No;
                                                   CaptionML=ENU=NAV Table Name }
    { 40  ;   ;Proc Seq            ;Integer       ;CaptionML=ENU=Proc Seq }
    { 45  ;   ;Origin              ;Option        ;OnValidate=BEGIN
                                                                IF xRec.Origin <> Rec.Origin THEN BEGIN
                                                                  xRec.Origin := Rec.Origin;
                                                                  xRec.Element := Element;
                                                                  Rec.INIT;
                                                                  Rec.Origin := xRec.Origin;
                                                                  Element := xRec.Element;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Origin;
                                                   OptionCaptionML=ENU=" ,Navision,EDI Out,EDI In,SDQ";
                                                   OptionString=[ ,Navision,EDI Out,EDI In,SDQ] }
    { 50  ;   ;Nav Field No.       ;Integer       ;TableRelation="EDI NAV Fields"."NAV Field No." WHERE (NAV Table No.=FIELD(NAV Table No.));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Field Name");
                                                                IF "Nav Field No." <> 0 THEN BEGIN
                                                                  EDINAVFields.GET("NAV Table No.","Nav Field No.");
                                                                  "Nav Data Type" := EDINAVFields.Type;
                                                                END ELSE
                                                                  "Nav Data Type" := "Nav Data Type"::"N/A";
                                                              END;

                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Nav Field No. }
    { 60  ;   ;Field Name          ;Text40        ;TableRelation=IF (Origin=FILTER(Navision)) "EDI NAV Fields"."NAV Field Name" WHERE (NAV Table No.=FIELD(NAV Table No.))
                                                                 ELSE IF (Origin=FILTER(EDI Out)) "EDI Fields"."EDI Field Code" WHERE (Navision Document=FIELD(Navision Document),
                                                                                                                                       Field Type=CONST(EDI Out))
                                                                                                                                       ELSE IF (Origin=FILTER(EDI In)) "EDI Fields"."EDI Field Code" WHERE (Field Type=FILTER(EDI In));
                                                   OnValidate=BEGIN
                                                                IF xRec."Field Name" <> "Field Name" THEN BEGIN
                                                                  xRec.Origin := Origin;
                                                                  xRec."NAV Table No." := "NAV Table No.";
                                                                  xRec."NAV Table Name" := "NAV Table Name";
                                                                  xRec."Field Name" := "Field Name";
                                                                  xRec.Element := Element;
                                                                  Rec.INIT;
                                                                  Origin := xRec.Origin;
                                                                  "NAV Table No." := xRec."NAV Table No.";
                                                                  "NAV Table Name" := xRec."NAV Table Name";
                                                                  "Field Name" := xRec."Field Name";
                                                                  Element := xRec.Element;
                                                                END;
                                                                //
                                                                CASE Origin OF
                                                                  Origin::Navision:
                                                                    BEGIN
                                                                      EDINAVFields.GET("NAV Table No.","Field Name");
                                                                      "Nav Field No." := EDINAVFields."NAV Field No.";
                                                                      "Nav Data Type" := EDINAVFields.Type;
                                                                      CASE EDINAVFields.Type OF
                                                                        EDINAVFields.Type::Decimal:
                                                                          "Data Type" := "Data Type"::"Dec Implied";
                                                                        EDINAVFields.Type::Integer:
                                                                          "Data Type" := "Data Type"::"Dec Implied";
                                                                        EDINAVFields.Type::Text:
                                                                          "Data Type" := "Data Type"::Text;
                                                                        EDINAVFields.Type::Code:
                                                                          "Data Type" := "Data Type"::Text;
                                                                        EDINAVFields.Type::Date:
                                                                          BEGIN
                                                                            "Data Type" := "Data Type"::"Date YYYYMMDD";
                                                                            VALIDATE(Length,8);
                                                                          END;
                                                                      END;
                                                                    END;
                                                                  Origin::"EDI Out":
                                                                    BEGIN
                                                                      EDIFields.GET("Navision Document",EDIFields."Field Type"::"EDI Out","Field Name");
                                                                      "Nav Field No." := 0;
                                                                      "Nav Data Type" := EDIFields."Data Type";
                                                                      CASE EDIFields."Data Type" OF
                                                                        EDIFields."Data Type"::Decimal:
                                                                          "Data Type" := "Data Type"::"Dec Implied";
                                                                        EDIFields."Data Type"::Integer:
                                                                          "Data Type" := "Data Type"::Integer;
                                                                        EDIFields."Data Type"::Text:
                                                                          "Data Type" := "Data Type"::Text;
                                                                        EDIFields."Data Type"::Code:
                                                                          "Data Type" := "Data Type"::Text;
                                                                        EDIFields."Data Type"::Date:
                                                                          BEGIN
                                                                            "Data Type" := "Data Type"::"Date YYYYMMDD";
                                                                            VALIDATE(Length,8);
                                                                          END;
                                                                      END;
                                                                    END;
                                                                  Origin::SDQ:
                                                                    IF ("Field Name" <> '') AND ("Field Name" <> 'SDQ SHIP TO') AND ("Field Name" <> 'SDQ SELL TO') THEN
                                                                      ERROR(
                                                                        Text001 +
                                                                        '  SDQ SHIP TO\' +
                                                                        '  SDQ SELL TO\');
                                                                ELSE BEGIN
                                                                    "Nav Field No." := 0;
                                                                    "Nav Data Type" := "Nav Data Type"::"N/A";
                                                                  END;
                                                                END;
                                                              END;

                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Field Name;
                                                   Editable=Yes }
    { 65  ;   ;Nav Data Type       ;Option        ;InitValue=Text;
                                                   CaptionML=ENU=Nav Data Type;
                                                   OptionCaptionML=ENU=Integer,Text,Code,Decimal,Option,Boolean,Date,Time,Binary,BLOB,N/A;
                                                   OptionString=Integer,Text,Code,Decimal,Option,Boolean,Date,Time,Binary,BLOB,N/A;
                                                   Editable=No }
    { 70  ;   ;Length              ;Integer       ;OnValidate=BEGIN
                                                                IF Length > 80 THEN
                                                                  ERROR(Text002);
                                                                BegPos := 1;
                                                                xEDIElement.RESET;
                                                                xEDIElement.SETRANGE(xEDIElement."Trade Partner No.","Trade Partner No.");
                                                                xEDIElement.SETRANGE(xEDIElement."EDI Document No.","EDI Document No.");
                                                                xEDIElement.SETRANGE(xEDIElement.Version,Version);
                                                                xEDIElement.SETRANGE(xEDIElement.Type,Type);
                                                                xEDIElement.SETRANGE(xEDIElement.Segment,Segment);
                                                                IF xEDIElement.FIND('-') THEN
                                                                  REPEAT
                                                                    IF ("Proc Seq" = xEDIElement."Proc Seq") THEN BEGIN
                                                                      "Beg. Pos." := BegPos;
                                                                      BegPos := BegPos + Length;
                                                                      "End Pos." := BegPos - 1;
                                                                    END ELSE BEGIN
                                                                      xEDIElement."Beg. Pos." := BegPos;
                                                                      BegPos := BegPos + xEDIElement.Length;
                                                                      xEDIElement."End Pos." := BegPos - 1;
                                                                      xEDIElement.MODIFY;
                                                                    END;
                                                                  UNTIL xEDIElement.NEXT = 0;
                                                              END;

                                                   CaptionML=ENU=Length }
    { 71  ;   ;Beg. Pos.           ;Integer       ;CaptionML=ENU=Beg. Pos.;
                                                   Editable=No }
    { 72  ;   ;End Pos.            ;Integer       ;CaptionML=ENU=End Pos.;
                                                   Editable=No }
    { 75  ;   ;Min Length          ;Integer       ;CaptionML=ENU=Min Length }
    { 80  ;   ;Data Type           ;Option        ;CaptionML=ENU=Data Type;
                                                   OptionCaptionML=ENU=Text,Dec Implied,Dec Explicit,Integer,Date YYMMDD,Date YYYYMMDD;
                                                   OptionString=Text,Dec Implied,Dec Explicit,Integer,Date YYMMDD,Date YYYYMMDD }
    { 85  ;   ;Decimals            ;Integer       ;CaptionML=ENU=Decimals }
    { 87  ;   ;Justify             ;Option        ;CaptionML=ENU=Justify;
                                                   OptionCaptionML=ENU=" ,Left,Right,Right Zero Fill";
                                                   OptionString=[ ,Left,Right,Right Zero Fill] }
    { 90  ;   ;Fixed Value         ;Code30        ;OnValidate=BEGIN
                                                                IF Origin <> Origin::" " THEN BEGIN
                                                                  "Fixed Value" := '';
                                                                  MESSAGE(Text003);
                                                                END ELSE BEGIN
                                                                  IF Length = 0 THEN
                                                                    VALIDATE(Length,STRLEN("Fixed Value"));
                                                                  Decimals := 0;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Fixed Value }
    { 100 ;   ;Trigger             ;Boolean       ;CaptionML=ENU=Trigger }
    { 110 ;   ;Viewable            ;Boolean       ;CaptionML=ENU=Viewable }
    { 111 ;   ;Item Description    ;Boolean       ;CaptionML=ENU=Item Description }
    { 120 ;   ;Conditional         ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("EDI Conditional" WHERE (Trade Partner No.=FIELD(Trade Partner No.),
                                                                                              Navision Document=FIELD(Navision Document),
                                                                                              EDI Document No.=FIELD(EDI Document No.),
                                                                                              Version=FIELD(Version),
                                                                                              Type=FIELD(Type),
                                                                                              Proc Seq=FIELD(Proc Seq),
                                                                                              Segment=FIELD(Segment),
                                                                                              Element=FIELD(Element)));
                                                   CaptionML=ENU=Conditional;
                                                   Editable=No }
    { 130 ;   ;Bypass Segment if Blank;Boolean    ;CaptionML=ENU=Bypass Segment if Blank }
    { 200 ;   ;Date Changed        ;Date          ;CaptionML=ENU=Date Changed }
    { 201 ;   ;Changed By          ;Code50        ;CaptionML=ENU=Changed By }
  }
  KEYS
  {
    {    ;Trade Partner No.,Navision Document,EDI Document No.,Version,Type,Segment,Proc Seq;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      "EDI Transaction"@1000000000 : Record 14000354;
      EDINAVTables@1000000001 : Record 14000351;
      EDINAVFields@1000000002 : Record 14000352;
      EDIFields@1000000003 : Record 14000356;
      xEDIElement@1000000004 : Record 14000355;
      EDIConditional@1000000005 : Record 14000363;
      BegPos@1000000006 : Integer;
      EndPos@1000000007 : Integer;
      Text001@1001 : TextConst 'ENU=If Origin is SDQ the Field Name must be one of the following:\';
      Text002@1002 : TextConst 'ENU=Maximum length for an element is 80 characters.';
      Text003@1003 : TextConst 'ENU=Origin must be blank to enter a fixed value.';

    BEGIN
    END.
  }
}

