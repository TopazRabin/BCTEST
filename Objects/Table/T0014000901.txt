OBJECT Table 14000901 E-Mail Header
{
  OBJECT-PROPERTIES
  {
    Date=09/24/20;
    Time=[ 5:43:48 AM];
    Modified=Yes;
    Version List=SE0.60.03;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               EMailLine.RESET;
               EMailLine.SETRANGE("E-Mail Code",Code);
               EMailLine.DELETEALL;

               EMailAttachment.RESET;
               EMailAttachment.SETRANGE("E-Mail Code",Code);
               EMailAttachment.DELETEALL;

               EMailElement.RESET;
               EMailElement.SETRANGE("E-Mail Code",Code);
               EMailElement.DELETEALL;

               EMailCommentLine.RESET;
               EMailCommentLine.SETRANGE("Table ID",DATABASE::"E-Mail Header");
               EMailCommentLine.SETRANGE(Code,Code);
               EMailCommentLine.DELETEALL;
             END;

    CaptionML=ENU=E-Mail Header;
    LookupPageID=Page14000915;
    DrillDownPageID=Page14000915;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text30        ;CaptionML=ENU=Description }
    { 3   ;   ;Subject Text        ;Text250       ;CaptionML=ENU=Subject Text }
    { 4   ;   ;Line Break Character;Text1         ;InitValue=\;
                                                   CaptionML=ENU=Line Break Character }
    { 11  ;   ;Confirm E-Mail manually;Boolean    ;CaptionML=ENU=Confirm E-Mail manually }
    { 12  ;   ;E-Mail Usage        ;Option        ;CaptionML=ENU=E-Mail Usage;
                                                   OptionCaptionML=ENU=Sales Order,Sales Shipment,Sales Invoice,Bill of Lading,Customer Statement,E-Mail List Mailing,Contact Mailing,Purchase,Issued Reminder,Issued Fin. Charge Memo,Purchase Receipt,Purchase Invoice,Mfr. Rep. Statement,Location Hdlg. Statement,Sales Credit Memo;
                                                   OptionString=Sales Order,Sales Shipment,Sales Invoice,Bill of Lading,Customer Statement,E-Mail List Mailing,Contact Mailing,Purchase,Issued Reminder,Issued Fin. Charge Memo,Purchase Receipt,Purchase Invoice,Mfr. Rep. Statement,Location Hdlg. Statement,Sales Credit Memo }
    { 21  ;   ;Only Lines with Content;Boolean    ;CaptionML=ENU=Only Lines with Content }
    { 22  ;   ;Line Type Filter    ;Text250       ;CaptionML=ENU=Line Type Filter }
    { 23  ;   ;Attachment Exist    ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("E-Mail Attachment" WHERE (E-Mail Code=FIELD(Code)));
                                                   CaptionML=ENU=Attachment Exist;
                                                   Editable=No }
    { 24  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("E-Mail Comment Line" WHERE (Table ID=CONST(14000901),
                                                                                                  Code=FIELD(Code)));
                                                   CaptionML=ENU=Comment;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EMailLine@1000000000 : Record 14000902;
      EMailElement@1000000001 : Record 14000903;
      EMailAttachment@1000000002 : Record 14000909;
      EMailCommentLine@1000000003 : Record 14000910;
      EMailComment@1000000004 : Page 14000921;
      Text002@1240030001 : TextConst 'ENU=E-Mail Header Code cannot be blank.';
      Text003@1240030000 : TextConst 'ENU=New E-Mail Header %1 has been created.';

    PROCEDURE CopyEMailHeaderAskNewCode@1240030000();
    VAR
      EShipInput@1240030000 : Record 14000728;
      EShipEnterNewEMailCode@1240030001 : Page 14050118;
    BEGIN
      CLEAR(EShipEnterNewEMailCode);
      EShipEnterNewEMailCode.LOOKUPMODE(TRUE);
      IF EShipEnterNewEMailCode.RUNMODAL = ACTION::LookupOK THEN BEGIN
        EShipEnterNewEMailCode.GetEShipInput(EShipInput);

        IF EShipInput."New E-Mail Code" = '' THEN
          ERROR(Text002);

        CopyEMailHeader(EShipInput."New E-Mail Code");

        MESSAGE(Text003,EShipInput."New E-Mail Code");
      END ELSE
        EXIT;
    END;

    PROCEDURE CopyEMailHeader@1(NewEMailHeaderCode@1000000000 : Code[10]);
    VAR
      NewEMailHeader@1000000001 : Record 14000901;
      NewEMailLine@1000000002 : Record 14000902;
      NewEMailElement@1000000003 : Record 14000903;
      NewEMailAttachment@1000000004 : Record 14000909;
    BEGIN
      NewEMailHeader := Rec;
      NewEMailHeader.Code := NewEMailHeaderCode;
      NewEMailHeader.INSERT;

      EMailLine.RESET;
      EMailLine.SETRANGE("E-Mail Code",Code);
      IF EMailLine.FIND('-') THEN
        REPEAT
          NewEMailLine := EMailLine;
          NewEMailLine."E-Mail Code" := NewEMailHeaderCode;
          NewEMailLine.INSERT;
        UNTIL EMailLine.NEXT = 0;

      EMailAttachment.RESET;
      EMailAttachment.SETRANGE("E-Mail Code",Code);
      IF EMailAttachment.FIND('-') THEN
        REPEAT
          NewEMailAttachment := EMailAttachment;
          NewEMailAttachment."E-Mail Code" := NewEMailHeaderCode;
          NewEMailAttachment.INSERT;
        UNTIL EMailAttachment.NEXT = 0;

      EMailElement.RESET;
      EMailElement.SETRANGE("E-Mail Code",Code);
      IF EMailElement.FIND('-') THEN
        REPEAT
          NewEMailElement := EMailElement;
          NewEMailElement."E-Mail Code" := NewEMailHeaderCode;
          NewEMailElement.INSERT;
        UNTIL EMailElement.NEXT = 0;
    END;

    PROCEDURE PrintEMailLayout@1240030002();
    VAR
      EMailHeader@1240030000 : Record 14000901;
    BEGIN
      EMailHeader.RESET;
      EMailHeader := Rec;
      EMailHeader.SETRECFILTER;
      REPORT.RUN(REPORT::"E-Mail Layout",TRUE,TRUE,EMailHeader);
    END;

    PROCEDURE EMailLoopDescription@1240030001();
    VAR
      SalesOrderEMail@1240030011 : Report 14000901;
      SalesShipmentEMail@1240030010 : Report 14000902;
      SalesInvoiceEMail@1240030009 : Report 14000903;
      BillOfLadingEMail@1240030008 : Report 14000908;
      CustomerStatementEMail@1240030007 : Report 14000910;
      ContactEMail@1240030006 : Report 14000912;
      EMailListEntryEMail@1240030005 : Report 14000913;
      PurchaseEMail@1240030004 : Report 14000914;
      IssuedReminderEMail@1240030003 : Report 14000916;
      IssuedFinChargeMemoEMail@1240030002 : Report 14000918;
      PurchaseReceiptEMail@1240030001 : Report 14000920;
      PurchaseInvoiceEMail@1240030000 : Report 14000921;
      MfrRepStatementEmail@1000000000 : Report 51706;
      LocHdlgStatementEMail@1000000001 : Report 51707;
      SalesCrMemoEMail@1000000003 : Report 51020;
      CommJnlLine@1000000002 : Record 51705;
    BEGIN
      CASE "E-Mail Usage" OF
        "E-Mail Usage"::"Sales Order":
          BEGIN
            CLEAR(SalesOrderEMail);
            SalesOrderEMail.InitializeRequest(0,'SHOWHELP',FALSE);
          END;
        "E-Mail Usage"::"Sales Shipment":
          BEGIN
            CLEAR(SalesShipmentEMail);
            SalesShipmentEMail.InitializeRequest('SHOWHELP',FALSE);
          END;
        "E-Mail Usage"::"Sales Invoice":
          BEGIN
            CLEAR(SalesInvoiceEMail);
            SalesInvoiceEMail.InitializeRequest('SHOWHELP',FALSE);
          END;
        "E-Mail Usage"::"Bill of Lading":
          BEGIN
            CLEAR(BillOfLadingEMail);
            BillOfLadingEMail.InitializeRequest('SHOWHELP');
          END;
        "E-Mail Usage"::"Customer Statement":
          BEGIN
            CLEAR(CustomerStatementEMail);
            CustomerStatementEMail.InitializeRequest('SHOWHELP',FALSE);  //added false PKS
          END;
        "E-Mail Usage"::"E-Mail List Mailing":
          BEGIN
            CLEAR(EMailListEntryEMail);
            EMailListEntryEMail.InitializeRequest('SHOWHELP');
          END;
        "E-Mail Usage"::"Contact Mailing":
          BEGIN
            CLEAR(ContactEMail);
            ContactEMail.InitializeRequest('SHOWHELP');
          END;
        "E-Mail Usage"::Purchase:
          BEGIN
            CLEAR(PurchaseEMail);
            PurchaseEMail.InitializeRequest(0,'SHOWHELP',FALSE);
          END;
        "E-Mail Usage"::"Issued Reminder":
          BEGIN
            CLEAR(IssuedReminderEMail);
            IssuedReminderEMail.InitializeRequest('SHOWHELP',FALSE);
          END;
        "E-Mail Usage"::"Issued Fin. Charge Memo":
          BEGIN
            CLEAR(IssuedFinChargeMemoEMail);
            IssuedFinChargeMemoEMail.InitializeRequest('SHOWHELP',FALSE);
          END;
        "E-Mail Usage"::"Purchase Receipt":
          BEGIN
            CLEAR(PurchaseReceiptEMail);
            PurchaseReceiptEMail.InitializeRequest('SHOWHELP',FALSE);
          END;
        "E-Mail Usage"::"Purchase Invoice":
          BEGIN
            CLEAR(PurchaseInvoiceEMail);
            PurchaseInvoiceEMail.InitializeRequest('SHOWHELP',FALSE);
          END;
        // <TPZ92, TPZ143>
        "E-Mail Usage"::"Mfr. Rep. Statement":
          BEGIN
            CLEAR(MfrRepStatementEmail);
            MfrRepStatementEmail.InitializeRequest('SHOWHELP',CommJnlLine,FALSE);
          END;
        "E-Mail Usage"::"Location Hdlg. Statement":
          BEGIN
            CLEAR(LocHdlgStatementEMail);
            LocHdlgStatementEMail.InitializeRequest('SHOWHELP',CommJnlLine,FALSE);
          END;
        "E-Mail Usage"::"Sales Credit Memo":
          BEGIN
            CLEAR(LocHdlgStatementEMail);
            SalesCrMemoEMail.InitializeRequest('SHOWHELP',FALSE);
          END;
        // </TPZ92, TPZ143>
      END;
    END;

    PROCEDURE LookupComment@2();
    BEGIN
      EMailCommentLine.RESET;
      EMailCommentLine.SETRANGE("Table ID",DATABASE::"E-Mail Header");
      EMailCommentLine.SETRANGE(Code,Code);
      CLEAR(EMailComment);
      EMailComment.SETTABLEVIEW(EMailCommentLine);
      EMailComment.RUNMODAL;
    END;

    BEGIN
    {
      2015-06-11 TPZ92 TAKHMETO
        Mfr. Rep. Statement, Location Hdlg. Statement options to E-mail Usage have been added
        EMailLoopDescription function has been modified
      2015-08-25 TPZ143 TAKHMETO
        Sales Credit Memo option to E-mail Usage have been added
        EMailLoopDescription function has been modified
    }
    END.
  }
}

