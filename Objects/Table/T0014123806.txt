OBJECT Table 14123806 ES Field Level Security Object
{
  OBJECT-PROPERTIES
  {
    Date=11/01/16;
    Time=12:00:00 PM;
    Version List=ES1.41.22;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit Field level Sec. Codes");
               END;

               ESFieldLevelSecurityCode.GET("Source Table ID","Field Level Security Code");

               ESSourceTableSetup.GET("Source Table ID");

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnModify=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit Field level Sec. Codes");
               END;

               ESFieldLevelSecurityCode.GET("Source Table ID","Field Level Security Code");

               ESSourceTableSetup.GET("Source Table ID");

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnDelete=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit Field level Sec. Codes");
               END;

               ESFieldLevelSecurityCode.GET("Source Table ID","Field Level Security Code");

               ESSourceTableSetup.GET("Source Table ID");

               DeleteRelated;

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnRename=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);
               TESTFIELD("Object ID");
               xRec.TESTFIELD("Object ID");

               ESFieldLevelSecurityCode.GET("Source Table ID","Field Level Security Code");

               ESSourceTableSetup.GET("Source Table ID");

               ESFLADSUpdateNo.UpdateNo;
             END;

    CaptionML=ENU=Field Level Security Object;
    LookupPageID=Page14123806;
    DrillDownPageID=Page14123806;
  }
  FIELDS
  {
    { 1   ;   ;Source Table ID     ;Integer       ;TableRelation="ES Source Table Setup";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Source Table Caption");
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupFieldLevelSecurityCode;
                                                            END;

                                                   CaptionML=ENU=Source Table ID;
                                                   NotBlank=Yes }
    { 2   ;   ;Field Level Security Code;Code20   ;TableRelation="ES Field Level Security Code".Code WHERE (Source Table ID=FIELD(Source Table ID));
                                                   OnLookup=BEGIN
                                                              LookupFieldLevelSecurityCode;
                                                            END;

                                                   CaptionML=ENU=Field Level Security Code;
                                                   NotBlank=Yes }
    { 3   ;   ;Object Type         ;Option        ;InitValue=Page;
                                                   OnValidate=BEGIN
                                                                IF "Object Type" = "Object Type"::"2" THEN BEGIN
                                                                  ESFLADSSetup.GET;
                                                                  ESFLADSSetup.TESTFIELD("Use Page Setup for Forms",FALSE);
                                                                END;

                                                                IF "Object Type" = "Object Type"::Page THEN BEGIN
                                                                  ESFLADSSetup.GET;
                                                                  ESFLADSSetup.TESTFIELD("Use Form Setup for Pages",FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Object Type;
                                                   OptionCaptionML=ENU=" ,,,,,,,,Page";
                                                   OptionString=[ ,,,,,,,,Page];
                                                   NotBlank=Yes }
    { 4   ;   ;Object ID           ;Integer       ;TableRelation="ES FLADS Relation"."Object ID" WHERE (Object Type=FIELD(Object Type),
                                                                                                        Type=FILTER(Source Table|Data Item),
                                                                                                        Relation Object Type=CONST(Table),
                                                                                                        Relation Object ID=FIELD(Source Table ID));
                                                   OnValidate=BEGIN
                                                                IF "Object ID" <> 0 THEN BEGIN
                                                                  ESFLADSSetup.GET;

                                                                  ESFLADSRelation.RESET;
                                                                  IF ESFLADSSetup."Use Form Setup for Pages" OR ESFLADSSetup."Use Page Setup for Forms" THEN
                                                                    ESFLADSRelation.SETFILTER("Object Type",'%1|%2',"Object Type"::"2","Object Type"::Page)
                                                                  ELSE
                                                                    ESFLADSRelation.SETRANGE("Object Type","Object Type");
                                                                  ESFLADSRelation.SETRANGE("Object ID","Object ID");
                                                                  ESFLADSRelation.SETRANGE(Type,ESFLADSRelation.Type::"Source Table");
                                                                  ESFLADSRelation.SETRANGE("Relation Object Type",ESFLADSRelation."Relation Object Type"::Table);
                                                                  ESFLADSRelation.SETRANGE("Relation Object ID","Source Table ID");
                                                                  ESFLADSRelation.FIND('-');
                                                                END;

                                                                CALCFIELDS("Object Caption");
                                                              END;

                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Object ID }
    { 11  ;   ;General Permission  ;Option        ;CaptionML=ENU=General Permission;
                                                   OptionCaptionML=ENU=Edit,View,Hide;
                                                   OptionString=Edit,View,Hide }
    { 12  ;   ;Source Table Caption;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Source Table ID)));
                                                   CaptionML=ENU=Source Table Caption;
                                                   Editable=No }
    { 13  ;   ;Object Caption      ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FIELD(Object Type),
                                                                                                                Object ID=FIELD(Object ID)));
                                                   CaptionML=ENU=Object Caption;
                                                   Editable=No }
    { 14  ;   ;Field Level Security Descr.;Text250;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Field Level Security Code".Description WHERE (Source Table ID=FIELD(Source Table ID),
                                                                                                                        Code=FIELD(Field Level Security Code)));
                                                   CaptionML=ENU=Field Level Security Descr.;
                                                   Editable=No }
    { 15  ;   ;Comment             ;Text250       ;CaptionML=ENU=Comment }
    { 21  ;   ;Fields              ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES Field Level Sec. Control" WHERE (Source Table ID=FIELD(Source Table ID),
                                                                                                          Field Level Security Code=FIELD(Field Level Security Code),
                                                                                                          Object Type=FIELD(Object Type),
                                                                                                          Object ID=FIELD(Object ID),
                                                                                                          Type=CONST(Field)));
                                                   CaptionML=ENU=Fields;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 22  ;   ;Controls            ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES Field Level Sec. Control" WHERE (Source Table ID=FIELD(Source Table ID),
                                                                                                          Field Level Security Code=FIELD(Field Level Security Code),
                                                                                                          Object Type=FIELD(Object Type),
                                                                                                          Object ID=FIELD(Object ID),
                                                                                                          Type=CONST(Control)));
                                                   CaptionML=ENU=Actions;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 23  ;   ;Code Implemented    ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES FLADS Relation"."Code Implemented" WHERE (Object Type=FIELD(Object Type),
                                                                                                                    Object ID=FIELD(Object ID),
                                                                                                                    Type=CONST(Source Table),
                                                                                                                    Relation Object Type=CONST(Table),
                                                                                                                    Relation Object ID=FIELD(Source Table ID)));
                                                   CaptionML=ENU=Code Implemented;
                                                   Editable=No }
    { 24  ;   ;From Setup Company  ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES FLADS Copy Setup"."From Setup Company" WHERE (Table ID=CONST(14123803),
                                                                                                                        Source Table ID=FIELD(Source Table ID),
                                                                                                                        Code=FIELD(Field Level Security Code)));
                                                   CaptionML=ENU=From Setup Company;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Source Table ID,Field Level Security Code,Object Type,Object ID;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ESSourceTableSetup@1240520011 : Record 14123802;
      ESFieldLevelSecurityCode@1240520008 : Record 14123803;
      ESFLADSRelation@1240520009 : Record 14123810;
      ESFLADSUpdateNo@1240520012 : Record 14123813;
      Text001@1001 : TextConst 'ENU=<%1 not specified later>';
      ESFLADSSetup@1240520013 : Record 14123801;
      Text002@1002 : TextConst 'ENU=%1 %2 %3 %4 %5 %6';
      Text003@1003 : TextConst 'ENU=%1 already exists.';
      ESLoginSetup@1240520001 : Record 14123540;

    PROCEDURE LookupFieldLevelSecurityCode@1240520008();
    BEGIN
      ESFieldLevelSecurityCode.RESET;
      IF "Source Table ID" <> 0 THEN
        ESFieldLevelSecurityCode.SETRANGE("Source Table ID","Source Table ID");

      ESFieldLevelSecurityCode."Source Table ID" := "Source Table ID";
      ESFieldLevelSecurityCode.Code := "Field Level Security Code";
      IF ESFieldLevelSecurityCode.FIND('=><') THEN
        ;

      IF PAGE.RUNMODAL(0,ESFieldLevelSecurityCode) = ACTION::LookupOK THEN BEGIN
        "Source Table ID" := ESFieldLevelSecurityCode."Source Table ID";
        "Field Level Security Code" := ESFieldLevelSecurityCode.Code;
      END;
    END;

    PROCEDURE DrillDownFieldsControls@1240520010();
    VAR
      ESFieldLevelSecurityObject@1240520001 : Record 14123806;
    BEGIN
      IF ISSERVICETIER THEN
        IF NOT ESFieldLevelSecurityObject.GET(
                 "Source Table ID","Field Level Security Code","Object Type","Object ID")
        THEN BEGIN
          INSERT(TRUE);

          COMMIT;
        END;

      PAGE.RUNMODAL(PAGE::"ES Field Level Security Object",Rec);
    END;

    PROCEDURE ObjectCaption@1240520011() : Text[250];
    BEGIN
      IF "Object ID" = 0 THEN
        EXIT(STRSUBSTNO(Text001,"Object Type"));

      CALCFIELDS("Object Caption");
      EXIT("Object Caption");
    END;

    PROCEDURE DataCaption@1240520009() : Text[250];
    BEGIN
      CALCFIELDS("Source Table Caption");
      EXIT(
        STRSUBSTNO(
          Text002,
          "Source Table ID","Source Table Caption","Field Level Security Code",
          "Object Type","Object ID",ObjectCaption));
    END;

    PROCEDURE CopyDetailsFromObject@1240520012();
    VAR
      ESFieldLevelSecControl@1240520008 : Record 14123807;
      ESFieldLevelSecControl2@1240520009 : Record 14123807;
      ESFieldLevelSecurityObject@1240520010 : Record 14123806;
    BEGIN
      ESFieldLevelSecControl.RESET;
      ESFieldLevelSecControl.SETRANGE("Source Table ID","Source Table ID");
      ESFieldLevelSecControl.SETRANGE("Field Level Security Code","Field Level Security Code");
      ESFieldLevelSecControl.SETRANGE("Object Type","Object Type");
      ESFieldLevelSecControl.SETRANGE("Object ID","Object ID");
      IF ESFieldLevelSecControl.FIND('-') THEN
        ERROR(Text003,ESFieldLevelSecControl.TABLECAPTION);

      ESFieldLevelSecurityObject.RESET;
      ESFieldLevelSecurityObject.SETRANGE("Source Table ID","Source Table ID");
      IF PAGE.RUNMODAL(0,ESFieldLevelSecurityObject) <> ACTION::LookupOK THEN
        EXIT;

      ESFieldLevelSecControl.RESET;
      ESFieldLevelSecControl.SETRANGE("Source Table ID",ESFieldLevelSecurityObject."Source Table ID");
      ESFieldLevelSecControl.SETRANGE(
        "Field Level Security Code",ESFieldLevelSecurityObject."Field Level Security Code");
      ESFieldLevelSecControl.SETRANGE("Object Type",ESFieldLevelSecurityObject."Object Type");
      ESFieldLevelSecControl.SETRANGE("Object ID",ESFieldLevelSecurityObject."Object ID");
      IF ESFieldLevelSecControl.FIND('-') THEN
        REPEAT
          IF (ESFieldLevelSecControl.Type = ESFieldLevelSecControl.Type::Field) OR
             (ESFieldLevelSecControl.ID = 0)
          THEN BEGIN
            ESFieldLevelSecControl2 := ESFieldLevelSecControl;
            ESFieldLevelSecControl2."Source Table ID" := "Source Table ID";
            ESFieldLevelSecControl2."Field Level Security Code" := "Field Level Security Code";
            ESFieldLevelSecControl2."Object Type" := "Object Type";
            ESFieldLevelSecControl2."Object ID" := "Object ID";
            ESFieldLevelSecControl2.INSERT(TRUE);
          END;
        UNTIL ESFieldLevelSecControl.NEXT = 0;
    END;

    PROCEDURE DeleteRelated@1240520014();
    VAR
      ESFieldLevelSecurityField@1240520008 : Record 14123807;
      ESFieldLevelSecurityField2@1240520009 : Record 14123807;
    BEGIN
      ESFieldLevelSecurityField.RESET;
      ESFieldLevelSecurityField.SETRANGE("Source Table ID","Source Table ID");
      ESFieldLevelSecurityField.SETRANGE("Field Level Security Code","Field Level Security Code");
      ESFieldLevelSecurityField.SETRANGE("Object Type","Object Type");
      ESFieldLevelSecurityField.SETRANGE("Object ID","Object ID");
      IF ESFieldLevelSecurityField.FIND('-') THEN
        REPEAT
          ESFieldLevelSecurityField2 := ESFieldLevelSecurityField;
          ESFieldLevelSecurityField2.DELETE(TRUE);
        UNTIL ESFieldLevelSecurityField.NEXT = 0;
    END;

    PROCEDURE ObjectWhereUsed@1240520015();
    VAR
      ESObjectWhereUsed@1240520008 : Page 14123562;
    BEGIN
      CLEAR(ESObjectWhereUsed);
      ESObjectWhereUsed.InitObject("Object Type","Object ID");
      ESObjectWhereUsed.RUN;
    END;

    BEGIN
    END.
  }
}

