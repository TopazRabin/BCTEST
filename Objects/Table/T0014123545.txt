OBJECT Table 14123545 ES Summary Result Line
{
  OBJECT-PROPERTIES
  {
    Date=07/13/18;
    Time=12:00:00 PM;
    Version List=ES1.41.53;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Access Complete" := CalculateAccessComplete;
             END;

    CaptionML=ENU=Summary Result Line;
  }
  FIELDS
  {
    { 1   ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=" ,Database Login,User,Permission Group,Permission Set,Windows Group,Recording,Live Database Login,Live User,Live Permission Set";
                                                   OptionString=[ ,Database Login,Windows Login,Role Group,Role,Windows Group,Recording,Live Database Login,Live Windows Login,Live Role];
                                                   NotBlank=Yes }
    { 2   ;   ;Code                ;Code50        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 3   ;   ;Company Name        ;Text30        ;TableRelation="ES Company".Name;
                                                   CaptionML=ENU=Company Name }
    { 4   ;   ;Summary Calculation Type;Option    ;CaptionML=ENU=Summary Calculation Type;
                                                   OptionCaptionML=ENU=" ,Violation,Ability";
                                                   OptionString=[ ,Violation,Ability] }
    { 5   ;   ;Summary Calculation Code;Code20    ;TableRelation="ES Summary Calculation".Code WHERE (Type=FIELD(Summary Calculation Type));
                                                   CaptionML=ENU=Summary Calculation Code }
    { 6   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 11  ;   ;Access Complete     ;Boolean       ;CaptionML=ENU=Access Complete }
    { 12  ;   ;FlowField Summary Calc. Type;Option;CaptionML=ENU=FlowField Summary Calculation Type;
                                                   OptionCaptionML=ENU=" ,Violation,Ability";
                                                   OptionString=[ ,Violation,Ability] }
    { 13  ;   ;FlowField Summary Calc. Code;Code20;TableRelation="ES Summary Calculation".Code WHERE (Type=FIELD(Summary Calculation Type));
                                                   CaptionML=ENU=FlowField Summary Calculation Code }
    { 14  ;   ;FlowField Line No.  ;Integer       ;CaptionML=ENU=FlowField Line No. }
    { 15  ;   ;Ability Summ. Calculation Code;Code20;
                                                   TableRelation="ES Summary Calculation".Code WHERE (Type=CONST(Ability));
                                                   CaptionML=ENU=Ability Summary Calculation Code }
    { 21  ;   ;Line Description    ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Summary Calculation Line".Description WHERE (Summary Calculation Type=FIELD(FlowField Summary Calc. Type),
                                                                                                                       Summary Calculation Code=FIELD(FlowField Summary Calc. Code),
                                                                                                                       Line No.=FIELD(FlowField Line No.)));
                                                   CaptionML=ENU=Line Description;
                                                   Editable=No }
    { 22  ;   ;Object Access Filter;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Summary Calculation Line"."Object Access Filter" WHERE (Summary Calculation Type=FIELD(FlowField Summary Calc. Type),
                                                                                                                                  Summary Calculation Code=FIELD(FlowField Summary Calc. Code),
                                                                                                                                  Line No.=FIELD(FlowField Line No.)));
                                                   CaptionML=ENU=Summary Code Filter;
                                                   Editable=No }
    { 23  ;   ;Object Type         ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Summary Calculation Line"."Object Type" WHERE (Summary Calculation Type=FIELD(FlowField Summary Calc. Type),
                                                                                                                         Summary Calculation Code=FIELD(FlowField Summary Calc. Code),
                                                                                                                         Line No.=FIELD(FlowField Line No.)));
                                                   CaptionML=ENU=Object Type;
                                                   OptionCaptionML=ENU=TableData,Table,Form,Report,Dataport,Codeunit,XMLport,MenuSuite,Page,Query,System,,,,,,,,,,Ability;
                                                   OptionString=TableData,Table,Form,Report,Dataport,Codeunit,XMLport,MenuSuite,Page,Query,System,,,,,,,,,,Ability;
                                                   Editable=No }
    { 24  ;   ;Object ID           ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Summary Calculation Line"."Object ID" WHERE (Summary Calculation Type=FIELD(FlowField Summary Calc. Type),
                                                                                                                       Summary Calculation Code=FIELD(FlowField Summary Calc. Code),
                                                                                                                       Line No.=FIELD(FlowField Line No.)));
                                                   TableRelation="ES Object Information"."Object ID" WHERE (Object Type=FIELD(Object Type));
                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Object ID;
                                                   Editable=No }
    { 25  ;   ;Description         ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Summary Calculation".Description WHERE (Type=FIELD(FlowField Summary Calc. Type),
                                                                                                                  Code=FIELD(FlowField Summary Calc. Code)));
                                                   CaptionML=ENU=Description;
                                                   Editable=No }
    { 26  ;   ;Object Caption      ;Text249       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Object Information"."Object Caption" WHERE (Object Type=FIELD(Object Type),
                                                                                                                      Object ID=FIELD(Object ID)));
                                                   CaptionML=ENU=Object Caption;
                                                   Editable=No }
    { 27  ;   ;Access Type         ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Summary Calculation Line"."Access Type" WHERE (Summary Calculation Type=FIELD(FlowField Summary Calc. Type),
                                                                                                                         Summary Calculation Code=FIELD(FlowField Summary Calc. Code),
                                                                                                                         Line No.=FIELD(FlowField Line No.)));
                                                   CaptionML=ENU=Access Type;
                                                   OptionCaptionML=ENU=" ,Read,Insert,Modify,Delete,Insert or Modify,Modify or Delete,Insert and Modify,Insert and Delete,Modify and Delete,Insert and Modify and Delete";
                                                   OptionString=[ ,Read,Insert,Modify,Delete,Insert or Modify,Modify or Delete,Insert and Modify,Insert and Delete,Modify and Delete,Insert and Modify and Delete];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Type,Code,Company Name,Summary Calculation Type,Summary Calculation Code,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=%1 %2 cannot be viewed.';

    PROCEDURE CalculateAccessComplete@1240520001() : Boolean;
    VAR
      ESSummaryCalcLine@1240520001 : Record 14123543;
      ESSummaryPerm@1240520002 : Record 14123537;
    BEGIN
      ESSummaryCalcLine.GET(
        "FlowField Summary Calc. Type","FlowField Summary Calc. Code","FlowField Line No.");

      IF ESSummaryCalcLine."Ability Summ. Calculation Code" <> '' THEN
        EXIT;

      ESSummaryCalcLine.TESTFIELD("Object Access Filter");

      ESSummaryPerm.RESET;
      ESSummaryPerm.SETRANGE(Type,Type);
      ESSummaryPerm.SETRANGE(Code,Code);
      ESSummaryPerm.SETFILTER("Object Access",ESSummaryCalcLine."Object Access Filter");
      IF "Company Name" <> '' THEN
        ESSummaryPerm.SETRANGE("Company Name","Company Name")
      ELSE
        ESSummaryPerm.SETRANGE("Company Name",'');
      IF ESSummaryPerm.FIND('-') THEN
        EXIT(TRUE);

      IF "Company Name" <> '' THEN BEGIN
        ESSummaryPerm.SETRANGE("Company Name",'');
        IF ESSummaryPerm.FIND('-') THEN
          EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    PROCEDURE OpenSummaryPermissions@1240520002();
    VAR
      ESSummaryCalcLine@1240520002 : Record 14123543;
      ESSummaryPerm@1240520001 : Record 14123537;
    BEGIN
      ESSummaryCalcLine.GET(
        "FlowField Summary Calc. Type","FlowField Summary Calc. Code","FlowField Line No.");
      ESSummaryCalcLine.TESTFIELD("Object Access Filter");

      IF ESSummaryCalcLine."Object Type" = ESSummaryCalcLine."Object Type"::Ability THEN
        ERROR(Text001,FIELDCAPTION("Object Type"),"Object Type");

      ESSummaryPerm.RESET;
      ESSummaryPerm.SETRANGE(Type,Type);
      ESSummaryPerm.SETRANGE(Code,Code);
      IF "Company Name" <> '' THEN
        ESSummaryPerm.SETFILTER("Company Name",'%1|%2','',"Company Name")
      ELSE
        ESSummaryPerm.SETRANGE("Company Name","Company Name");
      ESSummaryPerm.SETFILTER("Object Access",ESSummaryCalcLine."Object Access Filter");
      PAGE.RUNMODAL(0,ESSummaryPerm);
    END;

    PROCEDURE OpenObjectWhereUsed@1240520004();
    VAR
      ESSummaryCalcLine@1240520002 : Record 14123543;
      ESObjectWhereUsed@1240520001 : Page 14123562;
    BEGIN
      ESSummaryCalcLine.GET(
        "FlowField Summary Calc. Type","FlowField Summary Calc. Code","FlowField Line No.");

      IF ESSummaryCalcLine."Object Type" = ESSummaryCalcLine."Object Type"::Ability THEN
        ERROR(Text001,FIELDCAPTION("Object Type"),"Object Type");

      CLEAR(ESObjectWhereUsed);
      ESObjectWhereUsed.InitObject(ESSummaryCalcLine."Object Type",ESSummaryCalcLine."Object ID");
      ESObjectWhereUsed.RUN;
    END;

    PROCEDURE OpenAssignedFrom@1240520003();
    VAR
      ESSummaryCalcLine@1240520002 : Record 14123543;
      ESPermissionsFromOverview@1240520001 : Page 14123593;
    BEGIN
      ESSummaryCalcLine.GET(
        "FlowField Summary Calc. Type","FlowField Summary Calc. Code","FlowField Line No.");

      IF ESSummaryCalcLine."Object Type" = ESSummaryCalcLine."Object Type"::Ability THEN
        ERROR(Text001,FIELDCAPTION("Object Type"),"Object Type");

      CLEAR(ESPermissionsFromOverview);
      ESPermissionsFromOverview.SetReference(
        Type,Code,ESSummaryCalcLine."Object Type",ESSummaryCalcLine."Object ID",TRUE,"Company Name");
      ESPermissionsFromOverview.RUNMODAL;
    END;

    BEGIN
    END.
  }
}

