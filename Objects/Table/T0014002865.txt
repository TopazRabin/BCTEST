OBJECT Table 14002865 Outb. Whse. Req. Action Entry
{
  OBJECT-PROPERTIES
  {
    Date=06/18/21;
    Time=[ 5:08:13 AM];
    Modified=Yes;
    Version List=RF1.50.03,3235;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Source Type");
               TESTFIELD("Source No.");
               IF "Created By" = '' THEN
                 "Created By" := USERID;
             END;

    CaptionML=ENU=Outb. Whse. Req. Action Entry;
    DrillDownPageID=Page14002869;
  }
  FIELDS
  {
    { 1   ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Inbound,Outbound;
                                                   OptionString=Inbound,Outbound }
    { 2   ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=ENU=Location Code }
    { 3   ;   ;Source Type         ;Integer       ;CaptionML=ENU=Source Type }
    { 4   ;   ;Source Subtype      ;Option        ;CaptionML=ENU=Source Subtype;
                                                   OptionCaptionML=ENU=0,1,2,3,4,5,6,7,8,9,10;
                                                   OptionString=0,1,2,3,4,5,6,7,8,9,10 }
    { 5   ;   ;Source No.          ;Code50        ;CaptionML=ENU=Source No. }
    { 6   ;   ;Created By          ;Code50        ;TableRelation=User;
                                                   OnLookup=BEGIN
                                                              LoginMgt.LookupUserID("Created By");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Created By;
                                                   Editable=No }
    { 11  ;   ;Action Code         ;Code20        ;TableRelation="Outbound Whse. Request Action";
                                                   OnValidate=BEGIN
                                                                "Action Changed Date" := TODAY;
                                                                "Action Changed Time" := TIME;
                                                                "Action Changed By" := USERID;
                                                              END;

                                                   CaptionML=ENU=Action Code }
    { 12  ;   ;Responsible User ID ;Code50        ;TableRelation=User;
                                                   OnValidate=BEGIN
                                                                LoginMgt.ValidateUserID("Responsible User ID");

                                                                "Responsible Changed Date" := TODAY;
                                                                "Responsible Changed Time" := TIME;
                                                                "Responsible Changed By" := USERID;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LoginMgt.LookupUserID("Responsible User ID");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Responsible User ID }
    { 13  ;   ;Action Changed Date ;Date          ;CaptionML=ENU=Action Changed Date;
                                                   Editable=No }
    { 14  ;   ;Action Changed Time ;Time          ;CaptionML=ENU=Action Changed Time;
                                                   Editable=No }
    { 15  ;   ;Action Changed By   ;Code50        ;TableRelation=User;
                                                   OnLookup=BEGIN
                                                              LoginMgt.LookupUserID("Action Changed By");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Action Changed By;
                                                   Editable=No }
    { 16  ;   ;Responsible Changed Date;Date      ;CaptionML=ENU=Responsible Changed Date;
                                                   Editable=No }
    { 17  ;   ;Responsible Changed Time;Time      ;CaptionML=ENU=Responsible Changed Time;
                                                   Editable=No }
    { 18  ;   ;Responsible Changed By;Code50      ;TableRelation=User;
                                                   OnLookup=BEGIN
                                                              LoginMgt.LookupUserID("Responsible Changed By");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Responsible Changed By;
                                                   Editable=No }
    { 19  ;   ;Comment             ;Text250       ;CaptionML=ENU=Comment }
  }
  KEYS
  {
    {    ;Type,Location Code,Source Type,Source Subtype,Source No.,Created By;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      LoginMgt@1240040000 : Codeunit 418;

    PROCEDURE InsertModifyFromWhseRequest@1240040000(WarehouseRequest@1240040000 : Record 5765;CreatedBy@1240040007 : Code[50];NewActionCode@1240040002 : Code[20];ClearActionCode@1240040005 : Boolean;NewUserID@1240040003 : Code[50];ClearUserID@1240040006 : Boolean);
    VAR
      OutbWhseReqActionEntry@1240040001 : Record 14002865;
      EntryExists@1240040004 : Boolean;
    BEGIN
      OutbWhseReqActionEntry.INIT;
      OutbWhseReqActionEntry.Type := WarehouseRequest.Type;
      OutbWhseReqActionEntry."Location Code" := WarehouseRequest."Location Code";
      OutbWhseReqActionEntry."Source Type" := WarehouseRequest."Source Type";
      OutbWhseReqActionEntry."Source Subtype" := WarehouseRequest."Source Subtype";
      OutbWhseReqActionEntry."Source No." := WarehouseRequest."Source No.";
      OutbWhseReqActionEntry."Created By" := CreatedBy;
      EntryExists := OutbWhseReqActionEntry.FIND('=');

      IF NewActionCode <> '' THEN
        OutbWhseReqActionEntry.VALIDATE("Action Code",NewActionCode);
      IF ClearActionCode THEN
        OutbWhseReqActionEntry.VALIDATE("Action Code",'');

      IF NewUserID <> '' THEN
        OutbWhseReqActionEntry.VALIDATE("Responsible User ID",NewUserID);
      IF ClearUserID THEN
        OutbWhseReqActionEntry.VALIDATE("Responsible User ID",'');

      IF EntryExists THEN
        OutbWhseReqActionEntry.MODIFY(TRUE)
      ELSE
        OutbWhseReqActionEntry.INSERT(TRUE);

      Rec := OutbWhseReqActionEntry;
    END;

    PROCEDURE InsertModifyFromWhseRequest2@1240020000(WarehouseRequest@1240020004 : Record 5765;CreatedBy@1240020003 : Code[50];ClearActionCode@1240020002 : Boolean;NewUserID@1240020001 : Code[50];ClearUserID@1240020000 : Boolean);
    VAR
      OutbWhseReqActionEntry@1240020011 : Record 14002865;
      Action@1240020010 : Record 14002864;
      SalesLine@1240020009 : Record 37;
      PurchaseLine@1240020008 : Record 39;
      TransferLine@1240020007 : Record 5741;
      EntryExists@1240020006 : Boolean;
      NewActionCode@1240020005 : Code[20];
    BEGIN
      OutbWhseReqActionEntry.INIT;
      OutbWhseReqActionEntry.Type := WarehouseRequest.Type;
      OutbWhseReqActionEntry."Location Code" := WarehouseRequest."Location Code";
      OutbWhseReqActionEntry."Source Type" := WarehouseRequest."Source Type";
      OutbWhseReqActionEntry."Source Subtype" := WarehouseRequest."Source Subtype";
      OutbWhseReqActionEntry."Source No." := WarehouseRequest."Source No.";
      OutbWhseReqActionEntry."Created By" := CreatedBy;
      EntryExists := OutbWhseReqActionEntry.FIND('=');

      CASE WarehouseRequest."Source Type" OF
        DATABASE::"Sales Line":
          BEGIN
            SalesLine.SETRANGE("Document Type",WarehouseRequest."Source Subtype");
            SalesLine.SETRANGE("Document No.",WarehouseRequest."Source No.");
            SalesLine.SETRANGE(Type,SalesLine.Type::Item);
            //-->TPZ3235
            SalesLine.SETFILTER("Outstanding Quantity",'>%1',0);
            //<--TPZ3235
            IF SalesLine.FINDFIRST THEN
              NewActionCode := SalesLine."No.";
          END;
        DATABASE::"Purchase Line":
          BEGIN
            PurchaseLine.SETRANGE("Document Type",WarehouseRequest."Source Subtype");
            PurchaseLine.SETRANGE("Document No.",WarehouseRequest."Source No.");
            PurchaseLine.SETRANGE(Type,PurchaseLine.Type::Item);
            IF PurchaseLine.FINDFIRST THEN
              NewActionCode := PurchaseLine."No.";
          END;
        DATABASE::"Transfer Line":
          BEGIN
            TransferLine.SETRANGE("Document No.",WarehouseRequest."Source No.");
            IF TransferLine.FINDFIRST THEN
              NewActionCode := TransferLine."Item No.";
          END;
      END;

      IF NewActionCode <> '' THEN
        OutbWhseReqActionEntry."Action Code" := NewActionCode;
      IF ClearActionCode THEN
        OutbWhseReqActionEntry.VALIDATE("Action Code",'');

      IF NewUserID <> '' THEN
        OutbWhseReqActionEntry.VALIDATE("Responsible User ID",NewUserID);
      IF ClearUserID THEN
        OutbWhseReqActionEntry.VALIDATE("Responsible User ID",'');

      IF EntryExists THEN
        OutbWhseReqActionEntry.MODIFY(TRUE)
      ELSE
        OutbWhseReqActionEntry.INSERT(TRUE);

      Rec := OutbWhseReqActionEntry;
    END;

    BEGIN
    {
      001 TPZ3235 PKS 06172021 Added filter on outstanding quantity
    }
    END.
  }
}

