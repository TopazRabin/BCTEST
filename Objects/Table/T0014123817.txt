OBJECT Table 14123817 ES FLADS Group Member
{
  OBJECT-PROPERTIES
  {
    Date=05/11/18;
    Time=12:00:00 PM;
    Version List=ES1.41.52;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TestEditAllowed;

               GetESFLADSSetup;

               IF ESFLADSSetup."Manually Enter Group Members" THEN BEGIN
                 IF NOT ESFLADSSetup."Install Allowed" THEN
                   ESFLADSSetup.TESTFIELD("Copy Group Members",FALSE);

                 TESTFIELD("Group User ID");
                 TESTFIELD("Member User ID");


                 ESFLADSGroupMember.RESET;
                 ESFLADSGroupMember.SETRANGE("Group User ID","Member User ID");
                 IF ESFLADSGroupMember.FIND('-') THEN
                   ESFLADSGroupMember.TESTFIELD("Group User ID",'');

                 ESFLADSGroupMember.RESET;
                 ESFLADSGroupMember.SETRANGE("Member User ID","Group User ID");
                 IF ESFLADSGroupMember.FIND('-') THEN
                   ESFLADSGroupMember.TESTFIELD("Member User ID",'');

                 ESFLADSGroupMember.RESET;
                 ESFLADSGroupMember.SETFILTER("Group User ID",'<>%1',"Group User ID");
                 ESFLADSGroupMember.SETRANGE("Member User ID","Member User ID");
                 IF ESFLADSGroupMember.FIND('-') THEN
                   ESFLADSGroupMember.TESTFIELD("Member User ID",'');
               END;

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnModify=BEGIN
               TestEditAllowed;

               GetESFLADSSetup;

               IF ESFLADSSetup."Manually Enter Group Members" THEN
                 IF NOT ESFLADSSetup."Install Allowed" THEN
                   ESFLADSSetup.TESTFIELD("Copy Group Members",FALSE);

               IF (xRec.Priority <> Priority) AND ("Member User ID" = '') THEN BEGIN
                 ESFLADSGroupMember.RESET;
                 ESFLADSGroupMember.SETRANGE("Group User ID","Group User ID");
                 ESFLADSGroupMember.SETFILTER("Member User ID",'<>%1','');
                 IF ESFLADSGroupMember.FIND('-') THEN
                   ESFLADSGroupMember.MODIFYALL(Priority,Priority);
               END;

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnDelete=BEGIN
               GetESFLADSSetup;

               IF ESFLADSSetup."Manually Enter Group Members" THEN
                 IF NOT ESFLADSSetup."Install Allowed" THEN
                   ESFLADSSetup.TESTFIELD("Copy Group Members",FALSE);

               TestEditAllowed;

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnRename=BEGIN
               ERROR(Text003);
             END;

    CaptionML=ENU=Field Level and Data Security Group Member;
  }
  FIELDS
  {
    { 1   ;   ;Group User ID       ;Code50        ;OnValidate=BEGIN
                                                                IF "Group User ID" <> '' THEN BEGIN
                                                                  TestEditAllowed;

                                                                  LoginMgt.ValidateUserID("Group User ID");
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LoginMgt.LookupUserID("Group User ID");
                                                            END;

                                                   CaptionML=ENU=Group User ID;
                                                   NotBlank=Yes }
    { 2   ;   ;Member User ID      ;Code50        ;OnValidate=BEGIN
                                                                IF "Member User ID" <> '' THEN BEGIN
                                                                  TestEditAllowed;

                                                                  LoginMgt.ValidateUserID("Member User ID");
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LoginMgt.LookupUserID("Member User ID");
                                                            END;

                                                   CaptionML=ENU=Member User ID }
    { 11  ;   ;Priority            ;Integer       ;OnValidate=BEGIN
                                                                GetESFLADSSetup;
                                                                ESFLADSSetup.TESTFIELD("Use FLADS Groups",FALSE);
                                                                ESFLADSSetup.TESTFIELD("Manually Enter Group Members",FALSE);

                                                                TESTFIELD("Member User ID",'');
                                                              END;

                                                   CaptionML=ENU=Priority }
    { 12  ;   ;Created By User     ;Code50        ;CaptionML=ENU=Created By User;
                                                   Editable=No }
    { 13  ;   ;Created Date Time   ;DateTime      ;CaptionML=ENU=Created Date Time;
                                                   Editable=No }
    { 16  ;   ;Do Not Use          ;Boolean       ;OnValidate=BEGIN
                                                                GetESFLADSSetup;
                                                                ESFLADSSetup.TESTFIELD("Use FLADS Groups",FALSE);
                                                                ESFLADSSetup.TESTFIELD("Manually Enter Group Members",FALSE);

                                                                IF "Do Not Use" THEN
                                                                  TESTFIELD("Member User ID");
                                                              END;

                                                   CaptionML=ENU=Do Not Use }
  }
  KEYS
  {
    {    ;Group User ID,Member User ID            ;Clustered=Yes }
    {    ;Member User ID,Priority                  }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Group User ID,Member User ID,Priority    }
  }
  CODE
  {
    VAR
      ESFLADSSetup@1240520002 : Record 14123801;
      ESFLADSGroupMember@1240520001 : Record 14123817;
      Text001@1001 : TextConst 'ENU=%1 group with %2 members inserted.';
      Text002@1240520003 : TextConst 'ENU=Group User ID "%1" must be 20 characters or less.';
      Text003@1240520004 : TextConst 'ENU=Rename not allowed.';
      ESFLADSUpdateNo@1240520006 : Record 14123813;
      LoginMgt@1240520005 : Codeunit 418;
      ESFLADSSetupRetrieved@1000000000 : Boolean;
      Text004@1240520007 : TextConst 'ENU=%1 %2 inserted %3 already exists.';
      Text012@1240520008 : TextConst 'ENU=%1 is not valid content for this filetype.';
      Text013@1240520023 : TextConst 'ENU=File %1 already exists, overwrite?';
      Text014@1240520022 : TextConst 'ENU=Nothing has been exported.';
      Text015@1240520021 : TextConst 'ENU=Exporting %1 to file\';
      Text016@1240520020 : TextConst 'ENU=Process #1################################################';
      Text017@1240520019 : TextConst 'ENU=Updating Version';
      Text018@1240520018 : TextConst 'ENU=Creating File';
      Text021@1240520017 : TextConst 'ENU=Closing File';
      Text022@1240520016 : TextConst 'ENU=%1 %2 %3 exported to %4 with %5 records.';
      Text023@1240520015 : TextConst 'ENU=Nothing imported.';
      Text024@1240520014 : TextConst 'ENU=File Name must be entered.';
      Text025@1240520013 : TextConst 'ENU=Importing %1 from file\';
      Text026@1240520012 : TextConst 'ENU=Process #1################################################\';
      Text027@1240520011 : TextConst 'ENU=Opening file';
      Text028@1240520010 : TextConst 'ENU=%1 records have been imported from %2.';
      Text029@1240520009 : TextConst 'ENU=%1 %2 exported to %3 with %4 records.';
      Text005@1002 : TextConst 'ENU=%1 is already member of %2';
      Text101@2002 : TextConst 'ENU=Downloading File.';
      Text102@2000 : TextConst 'ENU=Uploading file.';

    PROCEDURE CopyFromESWindowsGroupMember@1240520008(UpdateWindowsGroups@1240520007 : Boolean;ShowStatus@1240520011 : Boolean);
    VAR
      ESWindowsGroupMember@1240520001 : Record 14123538;
      ESFLADSGroupMember@1240520002 : Record 14123817;
      ESFLADSGroupMember2@1240520004 : Record 14123817;
      ESFLADSGroupMemberTmp@1240520009 : TEMPORARY Record 14123817;
      WindowsLoginMember@1240520003 : Record 2000000120;
      ESLoginMember@1240520010 : Record 14123527;
      WindowsLoginGroup@1240520013 : Record 2000000120;
      ESLoginGroup@1240520012 : Record 14123527;
      InsertedRecords@1240520005 : Integer;
      InsertedGroupRecords@1240520006 : Integer;
    BEGIN
      GetESFLADSSetup;
      ESFLADSSetup.TESTFIELD("Install Allowed");
      ESFLADSSetup.TESTFIELD("Use Stored Windows Groups");
      ESFLADSSetup.TESTFIELD("Manually Enter Group Members",FALSE);

      IF UpdateWindowsGroups THEN BEGIN
        ESWindowsGroupMember.RESET;
        ESWindowsGroupMember.DELETEALL;

        ESWindowsGroupMember.CopyFromLiveWindowsGroupMember(ShowStatus);
      END;

      ESFLADSGroupMember.RESET;
      IF ESFLADSGroupMember.FIND('-') THEN
        REPEAT
          ESFLADSGroupMemberTmp := ESFLADSGroupMember;
          ESFLADSGroupMemberTmp.INSERT;
        UNTIL ESFLADSGroupMember.NEXT = 0;
      ESFLADSGroupMember.DELETEALL;

      ESWindowsGroupMember.RESET;
      ESWindowsGroupMember.SETFILTER("Member GUID",'<>%1','');
      IF ESWindowsGroupMember.FIND('-') THEN
        REPEAT
          IF WindowsLoginGroup.GET(ESWindowsGroupMember."Group SID") AND
             WindowsLoginMember.GET(ESWindowsGroupMember."Member SID")
          THEN BEGIN
            ESLoginGroup.RESET;
            ESLoginGroup.SETRANGE(Type,ESLoginGroup.Type::Windows);
            ESLoginGroup.SETRANGE("Login SID",WindowsLoginGroup."User Security ID");
            ESLoginGroup.FIND('-');

            ESLoginMember.RESET;
            ESLoginMember.SETRANGE(Type,ESLoginMember.Type::Windows);
            ESLoginMember.SETRANGE("Login SID",WindowsLoginMember."User Security ID");
            ESLoginMember.FIND('-');

            ESFLADSGroupMember.INIT;
            ESFLADSGroupMember."Group User ID" := ESLoginGroup."User ID";
            ESFLADSGroupMember."Member User ID" := ESLoginMember."User ID";
            ESFLADSGroupMember."Created By User" := USERID;
            ESFLADSGroupMember."Created Date Time" := CURRENTDATETIME;
            IF STRLEN(ESWindowsGroupMember."Group ID") > 20 THEN
              ERROR(Text002,ESWindowsGroupMember."Group ID");

            IF NOT ESFLADSGroupMember2.GET(ESFLADSGroupMember."Group User ID",'') THEN BEGIN
              ESFLADSGroupMember2 := ESFLADSGroupMember;
              ESFLADSGroupMember2."Member User ID" := '';

              IF ESFLADSGroupMemberTmp.GET(
                   ESFLADSGroupMember2."Group User ID",ESFLADSGroupMember2."Member User ID")
              THEN BEGIN
                ESFLADSGroupMember2.Priority := ESFLADSGroupMemberTmp.Priority;
                ESFLADSGroupMember2."Do Not Use" := ESFLADSGroupMemberTmp."Do Not Use";
              END;

              ESFLADSGroupMember2.INSERT;

              InsertedGroupRecords := InsertedGroupRecords + 1;
            END;

            ESFLADSGroupMember.Priority := ESFLADSGroupMember2.Priority;

            IF ESFLADSGroupMemberTmp.GET(
                 ESFLADSGroupMember."Group User ID",ESFLADSGroupMember."Member User ID")
            THEN
              ESFLADSGroupMember."Do Not Use" := ESFLADSGroupMemberTmp."Do Not Use";

            ESFLADSGroupMember.INSERT;

            InsertedRecords := InsertedRecords + 1;
          END;
        UNTIL ESWindowsGroupMember.NEXT = 0;

      IF ShowStatus THEN
        MESSAGE(Text001,InsertedGroupRecords,InsertedRecords);
    END;

    PROCEDURE CopyDataToOtherCompanies@1240520001();
    BEGIN
      GetESFLADSSetup;
      ESFLADSSetup.TESTFIELD("Install Allowed");
      ESFLADSSetup.CopyDataToOtherCompanies(FALSE,FALSE);
    END;

    PROCEDURE AddMultipleUsers@1240520002(ShowStatus@1240520001 : Boolean);
    VAR
      ESFLADSGroupMember@1240520010 : Record 14123817;
      WindowsLoginTmp@1240520011 : TEMPORARY Record 2000000120;
      ESLiveWindowsLogins@1240520008 : Page 14123541;
      NewRecords@1240520005 : Integer;
      ExistingRecords@1240520006 : Integer;
    BEGIN
      TestEditAllowed;
      TESTFIELD("Group User ID");

      ESLiveWindowsLogins.LOOKUPMODE(TRUE);
      IF ESLiveWindowsLogins.RUNMODAL = ACTION::LookupOK THEN BEGIN
        ESLiveWindowsLogins.GetSelection(WindowsLoginTmp);
        IF WindowsLoginTmp.FIND('-') THEN BEGIN
          REPEAT
            IF NOT ESFLADSGroupMember.GET("Group User ID",WindowsLoginTmp."User Name") THEN BEGIN
              ESFLADSGroupMember.RESET;
              ESFLADSGroupMember.SETRANGE("Member User ID",WindowsLoginTmp."User Name");
              IF ESFLADSGroupMember.FIND('-') THEN
                ERROR(
                  Text005,
                  WindowsLoginTmp."User Name",ESFLADSGroupMember."Group User ID");

              ESFLADSGroupMember.INIT;
              ESFLADSGroupMember."Group User ID" := "Group User ID";
              ESFLADSGroupMember."Member User ID" := WindowsLoginTmp."User Name";
              ESFLADSGroupMember.INSERT(TRUE);

              NewRecords := NewRecords + 1;
            END ELSE
              ExistingRecords := ExistingRecords + 1;
          UNTIL WindowsLoginTmp.NEXT = 0;
        END;
      END ELSE
        EXIT;

      MESSAGE(Text004,NewRecords,TABLECAPTION,ExistingRecords);
    END;

    PROCEDURE ExportMultiFLADSGroupMembers@1240520003(VAR ESFLADSGroupMember@1240520004 : Record 14123817;ShowStatus@1240520021 : Boolean;ExportFileName@1240520001 : Text[250]);
    VAR
      ESEnterFileName@1240520002 : Page 14123560;
      FileMgt@1000 : Codeunit 419;
      TempServerFile@1001 : File;
      TempServerFileName@1002 : Text[1000];
      Window@1240520005 : Dialog;
      TotalRecords@1240520008 : Integer;
      TotalCodes@1240520003 : Integer;
    BEGIN
      IF ExportFileName = '' THEN BEGIN
        CLEAR(ESEnterFileName);
        ESEnterFileName.LOOKUPMODE(TRUE);
        ESEnterFileName.SelectNewFile(TRUE);
        IF ESEnterFileName.RUNMODAL <> ACTION::LookupOK THEN
          ERROR(Text014);
        ExportFileName := ESEnterFileName.GetFileName;
      END;

      TempServerFile.WRITEMODE(TRUE);
      TempServerFile.TEXTMODE(TRUE);
      TempServerFile.CREATETEMPFILE;
      TempServerFileName := TempServerFile.NAME;
      TempServerFile.CLOSE;

      IF GUIALLOWED THEN
        Window.OPEN(Text016);

      IF ESFLADSGroupMember.FIND('-') THEN
        REPEAT
          IF GUIALLOWED THEN
            Window.UPDATE(1,ESFLADSGroupMember."Group User ID");

          TotalCodes := TotalCodes + 1;

          TotalRecords :=
            TotalRecords +
            ESFLADSGroupMember.ExportFLADSGroupMember(FALSE,TRUE,TRUE,TempServerFileName,FALSE);
        UNTIL ESFLADSGroupMember.NEXT = 0;

      IF GUIALLOWED THEN
        Window.UPDATE(1,Text101);
      FileMgt.DownloadToFile(TempServerFileName,ExportFileName);

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(
          Text029,
          TotalCodes,TABLECAPTION,ExportFileName,TotalRecords);
    END;

    PROCEDURE ExportFLADSGroupMember@1240520014(ShowStatus@1240520021 : Boolean;ConfirmOverwrite@1240520020 : Boolean;AppendToFile@1240520004 : Boolean;ExportFileName@1240520001 : Text[250];ShowProgress@1240520005 : Boolean) : Integer;
    VAR
      ESEnterFileName@1240520002 : Page 14123560;
      ESFileMgt@1240520003 : Codeunit 14123502;
      FileMgt@1000 : Codeunit 419;
      Window@1240520009 : Dialog;
      OutputFile@1240520019 : File;
      Value@1240520022 : ARRAY [100] OF Text[1000];
      LocalFileName@1001 : Text[250];
      TotalRecords@1240520008 : Integer;
    BEGIN
      IF NOT AppendToFile THEN BEGIN
        IF ExportFileName = '' THEN BEGIN
          CLEAR(ESEnterFileName);
          ESEnterFileName.LOOKUPMODE(TRUE);
          ESEnterFileName.SelectNewFile(TRUE);
          IF ESEnterFileName.RUNMODAL <> ACTION::LookupOK THEN
            ERROR(Text014);
          ExportFileName := ESEnterFileName.GetFileName;
        END;

        IF ConfirmOverwrite THEN
          IF EXISTS(ExportFileName) THEN
            IF NOT CONFIRM(Text013,FALSE,ExportFileName) THEN
              ERROR(Text014);

        LocalFileName := ExportFileName;
        OutputFile.WRITEMODE(TRUE);
        OutputFile.TEXTMODE(TRUE);
        OutputFile.CREATETEMPFILE;
        ExportFileName := OutputFile.NAME;
        OutputFile.CLOSE;
      END;

      IF GUIALLOWED AND ShowProgress THEN
        Window.OPEN(STRSUBSTNO(ESFLADSSetup.ExtendProgressText(Text015),TABLECAPTION) + Text016);

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,Text017);

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,Text018);
      OutputFile.WRITEMODE(TRUE);
      OutputFile.TEXTMODE(TRUE);
      IF EXISTS(ExportFileName) AND AppendToFile THEN BEGIN
        OutputFile.OPEN(ExportFileName);
        OutputFile.SEEK(OutputFile.LEN);
      END ELSE
        OutputFile.CREATE(ExportFileName);

      CLEAR(ESFileMgt);
      ESFileMgt.SetDelimiters('~"','"~',',');

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,'HEADER');
      Value[1] := ESFileMgt.FormatInteger(0) +'V1';
      Value[2] := 'HEADER';
      Value[3] := "Group User ID" + ' ' + "Member User ID";
      Value[4] := '';
      Value[5] := USERID;
      Value[6] := ESFileMgt.FormatDate(TODAY);
      Value[7] := ESFileMgt.FormatTime(TIME);
      OutputFile.WRITE(ESFileMgt.FormatValues(Value,7));

      Value[1] := ESFileMgt.FormatInteger(DATABASE::"ES FLADS Group Member") + 'V1';
      Value[2] := "Group User ID";
      Value[3] := "Member User ID";
      Value[4] := ESFileMgt.FormatInteger(Priority);
      Value[5] := ESFileMgt.FormatBoolean("Do Not Use");

      OutputFile.WRITE(ESFileMgt.FormatValues(Value,5));

      TotalRecords := TotalRecords + 1;

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,'TOTALS');
      Value[1] := ESFileMgt.FormatInteger(0) + 'V1';
      Value[2] := 'TOTALS';
      Value[3] := "Group User ID" + ' ' + "Member User ID";
      Value[4] := '';
      Value[5] := USERID;
      Value[6] := ESFileMgt.FormatDate(TODAY);
      Value[7] := ESFileMgt.FormatTime(TIME);
      Value[8] := ESFileMgt.FormatInteger(TotalRecords);
      OutputFile.WRITE(ESFileMgt.FormatValues(Value,8));

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,Text021);
      OutputFile.CLOSE;

      IF NOT AppendToFile THEN BEGIN
        IF GUIALLOWED AND ShowProgress THEN
          Window.UPDATE(1,Text101);
        FileMgt.DownloadToFile(ExportFileName,LocalFileName);
      END;

      IF GUIALLOWED AND ShowProgress THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(
          Text022,
          TABLECAPTION,"Group User ID","Member User ID",LocalFileName,TotalRecords);

      EXIT(TotalRecords);
    END;

    PROCEDURE ImportFLADSGroupMember@1240520013(ShowStatus@1240520011 : Boolean;ImportFileName@1240520018 : Text[250];OverWriteExisting@1240520001 : Boolean);
    VAR
      ESFLADSGroupMember@1240520002 : Record 14123817;
      ESFLADSGroupMember2@1240520006 : Record 14123817;
      ESEnterFileName@1240520019 : Page 14123560;
      ESFileMgt@1240520005 : Codeunit 14123502;
      FileMgt@1000 : Codeunit 419;
      InputFile@1240520009 : File;
      Window@1240520012 : Dialog;
      TempText@1240520010 : Text[1000];
      Value@1240520008 : ARRAY [100] OF Text[1000];
      ServerFileName@1001 : Text[1000];
      NextUpdatePos@1240520022 : Integer;
      TotalRecords@1240520023 : Integer;
    BEGIN
      IF ImportFileName = '' THEN BEGIN
        CLEAR(ESEnterFileName);
        ESEnterFileName.LOOKUPMODE(TRUE);
        IF ESEnterFileName.RUNMODAL <> ACTION::LookupOK THEN
          ERROR(Text023);
        ImportFileName := ESEnterFileName.GetFileName;
      END;

      IF ImportFileName = '' THEN
        ERROR(Text024);

      IF GUIALLOWED THEN
        Window.OPEN(
          STRSUBSTNO(ESFLADSSetup.ExtendProgressText(Text025),TABLECAPTION) +
          Text026 + '        @2@@@@@@@@@@@@@@@@@@@@@@@@@@@@');

      CLEAR(ESFileMgt);
      ESFileMgt.SetDelimiters('~"','"~',',');

      IF GUIALLOWED THEN
        Window.UPDATE(1,Text027);
      InputFile.WRITEMODE(FALSE);
      InputFile.TEXTMODE(TRUE);
      IF NOT InputFile.OPEN(ImportFileName) THEN BEGIN
        IF GUIALLOWED THEN
          Window.UPDATE(1,Text102);
        ServerFileName := FileMgt.UploadFileSilent(ImportFileName);
        IF GUIALLOWED THEN
          Window.UPDATE(1,Text027);

        IF NOT InputFile.OPEN(ServerFileName) THEN
          InputFile.OPEN(ImportFileName);
      END;
      WHILE InputFile.POS < InputFile.LEN DO BEGIN
        IF NextUpdatePos < InputFile.POS THEN BEGIN
          IF GUIALLOWED THEN
            Window.UPDATE(2,ROUND(10000 * (InputFile.POS / InputFile.LEN),1));
          NextUpdatePos := ROUND(InputFile.POS + InputFile.LEN / 500,1);
        END;

        InputFile.READ(TempText);
        ESFileMgt.SplitInValues(Value,TempText);

        CASE Value[1] OF
          '','0V1':
            ;
          '14123817V1':
            BEGIN
              IF GUIALLOWED THEN
                Window.UPDATE(1,STRSUBSTNO('%1 %2',Value[2],Value[3]));

              ESFLADSGroupMember.INIT;
              ESFLADSGroupMember."Group User ID" := Value[2];
              ESFLADSGroupMember."Member User ID" := Value[3];
              ESFLADSGroupMember.Priority := ESFileMgt.EvaluateInteger(Value[4]);
              ESFLADSGroupMember."Do Not Use" := ESFileMgt.EvaluateBoolean(Value[5]);

              IF OverWriteExisting AND
                 ESFLADSGroupMember2.GET(
                   ESFLADSGroupMember."Group User ID",ESFLADSGroupMember."Member User ID")
              THEN
                ESFLADSGroupMember2.DELETE(TRUE);

              ESFLADSGroupMember.INSERT(TRUE);

              TotalRecords := TotalRecords + 1;
            END;
          ELSE
            ERROR(Text012,Value[1]);
        END;
      END;

      InputFile.CLOSE;
      IF ServerFileName <> '' THEN
        ERASE(ServerFileName);

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(Text028,TotalRecords,ImportFileName);
    END;

    PROCEDURE TestEditAllowed@1000000002();
    VAR
      ESLoginSetup@1240520001 : Record 14123540;
    BEGIN
      GetESFLADSSetup;

      IF ESFLADSSetup."Use Login Setup" THEN BEGIN
        ESLoginSetup.GET(USERID);
        ESLoginSetup.TESTFIELD("Edit Group Members (FLADS)");
      END;

      ESFLADSSetup.TESTFIELD("Manually Enter Group Members");
      IF NOT ESFLADSSetup."Install Allowed" THEN
        ESFLADSSetup.TESTFIELD("Copy Group Members",FALSE);
    END;

    PROCEDURE GetESFLADSSetup@1000000003();
    BEGIN
      IF ESFLADSSetupRetrieved THEN
        EXIT;

      ESFLADSSetup.GET;
      ESFLADSSetupRetrieved := TRUE;
    END;

    BEGIN
    END.
  }
}

