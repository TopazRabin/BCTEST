OBJECT Table 23044501 WMDM Component Usage
{
  OBJECT-PROPERTIES
  {
    Date=10/02/19;
    Time=[ 2:04:26 PM];
    Version List=WHI2.4.7684.0;
  }
  PROPERTIES
  {
    DataCaptionFields=Prod. Order No.,Prod. Serial No.,Prod. Lot No.;
    CaptionML=[ENU=Component Usage;
               ESM=Uso del componente;
               FRC=Utilisation du composant;
               ENC=Component Usage];
  }
  FIELDS
  {
    { 1   ;   ;Line No.            ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=Line No.;
                                                              ESM=L¡nea Nø;
                                                              FRC=Ligne Nø;
                                                              ENC=Line No.];
                                                   Editable=No }
    { 2   ;   ;User ID             ;Code50        ;CaptionML=[ENU=User ID;
                                                              ESM=Id. usuario;
                                                              FRC=Code utilisateur;
                                                              ENC=User ID];
                                                   NotBlank=Yes }
    { 10  ;   ;Prod. Order No.     ;Code20        ;TableRelation="Production Order".No.;
                                                   CaptionML=[ENU=Prod. Order No.;
                                                              ESM=N§ orden producci¢n;
                                                              FRC=Nø bon de prod.;
                                                              ENC=Prod. Order No.];
                                                   NotBlank=Yes }
    { 11  ;   ;Prod. Order Line No.;Integer       ;CaptionML=[ENU=Prod. Order Line No.;
                                                              ESM=N§ l¡n. orden producc.;
                                                              FRC=Nø ligne bon prod.;
                                                              ENC=Prod. Order Line No.] }
    { 12  ;   ;Prod. Source No.    ;Code20        ;TableRelation=Item.No.;
                                                   CaptionML=[ENU=Prod. Source No.;
                                                              ESM=N§ Fuente producci¢n;
                                                              FRC=N§ Source Prod.;
                                                              ENC=Prod. Source No.] }
    { 13  ;   ;Prod. Source Variant Code;Code10   ;CaptionML=[ENU=Prod. Source Variant Code;
                                                              ESM=Fuente producci¢n c¢digo variante;
                                                              FRC=Source Prod. code variante;
                                                              ENC=Prod. Source Variant Code] }
    { 14  ;   ;Prod. Serial No.    ;Code100       ;CaptionML=[ENU=Prod. Serial No.;
                                                              ESM=Nos. serie producci¢n;
                                                              FRC=Nø de S‚ries prod.;
                                                              ENC=Prod. Serial No.] }
    { 15  ;   ;Prod. Lot No.       ;Code100       ;CaptionML=[ENU=Prod. Lot No.;
                                                              ESM=N§ lote producci¢n;
                                                              FRC=N§ lot prod.;
                                                              ENC=Prod. Lot No.] }
    { 16  ;   ;Prod. Order Comp. Line No.;Integer ;CaptionML=[ENU=Prod. Order Comp. Line No.;
                                                              ESM=N? l?n. comp. orden prod.;
                                                              FRC=N? ligne compos. bon de prod.;
                                                              ENC=Prod. Order Comp. Line No.] }
    { 20  ;   ;Cons. Item No.      ;Code20        ;CaptionML=[ENU=Cons. Item No.;
                                                              ESM=N§ producto consumo;
                                                              FRC=N§ Article consommation;
                                                              ENC=Cons. Item No.] }
    { 21  ;   ;Cons. Variant Code  ;Code10        ;CaptionML=[ENU=Cons. Variant Code;
                                                              ESM=C¢digo variante consumo;
                                                              FRC=Code Variante Consommation;
                                                              ENC=Cons. Variant Code] }
    { 22  ;   ;Cons. Serial No.    ;Code100       ;CaptionML=[ENU=Cons. Serial No.;
                                                              ESM=Nos. serie Consumo;
                                                              FRC=Nø de S‚ries Consommation;
                                                              ENC=Cons. Serial No.] }
    { 23  ;   ;Cons. Lot No.       ;Code100       ;CaptionML=[ENU=Cons. Lot No.;
                                                              ESM=N§ Lote consumo;
                                                              FRC=N§ Lot consommation;
                                                              ENC=Cons. Lot No.] }
    { 24  ;   ;Cons. Barcode Data  ;Text250       ;CaptionML=[ENU=Cons. Barcode Data;
                                                              ESM=Consumo de c¢digo de barras de datos;
                                                              FRC=Donn‚es de Consommation de code … barre;
                                                              ENC=Cons. Barcode Data] }
    { 25  ;   ;Cons. Quantity      ;Decimal       ;CaptionML=[ENU=Cons. Quantity;
                                                              ESM=Consumo Cantidad;
                                                              FRC=Quantit‚ de consommation;
                                                              ENC=Cons. Quantity] }
    { 31  ;   ;Cons. Unit of Measure Code;Code20  ;TableRelation="Item Unit of Measure"."Item No." WHERE (Item No.=FIELD(Cons. Item No.),
                                                                                                          Code=FIELD(Cons. Unit of Measure Code));
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Cons. Unit of Measure Code;
                                                              ESM=C¢digo de Unidad Medida de Consumo;
                                                              FRC=Code Unit‚ de Mesure de Consommation;
                                                              ENC=Cons. Unit of Measure Code] }
  }
  KEYS
  {
    {    ;Line No.                                ;Clustered=Yes }
    {    ;Prod. Source Variant Code,Cons. Item No. }
    {    ;Prod. Serial No.,Cons. Item No.          }
    {    ;Prod. Order No.,Prod. Serial No.,Cons. Item No. }
    {    ;Prod. Order No.,Prod. Order Line No.,Prod. Serial No.,Prod. Lot No.,Cons. Item No.,Cons. Variant Code;
                                                   SumIndexFields=Cons. Quantity }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE addUsage@1000000000(precItemJournalLine@1000000000 : Record 83);
    VAR
      lbHandled@1000000001 : Boolean;
    BEGIN
      IF( precItemJournalLine."Entry Type" = precItemJournalLine."Entry Type"::Consumption) THEN BEGIN
        lbHandled := FALSE;

        // check for negative consumption
        IF( precItemJournalLine.Quantity < 0 ) THEN BEGIN
          lbHandled := processNegativeConsumption(precItemJournalLine);
        END;

        IF( NOT lbHandled ) THEN BEGIN
          INIT;
          "User ID" := COPYSTR( USERID, 1, MAXSTRLEN( "User ID") );
          "Prod. Order No." := precItemJournalLine."Order No.";
          "Prod. Order Line No." := precItemJournalLine."Order Line No.";
          "Prod. Source No." := precItemJournalLine."Source No.";
          "Prod. Source Variant Code" := '';
          "Prod. Serial No." := precItemJournalLine."WHI Prod. Serial No.";
          "Prod. Lot No." := precItemJournalLine."WHI Prod. Lot No.";
          "Cons. Item No." := precItemJournalLine."Item No.";
          "Cons. Variant Code" := precItemJournalLine."Variant Code";
          "Cons. Serial No." := precItemJournalLine."WHI Cons. Serial No.";
          "Cons. Lot No." := precItemJournalLine."WHI Cons. Lot No.";
          "Cons. Barcode Data" := precItemJournalLine."WHI Barcode Data";
          "Cons. Quantity" := precItemJournalLine.Quantity;
          "Cons. Unit of Measure Code" := precItemJournalLine."Unit of Measure Code";
          INSERT;
        END;
      END;
    END;

    LOCAL PROCEDURE processNegativeConsumption@1000000002(precItemJournalLine@1000000002 : Record 83) : Boolean;
    VAR
      lbHandled@1000000000 : Boolean;
      lrecComponentUsage@1000000001 : Record 23044501;
    BEGIN
      lbHandled := FALSE;

      IF( precItemJournalLine.Quantity < 0 ) THEN BEGIN
        // check to see if we can delete the line from the usage.
        // Requires an exact match, if not the negative usage will be recorded
        lrecComponentUsage.SETRANGE("Prod. Order No.",precItemJournalLine."Order No.");
        lrecComponentUsage.SETRANGE("Prod. Source No.",precItemJournalLine."Source No.");
        lrecComponentUsage.SETRANGE("Prod. Serial No.",precItemJournalLine."WHI Prod. Serial No.");
        lrecComponentUsage.SETRANGE("Prod. Lot No.",precItemJournalLine."WHI Prod. Lot No.");
        lrecComponentUsage.SETRANGE("Cons. Item No.",precItemJournalLine."Item No.");
        lrecComponentUsage.SETRANGE("Cons. Variant Code",precItemJournalLine."Variant Code");
        lrecComponentUsage.SETRANGE("Cons. Serial No.", precItemJournalLine."WHI Cons. Serial No.");
        lrecComponentUsage.SETRANGE("Cons. Lot No.",precItemJournalLine."WHI Cons. Lot No.");
        lrecComponentUsage.SETRANGE("Cons. Quantity",ABS(precItemJournalLine.Quantity));

        IF( lrecComponentUsage.FINDFIRST ) THEN BEGIN
          lbHandled := lrecComponentUsage.DELETE;
        END;
      END;


      EXIT(lbHandled);
    END;

    BEGIN
    {
      ************************
      Copyright Notice
      This objects content is copyright of Insight Works 2011.  All rights reserved.
      Any redistribution or reproduction of part or all of the contents in any form is prohibited.
      ************************
    }
    END.
  }
}

