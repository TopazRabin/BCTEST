OBJECT Table 14000982 Export Source Document
{
  OBJECT-PROPERTIES
  {
    Date=06/28/17;
    Time=12:00:00 PM;
    Version List=SE0.54.18;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ExportSourceLine@1240030000 : Record 14000983;
               ExportDoc@1240030001 : Record 14000981;
               Package@1240030002 : Record 14000701;
               PostedPackage@1240030003 : Record 14000704;
               Package2@1240030005 : Record 14000701;
               PostedPackage2@1240030004 : Record 14000704;
             BEGIN
               ExportDoc.GET("Export Document No.");
               ExportDoc.TESTFIELD(Submitted,FALSE);

               LOCKTABLE;

               ShipperTypeMgt.DeleteExportSourceDoc(Rec);

               IF "Source Type" = DATABASE::Package THEN BEGIN
                 IF Package.GET("Source ID") THEN BEGIN
                   Package."Export Document No." := '';
                   Package.MODIFY;
                 END ELSE
                   IF PostedPackage.GET("Source ID") THEN BEGIN
                     PostedPackage."Export Document No." := '';
                     PostedPackage.MODIFY;
                 END;
               END ELSE BEGIN
                 Package.RESET;
                 Package.SETCURRENTKEY("Source Type","Source Subtype","Source ID");
                 Package.SETRANGE("Export Document No.","Export Document No.");
                 Package.SETRANGE("Source Type","Source Type");
                 Package.SETRANGE("Source Subtype","Source Subtype");
                 Package.SETRANGE("Source ID","Source ID");
                 IF Package.FIND('-') THEN REPEAT
                   Package2.GET(Package."No.");
                   Package2."Export Document No." := '';
                   Package2.MODIFY;
                 UNTIL Package.NEXT = 0;

                 PostedPackage.RESET;
                 PostedPackage.SETCURRENTKEY("Export Document No.");
                 PostedPackage.SETRANGE("Export Document No.","Export Document No.");
                 PostedPackage.SETRANGE("Source Type","Source Type");
                 PostedPackage.SETRANGE("Source Subtype","Source Subtype");
                 PostedPackage.SETRANGE("Source ID","Source ID");
                 IF PostedPackage.FIND('-') THEN REPEAT
                   PostedPackage2.GET(PostedPackage."No.");
                   PostedPackage2."Export Document No." := '';
                   PostedPackage2.MODIFY;
                 UNTIL PostedPackage.NEXT = 0;
               END;

               ExportSourceLine.RESET;
               ExportSourceLine.SETRANGE("Export Document No.","Export Document No.");
               ExportSourceLine.SETRANGE("Source Type","Source Type");
               ExportSourceLine.SETRANGE("Source SubType","Source Subtype");
               ExportSourceLine.SETRANGE("Source ID","Source ID");

               IF ExportSourceLine.FIND('-') THEN
                 ExportSourceLine.DELETEALL;

               IF ExportDoc."No." <> '' THEN
                 ExportDoc.UpdateSummaryLines;
             END;

    CaptionML=ENU=Export Source Document;
    LookupPageID=Page14000984;
    DrillDownPageID=Page14000984;
  }
  FIELDS
  {
    { 1   ;   ;Export Document No. ;Code20        ;CaptionML=ENU=Export Document No. }
    { 2   ;   ;Source Type         ;Integer       ;CaptionML=ENU=Source Type;
                                                   Editable=No }
    { 3   ;   ;Source Subtype      ;Option        ;CaptionML=ENU=Source Subtype;
                                                   OptionCaptionML=ENU=0,1,2,3,4,5,6,7,8,9,10;
                                                   OptionString=0,1,2,3,4,5,6,7,8,9,10;
                                                   Editable=No }
    { 4   ;   ;Source ID           ;Code20        ;CaptionML=ENU=Source ID;
                                                   Editable=No }
    { 12  ;   ;Location Code       ;Code20        ;CaptionML=ENU=Location Code }
  }
  KEYS
  {
    {    ;Export Document No.,Source Type,Source Subtype,Source ID;
                                                   Clustered=Yes }
    {    ;Source Type                              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ShipperTypeMgt@1240030000 : Codeunit 14000711;

    PROCEDURE ClosedPackagesExist@1240030000() : Boolean;
    VAR
      Package@1240030000 : Record 14000701;
    BEGIN
      Package.RESET;
      Package.SETRANGE(Closed,TRUE);
      Package.SETRANGE("Export Document No.","Export Document No.");
      Package.SETRANGE("Source Type","Source Type");
      Package.SETRANGE("Source Subtype","Source Subtype");
      Package.SETRANGE("Source ID","Source ID");
      Package.SETRANGE(Miscellaneous,FALSE);
      EXIT(Package.FIND('-'));
    END;

    PROCEDURE PostedPackagesExist@1240030003() : Boolean;
    VAR
      PostedPackage@1240030000 : Record 14000704;
    BEGIN
      PostedPackage.SETRANGE("Export Document No.","Export Document No.");
      PostedPackage.SETRANGE("Source Type","Source Type");
      PostedPackage.SETRANGE("Source Subtype","Source Subtype");
      PostedPackage.SETRANGE("Source ID","Source ID");
      PostedPackage.SETRANGE(Miscellaneous,FALSE);
      EXIT(PostedPackage.FIND('-'));
    END;

    PROCEDURE ClosedMiscPackagesExist@1240030001() : Boolean;
    VAR
      Package@1240030000 : Record 14000701;
    BEGIN
      Package.SETRANGE(Closed,TRUE);
      Package.SETRANGE(Miscellaneous,TRUE);
      Package.SETRANGE("No.","Source ID");
      EXIT(Package.FIND('-'));
    END;

    PROCEDURE PostedMiscPackagesExist@1240030002() : Boolean;
    VAR
      PostedPackage@1240030000 : Record 14000704;
    BEGIN
      PostedPackage.SETRANGE(Miscellaneous,TRUE);
      PostedPackage.SETRANGE("No.","Source ID");
      EXIT(PostedPackage.FIND('-'));
    END;

    BEGIN
    END.
  }
}

