OBJECT Table 14123804 ES Data Security Code
{
  OBJECT-PROPERTIES
  {
    Date=04/30/18;
    Time=12:00:00 PM;
    Version List=ES1.41.51;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               TestEditAllowed(FALSE);

               ESSourceTableSetup.GET("Source Table ID");

               IF NOT NoExtendedInsertDelete THEN BEGIN
                 ESDataSecurityCode.RESET;
                 ESDataSecurityCode.SETRANGE("Source Table ID","Source Table ID");
                 ESDataSecurityCode.SETFILTER(Code,'<>%1',Code);
                 ESDataSecurityCode.SETRANGE(Default,TRUE);
                 IF ESDataSecurityCode.FIND('-') THEN
                   IF CONFIRM(Text001,FALSE) THEN
                     CopyObjectsFromDefault(ESDataSecurityCode.Code);
               END;

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnModify=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               TestEditAllowed(FALSE);

               ESSourceTableSetup.GET("Source Table ID");

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnDelete=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               TestEditAllowed(FALSE);

               ESSourceTableSetup.GET("Source Table ID");

               IF NOT NoExtendedInsertDelete THEN BEGIN
                 ESUserSecuritySetup.RESET;
                 ESUserSecuritySetup.SETRANGE("Source Table ID","Source Table ID");
                 ESUserSecuritySetup.SETRANGE("Data Security Code",Code);
                 IF ESUserSecuritySetup.FIND('-') THEN
                   ESUserSecuritySetup.TESTFIELD("Data Security Code",'');
               END;

               DeleteRelated;

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnRename=BEGIN
               ERROR(Text020);
             END;

    CaptionML=ENU=Data Security Code;
    LookupPageID=Page14123804;
    DrillDownPageID=Page14123804;
  }
  FIELDS
  {
    { 1   ;   ;Source Table ID     ;Integer       ;TableRelation="ES Source Table Setup";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Source Table Caption");
                                                              END;

                                                   CaptionML=ENU=Source Table ID;
                                                   NotBlank=Yes }
    { 2   ;   ;Code                ;Code20        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 11  ;   ;Description         ;Text250       ;CaptionML=ENU=Description }
    { 12  ;   ;Default             ;Boolean       ;OnValidate=BEGIN
                                                                IF Default THEN BEGIN
                                                                  ESDataSecurityCode.RESET;
                                                                  ESDataSecurityCode.SETRANGE("Source Table ID","Source Table ID");
                                                                  ESDataSecurityCode.SETFILTER(Code,'<>%1',Code);
                                                                  ESDataSecurityCode.SETRANGE(Default,TRUE);
                                                                  IF ESDataSecurityCode.FIND('-') THEN
                                                                    ESDataSecurityCode.TESTFIELD(Default,FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Default }
    { 21  ;   ;Objects             ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES Data Security Object" WHERE (Source Table ID=FIELD(Source Table ID),
                                                                                                      Data Security Code=FIELD(Code)));
                                                   CaptionML=ENU=Objects;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 22  ;   ;User Security Setups;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES User Security Setup" WHERE (Source Table ID=FIELD(Source Table ID),
                                                                                                     Data Security Code=FIELD(Code)));
                                                   CaptionML=ENU=User Security Setups;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 23  ;   ;From Setup Company  ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES FLADS Copy Setup"."From Setup Company" WHERE (Table ID=CONST(14123804),
                                                                                                                        Source Table ID=FIELD(Source Table ID),
                                                                                                                        Code=FIELD(Code)));
                                                   CaptionML=ENU=From Setup Company;
                                                   Editable=No }
    { 24  ;   ;Source Table Caption;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Source Table ID)));
                                                   CaptionML=ENU=Source Table Caption;
                                                   Editable=No }
    { 25  ;   ;Copy Setup Exists   ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("ES FLADS Copy Setup" WHERE (Table ID=CONST(14123804),
                                                                                                  Source Table ID=FIELD(Source Table ID),
                                                                                                  Code=FIELD(Code)));
                                                   CaptionML=ENU=Copy Setup Exists;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Source Table ID,Code                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Source Table ID,Code,Description,Default,Objects }
  }
  CODE
  {
    VAR
      ESSourceTableSetup@1240520009 : Record 14123802;
      ESUserSecuritySetup@1240520010 : Record 14123805;
      ESFLADSUpdateNo@1240520012 : Record 14123813;
      ESDataSecurityCode@1240520008 : Record 14123804;
      Text001@1001 : TextConst 'ENU=Copy permissions from Default?';
      ESFLADSSetup@1240520002 : Record 14123801;
      NoExtendedInsertDelete@1240520003 : Boolean;
      Text003@1240520021 : TextConst 'ENU=File %1 already exists, overwrite?';
      Text004@1240520020 : TextConst 'ENU=Nothing has been exported.';
      Text005@1240520019 : TextConst 'ENU=Exporting %1 to file\';
      Text006@1240520018 : TextConst 'ENU=Process #1################################################';
      Text007@1240520017 : TextConst 'ENU=Updating Version';
      Text008@1240520016 : TextConst 'ENU=Creating File';
      Text011@1240520015 : TextConst 'ENU=Closing File';
      Text012@1240520014 : TextConst 'ENU=%1 %2 %3 exported to %4 with %5 records.';
      Text013@1240520013 : TextConst 'ENU=Nothing imported.';
      Text014@1240520011 : TextConst 'ENU=File Name must be entered.';
      Text015@1240520007 : TextConst 'ENU=Importing %1 from file\';
      Text016@1240520006 : TextConst 'ENU=Process #1################################################\';
      Text017@1240520005 : TextConst 'ENU=Opening file';
      Text018@1240520004 : TextConst 'ENU=%1 records have been imported from %2.';
      Text019@1240520022 : TextConst 'ENU=%1 %2 exported to %3 with %4 records.';
      Text002@1002 : TextConst 'ENU=%1 is not valid content for this filetype.';
      Text020@1240520001 : TextConst 'ENU=Rename not allowed.';
      Text101@1003 : TextConst 'ENU=Downloading File.';
      Text102@1000 : TextConst 'ENU=Uploading file.';

    PROCEDURE CopyObjectsFromDefault@1240520008(FromCode@1240520012 : Code[20]);
    VAR
      ESDataSecurityObject@1240520008 : Record 14123808;
      ESDataSecurityObject2@1240520010 : Record 14123808;
      ESDataSecurityField@1240520009 : Record 14123809;
      ESDataSecurityField2@1240520011 : Record 14123809;
    BEGIN
      IF ("Source Table ID" = 0) OR (Code = '') THEN
        EXIT;

      ESDataSecurityObject.RESET;
      ESDataSecurityObject.SETRANGE("Source Table ID","Source Table ID");
      ESDataSecurityObject.SETRANGE("Data Security Code",FromCode);
      IF ESDataSecurityObject.FIND('-') THEN
        REPEAT
          ESDataSecurityObject2 := ESDataSecurityObject;
          ESDataSecurityObject2."Data Security Code" := Code;
          ESDataSecurityObject2.INSERT;
        UNTIL ESDataSecurityObject.NEXT = 0;

      ESDataSecurityField.RESET;
      ESDataSecurityField.SETRANGE("Source Table ID","Source Table ID");
      ESDataSecurityField.SETRANGE("Data Security Code",FromCode);
      IF ESDataSecurityField.FIND('-') THEN
        REPEAT
          ESDataSecurityField2 := ESDataSecurityField;
          ESDataSecurityField2."Data Security Code" := Code;
          ESDataSecurityField2.INSERT;
        UNTIL ESDataSecurityField.NEXT = 0;
    END;

    PROCEDURE DeleteRelated@1240520009();
    VAR
      ESDataSecurityObject@1240520008 : Record 14123808;
      ESDataSecurityObject2@1240520010 : Record 14123808;
      ESFLADSCopySetup@1240520001 : Record 14123816;
    BEGIN
      ESDataSecurityObject.RESET;
      ESDataSecurityObject.SETRANGE("Source Table ID","Source Table ID");
      ESDataSecurityObject.SETRANGE("Data Security Code",Code);
      IF ESDataSecurityObject.FIND('-') THEN
        REPEAT
          ESDataSecurityObject2 := ESDataSecurityObject;
          ESDataSecurityObject2.DELETE(TRUE);
        UNTIL ESDataSecurityObject.NEXT = 0;

      IF ESFLADSCopySetup.GET(DATABASE::"ES Data Security Code","Source Table ID",Code) THEN
        ESFLADSCopySetup.DELETE;
    END;

    PROCEDURE ExportMultiFieldLevelSecCodes@1240520002(VAR ESDataSecurityCode@1240520004 : Record 14123804;ShowStatus@1240520021 : Boolean;ExportFileName@1240520001 : Text[250]);
    VAR
      FileMgt@1000 : Codeunit 419;
      ESEnterFileName@1240520002 : Page 14123560;
      TempServerFile@1002 : File;
      Window@1001 : Dialog;
      TempServerFileName@1003 : Text[1000];
      TotalRecords@1240520008 : Integer;
      TotalCodes@1240520003 : Integer;
    BEGIN
      IF ExportFileName = '' THEN BEGIN
        CLEAR(ESEnterFileName);
        ESEnterFileName.LOOKUPMODE(TRUE);
        ESEnterFileName.SelectNewFile(TRUE);
        IF ESEnterFileName.RUNMODAL <> ACTION::LookupOK THEN
          ERROR(Text004);
        ExportFileName := ESEnterFileName.GetFileName;
      END;

      TempServerFile.WRITEMODE(TRUE);
      TempServerFile.TEXTMODE(TRUE);
      TempServerFile.CREATETEMPFILE;
      TempServerFileName := TempServerFile.NAME;
      TempServerFile.CLOSE;

      IF GUIALLOWED THEN
        Window.OPEN(Text006);

      IF ESDataSecurityCode.FIND('-') THEN
        REPEAT
          IF GUIALLOWED THEN
            Window.UPDATE(
              1,STRSUBSTNO('%1 %2',ESDataSecurityCode."Source Table ID",ESDataSecurityCode.Code));

          TotalCodes := TotalCodes + 1;

          TotalRecords :=
            TotalRecords +
            ESDataSecurityCode.ExportDataSecurityCode(FALSE,TRUE,TRUE,TempServerFileName,FALSE);
        UNTIL ESDataSecurityCode.NEXT = 0;

      IF GUIALLOWED THEN
        Window.UPDATE(1,Text101);
      FileMgt.DownloadToFile(TempServerFileName,ExportFileName);

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(
          Text019,
          TotalCodes,TABLECAPTION,ExportFileName,TotalRecords);
    END;

    PROCEDURE ExportDataSecurityCode@1240520014(ShowStatus@1240520021 : Boolean;ConfirmOverwrite@1240520020 : Boolean;AppendToFile@1240520004 : Boolean;ExportFileName@1240520001 : Text[250];ShowProgress@1240520005 : Boolean) : Integer;
    VAR
      ESDataSecurityObject@1240520012 : Record 14123808;
      ESDataSecurityField@1240520014 : Record 14123809;
      ESEnterFileName@1240520002 : Page 14123560;
      ESFileMgt@1240520003 : Codeunit 14123502;
      FileMgt@1000 : Codeunit 419;
      Window@1240520009 : Dialog;
      OutputFile@1240520019 : File;
      Value@1240520022 : ARRAY [100] OF Text[1000];
      LocalFileName@1001 : Text[1000];
      TotalRecords@1240520008 : Integer;
    BEGIN
      IF NOT AppendToFile THEN BEGIN
        IF ExportFileName = '' THEN BEGIN
          CLEAR(ESEnterFileName);
          ESEnterFileName.LOOKUPMODE(TRUE);
          ESEnterFileName.SelectNewFile(TRUE);
          IF ESEnterFileName.RUNMODAL <> ACTION::LookupOK THEN
            ERROR(Text004);
          ExportFileName := ESEnterFileName.GetFileName;
        END;

        IF ConfirmOverwrite THEN
          IF EXISTS(ExportFileName) THEN
            IF NOT CONFIRM(Text003,FALSE,ExportFileName) THEN
              ERROR(Text004);

        LocalFileName := ExportFileName;
        OutputFile.WRITEMODE(TRUE);
        OutputFile.TEXTMODE(TRUE);
        OutputFile.CREATETEMPFILE;
        ExportFileName := OutputFile.NAME;
        OutputFile.CLOSE;
      END;

      IF GUIALLOWED AND ShowProgress THEN
        Window.OPEN(STRSUBSTNO(ESFLADSSetup.ExtendProgressText(Text005),TABLECAPTION) + Text006);

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,Text007);

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,Text008);
      OutputFile.WRITEMODE(TRUE);
      OutputFile.TEXTMODE(TRUE);
      IF EXISTS(ExportFileName) AND AppendToFile THEN BEGIN
        OutputFile.OPEN(ExportFileName);
        OutputFile.SEEK(OutputFile.LEN);
      END ELSE
        OutputFile.CREATE(ExportFileName);

      CLEAR(ESFileMgt);
      ESFileMgt.SetDelimiters('~"','"~',',');

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,'HEADER');
      Value[1] := ESFileMgt.FormatInteger(0) +'V1';
      Value[2] := 'HEADER';
      Value[3] := ESFileMgt.FormatInteger("Source Table ID") + ' ' + Code;
      Value[4] := Description;
      Value[5] := USERID;
      Value[6] := ESFileMgt.FormatDate(TODAY);
      Value[7] := ESFileMgt.FormatTime(TIME);
      OutputFile.WRITE(ESFileMgt.FormatValues(Value,7));

      Value[1] := ESFileMgt.FormatInteger(DATABASE::"ES Data Security Code") + 'V1';
      Value[2] := ESFileMgt.FormatInteger("Source Table ID");
      Value[3] := Code;
      Value[4] := Description;

      OutputFile.WRITE(ESFileMgt.FormatValues(Value,4));

      TotalRecords := TotalRecords + 1;

      ESDataSecurityObject.RESET;
      ESDataSecurityObject.SETRANGE("Source Table ID","Source Table ID");
      ESDataSecurityObject.SETRANGE("Data Security Code",Code);
      IF ESDataSecurityObject.FIND('-') THEN
        REPEAT
          Value[1] := ESFileMgt.FormatInteger(DATABASE::"ES Data Security Object") + 'V1';
          Value[2] := ESFileMgt.FormatInteger(ESDataSecurityObject."Source Table ID");
          Value[3] := ESDataSecurityObject."Data Security Code";
          Value[4] := ESFileMgt.FormatInteger(ESDataSecurityObject."Object Type");
          Value[5] := ESFileMgt.FormatInteger(ESDataSecurityObject."Object ID");
          Value[6] := ESDataSecurityObject.Comment;
          OutputFile.WRITE(ESFileMgt.FormatValues(Value,6));

          TotalRecords := TotalRecords + 1;
        UNTIL ESDataSecurityObject.NEXT = 0;

      ESDataSecurityField.RESET;
      ESDataSecurityField.SETRANGE("Source Table ID","Source Table ID");
      ESDataSecurityField.SETRANGE("Data Security Code",Code);
      IF ESDataSecurityField.FIND('-') THEN
        REPEAT
          Value[1] := ESFileMgt.FormatInteger(DATABASE::"ES Data Security Field") + 'V1';
          Value[2] := ESFileMgt.FormatInteger(ESDataSecurityField."Source Table ID");
          Value[3] := ESDataSecurityField."Data Security Code";
          Value[4] := ESFileMgt.FormatInteger(ESDataSecurityField."Object Type");
          Value[5] := ESFileMgt.FormatInteger(ESDataSecurityField."Object ID");
          Value[6] := ESFileMgt.FormatInteger(ESDataSecurityField."Field ID");
          Value[7] := ESDataSecurityField.Comment;
          Value[8] := ESFileMgt.FormatInteger(ESDataSecurityField."Filter Type");
          Value[9] := ESDataSecurityField."Filter Value";
          Value[10] := ESFileMgt.FormatBoolean(ESDataSecurityField."Substitute Filter Value");
          Value[11] := ESFileMgt.FormatBoolean(ESDataSecurityField."Always Substitute Filter Value");
          OutputFile.WRITE(ESFileMgt.FormatValues(Value,11));

          TotalRecords := TotalRecords + 1;
        UNTIL ESDataSecurityField.NEXT = 0;

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,'TOTALS');
      Value[1] := ESFileMgt.FormatInteger(0) + 'V1';
      Value[2] := 'TOTALS';
      Value[3] := ESFileMgt.FormatInteger("Source Table ID") + ' ' + Code;
      Value[4] := Description;
      Value[5] := USERID;
      Value[6] := ESFileMgt.FormatDate(TODAY);
      Value[7] := ESFileMgt.FormatTime(TIME);
      Value[8] := ESFileMgt.FormatInteger(TotalRecords);
      OutputFile.WRITE(ESFileMgt.FormatValues(Value,8));

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,Text011);
      OutputFile.CLOSE;

      IF NOT AppendToFile THEN BEGIN
        IF GUIALLOWED AND ShowProgress THEN
          Window.UPDATE(1,Text101);
        FileMgt.DownloadToFile(ExportFileName,LocalFileName);
      END;

      IF GUIALLOWED AND ShowProgress THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(
          Text012,
          TABLECAPTION,"Source Table ID",Code,LocalFileName,TotalRecords);

      EXIT(TotalRecords);
    END;

    PROCEDURE ImportDataSecurityCode@1240520013(ShowStatus@1240520011 : Boolean;ImportFileName@1240520018 : Text[250];OverWriteExisting@1240520001 : Boolean);
    VAR
      ESDataSecurityCode@1240520002 : Record 14123804;
      ESDataSecurityCode2@1240520006 : Record 14123804;
      ESDataSecurityObject@1240520003 : Record 14123808;
      ESDataSecurityField@1240520004 : Record 14123809;
      ESEnterFileName@1240520019 : Page 14123560;
      ESFileMgt@1240520005 : Codeunit 14123502;
      FileMgt@1000 : Codeunit 419;
      InputFile@1240520009 : File;
      Window@1240520012 : Dialog;
      TempText@1240520010 : Text[1000];
      Value@1240520008 : ARRAY [100] OF Text[1000];
      ServerFileName@1001 : Text[1000];
      NextUpdatePos@1240520022 : Integer;
      TotalRecords@1240520023 : Integer;
    BEGIN
      IF ImportFileName = '' THEN BEGIN
        CLEAR(ESEnterFileName);
        ESEnterFileName.LOOKUPMODE(TRUE);
        IF ESEnterFileName.RUNMODAL <> ACTION::LookupOK THEN
          ERROR(Text013);
        ImportFileName := ESEnterFileName.GetFileName;
      END;

      IF ImportFileName = '' THEN
        ERROR(Text014);

      IF GUIALLOWED THEN
        Window.OPEN(
          STRSUBSTNO(ESFLADSSetup.ExtendProgressText(Text015),TABLECAPTION) +
          Text016 + '        @2@@@@@@@@@@@@@@@@@@@@@@@@@@@@');

      CLEAR(ESFileMgt);
      ESFileMgt.SetDelimiters('~"','"~',',');

      IF GUIALLOWED THEN
        Window.UPDATE(1,Text017);
      InputFile.WRITEMODE(FALSE);
      InputFile.TEXTMODE(TRUE);
      IF NOT InputFile.OPEN(ImportFileName) THEN BEGIN
        IF GUIALLOWED THEN
          Window.UPDATE(1,Text102);
        ServerFileName := FileMgt.UploadFileSilent(ImportFileName);
        IF GUIALLOWED THEN
          Window.UPDATE(1,Text017);

        IF NOT InputFile.OPEN(ServerFileName) THEN
          InputFile.OPEN(ImportFileName);
      END;
      WHILE InputFile.POS < InputFile.LEN DO BEGIN
        IF NextUpdatePos < InputFile.POS THEN BEGIN
          IF GUIALLOWED THEN
            Window.UPDATE(2,ROUND(10000 * (InputFile.POS / InputFile.LEN),1));
          NextUpdatePos := ROUND(InputFile.POS + InputFile.LEN / 500,1);
        END;

        InputFile.READ(TempText);
        ESFileMgt.SplitInValues(Value,TempText);

        CASE Value[1] OF
          '','0V1':
            ;
          '14123804V1':
            BEGIN
              IF GUIALLOWED THEN
                Window.UPDATE(1,STRSUBSTNO('%1 %2',Value[2],Value[3]));

              ESDataSecurityCode.INIT;
              ESDataSecurityCode."Source Table ID" := ESFileMgt.EvaluateInteger(Value[2]);
              ESDataSecurityCode.Code := Value[3];
              ESDataSecurityCode.Description := Value[4];

              IF OverWriteExisting AND
                 ESDataSecurityCode2.GET(
                   ESDataSecurityCode."Source Table ID",ESDataSecurityCode.Code)
              THEN BEGIN
                ESDataSecurityCode2.SetNoExtendedInsertDelete(TRUE);
                ESDataSecurityCode2.DELETE(TRUE);
              END;

              ESDataSecurityCode.SetNoExtendedInsertDelete(TRUE);
              ESDataSecurityCode.INSERT(TRUE);

              TotalRecords := TotalRecords + 1;
            END;
          '14123808V1':
            BEGIN
              ESDataSecurityObject.INIT;
              ESDataSecurityObject."Source Table ID" := ESFileMgt.EvaluateInteger(Value[2]);
              ESDataSecurityObject."Data Security Code" := Value[3];
              ESDataSecurityObject."Object Type" := ESFileMgt.EvaluateInteger(Value[4]);
              ESDataSecurityObject."Object ID" := ESFileMgt.EvaluateInteger(Value[5]);
              ESDataSecurityObject.Comment := Value[6];
              ESDataSecurityObject.INSERT(TRUE);

              TotalRecords := TotalRecords + 1;
            END;
          '14123809V1':
            BEGIN
              ESDataSecurityField.INIT;
              ESDataSecurityField."Source Table ID" := ESFileMgt.EvaluateInteger(Value[2]);
              ESDataSecurityField."Data Security Code" := Value[3];
              ESDataSecurityField."Object Type" := ESFileMgt.EvaluateInteger(Value[4]);
              ESDataSecurityField."Object ID" := ESFileMgt.EvaluateInteger(Value[5]);
              ESDataSecurityField."Field ID" := ESFileMgt.EvaluateInteger(Value[6]);
              ESDataSecurityField.Comment := Value[7];
              ESDataSecurityField."Filter Type" := ESFileMgt.EvaluateInteger(Value[8]);
              ESDataSecurityField."Filter Value" := Value[9];
              ESDataSecurityField."Substitute Filter Value" := ESFileMgt.EvaluateBoolean(Value[10]);
              ESDataSecurityField."Always Substitute Filter Value" :=
                ESFileMgt.EvaluateBoolean(Value[11]);
              ESDataSecurityField.INSERT(TRUE);

              TotalRecords := TotalRecords + 1;
            END;
          ELSE
            ERROR(Text002,Value[1]);
        END;
      END;

      InputFile.CLOSE;
      IF ServerFileName <> '' THEN
        ERASE(ServerFileName);

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(Text018,TotalRecords,ImportFileName);
    END;

    PROCEDURE SetNoExtendedInsertDelete@1240520001(NewValue@1240520001 : Boolean);
    BEGIN
      NoExtendedInsertDelete := NewValue;
    END;

    PROCEDURE TestEditAllowed@1240520003(RetrieveSetup@1240520001 : Boolean);
    VAR
      ESLoginSetup@1240520002 : Record 14123540;
    BEGIN
      IF RetrieveSetup THEN
        ESFLADSSetup.GET('');

      IF ESFLADSSetup."Use Login Setup" THEN BEGIN
        ESLoginSetup.GET(USERID);
        ESLoginSetup.TESTFIELD("Edit Data Security Codes");
      END;
    END;

    BEGIN
    END.
  }
}

