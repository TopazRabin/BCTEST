OBJECT Table 14000605 Receive Label File
{
  OBJECT-PROPERTIES
  {
    Date=06/28/17;
    Time=12:00:00 PM;
    Version List=SE0.60.03;
  }
  PROPERTIES
  {
    CaptionML=ENU=Receive Label File;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;CaptionML=ENU=No. }
    { 2   ;   ;File No.            ;Integer       ;CaptionML=ENU=File No. }
    { 3   ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Receive,Manifest;
                                                   OptionString=Receive,Manifest }
    { 11  ;   ;Description         ;Text30        ;CaptionML=ENU=Description }
    { 12  ;   ;File Type           ;Option        ;CaptionML=ENU=File Type;
                                                   OptionCaptionML=ENU=" ,Receive Label,Item Label";
                                                   OptionString=[ ,Receive Label,Item Label];
                                                   Editable=No }
    { 13  ;   ;File (Binary)       ;BLOB          ;CaptionML=ENU=File (Binary) }
    { 14  ;   ;File Name           ;Text250       ;CaptionML=ENU=File Name;
                                                   Editable=No }
    { 15  ;   ;Printer Port        ;Code100       ;CaptionML=ENU=Printer Port }
    { 16  ;   ;Receive Station Code;Code10        ;TableRelation="Receive Station";
                                                   CaptionML=ENU=Receive Station Code }
    { 17  ;   ;File Not Imported   ;Boolean       ;CaptionML=ENU=File Not Imported }
    { 21  ;   ;Created By          ;Code50        ;TableRelation=User;
                                                   CaptionML=ENU=Created By;
                                                   Editable=No }
    { 22  ;   ;Date Created        ;Date          ;CaptionML=ENU=Date Created;
                                                   Editable=No }
    { 23  ;   ;Time Created        ;Time          ;CaptionML=ENU=Time Created;
                                                   Editable=No }
    { 24  ;   ;No. of Copies Printed;Integer      ;CaptionML=ENU=No. of Copies Printed;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Type,No.,File No.                       ;Clustered=Yes }
    {    ;Type,No.,File Type                       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ReceiveStation@1240030000 : Record 14000608;

    PROCEDURE PrintLabel@1();
    VAR
      PrinterPort@1240030001 : Code[100];
      LabelFileName@1240030000 : Text[1000];
    BEGIN
      TESTFIELD("File Not Imported",FALSE);

      CALCFIELDS("File (Binary)");

      ReceiveStation.GetReceiveStation;
      ReceiveStation.TESTFIELD("Label Buffer File");
      LabelFileName := ReceiveStation.CalcLabelFileName(ReceiveStation."Label Buffer File");
      ExportFileBinary(LabelFileName,FALSE);
      IF ReceiveStation.Code = "Receive Station Code" THEN BEGIN
        ReceiveStation.PrintLabelFile(
          LabelFileName,"Printer Port",
          ReceiveStation."No Label Printer",ReceiveStation."Auto Delete Label Buffer File");

        IF NOT ReceiveStation."No Label Printer" THEN BEGIN
          "No. of Copies Printed" := "No. of Copies Printed" + 1;
          MODIFY;
        END;
      END ELSE BEGIN
        PrinterPort := '';
        CASE "File Type" OF
          "File Type"::"Receive Label":
            BEGIN
              ReceiveStation.TESTFIELD("Label Printer Port");
              PrinterPort := ReceiveStation."Label Printer Port";
            END;
          "File Type"::"Item Label":
            BEGIN
              ReceiveStation.TESTFIELD("Item Label Printer Port");
              PrinterPort := ReceiveStation."Item Label Printer Port";
            END;
        END;

        IF PrinterPort <> '' THEN BEGIN
          ReceiveStation.PrintLabelFile(
            LabelFileName,PrinterPort,
            ReceiveStation."No Label Printer",ReceiveStation."Auto Delete Label Buffer File");

          IF NOT ReceiveStation."No Label Printer" THEN BEGIN
            "No. of Copies Printed" := "No. of Copies Printed" + 1;
            MODIFY;
          END;
        END;
      END;
    END;

    PROCEDURE ImportFileBinary@1240030000(ImportFileName@1240030000 : Text[1000];CommonDialog@1240030001 : Boolean);
    VAR
      ImportFile@1240030004 : File;
      NVInStream@1240030003 : InStream;
      NVOutStream@1240030002 : OutStream;
    BEGIN
      IF ISSERVICETIER THEN BEGIN
        ImportFile.WRITEMODE(FALSE);
        ImportFile.TEXTMODE(FALSE);
        ImportFile.OPEN(ImportFileName);
        ImportFile.CREATEINSTREAM(NVInStream);
        "File (Binary)".CREATEOUTSTREAM(NVOutStream);
        COPYSTREAM(NVOutStream,NVInStream);
      END ELSE
        "File (Binary)".IMPORT(ImportFileName,CommonDialog);
    END;

    PROCEDURE ExportFileBinary@1240030001(ExportFileName@1240030001 : Text[1000];CommonDialog@1240030000 : Boolean);
    VAR
      ExportFile@1240030004 : File;
      NVInStream@1240030003 : InStream;
      NVOutStream@1240030002 : OutStream;
    BEGIN
      CALCFIELDS("File (Binary)");
      IF NOT "File (Binary)".HASVALUE THEN
        TESTFIELD("File (Binary)");

      IF ISSERVICETIER THEN BEGIN
        ExportFile.WRITEMODE(TRUE);
        ExportFile.TEXTMODE(FALSE);
        ExportFile.CREATE(ExportFileName);
        "File (Binary)".CREATEINSTREAM(NVInStream);
        ExportFile.CREATEOUTSTREAM(NVOutStream);
        COPYSTREAM(NVOutStream,NVInStream);
        ExportFile.CLOSE;
        IF CommonDialog THEN BEGIN
          ReceiveStation.GetReceiveStation;
          ReceiveStation.ViewFile(ExportFileName);
        END;
      END ELSE
        "File (Binary)".EXPORT(ExportFileName,CommonDialog);
    END;

    PROCEDURE ViewFileBinary@2();
    VAR
      LabelFileName@1240030000 : Text[1000];
    BEGIN
      TESTFIELD("File Not Imported",FALSE);

      CALCFIELDS("File (Binary)");

      ReceiveStation.GetReceiveStation;
      ReceiveStation.TESTFIELD("Label Buffer File");
      LabelFileName := ReceiveStation.CalcLabelFileName(ReceiveStation."Label Buffer File");
      ExportFileBinary(LabelFileName,FALSE);
      ReceiveStation.ViewFile(LabelFileName);
    END;

    BEGIN
    END.
  }
}

