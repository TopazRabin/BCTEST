OBJECT Table 14123601 ES Relation
{
  OBJECT-PROPERTIES
  {
    Date=04/30/18;
    Time=12:00:00 PM;
    Version List=ES1.41.51;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Created By User" := USERID;
               "Created Date Time" := CURRENTDATETIME;
             END;

    OnDelete=BEGIN
               Comment(4,'');
             END;

    CaptionML=ENU=Relation;
    LookupPageID=Page14123601;
    DrillDownPageID=Page14123601;
  }
  FIELDS
  {
    { 1   ;   ;Object Type         ;Option        ;CaptionML=ENU=Object Type;
                                                   OptionCaptionML=ENU=TableData,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System;
                                                   OptionString=TableData,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System }
    { 3   ;   ;Object ID           ;Integer       ;TableRelation="ES Object Information"."Object ID" WHERE (Object Type=FIELD(Object Type));
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Object ID;
                                                   NotBlank=Yes }
    { 4   ;   ;Object Caption      ;Text249       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Object Information"."Object Caption" WHERE (Object Type=FIELD(Object Type),
                                                                                                                      Object ID=FIELD(Object ID)));
                                                   CaptionML=ENU=Object Caption;
                                                   Editable=No }
    { 5   ;   ;Entry No.           ;Integer       ;CaptionML=ENU=Entry No.;
                                                   NotBlank=Yes }
    { 6   ;   ;Field ID            ;Integer       ;CaptionML=ENU=Field ID }
    { 7   ;   ;Field Name          ;Text30        ;CaptionML=ENU=Field Name }
    { 8   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF (Type <> xRec.Type) AND (Type = Type::"Parser Error") THEN
                                                                  IF "Entry No." >= 0 THEN
                                                                    ERROR(Text001,FIELDCAPTION("Entry No."),Type);
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Parser Error,Table Relation,Flowfield,Source Table,Data Item,Permission,Manual,Data Per Company,PagePartID,MenuItem,Editable,Field Level and Data Security Change,OnLookup;
                                                   OptionString=Parser Error,Table Relation,Flowfield,Source Table,Data Item,Permission,Manual,Data Per Company,PagePartID,MenuItem,Editable,FLADS Change,OnLookup }
    { 9   ;   ;Relation Object ID  ;Integer       ;TableRelation="ES Object Information"."Object ID" WHERE (Object Type=CONST(Table));
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Relation Object ID }
    { 10  ;   ;Source Code         ;Text250       ;CaptionML=ENU=Source Code }
    { 11  ;   ;Relation Object Caption;Text249    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Object Information"."Object Caption" WHERE (Object Type=FIELD(Relation Object Type),
                                                                                                                      Object ID=FIELD(Relation Object ID)));
                                                   CaptionML=ENU=Relation Object Caption;
                                                   Editable=No }
    { 12  ;   ;File Position       ;Integer       ;CaptionML=ENU=File Position }
    { 13  ;   ;Source Code Line No.;Integer       ;CaptionML=ENU=Source Code Line No. }
    { 14  ;   ;Limited Access Object;Boolean      ;FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Object Property" WHERE (Object Type=FIELD(Object Type),
                                                                                                 Object ID=FIELD(Object ID),
                                                                                                 Object Level Permissions=CONST(Yes)));
                                                   CaptionML=ENU=Limited Access Object;
                                                   Editable=No }
    { 15  ;   ;Limited License Object;Boolean     ;FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Object Property" WHERE (Object Type=FIELD(Object Type),
                                                                                                 Object ID=FIELD(Object ID),
                                                                                                 Limited License Object=CONST(Yes)));
                                                   CaptionML=ENU=Limited License Object;
                                                   Editable=No }
    { 16  ;   ;Limited License Relation Obj.;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Object Property" WHERE (Object Type=FIELD(Relation Object Type),
                                                                                                 Object ID=FIELD(Relation Object ID),
                                                                                                 Limited License Object=CONST(Yes)));
                                                   CaptionML=ENU=Limited License Relation Object;
                                                   Editable=No }
    { 17  ;   ;Parser Error        ;Boolean       ;CaptionML=ENU=Parser Error }
    { 18  ;   ;Created By User     ;Code50        ;CaptionML=ENU=Created By User;
                                                   Editable=No }
    { 19  ;   ;Created Date Time   ;DateTime      ;CaptionML=ENU=Created Date Time;
                                                   Editable=No }
    { 20  ;   ;Free Access Object  ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Object Property" WHERE (Object Type=FIELD(Object Type),
                                                                                                 Object ID=FIELD(Object ID),
                                                                                                 Object Level Permissions=CONST(No),
                                                                                                 Limited License Object=CONST(No)));
                                                   CaptionML=ENU=Free Access Object;
                                                   Editable=No }
    { 21  ;   ;Relation Object Type;Option        ;CaptionML=ENU=Relation Object Type;
                                                   OptionCaptionML=ENU=TableData,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System;
                                                   OptionString=TableData,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System }
    { 22  ;   ;Limited Access Relation Object;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Object Property" WHERE (Object Type=FIELD(Relation Object Type),
                                                                                                 Object ID=FIELD(Relation Object ID),
                                                                                                 Object Level Permissions=CONST(Yes)));
                                                   CaptionML=ENU=Limited Access Relation Object;
                                                   Editable=No }
    { 23  ;   ;Free Access Relation Object;Boolean;FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Object Property" WHERE (Object Type=FIELD(Relation Object Type),
                                                                                                 Object ID=FIELD(Relation Object ID),
                                                                                                 Object Level Permissions=CONST(No),
                                                                                                 Limited License Object=CONST(No)));
                                                   CaptionML=ENU=Free Access Relation Object;
                                                   Editable=No }
    { 24  ;   ;Security Filter Object;Boolean     ;FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Object Property" WHERE (Object Type=FIELD(Object Type),
                                                                                                 Object ID=FIELD(Object ID),
                                                                                                 Security Filter Table=CONST(Yes)));
                                                   CaptionML=ENU=Security Filter Object;
                                                   Editable=No }
    { 25  ;   ;Security Filter Relation Obj.;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Object Property" WHERE (Object Type=FIELD(Object Type),
                                                                                                 Object ID=FIELD(Object ID),
                                                                                                 Security Filter Table=CONST(Yes)));
                                                   CaptionML=ENU=Security Filter Relation Object;
                                                   Editable=No }
    { 26  ;   ;Limited User Included Object;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Object Property" WHERE (Object Type=FIELD(Object Type),
                                                                                                 Object ID=FIELD(Object ID),
                                                                                                 Limited User Table Access=CONST(Included)));
                                                   CaptionML=ENU=Limited User Included Object;
                                                   Editable=No }
    { 27  ;   ;Limited User Incl. Rel. Object;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Object Property" WHERE (Object Type=FIELD(Relation Object Type),
                                                                                                 Object ID=FIELD(Relation Object ID),
                                                                                                 Limited User Table Access=CONST(Included)));
                                                   CaptionML=ENU=Limited User Included Relation Object;
                                                   Editable=No }
    { 28  ;   ;Quick Security Type ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Object Property"."Quick Security Type" WHERE (Object Type=FIELD(Object Type),
                                                                                                                        Object ID=FIELD(Object ID)));
                                                   CaptionML=ENU=Quick Security Type;
                                                   OptionCaptionML=ENU=" ,Read Only,No Access";
                                                   OptionString=[ ,Read Only,No Access];
                                                   Editable=No }
    { 29  ;   ;Relation Quick Security Type;Option;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Object Property"."Quick Security Type" WHERE (Object Type=FIELD(Relation Object Type),
                                                                                                                        Object ID=FIELD(Relation Object ID)));
                                                   CaptionML=ENU=Relation Quick Security Type;
                                                   OptionCaptionML=ENU=" ,Read Only,No Access";
                                                   OptionString=[ ,Read Only,No Access];
                                                   Editable=No }
    { 30  ;   ;Origin              ;Option        ;CaptionML=ENU=Origin;
                                                   OptionCaptionML=ENU=Text File,Manual,Fields,Table Relations Metadata;
                                                   OptionString=Text File,Manual,Fields,Table Relations Metadata }
  }
  KEYS
  {
    {    ;Object Type,Object ID,Entry No.         ;Clustered=Yes }
    {    ;Type,Relation Object ID                  }
    {    ;Created Date Time                        }
    {    ;Created By User,Created Date Time        }
    {    ;Origin                                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=%1 must be negative when using %2.';
      Text002@1002 : TextConst 'ENU=Adding to Object Properties\';
      Text003@1003 : TextConst 'ENU=Process #1################################################';
      Text004@1004 : TextConst 'ENU=Adding to Object Properties';
      Text005@1005 : TextConst 'ENU=%1 objects have been added %2 modified.';
      Text006@1006 : TextConst 'ENU=%1 %2 inserted from %3.';
      Text007@1007 : TextConst 'ENU=Full Access,Read Only,No Access';
      Text008@1008 : TextConst 'ENU=Select %1';
      Text009@1009 : TextConst 'ENU=TableData and other Object Types cannot be mixed.';
      Text010@1010 : TextConst 'ENU=Full Access,,No Access';
      ESSecuritySetup@1240520001 : Record 14123521;
      Text011@1240520002 : TextConst 'ENU=Adding Missing from Fields\';
      Text012@1240520003 : TextConst 'ENU=Add Missing from Table Relations Metadata\';

    PROCEDURE AddToObjectProperty@1240520008(VAR ESRelation@1240520008 : Record 14123601;ShowStatus@1240520012 : Boolean;AddAsLimited@1240520013 : Boolean;AskForQuickSecurityType@1240520001 : Boolean);
    VAR
      ESObjectProperty@1240520009 : Record 14123522;
      Window@1240520010 : Dialog;
      InsertedRecords@1240520011 : Integer;
      ModifiedRecords@1240520014 : Integer;
      QuickSecurityType@1240520002 : Integer;
    BEGIN
      IF AskForQuickSecurityType THEN BEGIN
        ESRelation.FIND('-');
        IF ESRelation."Object Type" = ESRelation."Object Type"::TableData THEN BEGIN
          ESRelation.FIND('+');
          IF ESRelation."Object Type" > ESRelation."Object Type"::TableData THEN
            ERROR(Text009);
        END;

        IF ESRelation."Object Type" = ESRelation."Object Type"::TableData THEN
          QuickSecurityType :=
            STRMENU(
              Text007,2,
              STRSUBSTNO(Text008,ESObjectProperty.FIELDCAPTION("Quick Security Type")))
        ELSE
          QuickSecurityType :=
            STRMENU(
              Text010,3,
              STRSUBSTNO(Text008,ESObjectProperty.FIELDCAPTION("Quick Security Type")));

        IF QuickSecurityType = 0 THEN
          EXIT;
        QuickSecurityType := QuickSecurityType - 1;
      END;

      IF GUIALLOWED THEN
        Window.OPEN(ESSecuritySetup.ExtendProgressText(Text002) + Text003);

      IF GUIALLOWED THEN
        Window.UPDATE(1,Text004);
      IF ESRelation.FIND('-') THEN
        REPEAT
          IF AskForQuickSecurityType THEN
            IF NOT (QuickSecurityType IN
                    [ESObjectProperty."Quick Security Type"::"Full Access",
                     ESObjectProperty."Quick Security Type"::"No Access"])
            THEN
              ESRelation.TESTFIELD("Object Type",ESRelation."Object Type"::TableData);

          ESObjectProperty.INIT;
          ESObjectProperty."Object Type" := ESRelation."Object Type";
          ESObjectProperty."Object ID" := ESRelation."Object ID";
          ESObjectProperty.InitObjectLevelPermissions;
          IF AskForQuickSecurityType THEN
            ESObjectProperty."Quick Security Type" := QuickSecurityType
          ELSE
            ESObjectProperty."Object Level Permissions" := AddAsLimited;
          ESObjectProperty.SetNoInitObjectLevel(TRUE);
          IF NOT ESObjectProperty.INSERT(TRUE) THEN BEGIN
            ESObjectProperty.GET(ESRelation."Object Type",ESRelation."Object ID");
            IF AskForQuickSecurityType THEN
              ESObjectProperty."Quick Security Type" := QuickSecurityType
            ELSE
              IF ESObjectProperty."Object Level Permissions" <> AddAsLimited THEN
                ESObjectProperty."Object Level Permissions" := AddAsLimited;
            ESObjectProperty.MODIFY(TRUE);

            ModifiedRecords := ModifiedRecords + 1;
          END ELSE
            InsertedRecords := InsertedRecords + 1;
        UNTIL ESRelation.NEXT = 0;

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(Text005,InsertedRecords,ModifiedRecords);
    END;

    PROCEDURE AddMissingFromFields@1240520001(ShowStatus@1240520005 : Boolean);
    VAR
      Field@1240520001 : Record 2000000041;
      ESRelation@1240520002 : Record 14123601;
      ESSecuritySetup@1240520006 : Record 14123521;
      Window@1240520003 : Dialog;
      InsertedRecords@1240520004 : Integer;
    BEGIN
      IF GUIALLOWED THEN
        Window.OPEN(ESSecuritySetup.ExtendProgressText(Text011) + Text003);

      ESRelation.RESET;
      ESRelation.SETRANGE(Origin,ESRelation.Origin::Fields);
      ESRelation.DELETEALL;

      Field.RESET;
      Field.SETFILTER(RelationTableNo,'<>0');
      IF Field.FIND('-') THEN
        REPEAT
          IF GUIALLOWED THEN
            Window.UPDATE(1,STRSUBSTNO('%1 %2 %3',Field.TableNo,Field.TableName,Field.FieldName));

          ESRelation.RESET;
          ESRelation.SETRANGE("Object Type",ESRelation."Object Type"::TableData);
          ESRelation.SETRANGE("Object ID",Field.TableNo);
          ESRelation.SETRANGE(Type,ESRelation.Type::"Table Relation");
          ESRelation.SETRANGE("Relation Object Type",ESRelation."Relation Object Type"::TableData);
          ESRelation.SETRANGE("Relation Object ID",Field.RelationTableNo);
          IF NOT ESRelation.FIND('-') THEN BEGIN
            ESRelation.RESET;
            ESRelation.SETRANGE("Object Type",ESRelation."Object Type"::TableData);
            ESRelation.SETRANGE("Object ID",Field.TableNo);
            IF NOT ESRelation.FIND('+') THEN BEGIN
              ESRelation."Object Type" := ESRelation."Object Type"::TableData;
              ESRelation."Object ID" := Field.TableNo;
              ESRelation."Entry No." := 0;
            END;

            ESRelation.INIT;
            ESRelation."Entry No." := ESRelation."Entry No." + 1;
            ESRelation."Field ID" := Field."No.";
            ESRelation.Type := ESRelation.Type::"Table Relation";
            ESRelation."Relation Object Type" := ESRelation."Relation Object Type"::TableData;
            ESRelation."Relation Object ID" := Field.RelationTableNo;
            ESRelation.Origin := ESRelation.Origin::Fields;
            ESRelation.INSERT(TRUE);

            ESRelation."Object Type" := ESRelation."Object Type"::Table;
            ESRelation.INSERT(TRUE);

            InsertedRecords := InsertedRecords + 1;
          END;
        UNTIL Field.NEXT = 0;

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(Text006,InsertedRecords,ESRelation.TABLECAPTION,Field.TABLECAPTION);
      ESSecuritySetup.Comment(
        1,STRSUBSTNO(Text006,InsertedRecords,ESRelation.TABLECAPTION,Field.TABLECAPTION));
    END;

    PROCEDURE AddMissingFromTableRelMetadata@1240520002(ShowStatus@1240520001 : Boolean);
    VAR
      TableRelationsMetadata@1000 : Record 2000000141;
      ESRelation@1003 : Record 14123601;
      ESSecuritySetup@1002 : Record 14123521;
      Window@1240520002 : Dialog;
      InsertedRecords@1004 : Integer;
    BEGIN
      IF GUIALLOWED THEN
        Window.OPEN(ESSecuritySetup.ExtendProgressText(Text012) + Text003);

      ESRelation.RESET;
      ESRelation.SETRANGE(Origin,ESRelation.Origin::"Table Relations Metadata");
      ESRelation.DELETEALL;

      TableRelationsMetadata.RESET;
      IF TableRelationsMetadata.FIND('-') THEN
        REPEAT
          IF GUIALLOWED THEN
            Window.UPDATE(
              1,
              STRSUBSTNO(
                '%1 %2 %3',TableRelationsMetadata."Table ID",TableRelationsMetadata."Field No.",
                TableRelationsMetadata."Related Table ID"));

          ESRelation.RESET;
          ESRelation.SETRANGE("Object Type",ESRelation."Object Type"::TableData);
          ESRelation.SETRANGE("Object ID",TableRelationsMetadata."Table ID");
          ESRelation.SETRANGE(Type,ESRelation.Type::"Table Relation");
          ESRelation.SETRANGE("Relation Object Type",ESRelation."Relation Object Type"::TableData);
          ESRelation.SETRANGE("Relation Object ID",TableRelationsMetadata."Related Table ID");
          IF NOT ESRelation.FIND('-') THEN BEGIN
            ESRelation.RESET;
            ESRelation.SETRANGE("Object Type",ESRelation."Object Type"::TableData);
            ESRelation.SETRANGE("Object ID",TableRelationsMetadata."Table ID");
            IF NOT ESRelation.FIND('+') THEN BEGIN
              ESRelation."Object Type" := ESRelation."Object Type"::TableData;
              ESRelation."Object ID" := TableRelationsMetadata."Table ID";
              ESRelation."Entry No." := 0;
            END;

            ESRelation.INIT;
            ESRelation."Entry No." := ESRelation."Entry No." + 1;
            ESRelation."Field ID" := TableRelationsMetadata."Field No.";
            ESRelation.Type := ESRelation.Type::"Table Relation";
            ESRelation."Relation Object Type" := ESRelation."Relation Object Type"::TableData;
            ESRelation."Relation Object ID" := TableRelationsMetadata."Related Table ID";
            ESRelation.Origin := ESRelation.Origin::"Table Relations Metadata";
            ESRelation.INSERT(TRUE);

            ESRelation."Object Type" := ESRelation."Object Type"::Table;
            ESRelation.INSERT(TRUE);

            InsertedRecords := InsertedRecords + 1;
          END;
        UNTIL TableRelationsMetadata.NEXT = 0;

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(Text006,InsertedRecords,ESRelation.TABLECAPTION,TableRelationsMetadata.TABLECAPTION);
      ESSecuritySetup.Comment(
        1,
        STRSUBSTNO(Text006,InsertedRecords,ESRelation.TABLECAPTION,TableRelationsMetadata.TABLECAPTION));
    END;

    PROCEDURE ObjectWhereUsed@1240520012();
    VAR
      ESObjectWhereUsed@1240520008 : Page 14123562;
    BEGIN
      CLEAR(ESObjectWhereUsed);
      ESObjectWhereUsed.InitObject("Object Type","Object ID");
      ESObjectWhereUsed.RUN;
    END;

    PROCEDURE FormatAsTextLine@1240520059(VAR ESTextLine@1240520011 : Record 14123520;NoOfRelatedKeyFields@1240520009 : Integer;CalcFlowFields@1240520012 : Boolean;UserDateTimeDetails@1240520013 : Boolean) : Boolean;
    VAR
      ESFormatMgt@1240520014 : Codeunit 14123505;
      RecRef@1240520010 : RecordRef;
    BEGIN
      ESFormatMgt.NewRecord(
        TABLECAPTION,FORMAT("Object Type"),FORMAT("Object ID"),FORMAT("Entry No."),
        '','','','','','','',3,NoOfRelatedKeyFields);
      ESFormatMgt.AddField(FIELDCAPTION("Field ID"),FORMAT("Field ID"));
      ESFormatMgt.AddField(FIELDCAPTION("Field Name"),FORMAT("Field Name"));
      ESFormatMgt.AddField(FIELDCAPTION(Type),FORMAT(Type));
      ESFormatMgt.AddField(FIELDCAPTION("Relation Object Type"),FORMAT("Relation Object Type"));
      ESFormatMgt.AddField(FIELDCAPTION("Relation Object ID"),FORMAT("Relation Object ID"));
      ESFormatMgt.AddField(FIELDCAPTION("Source Code"),FORMAT("Source Code"));
      ESFormatMgt.AddField(FIELDCAPTION("File Position"),FORMAT("File Position"));
      ESFormatMgt.AddField(FIELDCAPTION("Source Code Line No."),FORMAT("Source Code Line No."));
      IF "Parser Error" THEN
        ESFormatMgt.AddField(FIELDCAPTION("Parser Error"),FORMAT("Parser Error"));

      IF UserDateTimeDetails THEN BEGIN
        ESFormatMgt.AddField(FIELDCAPTION("Created By User"),FORMAT("Created By User"));
        IF "Created Date Time" <> 0DT THEN
          ESFormatMgt.AddField(FIELDCAPTION("Created Date Time"),FORMAT("Created Date Time"));
      END;

      IF CalcFlowFields THEN BEGIN
        CALCFIELDS(
          "Object Caption","Relation Object Caption","Limited Access Object","Limited License Object",
          "Limited License Relation Obj.","Free Access Object","Limited Access Relation Object",
          "Free Access Relation Object");

        ESFormatMgt.AddFlowField(FIELDCAPTION("Object Caption"),FORMAT("Object Caption"));
        ESFormatMgt.AddFlowField(FIELDCAPTION("Relation Object Caption"),FORMAT("Relation Object Caption"));
        IF "Limited Access Object" THEN
          ESFormatMgt.AddFlowField(
            FIELDCAPTION("Limited Access Object"),FORMAT("Limited Access Object"));
        IF "Limited License Object" THEN
          ESFormatMgt.AddFlowField(
            FIELDCAPTION("Limited License Object"),FORMAT("Limited License Object"));
        IF "Limited License Relation Obj." THEN
          ESFormatMgt.AddFlowField(
            FIELDCAPTION("Limited License Relation Obj."),FORMAT("Limited License Relation Obj."));
        IF "Free Access Object" THEN
          ESFormatMgt.AddFlowField(
            FIELDCAPTION("Free Access Object"),FORMAT("Free Access Object"));
        IF "Limited Access Relation Object" THEN
          ESFormatMgt.AddFlowField(
            FIELDCAPTION("Limited Access Relation Object"),FORMAT("Limited Access Relation Object"));
        IF "Free Access Relation Object" THEN
          ESFormatMgt.AddFlowField(
            FIELDCAPTION("Free Access Relation Object"),FORMAT("Free Access Relation Object"));
      END;

      RecRef.OPEN(DATABASE::"ES Relation");
      RecRef.GETTABLE(Rec);
      ESFormatMgt.FormatNewESTextLine(ESTextLine,DATABASE::"ES Relation",RecRef.RECORDID);

      EXIT(ESFormatMgt.NoTextOverflow);
    END;

    PROCEDURE Comment@1240520020(FunctionNo@1240520008 : ' ,Insert,Show,Exist,DeleteAll';NewText@1240520010 : Text[250]) : Boolean;
    VAR
      ESComment@1240520009 : Record 14123524;
    BEGIN
      ESComment.RESET;
      ESComment.SETRANGE("Table ID",DATABASE::"ES Relation");
      ESComment.SETRANGE(Type,"Object Type");
      ESComment.SETRANGE("Object ID","Object ID");
      ESComment.SETRANGE("Entry No.","Entry No.");
      EXIT(ESComment.Functions(FunctionNo,NewText));
    END;

    BEGIN
    END.
  }
}

