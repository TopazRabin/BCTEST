OBJECT Table 14123530 ES Company Group
{
  OBJECT-PROPERTIES
  {
    Date=04/04/18;
    Time=12:00:00 PM;
    Version List=ES1.41.49;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ESSecuritySetup.GET;
               ESSecuritySetup.TestPublishCompany;

               Comment(1,STRSUBSTNO(Text001,TABLECAPTION,"Group ID"));

               "Created By User" := USERID;
               "Created Date Time" := CURRENTDATETIME;
             END;

    OnModify=BEGIN
               Comment(1,STRSUBSTNO(Text007,TABLECAPTION,"Group ID"));
             END;

    OnDelete=BEGIN
               ESLoginAccessControl.RESET;
               ESLoginAccessControl.SETRANGE("Company Group ID","Group ID");
               IF ESLoginAccessControl.FIND('-') THEN
                 ESLoginAccessControl.TESTFIELD("Company Group ID",'');

               ESCompanyGroupLine.RESET;
               ESCompanyGroupLine.SETRANGE("Company Group ID","Group ID");
               ESCompanyGroupLine.DELETEALL(TRUE);

               Comment(1,STRSUBSTNO(Text008,TABLECAPTION,"Group ID"));
             END;

    CaptionML=ENU=Company Group;
    LookupPageID=Page14123535;
    DrillDownPageID=Page14123535;
  }
  FIELDS
  {
    { 1   ;   ;Group ID            ;Code30        ;CaptionML=ENU=Group ID;
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text30        ;CaptionML=ENU=Name }
    { 3   ;   ;Login Access Controls;Integer      ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES Login Access Control" WHERE (Company Group ID=FIELD(Group ID)));
                                                   CaptionML=ENU=Login Access Controls;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 4   ;   ;Companies           ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES Company Group Line" WHERE (Company Group ID=FIELD(Group ID)));
                                                   CaptionML=ENU=Companies;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 5   ;   ;Created By User     ;Code50        ;CaptionML=ENU=Created By User;
                                                   Editable=No }
    { 6   ;   ;Created Date Time   ;DateTime      ;CaptionML=ENU=Created Date Time;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Group ID                                ;Clustered=Yes }
    {    ;Created Date Time                        }
    {    ;Created By User,Created Date Time        }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ESLoginAccessControl@1240520009 : Record 14123528;
      ESCompanyGroupLine@1240520008 : Record 14123531;
      Text002@1002 : TextConst 'ENU=1 Company';
      Text003@1003 : TextConst 'ENU=%1 Companies';
      Text004@1004 : TextConst 'ENU=, Incomplete list';
      Text005@1005 : TextConst 'ENU=Get New Companies';
      Text006@1006 : TextConst 'ENU=%1 Company Groups have been created.';
      ESSecuritySetup@1240520010 : Record 14123521;
      Text001@1001 : TextConst 'ENU=Inserted %1 %2';
      Text007@1007 : TextConst 'ENU=Modified %1 %2';
      Text008@1008 : TextConst 'ENU=Deleted %1 %2';

    PROCEDURE GetNewCompanies@1240520009(ShowStatus@1240520009 : Boolean);
    VAR
      ESMgt@1240520008 : Codeunit 14123501;
    BEGIN
      CLEAR(ESMgt);

      ESMgt.Live2ES(
        ShowStatus,Text005,FALSE,FALSE,FALSE,FALSE,TRUE,FALSE,FALSE);

      CreateCompanyGroupPerCompany(TRUE,ShowStatus);
    END;

    PROCEDURE CreateCompanyGroupPerCompany@1240520010(OnlyNewCompanies@1240520002 : Boolean;ShowStatus@1240520013 : Boolean);
    VAR
      ESCompany@1240520008 : Record 14123507;
      ESCompanyGroup@1240520010 : Record 14123530;
      ESCompanyGroupLine@1240520011 : Record 14123531;
      ESSecuritySetup@1240520001 : Record 14123521;
      GroupsCreated@1240520009 : Integer;
      CreateCompanyGroup@1240520012 : Boolean;
    BEGIN
      ESSecuritySetup.GET;
      ESSecuritySetup.TestPublishCompany;

      ESCompany.RESET;
      IF ESCompany.FIND('-') THEN
        REPEAT
          ESCompanyGroupLine.RESET;
          ESCompanyGroupLine.SETRANGE("Company Name",ESCompany.Name);
          IF ESCompanyGroupLine.FIND('-') THEN BEGIN
            IF NOT OnlyNewCompanies THEN BEGIN
              CreateCompanyGroup := TRUE;

              REPEAT
                ESCompanyGroup.GET(ESCompanyGroupLine."Company Group ID");
                ESCompanyGroup.CALCFIELDS(Companies);
                IF ESCompanyGroup.Companies = 1 THEN
                  CreateCompanyGroup := FALSE;
              UNTIL (ESCompanyGroupLine.NEXT = 0) OR NOT CreateCompanyGroup;
            END ELSE
              CreateCompanyGroup := FALSE;
          END ELSE
            CreateCompanyGroup := TRUE;

          IF CreateCompanyGroup THEN
            IF NOT ESCompanyGroup.GET(ESCompany.Name) THEN BEGIN
              ESCompanyGroup.INIT;
              ESCompanyGroup."Group ID" := ESCompany.Name;
              ESCompanyGroup.Name := ESCompany.Name;
              ESCompanyGroup.INSERT(TRUE);

              ESCompanyGroupLine.INIT;
              ESCompanyGroupLine."Company Group ID" := ESCompany.Name;
              ESCompanyGroupLine."Company Name" := ESCompany.Name;
              ESCompanyGroupLine.INSERT(TRUE);

              GroupsCreated := GroupsCreated + 1;
            END;
        UNTIL ESCompany.NEXT = 0;

      IF ShowStatus AND (NOT OnlyNewCompanies OR (GroupsCreated > 0)) THEN
        MESSAGE(Text006,GroupsCreated);
      ESSecuritySetup.Comment(1,STRSUBSTNO(Text006,GroupsCreated));
    END;

    PROCEDURE CompanyList@1240520008(CompanyGroupID@1240520008 : Code[30]) CompanyText : Text[1000];
    VAR
      ESCompanyGroupLine@1240520009 : Record 14123531;
      ESCompanyGroupLineTmp@1240520010 : TEMPORARY Record 14123531;
      TotalTextLen@1240520011 : Integer;
      TotalCompanies@1240520012 : Integer;
    BEGIN
      ESCompanyGroupLine.RESET;
      ESCompanyGroupLine.SETRANGE("Company Group ID",CompanyGroupID);
      IF ESCompanyGroupLine.FIND('-') THEN
        REPEAT
          ESCompanyGroupLineTmp := ESCompanyGroupLine;
          ESCompanyGroupLineTmp.INSERT;

          TotalTextLen := TotalTextLen + STRLEN(ESCompanyGroupLine."Company Name");
          TotalCompanies := TotalCompanies + 1;
        UNTIL ESCompanyGroupLine.NEXT = 0;

      IF TotalCompanies = 1 THEN
        CompanyText := Text002
      ELSE
        CompanyText := STRSUBSTNO(Text003,TotalCompanies);
      IF STRLEN(CompanyText) + TotalTextLen + 4 * TotalCompanies > MAXSTRLEN(CompanyText) THEN
        CompanyText := CompanyText + Text004;

      IF ESCompanyGroupLineTmp.FIND('-') THEN
        REPEAT
          IF STRLEN(CompanyText) + 4 + STRLEN(ESCompanyGroupLineTmp."Company Name") <=
             MAXSTRLEN(CompanyText)
          THEN
            CompanyText := CompanyText + STRSUBSTNO(', "%1"',ESCompanyGroupLineTmp."Company Name");
        UNTIL ESCompanyGroupLineTmp.NEXT = 0;
    END;

    PROCEDURE FormatAsTextLine@1240520050(VAR ESTextLine@1240520011 : Record 14123520;NoOfRelatedKeyFields@1240520009 : Integer;CalcFlowFields@1240520012 : Boolean;UserDateTimeDetails@1240520008 : Boolean) : Boolean;
    VAR
      ESFormatMgt@1240520013 : Codeunit 14123505;
      RecRef@1240520010 : RecordRef;
    BEGIN
      ESFormatMgt.NewRecord(TABLECAPTION,"Group ID",'','','','','','','','','',1,NoOfRelatedKeyFields);
      ESFormatMgt.AddField(FIELDCAPTION(Name),FORMAT(Name));

      IF UserDateTimeDetails THEN BEGIN
        ESFormatMgt.AddField(FIELDCAPTION("Created By User"),FORMAT("Created By User"));
        IF "Created Date Time" <> 0DT THEN
          ESFormatMgt.AddField(FIELDCAPTION("Created Date Time"),FORMAT("Created Date Time"));
      END;

      IF CalcFlowFields THEN BEGIN
        CALCFIELDS("Login Access Controls",Companies);

        IF "Login Access Controls" <> 0 THEN
          ESFormatMgt.AddFlowField(FIELDCAPTION(
            "Login Access Controls"),FORMAT("Login Access Controls"));
        IF Companies <> 0 THEN
          ESFormatMgt.AddFlowField(FIELDCAPTION(Companies),FORMAT(Companies));
      END;

      RecRef.OPEN(DATABASE::"ES Company Group");
      RecRef.GETTABLE(Rec);
      ESFormatMgt.FormatNewESTextLine(ESTextLine,DATABASE::"ES Company Group",RecRef.RECORDID);

      EXIT(ESFormatMgt.NoTextOverflow);
    END;

    PROCEDURE Comment@1240520020(FunctionNo@1240520008 : ' ,Insert,Show,Exist,DeleteAll';NewText@1240520010 : Text[250]) : Boolean;
    VAR
      ESComment@1240520009 : Record 14123524;
    BEGIN
      ESComment.RESET;
      ESComment.SETRANGE("Table ID",DATABASE::"ES Company Group");
      ESComment.SETRANGE(Code,"Group ID");
      EXIT(ESComment.Functions(FunctionNo,NewText));
    END;

    BEGIN
    END.
  }
}

