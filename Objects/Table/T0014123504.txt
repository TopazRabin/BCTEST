OBJECT Table 14123504 ES Windows Access Control
{
  OBJECT-PROPERTIES
  {
    Date=04/14/15;
    Time=12:00:00 PM;
    Version List=ES1.41;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               Comment(4,'');
             END;

    OnRename=BEGIN
               ERROR(Text001);
             END;

    CaptionML=ENU=User Access Control;
    LookupPageID=Page14123504;
    DrillDownPageID=Page14123504;
  }
  FIELDS
  {
    { 1   ;   ;Login SID           ;GUID          ;TableRelation="ES Windows Login".SID;
                                                   CaptionML=ENU=User Security ID;
                                                   NotBlank=Yes }
    { 2   ;   ;Role ID             ;Code20        ;TableRelation="ES Role"."Role ID";
                                                   CaptionML=ENU=Permission Set ID;
                                                   NotBlank=Yes }
    { 3   ;   ;Company Name        ;Text30        ;TableRelation="ES Company".Name;
                                                   CaptionML=ENU=Company Name }
    { 5   ;   ;Login ID            ;Text131       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Windows Login".ID WHERE (SID=FIELD(Login SID)));
                                                   CaptionML=ENU=Login ID;
                                                   Editable=No }
    { 7   ;   ;Role Name           ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Role".Name WHERE (Role ID=FIELD(Role ID)));
                                                   CaptionML=ENU=Permission Set Name;
                                                   Editable=No }
    { 1002;   ;User ID             ;Code50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Windows Login"."User ID" WHERE (SID=FIELD(Login SID)));
                                                   CaptionML=ENU=User ID;
                                                   Editable=No }
    { 1003;   ;Windows Login Exists;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Windows Login" WHERE (SID=FIELD(Login SID)));
                                                   CaptionML=ENU=User Exists;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Login SID,Role ID,Company Name          ;Clustered=Yes }
    {    ;Role ID                                  }
    {    ;Company Name,Role ID                     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Rename not allowed.';

    PROCEDURE RoleWhereUsed@1240520013();
    VAR
      ESRoleWhereUsed@1240520008 : Page 14123565;
    BEGIN
      CLEAR(ESRoleWhereUsed);
      ESRoleWhereUsed.InitRole("Role ID");
      ESRoleWhereUsed.RUN;
    END;

    PROCEDURE CompanyWhereUsed@1240520008();
    VAR
      ESCompanyWhereUsed@1240520008 : Page 14123566;
    BEGIN
      CLEAR(ESCompanyWhereUsed);
      ESCompanyWhereUsed.InitCompany("Company Name");
      ESCompanyWhereUsed.RUN;
    END;

    PROCEDURE FormatAsTextLine@1240520011(VAR ESTextLine@1240520011 : Record 14123520;NoOfRelatedKeyFields@1240520009 : Integer;CalcFlowFields@1240520012 : Boolean) : Boolean;
    VAR
      ESFormatMgt@1240520013 : Codeunit 14123505;
      RecRef@1240520010 : RecordRef;
    BEGIN
      ESFormatMgt.NewRecord(
        TABLECAPTION,"Login SID","Role ID","Company Name",'','','','','','','',3,NoOfRelatedKeyFields);
      ESFormatMgt.AddField(FIELDCAPTION("Login ID"),FORMAT("Login ID"));
      IF CalcFlowFields THEN BEGIN
        CALCFIELDS("Role Name","User ID","Windows Login Exists");

        ESFormatMgt.AddFlowField(FIELDCAPTION("Role Name"),FORMAT("Role Name"));
        ESFormatMgt.AddFlowField(FIELDCAPTION("User ID"),FORMAT("User ID"));
        IF "Windows Login Exists" THEN
          ESFormatMgt.AddFlowField(
            FIELDCAPTION("Windows Login Exists"),FORMAT("Windows Login Exists"));
      END;

      RecRef.OPEN(DATABASE::"ES Windows Access Control");
      RecRef.GETTABLE(Rec);
      ESFormatMgt.FormatNewESTextLine(ESTextLine,DATABASE::"ES Windows Access Control",RecRef.RECORDID);

      EXIT(ESFormatMgt.NoTextOverflow);
    END;

    PROCEDURE Comment@1240520009(FunctionNo@1240520008 : ' ,Insert,Show,Exist,DeleteAll';NewText@1240520010 : Text[250]) : Boolean;
    VAR
      ESComment@1240520009 : Record 14123524;
    BEGIN
      ESComment.RESET;
      ESComment.SETRANGE("Table ID",DATABASE::"ES Windows Access Control");
      ESComment.SETRANGE("Role ID","Role ID");
      ESComment.SETRANGE(SID,COPYSTR("Login SID",1,MAXSTRLEN(ESComment.SID)));
      ESComment.SETRANGE("Company Name","Company Name");
      EXIT(ESComment.Functions(FunctionNo,NewText));
    END;

    BEGIN
    END.
  }
}

