OBJECT Table 14123525 ES Role Group
{
  OBJECT-PROPERTIES
  {
    Date=04/30/18;
    Time=12:00:00 PM;
    Version List=ES1.41.51;
  }
  PROPERTIES
  {
    DataCaptionFields=Group ID,Name;
    OnInsert=BEGIN
               ESSecuritySetup.GET;
               ESSecuritySetup.TestPublishCompany;

               IF "File Name" = '' THEN BEGIN
                 IF ESSecuritySetup."Default Role Group File Name" <> '' THEN
                   "File Name" :=
                     ESSecuritySetup.BuildDefaultFileName(
                       ESSecuritySetup."Default Role Group File Name",MAXSTRLEN("File Name"),"Group ID");
               END;

               Comment(1,STRSUBSTNO(Text019,TABLECAPTION,"Group ID"));

               "Created By User" := USERID;
               "Created Date Time" := CURRENTDATETIME;
             END;

    OnModify=BEGIN
               Comment(1,STRSUBSTNO(Text020,TABLECAPTION,"Group ID"));
             END;

    OnDelete=BEGIN
               ESRoleGroupLine.RESET;
               ESRoleGroupLine.SETRANGE("Role Group ID",'<>%1',"Group ID");
               ESRoleGroupLine.SETRANGE("Role Type",ESRoleGroupLine."Role Type"::Group);
               ESRoleGroupLine.SETRANGE("Role ID","Group ID");
               IF ESRoleGroupLine.FIND('-') THEN
                 ESRoleGroupLine.TESTFIELD("Role ID",'');

               ESLoginAccessControl.RESET;
               ESLoginAccessControl.SETRANGE("Role Type",ESLoginAccessControl."Role Type"::Group);
               ESLoginAccessControl.SETRANGE("Role ID","Group ID");
               IF ESLoginAccessControl.FIND('-') THEN
                 ESLoginAccessControl.TESTFIELD("Role ID",'');

               ESRoleGroupLine.RESET;
               ESRoleGroupLine.SETRANGE("Role Group ID","Group ID");
               ESRoleGroupLine.DELETEALL(TRUE);

               Comment(1,STRSUBSTNO(Text021,TABLECAPTION,"Group ID"));
             END;

    OnRename=BEGIN
               ERROR(Text001);
             END;

    CaptionML=ENU=Permission Group;
    LookupPageID=Page14123532;
    DrillDownPageID=Page14123532;
  }
  FIELDS
  {
    { 1   ;   ;Group ID            ;Code20        ;CaptionML=ENU=Group ID;
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text250       ;CaptionML=ENU=Name }
    { 3   ;   ;Login Access Controls;Integer      ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES Login Access Control" WHERE (Role Type=CONST(Group),
                                                                                                      Role ID=FIELD(Group ID)));
                                                   CaptionML=ENU=Login Access Controls;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 4   ;   ;Created By User     ;Code50        ;CaptionML=ENU=Created By User;
                                                   Editable=No }
    { 5   ;   ;Created Date Time   ;DateTime      ;CaptionML=ENU=Created Date Time;
                                                   Editable=No }
    { 6   ;   ;Lines               ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES Role Group Line" WHERE (Role Group ID=FIELD(Group ID)));
                                                   CaptionML=ENU=Lines;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 7   ;   ;Role Lines          ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES Role Group Line" WHERE (Role Group ID=FIELD(Group ID),
                                                                                                 Role Type=CONST(Role)));
                                                   CaptionML=ENU=Permission Set Lines;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 8   ;   ;Role Group Lines    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES Role Group Line" WHERE (Role Group ID=FIELD(Group ID),
                                                                                                 Role Type=CONST(Group)));
                                                   CaptionML=ENU=Permission Group Lines;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 9   ;   ;Exported Date Time  ;DateTime      ;CaptionML=ENU=Exported Date Time;
                                                   Editable=No }
    { 10  ;   ;Imported Date Time  ;DateTime      ;CaptionML=ENU=Imported Date Time;
                                                   Editable=No }
    { 11  ;   ;File Name           ;Text250       ;OnValidate=VAR
                                                                ESObjectExplorer@1240520001 : Record 14123901;
                                                              BEGIN
                                                                ESObjectExplorer.CheckCorrectFileName("File Name",TRUE);
                                                              END;

                                                   OnLookup=VAR
                                                              ESObjectExplorer@1240520001 : Record 14123901;
                                                              CommonDialogMgt@1240520008 : Codeunit 14123901;
                                                            BEGIN
                                                              "File Name" := CommonDialogMgt.OpenFile(FIELDCAPTION("File Name"),"File Name",1,'*.txt',1);
                                                              ESObjectExplorer.CheckCorrectFileName("File Name",TRUE);
                                                            END;

                                                   CaptionML=ENU=File Name }
    { 12  ;   ;Summary Permissions ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES Summary Permission" WHERE (Type=CONST(Role Group),
                                                                                                    Code=FIELD(Group ID)));
                                                   CaptionML=ENU=Summary Permissions;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 13  ;   ;Summary Results     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES Summary Result" WHERE (Type=CONST(Role Group),
                                                                                                Code=FIELD(Group ID)));
                                                   CaptionML=ENU=Summary Results;
                                                   BlankZero=Yes;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Group ID                                ;Clustered=Yes }
    {    ;Created Date Time                        }
    {    ;Created By User,Created Date Time        }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ESRoleGroupLine@1240520008 : Record 14123526;
      ESLoginAccessControl@1240520009 : Record 14123528;
      Text001@1001 : TextConst 'ENU=Rename not allowed.';
      ESSecuritySetup@1240520010 : Record 14123521;
      Text002@1240520021 : TextConst 'ENU=Nothing added.';
      Text003@1240520019 : TextConst 'ENU=Only %1 existing Permission Group Lines selected.';
      Text004@1240520018 : TextConst 'ENU=Add %1 Permission Group Lines to Permission Group %2?\';
      Text005@1240520017 : TextConst 'ENU=%3 Permission Group Lines already exists';
      Text006@1240520015 : TextConst 'ENU=%1 Permission Group Lines added (%2 existing).';
      Text013@1240520002 : TextConst 'ENU=Nothing imported.';
      Text014@1240520001 : TextConst 'ENU=File Name must be entered.';
      Text027@1240520003 : TextConst 'ENU=%1 %2 exported to %3 with %4 records.';
      Text033@1240520014 : TextConst 'ENU=File %1 already exists, overwrite?';
      Text034@1240520013 : TextConst 'ENU=Nothing has been exported.';
      Text035@1240520012 : TextConst 'ENU=Exporting %1 to file\';
      Text036@1240520011 : TextConst 'ENU=Process #1################################################';
      Text037@1240520007 : TextConst 'ENU=Updating Version';
      Text038@1240520006 : TextConst 'ENU=Creating File';
      Text011@1240520005 : TextConst 'ENU=Closing File';
      Text012@1240520004 : TextConst 'ENU=Permission Group %1 exported to %2 with %3 records.';
      ESFileMgt@1240520016 : Codeunit 14123502;
      Text015@1240520024 : TextConst 'ENU=Importing %1 from file\';
      Text016@1240520023 : TextConst 'ENU=Process #1################################################\';
      Text017@1240520022 : TextConst 'ENU=Opening file';
      Text018@1240520020 : TextConst 'ENU=%1 records have been imported from %2.';
      Text007@1002 : TextConst 'ENU=Checking Permission Group Lines';
      Text008@1003 : TextConst 'ENU=%1 is not valid content for this filetype.';
      Text009@1004 : TextConst 'ENU=Nothing has been exported.';
      Text010@1005 : TextConst 'ENU=Imported %1';
      Text019@1006 : TextConst 'ENU=Inserted %1 %2';
      Text020@1007 : TextConst 'ENU=Modified %1 %2';
      Text021@1008 : TextConst 'ENU=Deleted %1 %2';
      Text101@1104 : TextConst 'ENU=Downloading File.';
      Text102@1100 : TextConst 'ENU=Uploading file.';
      Text201@1009 : TextConst 'ENU=%1 for %2 is not unique.';
      Text202@1010 : TextConst 'ENU=Inserted %1 %2 and %3 %4.';
      Text203@1011 : TextConst 'ENU=Deleted %1 Duplicate %2.';

    PROCEDURE AddRoleGroupLinesFromRoles@1240520015();
    VAR
      ESRoleTmp@1240520008 : TEMPORARY Record 14123505;
      ESRoleGroupLine@1240520012 : Record 14123526;
      ESRolesForm@1240520009 : Page 14123505;
      ExistingRoles@1240520010 : Integer;
      NewRoles@1240520011 : Integer;
      AddedRoles@1240520013 : Integer;
    BEGIN
      TESTFIELD("Group ID");

      CLEAR(ESRolesForm);
      ESRolesForm.LOOKUPMODE(TRUE);
      IF ESRolesForm.RUNMODAL <> ACTION::LookupOK THEN
        EXIT;

      ESRoleTmp.RESET;
      ESRoleTmp.DELETEALL;
      ESRolesForm.GetSelection(ESRoleTmp);
      IF NOT ESRoleTmp.FIND('-') THEN
        ERROR(Text002);

      ExistingRoles := 0;
      NewRoles := 0;
      ESRoleTmp.FIND('-');
      REPEAT
        IF ESRoleGroupLine.GET("Group ID",ESRoleGroupLine."Role Type"::Role,ESRoleTmp."Role ID") THEN
          ExistingRoles := ExistingRoles + 1
        ELSE
          NewRoles := NewRoles + 1;
      UNTIL ESRoleTmp.NEXT = 0;

      IF NewRoles = 0 THEN
        ERROR(Text003,ExistingRoles);

      IF NOT CONFIRM(
               Text004 +
               Text005,
               FALSE,NewRoles,"Group ID",ExistingRoles)
      THEN
        ERROR(Text002);

      ExistingRoles := 0;
      AddedRoles := 0;
      ESRoleTmp.FIND('-');
      REPEAT
        IF NOT ESRoleGroupLine.GET("Group ID",ESRoleGroupLine."Role Type"::Role,ESRoleTmp."Role ID")
        THEN BEGIN
          IF NOT ESRoleGroupLine.GET("Group ID",ESRoleGroupLine."Role Type"::Role,ESRoleTmp."Role ID")
          THEN BEGIN
            ESRoleGroupLine.INIT;
            ESRoleGroupLine."Role Group ID" := "Group ID";
            ESRoleGroupLine."Role Type" := ESRoleGroupLine."Role Type"::Role;
            ESRoleGroupLine."Role ID" := ESRoleTmp."Role ID";
            ESRoleGroupLine.INSERT(TRUE);

            AddedRoles := AddedRoles + 1;
          END;
        END;
      UNTIL ESRoleTmp.NEXT = 0;

      MESSAGE(Text006,AddedRoles,ExistingRoles);
    END;

    PROCEDURE AddRoleGrLinesFromRoleGroups@1240520012();
    VAR
      ESRoleGroupTmp@1240520008 : TEMPORARY Record 14123525;
      ESRoleGroupLine@1240520012 : Record 14123526;
      ESRoleGroupsForm@1240520009 : Page 14123532;
      ExistingRoles@1240520010 : Integer;
      NewRoles@1240520011 : Integer;
      AddedRoles@1240520013 : Integer;
    BEGIN
      TESTFIELD("Group ID");

      CLEAR(ESRoleGroupsForm);
      ESRoleGroupsForm.LOOKUPMODE(TRUE);
      IF ESRoleGroupsForm.RUNMODAL <> ACTION::LookupOK THEN
        EXIT;

      ESRoleGroupTmp.RESET;
      ESRoleGroupTmp.DELETEALL;
      ESRoleGroupsForm.GetSelection(ESRoleGroupTmp);
      IF NOT ESRoleGroupTmp.FIND('-') THEN
        ERROR(Text002);

      ExistingRoles := 0;
      NewRoles := 0;
      ESRoleGroupTmp.FIND('-');
      REPEAT
        IF ESRoleGroupLine.GET(
             "Group ID",ESRoleGroupLine."Role Type"::Group,ESRoleGroupTmp."Group ID")
        THEN
          ExistingRoles := ExistingRoles + 1
        ELSE
          NewRoles := NewRoles + 1;
      UNTIL ESRoleGroupTmp.NEXT = 0;

      IF NewRoles = 0 THEN
        ERROR(Text003,ExistingRoles);

      IF NOT CONFIRM(
               Text004 +
               Text005,
               FALSE,NewRoles,"Group ID",ExistingRoles)
      THEN
        ERROR(Text002);

      ExistingRoles := 0;
      AddedRoles := 0;
      ESRoleGroupTmp.FIND('-');
      REPEAT
        IF NOT ESRoleGroupLine.GET(
                 "Group ID",ESRoleGroupLine."Role Type"::Group,ESRoleGroupTmp."Group ID")
        THEN BEGIN
          ESRoleGroupLine.INIT;
          ESRoleGroupLine."Role Group ID" := "Group ID";
          ESRoleGroupLine."Role Type" := ESRoleGroupLine."Role Type"::Group;
          ESRoleGroupLine."Role ID" := ESRoleGroupTmp."Group ID";
          ESRoleGroupLine.INSERT(TRUE);

          AddedRoles := AddedRoles + 1;
        END;
      UNTIL ESRoleGroupTmp.NEXT = 0;

      MESSAGE(Text006,AddedRoles,ExistingRoles);
    END;

    PROCEDURE CopyNAVUserGroups@1240520003(RemoveDuplicates@1014 : Boolean;ShowStatus@1002 : Boolean);
    VAR
      UserGroup@1000 : Record 9000;
      UserGroupPermissionSet@1001 : Record 9003;
      ESRoleGroup@1003 : Record 14123525;
      ESRoleGroupLine@1004 : Record 14123526;
      ESLogin@1010 : Record 14123527;
      ESLoginAccessControl@1009 : Record 14123528;
      ESCompanyGroup@1012 : Record 14123530;
      ESCompanyGroupLine@1013 : Record 14123531;
      UserGroupMember@1005 : Record 9001;
      NewRoleGroups@1007 : Integer;
      NewLoginAccessControls@1008 : Integer;
      DeletedAccessControls@1015 : Integer;
      TempText@1016 : Text[1000];
    BEGIN
      ESSecuritySetup.GET;
      ESSecuritySetup.TestPublishCompany;

      UserGroup.RESET;
      IF UserGroup.FIND('-') THEN BEGIN
        REPEAT
          ESRoleGroup.INIT;
          ESRoleGroup.VALIDATE("Group ID",UserGroup.Code);
          ESRoleGroup.VALIDATE(Name,UserGroup.Name);
          ESRoleGroup.INSERT(TRUE);

          UserGroupPermissionSet.RESET;
          UserGroupPermissionSet.SETRANGE("User Group Code",UserGroup.Code);
          IF UserGroupPermissionSet.FIND('-') THEN BEGIN
            REPEAT
              ESRoleGroupLine.INIT;
              ESRoleGroupLine.VALIDATE("Role Group ID",ESRoleGroup."Group ID");
              ESRoleGroupLine.VALIDATE("Role Type",ESRoleGroupLine."Role Type"::Role);
              ESRoleGroupLine.VALIDATE("Role ID",UserGroupPermissionSet."Role ID");
              ESRoleGroupLine.INSERT(TRUE);
            UNTIL UserGroupPermissionSet.NEXT = 0;
          END;

          NewRoleGroups := NewRoleGroups + 1;
        UNTIL UserGroup.NEXT = 0;
      END;

      UserGroupMember.RESET;
      IF UserGroupMember.FIND('-') THEN
        REPEAT
          ESLogin.RESET;
          ESLogin.SETRANGE("Login SID",UserGroupMember."User Security ID");
          ESLogin.FIND('-');

          ESLoginAccessControl.INIT;
          ESLoginAccessControl.VALIDATE("Login Type",ESLogin.Type);
          ESLoginAccessControl.VALIDATE("Login User ID",ESLogin."User ID");
          ESLoginAccessControl.VALIDATE("Role Type",ESLoginAccessControl."Role Type"::Group);
          ESLoginAccessControl.VALIDATE("Role ID",UserGroupMember."User Group Code");
          IF UserGroupMember."Company Name" <> '' THEN BEGIN
            ESCompanyGroupLine.RESET;
            ESCompanyGroupLine.SETRANGE("Company Name",UserGroupMember."Company Name");
            ESCompanyGroupLine.FIND('-');
            IF ESCompanyGroupLine.NEXT > 0 THEN
              ERROR(Text201,ESCompanyGroup.TABLECAPTION,UserGroupMember."Company Name");
            ESLoginAccessControl.VALIDATE("Company Group ID",ESCompanyGroupLine."Company Group ID");
          END ELSE
            ESLoginAccessControl."Company Group ID" := '';
          ESLoginAccessControl.SetAllowBlankNoQuestion(TRUE,FALSE);
          ESLoginAccessControl.INSERT(TRUE);

          NewLoginAccessControls := NewLoginAccessControls + 1;
        UNTIL UserGroupMember.NEXT = 0;

      TempText := Text202;

      IF RemoveDuplicates THEN BEGIN
        ESLogin.RESET;
        IF ESLogin.FIND('-') THEN
          REPEAT
            DeletedAccessControls := DeletedAccessControls + ESLogin.RemoveDuplicateRoles(FALSE);
          UNTIL ESLogin.NEXT = 0;

        TempText :=
          TempText + ' ' +
          STRSUBSTNO(Text203,DeletedAccessControls,ESLoginAccessControl.TABLECAPTION);
      END;

      IF ShowStatus THEN
        MESSAGE(
          TempText,NewRoleGroups,ESRoleGroup.TABLECAPTION,
          NewLoginAccessControls,ESLoginAccessControl.TABLECAPTION);
      ESSecuritySetup.Comment(
        1,
        STRSUBSTNO(
          TempText,NewRoleGroups,ESRoleGroup.TABLECAPTION,
          NewLoginAccessControls,ESLoginAccessControl.TABLECAPTION));
    END;

    PROCEDURE UpdateSummaryPermissions@1240520001(ShowStatus@1240520002 : Boolean;ShowProgress@1240520003 : Boolean);
    VAR
      ESMgt@1240520001 : Codeunit 14123501;
    BEGIN
      ESMgt.UpdateRoleGroupSummaryPerm(Rec,TRUE,ShowStatus,ShowProgress);
    END;

    PROCEDURE ExportMultipleRoleGroups@1240520002(VAR ESRoleGroup@1240520004 : Record 14123525;ShowStatus@1240520021 : Boolean;ExportFileName@1240520001 : Text[250]);
    VAR
      ESEnterFileName@1240520002 : Page 14123560;
      FileMgt@1000 : Codeunit 419;
      TempServerFile@1001 : File;
      Window@1003 : Dialog;
      TempServerFileName@1002 : Text[1000];
      TotalRecords@1240520008 : Integer;
      TotalCodes@1240520003 : Integer;
    BEGIN
      IF ExportFileName = '' THEN BEGIN
        CLEAR(ESEnterFileName);
        ESEnterFileName.LOOKUPMODE(TRUE);
        ESEnterFileName.SelectNewFile(TRUE);
        IF ESEnterFileName.RUNMODAL <> ACTION::LookupOK THEN
          ERROR(Text009);
        ExportFileName := ESEnterFileName.GetFileName;
      END;

      TempServerFile.WRITEMODE(TRUE);
      TempServerFile.TEXTMODE(TRUE);
      TempServerFile.CREATETEMPFILE;
      TempServerFileName := TempServerFile.NAME;
      TempServerFile.CLOSE;

      IF GUIALLOWED THEN
        Window.OPEN(Text036);

      IF ESRoleGroup.FIND('-') THEN
        REPEAT
          IF GUIALLOWED THEN
            Window.UPDATE(1,ESRoleGroup."Group ID");

          TotalCodes := TotalCodes + 1;

          TotalRecords :=
            TotalRecords + ESRoleGroup.ExportRoleGroup(FALSE,TRUE,TRUE,TempServerFileName,FALSE);
        UNTIL ESRoleGroup.NEXT = 0;

      IF GUIALLOWED THEN
        Window.UPDATE(1,Text101);
      FileMgt.DownloadToFile(TempServerFileName,ExportFileName);

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(
          Text027,
          TotalCodes,TABLECAPTION,ExportFileName,TotalRecords);
    END;

    PROCEDURE ExportRoleGroup@1240520014(ShowStatus@1240520021 : Boolean;ConfirmOverwrite@1240520020 : Boolean;AppendToFile@1240520001 : Boolean;ExportFileName@1240520002 : Text[250];ShowProgress@1240520004 : Boolean) : Integer;
    VAR
      ESEnterFileName@1240520003 : Page 14123560;
      FileMgt@1001 : Codeunit 419;
      Window@1240520009 : Dialog;
      OutputFile@1240520019 : File;
      Value@1240520022 : ARRAY [100] OF Text[1000];
      LocalFileName@1000 : Text[1000];
      TotalRecords@1240520008 : Integer;
    BEGIN
      IF NOT AppendToFile THEN BEGIN
        IF ExportFileName = '' THEN BEGIN
          CLEAR(ESEnterFileName);
          ESEnterFileName.LOOKUPMODE(TRUE);
          ESEnterFileName.SelectNewFile(TRUE);
          IF ESEnterFileName.RUNMODAL <> ACTION::LookupOK THEN
            ERROR(Text009);
          ExportFileName := ESEnterFileName.GetFileName;
        END;

        IF ConfirmOverwrite THEN
          IF EXISTS(ExportFileName) THEN
            IF NOT CONFIRM(Text033,FALSE,ExportFileName) THEN
              ERROR(Text034);

        LocalFileName := ExportFileName;
        OutputFile.WRITEMODE(TRUE);
        OutputFile.TEXTMODE(TRUE);
        OutputFile.CREATETEMPFILE;
        ExportFileName := OutputFile.NAME;
        OutputFile.CLOSE;
      END;

      IF GUIALLOWED AND ShowProgress THEN
        Window.OPEN(STRSUBSTNO(ESSecuritySetup.ExtendProgressText(Text035),TABLECAPTION) + Text036);

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,Text037);
      "Exported Date Time" := CREATEDATETIME(TODAY,TIME);
      MODIFY(TRUE);

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,Text038);
      OutputFile.WRITEMODE(TRUE);
      OutputFile.TEXTMODE(TRUE);
      IF EXISTS(ExportFileName) AND AppendToFile THEN BEGIN
        OutputFile.OPEN(ExportFileName);
        OutputFile.SEEK(OutputFile.LEN);
      END ELSE
        OutputFile.CREATE(ExportFileName);

      CLEAR(ESFileMgt);
      ESFileMgt.SetDelimiters('~"','"~',',');

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,'HEADER');
      Value[1] := ESFileMgt.FormatInteger(0) +'V1';
      Value[2] := 'HEADER';
      Value[3] := "Group ID";
      Value[4] := Name;
      Value[5] := USERID;
      Value[6] := ESFileMgt.FormatDate(TODAY);
      Value[7] := ESFileMgt.FormatTime(TIME);
      OutputFile.WRITE(ESFileMgt.FormatValues(Value,7));

      Value[1] := ESFileMgt.FormatInteger(DATABASE::"ES Role Group") + 'V1';
      Value[2] := "Group ID";
      Value[3] := Name;
      Value[4] := "Created By User";
      Value[5] := ESFileMgt.FormatDateTime("Created Date Time");

      OutputFile.WRITE(ESFileMgt.FormatValues(Value,5));

      TotalRecords := TotalRecords + 1;

      ESRoleGroupLine.RESET;
      ESRoleGroupLine.SETRANGE("Role Group ID","Group ID");
      IF ESRoleGroupLine.FIND('-') THEN
        REPEAT
          Value[1] := ESFileMgt.FormatInteger(DATABASE::"ES Role Group Line") + 'V1';
          Value[2] := ESRoleGroupLine."Role Group ID";
          Value[3] := ESFileMgt.FormatInteger(ESRoleGroupLine."Role Type");
          Value[4] := ESRoleGroupLine."Role ID";
          Value[5] := ESRoleGroupLine."Created By User";
          Value[6] := ESFileMgt.FormatDateTime(ESRoleGroupLine."Created Date Time");
          OutputFile.WRITE(ESFileMgt.FormatValues(Value,6));

          TotalRecords := TotalRecords + 1;
        UNTIL ESRoleGroupLine.NEXT = 0;

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,'TOTALS');
      Value[1] := ESFileMgt.FormatInteger(0) + 'V1';
      Value[2] := 'TOTALS';
      Value[3] := "Group ID";
      Value[4] := Name;
      Value[5] := USERID;
      Value[6] := ESFileMgt.FormatDate(TODAY);
      Value[7] := ESFileMgt.FormatTime(TIME);
      Value[8] := ESFileMgt.FormatInteger(TotalRecords);
      OutputFile.WRITE(ESFileMgt.FormatValues(Value,8));

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,Text011);
      OutputFile.CLOSE;

      IF NOT AppendToFile THEN BEGIN
        IF GUIALLOWED AND ShowProgress THEN
          Window.UPDATE(1,Text101);
        FileMgt.DownloadToFile(ExportFileName,LocalFileName);
      END;

      IF GUIALLOWED AND ShowProgress THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(
          Text012,
          "Group ID",LocalFileName,TotalRecords);

      EXIT(TotalRecords);
    END;

    PROCEDURE ImportRoleGroup@1240520013(ShowStatus@1240520011 : Boolean;ImportFileName@1240520018 : Text[250];OverWriteExisting@1240520001 : Boolean);
    VAR
      ESRoleGroup@1240520015 : Record 14123525;
      ESRoleGroup2@1240520004 : Record 14123525;
      ESRoleGroupLineTmp@1240520002 : TEMPORARY Record 14123526;
      ESRole@1240520003 : Record 14123505;
      ESEnterFileName@1240520019 : Page 14123560;
      FileMgt@1001 : Codeunit 419;
      InputFile@1240520009 : File;
      Window@1240520012 : Dialog;
      TempText@1240520010 : Text[1000];
      Value@1240520008 : ARRAY [100] OF Text[1000];
      ServerFileName@1000 : Text[1000];
      NextUpdatePos@1240520022 : Integer;
      TotalRecords@1240520023 : Integer;
    BEGIN
      IF ImportFileName = '' THEN BEGIN
        CLEAR(ESEnterFileName);
        ESEnterFileName.LOOKUPMODE(TRUE);
        IF ESEnterFileName.RUNMODAL <> ACTION::LookupOK THEN
          ERROR(Text013);
        ImportFileName := ESEnterFileName.GetFileName;
      END;

      IF ImportFileName = '' THEN
        ERROR(Text014);

      IF GUIALLOWED THEN
        Window.OPEN(
          STRSUBSTNO(ESSecuritySetup.ExtendProgressText(Text015),TABLECAPTION) +
          Text016 + '        @2@@@@@@@@@@@@@@@@@@@@@@@@@@@@');

      CLEAR(ESFileMgt);
      ESFileMgt.SetDelimiters('~"','"~',',');

      IF GUIALLOWED THEN
        Window.UPDATE(1,Text017);
      InputFile.WRITEMODE(FALSE);
      InputFile.TEXTMODE(TRUE);
      IF NOT InputFile.OPEN(ImportFileName) THEN BEGIN
        IF GUIALLOWED THEN
          Window.UPDATE(1,Text102);
        ServerFileName := FileMgt.UploadFileSilent(ImportFileName);
        IF GUIALLOWED THEN
          Window.UPDATE(1,Text017);

        IF NOT InputFile.OPEN(ServerFileName) THEN
          InputFile.OPEN(ImportFileName);
      END;
      WHILE InputFile.POS < InputFile.LEN DO BEGIN
        IF NextUpdatePos < InputFile.POS THEN BEGIN
          IF GUIALLOWED THEN
            Window.UPDATE(2,ROUND(10000 * (InputFile.POS / InputFile.LEN),1));
          NextUpdatePos := ROUND(InputFile.POS + InputFile.LEN / 500,1);
        END;

        InputFile.READ(TempText);
        ESFileMgt.SplitInValues(Value,TempText);

        CASE Value[1] OF
          '','0V1':
            ;
          '14123525V1':
            BEGIN
              IF GUIALLOWED THEN
                Window.UPDATE(1,Value[2]);

              ESRoleGroup.INIT;
              ESRoleGroup."Group ID" := Value[2];
              ESRoleGroup.Name := Value[3];
              ESRoleGroup."Created By User" := Value[4];
              ESRoleGroup."Created Date Time" := ESFileMgt.EvaluateDateTime(Value[5]);

              ESRoleGroup."Imported Date Time" := CREATEDATETIME(TODAY,TIME);

              IF OverWriteExisting AND ESRoleGroup2.GET(ESRoleGroup."Group ID") THEN BEGIN
                ESRoleGroup2.DELETE;

                ESRoleGroupLine.RESET;
                ESRoleGroupLine.SETRANGE("Role Group ID",ESRoleGroup2."Group ID");
                ESRoleGroupLine.DELETEALL;
              END;

              IF NOT ESRoleGroup.INSERT(TRUE) THEN
                ESRoleGroup.MODIFY(TRUE);

              ESRoleGroup.Comment(1,STRSUBSTNO(Text010,ESRoleGroup.TABLECAPTION));

              TotalRecords := TotalRecords + 1;
            END;
          '14123526V1':
            BEGIN
              ESRoleGroupLine.INIT;
              ESRoleGroupLine."Role Group ID" := Value[2];
              ESRoleGroupLine."Role Type" := ESFileMgt.EvaluateInteger(Value[3]);
              ESRoleGroupLine."Role ID" := Value[4];
              ESRoleGroupLine."Created By User" := Value[5];
              ESRoleGroupLine."Created Date Time" := ESFileMgt.EvaluateDateTime(Value[6]);

              ESRoleGroupLine.INSERT(TRUE);
              ESRoleGroupLineTmp := ESRoleGroupLine;
              ESRoleGroupLineTmp.INSERT;

              TotalRecords := TotalRecords + 1;
            END;
          ELSE
            ERROR(Text008,Value[1]);
        END;
      END;

      InputFile.CLOSE;
      IF ServerFileName <> '' THEN
        ERASE(ServerFileName);

      IF ESRoleGroupLineTmp.FIND('-') THEN BEGIN
        Window.UPDATE(1,Text007);

        REPEAT
          CASE ESRoleGroupLineTmp."Role Type" OF
            ESRoleGroupLineTmp."Role Type"::Role:
              ESRole.GET(ESRoleGroupLineTmp."Role ID");
            ESRoleGroupLineTmp."Role Type"::Group:
              ESRoleGroup.GET(ESRoleGroupLineTmp."Role ID");
          END;
        UNTIL ESRoleGroupLineTmp.NEXT = 0;
      END;

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(Text018,TotalRecords,ImportFileName);
    END;

    PROCEDURE FormatAsTextLine@1240520045(VAR ESTextLine@1240520011 : Record 14123520;NoOfRelatedKeyFields@1240520009 : Integer;CalcFlowFields@1240520012 : Boolean;UserDateTimeDetails@1240520008 : Boolean) : Boolean;
    VAR
      ESFormatMgt@1240520013 : Codeunit 14123505;
      RecRef@1240520010 : RecordRef;
    BEGIN
      ESFormatMgt.NewRecord(TABLECAPTION,"Group ID",'','','','','','','','','',1,NoOfRelatedKeyFields);
      ESFormatMgt.AddField(FIELDCAPTION(Name),FORMAT(Name));

      IF UserDateTimeDetails THEN BEGIN
        ESFormatMgt.AddField(FIELDCAPTION("Created By User"),FORMAT("Created By User"));
        IF "Created Date Time" <> 0DT THEN
          ESFormatMgt.AddField(FIELDCAPTION("Created Date Time"),FORMAT("Created Date Time"));
      END;

      IF CalcFlowFields THEN BEGIN
        CALCFIELDS("Login Access Controls",Lines,"Role Lines","Role Group Lines",
        "Summary Permissions","Summary Results");

        IF "Login Access Controls" <> 0 THEN
          ESFormatMgt.AddFlowField(
            FIELDCAPTION("Login Access Controls"),FORMAT("Login Access Controls"));
        IF Lines <> 0 THEN
          ESFormatMgt.AddFlowField(FIELDCAPTION(Lines),FORMAT(Lines));
        IF "Role Lines" <> 0 THEN
          ESFormatMgt.AddFlowField(FIELDCAPTION("Role Lines"),FORMAT("Role Lines"));
        IF "Role Group Lines" <> 0 THEN
          ESFormatMgt.AddFlowField(FIELDCAPTION("Role Group Lines"),FORMAT("Role Group Lines"));
        IF "Summary Permissions" <> 0 THEN
          ESFormatMgt.AddFlowField(FIELDCAPTION("Summary Permissions"),FORMAT("Summary Permissions"));
        IF "Summary Results" <> 0 THEN
          ESFormatMgt.AddFlowField(FIELDCAPTION("Summary Results"),FORMAT("Summary Results"));
      END;

      RecRef.OPEN(DATABASE::"ES Role Group");
      RecRef.GETTABLE(Rec);
      ESFormatMgt.FormatNewESTextLine(ESTextLine,DATABASE::"ES Role Group",RecRef.RECORDID);

      EXIT(ESFormatMgt.NoTextOverflow);
    END;

    PROCEDURE Comment@1240520020(FunctionNo@1240520008 : ' ,Insert,Show,Exist,DeleteAll';NewText@1240520010 : Text[250]) : Boolean;
    VAR
      ESComment@1240520009 : Record 14123524;
    BEGIN
      ESComment.RESET;
      ESComment.SETRANGE("Table ID",DATABASE::"ES Role Group");
      ESComment.SETRANGE(Code,"Group ID");
      EXIT(ESComment.Functions(FunctionNo,NewText));
    END;

    BEGIN
    END.
  }
}

