OBJECT Table 14002966 Pallet Journal Line
{
  OBJECT-PROPERTIES
  {
    Date=08/21/18;
    Time=12:00:00 PM;
    Version List=RF1.50.13;
  }
  PROPERTIES
  {
    CaptionML=ENU=Pallet Journal Line;
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Pallet Journal Template"."Pallet Journal Template";
                                                   CaptionML=ENU=Journal Template Name }
    { 2   ;   ;Journal Batch Name  ;Code10        ;CaptionML=ENU=Journal Batch Name }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 4   ;   ;Registering Date    ;Date          ;CaptionML=ENU=Registering Date }
    { 11  ;   ;Location Code       ;Code10        ;TableRelation=Location.Code;
                                                   CaptionML=ENU=Location Code }
    { 12  ;   ;Bin Code            ;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Location Code));
                                                   CaptionML=ENU=Bin Code }
    { 13  ;   ;Pallet No.          ;Code20        ;CaptionML=ENU=Pallet No. }
    { 14  ;   ;Box No.             ;Code20        ;CaptionML=ENU=Box No. }
    { 15  ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionString=Pallet,Box }
    { 25  ;   ;Item No.            ;Code20        ;TableRelation=Item.No.;
                                                   CaptionML=ENU=Item No. }
    { 26  ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=ENU=Variant Code }
    { 27  ;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=ENU=Unit of Measure Code }
    { 28  ;   ;Quantity Calc.      ;Decimal       ;CaptionML=ENU=Quantity Calc. }
    { 29  ;   ;Description         ;Text50        ;CaptionML=ENU=Description }
    { 30  ;   ;Quantity            ;Decimal       ;CaptionML=ENU=Quantity }
    { 32  ;   ;Zone Code           ;Code10        ;CaptionML=ENU=Zone Code }
    { 33  ;   ;To Bin Code         ;Code20        ;TableRelation=Bin.Code;
                                                   CaptionML=ENU=To Bin Code }
    { 34  ;   ;From Bin Code       ;Code20        ;TableRelation=Bin.Code;
                                                   CaptionML=ENU=From Bin Code }
    { 35  ;   ;To Zone Code        ;Code10        ;CaptionML=ENU=To Zone Code }
    { 36  ;   ;From Zone Code      ;Code10        ;CaptionML=ENU=From Zone Code }
    { 37  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code".Code;
                                                   CaptionML=ENU=Reason Code }
    { 38  ;   ;Qty. (Calculated)   ;Decimal       ;CaptionML=ENU=Qty. (Calculated) }
    { 39  ;   ;Qty. (Phys. Inventory);Decimal     ;CaptionML=ENU=Qty. (Phys. Inventory) }
    { 40  ;   ;Qty. (Base)         ;Decimal       ;CaptionML=ENU=Qty. (Base) }
    { 41  ;   ;Qty. (Absolute)     ;Decimal       ;CaptionML=ENU=Qty. (Absolute) }
    { 42  ;   ;Qty. (Absolute, Base);Decimal      ;CaptionML=ENU=Qty. (Absolute, Base) }
    { 43  ;   ;Qty. (Calculated) (Base);Decimal   ;CaptionML=ENU=Qty. (Calculated) (Base) }
    { 44  ;   ;Qty. Per Unit of Measure;Decimal   ;CaptionML=ENU=Qty. Per Unit of Measure }
    { 50  ;   ;Phys. Inventory     ;Boolean       ;CaptionML=ENU=Phys. Inventory }
    { 51  ;   ;Serial No.          ;Code20        ;CaptionML=ENU=Serial No. }
    { 52  ;   ;Lot No.             ;Code20        ;CaptionML=ENU=Lot No. }
    { 54  ;   ;Warranty Date       ;Date          ;CaptionML=ENU=Warranty Date }
    { 55  ;   ;Expiration Date     ;Date          ;CaptionML=ENU=Expiration Date }
    { 56  ;   ;Original Qty. (Calculated);Decimal ;CaptionML=ENU=Original Qty. (Calculated) }
    { 57  ;   ;Qty. (Phys. Inventory, Hide);Decimal;
                                                   CaptionML=ENU=Qty. (Phys. inventory, Hide) }
    { 58  ;   ;Cubage              ;Decimal       ;CaptionML=ENU=Cubage }
    { 59  ;   ;Weight              ;Decimal       ;CaptionML=ENU=Weight }
    { 60  ;   ;From Bin Type Code  ;Code10        ;CaptionML=ENU=From Bin Type Code }
    { 61  ;   ;To Bin Type Code    ;Code10        ;CaptionML=ENU=To Bin Type Code }
    { 62  ;   ;Entry Type          ;Option        ;CaptionML=ENU=Entry Type;
                                                   OptionString=Negative Adjmt.,Positive Adjmt.,Movement }
    { 63  ;   ;Qty (Inventory)     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Item Ledger Entry".Quantity WHERE (Item No.=FIELD(Item No.),
                                                                                                       Variant Code=FIELD(Variant Code),
                                                                                                       Location Code=FIELD(Location Code),
                                                                                                       Lot No.=FIELD(Lot No.)));
                                                   CaptionML=ENU=Qty (Inventory) }
    { 64  ;   ;Qty (Warehouse)     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Warehouse Entry".Quantity WHERE (Location Code=FIELD(Location Code),
                                                                                                     Item No.=FIELD(Item No.),
                                                                                                     Variant Code=FIELD(Variant Code),
                                                                                                     Lot No.=FIELD(Lot No.),
                                                                                                     Bin Code=FIELD(Bin Code)));
                                                   CaptionML=ENU=Qty (Warehouse) }
    { 65  ;   ;Qty (Inventory all Lots);Decimal   ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Item Ledger Entry".Quantity WHERE (Item No.=FIELD(Item No.),
                                                                                                       Variant Code=FIELD(Variant Code),
                                                                                                       Location Code=FIELD(Location Code)));
                                                   CaptionML=ENU=Qty (Inventory all Lots) }
    { 66  ;   ;Qty (Warehouse All Lots);Decimal   ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Warehouse Entry".Quantity WHERE (Location Code=FIELD(Location Code),
                                                                                                     Item No.=FIELD(Item No.),
                                                                                                     Variant Code=FIELD(Variant Code),
                                                                                                     Bin Code=FIELD(Bin Code)));
                                                   CaptionML=ENU=Qty (Warehouse All Lots) }
    { 67  ;   ;Qty (Package Line)  ;Decimal       ;FieldClass=Normal;
                                                   CaptionML=ENU=Qty (Package Line) }
    { 100 ;   ;Warehouse Employee Code;Code50     ;CaptionML=ENU=Warehouse Employee Code }
    { 101 ;   ;First Scan Date     ;Date          ;CaptionML=ENU=First Scan Date }
    { 102 ;   ;First Scan Time     ;Time          ;CaptionML=ENU=First Scan Time }
    { 103 ;   ;Last Scan Date      ;Date          ;CaptionML=ENU=Last Scan Date }
    { 104 ;   ;Last Scan Time      ;Time          ;CaptionML=ENU=Last Scan Time }
    { 105 ;   ;Added from RF       ;Boolean       ;CaptionML=ENU=Added from RF }
    { 106 ;   ;Counted             ;Boolean       ;CaptionML=ENU=Counted }
    { 107 ;   ;Phys. Inv. Document No.;Code20     ;CaptionML=ENU=Phys. Inv. Document No. }
    { 108 ;   ;Physical Inv. Verified;Boolean     ;CaptionML=ENU=Physical Inv. Verified }
    { 109 ;   ;Physical Inv. Line  ;Integer       ;CaptionML=ENU=Physical Inv. Line }
    { 110 ;   ;Qty (License Plate Entry);Decimal  ;FieldClass=FlowField;
                                                   CalcFormula=Sum("License Plate Entry".Quantity WHERE (Location=FIELD(Location Code),
                                                                                                         Bin=FIELD(Bin Code),
                                                                                                         Pallet=FIELD(Pallet No.),
                                                                                                         Box=FIELD(Box No.),
                                                                                                         Item=FIELD(Item No.),
                                                                                                         Lot No.=FIELD(Lot No.)));
                                                   CaptionML=ENU=Qty (License Plate Entry) }
    { 111 ;   ;Qty Base (License Plate Entry);Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("License Plate Entry"."Quantity (Base)" WHERE (Location=FIELD(Location Code),
                                                                                                                  Bin=FIELD(Bin Code),
                                                                                                                  Pallet=FIELD(Pallet No.),
                                                                                                                  Box=FIELD(Box No.),
                                                                                                                  Item=FIELD(Item No.),
                                                                                                                  Lot No.=FIELD(Lot No.)));
                                                   CaptionML=ENU=Qty Base (License Plate Entry) }
  }
  KEYS
  {
    {    ;Journal Template Name,Journal Batch Name,Location Code,Line No.;
                                                   Clustered=Yes }
    {    ;Bin Code,Pallet No.,Box No.,Counted     ;SumIndexFields=Qty. (Calculated),Qty. (Phys. Inventory) }
    {    ;Location Code,Bin Code,Pallet No.,Box No.,Item No.,Variant Code,Unit of Measure Code,Lot No.,Serial No. }
    {    ;Journal Template Name,Journal Batch Name,Physical Inv. Line,Physical Inv. Verified,Counted;
                                                   SumIndexFields=Quantity Calc.,Qty. (Calculated),Qty. (Phys. Inventory) }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1240020000 : TextConst 'ENU=Phys. Inv. must not be No.';
      Text002@1240020004 : TextConst 'ENU=Pallet Journal';
      Text004@1240020003 : TextConst 'ENU=You must first set up user %1 as a warehouse employee.;ESM=Primero debe configurar usuario %1 como empleado almac‚n.;FRC=Vous devez d''abord ‚tablir l''utilisateur %1 … titre de magasinier.;ENC=You must first set up user %1 as a warehouse employee.';
      Text005@1240020002 : TextConst 'ENU=The location %1 of Pallet Journal Batch %2 is not enabled for user %3.;ESM=El almac‚n %1 de la secc. diario almac‚n %2 no est  habilitada para usuario %3.;FRC=Le magasin %1 de la feuille magasin %2 n''est pas actif pour l''utilisateur %3.;ENC=The location %1 of warehouse journal batch %2 is not enabled for user %3.';
      OpenFromBatch@1240020001 : Boolean;
      Text006@1240020005 : TextConst 'ENU=A Pallet Journal Template must be set on RF Setup.';
      Text007@1240020006 : TextConst 'ENU=The Pallet Journal Template does not exist.';
      Text008@1240020007 : TextConst 'ENU=An RF Location Setup Code must exist for %1.';

    [External]
    PROCEDURE CleanupAdjustmentBin@1240020000(LocationCode@1240020000 : Code[20];WarehouseEmployeeCode@1240020001 : Code[50]);
    VAR
      RFLocationSetup@1240020003 : Record 14099031;
      PalletBinContent@1240020004 : Record 14002963;
      LicensePlateMgt@1240020002 : Codeunit 14002971;
      Remark@1240020005 : Text[250];
    BEGIN
      IF RFLocationSetup.GET(LocationCode) THEN BEGIN
        RFLocationSetup.TESTFIELD("LP Pallet Adjustment Bin");
        PalletBinContent.SETRANGE(Bin,RFLocationSetup."LP Pallet Adjustment Bin");
        PalletBinContent.SETRANGE(Location,LocationCode);
        IF PalletBinContent.FINDSET THEN BEGIN
          REPEAT
            LicensePlateMgt.DestroyStorageID(PalletBinContent."Pallet No.",PalletBinContent."Box No.",
              WarehouseEmployeeCode,Remark);
          UNTIL PalletBinContent.NEXT = 0;
        END;
      END ELSE
        Remark := STRSUBSTNO(Text008,LocationCode);

      IF Remark <> '' THEN
        ERROR(Remark);
    END;

    PROCEDURE LookupName@7(VAR CurrentJnlTemplate@1240020000 : Code[10];VAR CurrentJnlBatchName@1000 : Code[10];VAR CurrentLocationCode@1003 : Code[10];VAR PalletJnlLine@1001 : Record 14002966) : Boolean;
    VAR
      PalletJnlBatch@1002 : Record 14002965;
      LocationFilter@1240020001 : Text[250];
    BEGIN
      COMMIT;

      LocationFilter := GetWhseEmpLocations;

      PalletJnlBatch.SETFILTER("Location Code",LocationFilter);

      IF PAGE.RUNMODAL(PAGE::"Pallet Journal Batch",PalletJnlBatch) = ACTION::LookupOK THEN BEGIN
        CurrentJnlTemplate := PalletJnlBatch."Journal Template Name";
        CurrentJnlBatchName := PalletJnlBatch.Name;
        CurrentLocationCode := PalletJnlBatch."Location Code";
        SetName(CurrentJnlTemplate,CurrentJnlBatchName,CurrentLocationCode,PalletJnlLine);
      END;
    END;

    PROCEDURE SetName@9(CurrentJnlTemplate@1240020000 : Code[10];CurrentJnlBatchName@1000 : Code[10];CurrentLocationCode@1002 : Code[10];VAR PalletJnlLine@1001 : Record 14002966);
    BEGIN
      PalletJnlLine.FILTERGROUP := 2;
      PalletJnlLine.SETRANGE("Journal Template Name",CurrentJnlTemplate);
      PalletJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      PalletJnlLine.SETRANGE("Location Code",CurrentLocationCode);
      PalletJnlLine.FILTERGROUP := 0;
      IF PalletJnlLine.FIND('-') THEN;
    END;

    PROCEDURE OpenJnl@12(VAR CurrentJnlBatchName@1000 : Code[10];VAR CurrentLocationCode@1002 : Code[10];VAR PalletJnlLine@1001 : Record 14002966);
    VAR
      PalletJnlTemplate@1240020000 : Record 14002969;
      CurrentJnlTemplate@1240020001 : Code[10];
    BEGIN
      CurrentJnlTemplate := GetPalletJnlTemplate;

      CheckWhseEmployee;
      CheckTemplateName(
        PalletJnlLine.GETRANGEMAX("Journal Template Name"),CurrentLocationCode,CurrentJnlBatchName,CurrentJnlTemplate);
      PalletJnlLine.FILTERGROUP := 2;
      PalletJnlLine.SETRANGE("Journal Template Name",CurrentJnlTemplate);
      PalletJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      IF CurrentLocationCode <> '' THEN
        PalletJnlLine.SETRANGE("Location Code",CurrentLocationCode);
      PalletJnlLine.FILTERGROUP := 0;
      IF PalletJnlLine.FIND('-') THEN;
    END;

    LOCAL PROCEDURE CheckTemplateName@11(CurrentJnlTemplateName@1000 : Code[10];VAR CurrentLocationCode@1005 : Code[10];VAR CurrentJnlBatchName@1001 : Code[10];VAR CurrentJnlTemplate@1240020000 : Code[10]);
    VAR
      PalletJnlBatch@1002 : Record 14002965;
      WhseEmployee@1004 : Record 7301;
      WmsMgt@1003 : Codeunit 7302;
      LocationFilter@1240020001 : Text[250];
    BEGIN
      PalletJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
      IF NOT PalletJnlBatch.GET(CurrentJnlTemplateName,CurrentJnlBatchName,CurrentLocationCode) OR
         NOT WhseEmployee.GET(USERID,CurrentLocationCode)
      THEN BEGIN
        IF USERID <> '' THEN BEGIN
          LocationFilter := GetWhseEmpLocations;

          PalletJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
          PalletJnlBatch.SETFILTER("Location Code",LocationFilter);
        END;
        IF PalletJnlBatch.ISEMPTY THEN BEGIN
          ERROR('A Pallet Journal Batch must be setup first');
        END;
        CurrentJnlTemplate := PalletJnlBatch."Journal Template Name";
        CurrentJnlBatchName := PalletJnlBatch.Name;
        CurrentLocationCode := PalletJnlBatch."Location Code";
      END;
    END;

    PROCEDURE CheckName@10(CurrentJnlBatchName@1000 : Code[10];CurrentLocationCode@1003 : Code[10];VAR PalletJnlLine@1001 : Record 14002966);
    VAR
      PalletJnlBatch@1002 : Record 14002965;
      WhseEmployee@1004 : Record 7301;
    BEGIN
      PalletJnlBatch.GET(
        PalletJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName,CurrentLocationCode);
      IF (USERID <> '') AND NOT WhseEmployee.GET(USERID,CurrentLocationCode) THEN
        ERROR(Text005,CurrentLocationCode,CurrentJnlBatchName,USERID);
    END;

    LOCAL PROCEDURE CheckWhseEmployee@15();
    VAR
      WhseEmployee@1000 : Record 7301;
    BEGIN
      IF USERID <> '' THEN BEGIN
        WhseEmployee.SETRANGE("User ID",USERID);
        IF NOT WhseEmployee.FINDFIRST THEN
          ERROR(Text004,USERID);
      END;
    END;

    PROCEDURE IsOpenedFromBatch@42() : Boolean;
    VAR
      PalletJournalBatch@1002 : Record 14002965;
      TemplateFilter@1001 : Text;
      BatchFilter@1000 : Text;
    BEGIN
      BatchFilter := GETFILTER("Journal Batch Name");
      IF BatchFilter <> '' THEN BEGIN
        TemplateFilter := GETFILTER("Journal Template Name");
        IF TemplateFilter <> '' THEN
          PalletJournalBatch.SETFILTER("Journal Template Name",TemplateFilter);
        PalletJournalBatch.SETFILTER(Name,BatchFilter);
        PalletJournalBatch.FINDFIRST;
      END;

      EXIT((("Journal Batch Name" <> '') AND ("Journal Template Name" = '')) OR (BatchFilter <> ''));
    END;

    PROCEDURE TemplateSelection@13(PhysInventory@1000 : Boolean;VAR PalletJnlLine@1003 : Record 14002966;VAR JnlSelected@1001 : Boolean;VAR PalletJnlBatchSelection@1240020000 : Code[10];VAR PalletJnlLocSelection@1240020001 : Code[10]);
    VAR
      PalletJnlBatch@1240020002 : Record 14002965;
      LocationFilter@1240020003 : Text[250];
      PalletJnlTemplateCode@1240020004 : Code[10];
    BEGIN
      JnlSelected := TRUE;

      PalletJnlTemplateCode := GetPalletJnlTemplate;
      LocationFilter := GetWhseEmpLocations;
      PalletJnlBatch.SETRANGE("Journal Template Name",PalletJnlTemplateCode);
      PalletJnlBatch.SETFILTER("Location Code",LocationFilter);
      IF PalletJnlBatch.FIND('-') THEN
        JnlSelected := PAGE.RUNMODAL(PAGE::"Pallet Journal Batch",PalletJnlBatch) = ACTION::LookupOK;

      IF JnlSelected THEN BEGIN
        PalletJnlLine.FILTERGROUP := 2;
        PalletJnlLine.SETRANGE("Journal Template Name",PalletJnlTemplateCode);
        PalletJnlLine.FILTERGROUP := 0;
        IF OpenFromBatch THEN BEGIN
          PalletJnlLine."Journal Template Name" := '';
          PAGE.RUN(PAGE::"Pallet Journal Template",PalletJnlLine);
        END ELSE BEGIN
          PalletJnlBatchSelection := PalletJnlBatch.Name;
          PalletJnlLocSelection := PalletJnlBatch."Location Code";
        END;
      END;
    END;

    PROCEDURE TemplateSelectionFromBatch@21(VAR PalletJnlBatch@1000 : Record 14002965);
    VAR
      PalletJnlLine@1002 : Record 14002966;
      JnlSelected@1001 : Boolean;
      PalletJnlBatchSelection@1240020000 : Code[10];
      PalletJnlLocSelection@1240020001 : Code[10];
    BEGIN
      OpenFromBatch := TRUE;
      PalletJnlLine."Journal Batch Name" := PalletJnlBatch.Name;
      PalletJnlLine."Location Code" := PalletJnlBatch."Location Code";
      TemplateSelection(TRUE,PalletJnlLine,JnlSelected,PalletJnlBatchSelection,PalletJnlLocSelection);
    END;

    LOCAL PROCEDURE GetWhseEmpLocations@1240020003() : Text[250];
    VAR
      Location@1240020000 : Record 14;
      WhseEmployee@1240020001 : Record 7301;
      LocationFilter@1240020002 : Text[250];
      FirstRecord@1240020003 : Boolean;
    BEGIN
      FirstRecord := TRUE;
      WhseEmployee.RESET;
      WhseEmployee.SETCURRENTKEY("User ID","Location Code");
      WhseEmployee.SETRANGE("User ID",USERID);
      IF WhseEmployee.FIND('-') THEN BEGIN
        REPEAT
          IF NOT FirstRecord THEN
            LocationFilter += '|';
          Location.GET(WhseEmployee."Location Code");
          LocationFilter += Location.Code;
          FirstRecord := FALSE;
        UNTIL WhseEmployee.NEXT = 0;
      END;

      EXIT(LocationFilter);
    END;

    PROCEDURE GetPalletJnlTemplate@1240020001() : Code[10];
    VAR
      PalletJnlTemplate@1240020000 : Record 14002969;
      RFSetup@1240020001 : Record 14002801;
    BEGIN
      RFSetup.GET;
      IF RFSetup."Pallet Journal Template" = '' THEN
        ERROR(Text006);

      IF NOT PalletJnlTemplate.GET(RFSetup."Pallet Journal Template") THEN
        ERROR(Text007);

      EXIT(PalletJnlTemplate."Pallet Journal Template");
    END;

    BEGIN
    END.
  }
}

