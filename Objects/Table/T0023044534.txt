OBJECT Table 23044534 WMDM Event
{
  OBJECT-PROPERTIES
  {
    Date=02/13/20;
    Time=[ 3:47:48 PM];
    Version List=WHI2.4.7684.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CALCFIELDS("CodeUnit Name");
               CheckFunctionName();  //<IW author="R.Trudeau" date="10/09/18" version="IW18.10" issue="TFS4059" />
             END;

    CaptionML=[ENU=WHI Event;
               ESM=Evento WHI;
               FRC=êvÇnement WHI;
               ENC=WHI Event];
    LookupPageID=Page23044539;
    DrillDownPageID=Page23044539;
  }
  FIELDS
  {
    { 1   ;   ;Event ID            ;Integer       ;CaptionML=[ENU=Event ID;
                                                              ESM=Identificador de evento;
                                                              FRC=ID d'ÇvÇnement;
                                                              ENC=Event ID] }
    { 2   ;   ;Category            ;Code30        ;OnValidate=BEGIN
                                                                CheckFunctionName();  //<IW author="R.Trudeau" date="10/09/18" version="IW18.10" issue="TFS4059" />
                                                              END;

                                                   CaptionML=[ENU=Category;
                                                              ESM=Categor°a;
                                                              FRC=CatÇgorie;
                                                              ENC=Category] }
    { 3   ;   ;ActionCode          ;Code30        ;CaptionML=[ENU=Action;
                                                              ESM=Acci¢n;
                                                              FRC=Action;
                                                              ENC=Action] }
    { 4   ;   ;CodeUnit            ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("CodeUnit Name");
                                                              END;

                                                   CaptionML=[ENU=CodeUnit;
                                                              ESM=CodeUnit;
                                                              FRC=CodeUnit;
                                                              ENC=CodeUnit] }
    { 5   ;   ;CodeUnit Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Codeunit),
                                                                                                                Object ID=FIELD(CodeUnit)));
                                                   CaptionML=[ENU=CodeUnit Name;
                                                              ESM=Nombre de CodeUnit;
                                                              FRC=Nom du CodeUnit;
                                                              ENC=CodeUnit Name];
                                                   Editable=No }
    { 6   ;   ;Function Name       ;Text100       ;OnValidate=BEGIN
                                                                CheckFunctionName();  //<IW author="R.Trudeau" date="10/09/18" version="IW18.10" issue="TFS4059" />
                                                              END;

                                                   CaptionML=[ENU=Function Name;
                                                              ESM=Nombre de la funci¢n;
                                                              FRC=Nom de la Fonction;
                                                              ENC=Function Name] }
    { 7   ;   ;Short Name          ;Text100       ;CaptionML=[ENU=Short Name;
                                                              ESM=Nombre corto;
                                                              FRC=Nom court;
                                                              ENC=Short Name] }
    { 8   ;   ;Friendly Name       ;Text100       ;CaptionML=[ENU=Friendly Name;
                                                              ESM=Nombre amigable;
                                                              FRC=Nom Familier;
                                                              ENC=Friendly Name] }
    { 9   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 10  ;   ;Log Events          ;Boolean       ;CaptionML=[ENU=Log Events;
                                                              ESM=Registrar los eventos;
                                                              FRC=Enregistrer EvÇnements;
                                                              ENC=Log Events] }
    { 11  ;   ;Summary Type        ;Option        ;CaptionML=[ENU=Summary Type;
                                                              ESM=tipo de resumen;
                                                              FRC=Type du RÇsumÇ;
                                                              ENC=Summary Type];
                                                   OptionCaptionML=[ENU=" ,Open Document,Post Document,Change Line,List,Scan,Assign LP,Print,Output,PDA Login,Close Document";
                                                                    ESM=" ,Abrir Documento, Postar Documento, Cambiar L°nea, Listar, Escanear, Asignar LP, Imprimir, Salida, Inicio de sesi¢n PDA, Cerrar documento";
                                                                    FRC=" ,Ouvrir un document, un document, un changement de ligne, une liste, un scan, une affectation de LP, une impression, une sortie, une connexion PDA, un document de fermeture";
                                                                    ENC=" ,Open Document,Post Document,Change Line,List,Scan,Assign LP,Print,Output,PDA Login,Close Document"];
                                                   OptionString=[ ,Open Document,Post Document,Change Line,List,Scan,Assign LP,Print,Output,PDA Login,Close Document] }
    { 12  ;   ;Deployment Tag      ;Code100       ;CaptionML=[ENU=Deployment Tag;
                                                              ESM=etiqueta de despliegue;
                                                              FRC=Marque de dÇploiement;
                                                              ENC=Deployment Tag] }
    { 13  ;   ;Deployment Version  ;Code20        ;CaptionML=[ENU=Deployment Version;
                                                              ESM=Versi¢n de implementaci¢n;
                                                              FRC=Version de dÇploiement;
                                                              ENC=Deployment Version] }
    { 14  ;   ;Publish Event       ;Option        ;InitValue=Yes;
                                                   OptionCaptionML=[ENU=No,Yes;
                                                                    ESM=No,S°;
                                                                    FRC=Non,Oui;
                                                                    ENC=No,Yes];
                                                   OptionString=No,Yes }
  }
  KEYS
  {
    {    ;Event ID                                ;Clustered=Yes }
    {    ;Category,CodeUnit,Short Name,Friendly Name }
    {    ;Category,Function Name                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      cuTranslationMgmt@1000000000 : Codeunit 23044526;

    PROCEDURE CheckFunctionName@1000000001();
    VAR
      lrecEvent@1000000000 : Record 23044534;
    BEGIN
      //  Ensures function name is unique

      IF ("Function Name" = '') OR (Category = '') THEN
        EXIT;

      lrecEvent.SETRANGE(Category,Category);
      lrecEvent.SETRANGE("Function Name","Function Name");
      lrecEvent.SETFILTER("Event ID",'<>%1',"Event ID");
      IF lrecEvent.FINDFIRST THEN
        ERROR(cuTranslationMgmt.getTranslatedString('ERR_FUNCTION_NAME_NOT_UNIQUE', ''),lrecEvent."Event ID",lrecEvent."Function Name");
    END;

    PROCEDURE GetFriendlyEvent@1000000003(piEventID@1000000001 : Integer) rtxtOut : Text;
    VAR
      lrecEventCode@1000000000 : Record 23044534;
    BEGIN
      IF lrecEventCode.GET(piEventID) THEN BEGIN
        IF ( (lrecEventCode.Category<>'') AND (lrecEventCode."Function Name"<>'') ) THEN BEGIN
          rtxtOut := STRSUBSTNO('%1::%2',lrecEventCode.Category,lrecEventCode."Function Name");
        END;
      END;

      IF ( rtxtOut = '' ) THEN BEGIN
        // this condition is for patches, upgrades, etc... where event codes
        // might be incomplete, or completely unavailable.
        // in that case, be helpfull and show the event id like the integrator
        // is typically used to.
        IF ( piEventID <> 0 ) THEN BEGIN
          rtxtOut := FORMAT(piEventID);
        END;
      END;
    END;

    PROCEDURE SetFriendlyEvent@1000000000(psEventName@1000000000 : Text) : Integer;
    VAR
      lrecEventCode@1000000002 : Record 23044534;
      liPossibleEvent@1000000001 : Integer;
      liPos@1000000004 : Integer;
      lsFunctionName@1000000006 : Text;
      lcodCategory@1000000005 : Code[30];
      liEventIDOut@1000000007 : Integer;
    BEGIN
      liEventIDOut := -1;

      IF ( ( psEventName = '' ) OR ( psEventName = '0' ) ) THEN BEGIN
        liEventIDOut := 0;
      END ELSE BEGIN
        liPos := STRPOS(psEventName,'::');
        IF(liPos>0) THEN BEGIN
          lcodCategory := COPYSTR(psEventName,1,liPos-1);
          lsFunctionName := COPYSTR(psEventName,liPos+2);
        END ELSE BEGIN
          lsFunctionName := COPYSTR(psEventName,1,100);
        END;

        lrecEventCode.SETRANGE(Category,lcodCategory);
        lrecEventCode.SETRANGE("Function Name",lsFunctionName);
        IF(lrecEventCode.FINDSET(FALSE)) THEN BEGIN
          liEventIDOut := lrecEventCode."Event ID";
        END ELSE BEGIN
          IF ( EVALUATE( liPossibleEvent, psEventName ) ) THEN BEGIN
            liEventIDOut := liPossibleEvent;
          END;
        END;
      END;

      IF liEventIDOut = -1 THEN
        ERROR(cuTranslationMgmt.getTranslatedString('ERR_NO_MATCHING_EVENT', ''), psEventName);

      EXIT(liEventIDOut);
    END;

    PROCEDURE ChooseEventFromPage@1000000002() : Integer;
    VAR
      lrecEvent@1000000002 : Record 23044534;
      liEventIDOut@1000000000 : Integer;
    BEGIN
      liEventIDOut := -1;

      IF (PAGE.RUNMODAL(PAGE::"WHI Event Codes", lrecEvent) = ACTION::LookupOK) THEN BEGIN
        liEventIDOut := lrecEvent."Event ID";
      END;

      EXIT(liEventIDOut);
    END;

    BEGIN
    {
      ************************
      Copyright Notice
      This objects content is copyright of Insight Works 2011.  All rights reserved.
      Any redistribution or reproduction of part or all of the contents in any form is prohibited.
      ************************
    }
    END.
  }
}

