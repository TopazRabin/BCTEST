OBJECT Table 23044390 IWX Custom Config. Template
{
  OBJECT-PROPERTIES
  {
    Date=04/26/19;
    Time=11:38:18 AM;
    Version List=IWX2.4.7684.0;
  }
  PROPERTIES
  {
    CaptionML=[ENU=IW Custom Config. Template;
               ESM=IW Configuraci?n personalizada de plantilla;
               FRC=IW Config personnalis?e. Mod?le;
               ENC=IW Custom Config. Template];
    LookupPageID=Page23044390;
  }
  FIELDS
  {
    { 10  ;   ;Key                 ;Text20        ;CaptionML=[ENU=Key;
                                                              ESM=Clave;
                                                              FRC=Cl‚;
                                                              ENC=Key];
                                                   Description=Text instead of code is intentional to allow for Pascal case and keys with spaces. }
    { 20  ;   ;Description         ;Text200       ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 30  ;   ;Table ID            ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Table ID;
                                                              ESM=ID de Tabla;
                                                              FRC=ID de la Table;
                                                              ENC=Table ID] }
    { 40  ;   ;Table Name          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObj."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                  Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Name;
                                                              ESM=Nombre de Tabla;
                                                              FRC=Nom de la Table;
                                                              ENC=Table Name];
                                                   Editable=No }
    { 100 ;   ;Default Value       ;Code100       ;OnValidate=VAR
                                                                liChoice@1000000000 : Integer;
                                                                lrecValue@1000000001 : Record 23044391;
                                                              BEGIN
                                                                //Just a Template,Update All %1 Entries to be %2,Update All %1 Entries that were %3 to be %2
                                                                IF ( "Table ID" <> 0 ) THEN BEGIN
                                                                  recRef.OPEN( "Table ID");
                                                                  IF ( GUIALLOWED ) THEN BEGIN
                                                                    liChoice := STRMENU(
                                                                      STRSUBSTNO( tcUpdateOption, recRef.NAME, "Default Value", xRec."Default Value" ) );
                                                                  END; // if guiallowed;

                                                                  CASE liChoice OF
                                                                    2 : // Update All entries.
                                                                      BEGIN
                                                                        IF ( recRef.FIND('-') ) THEN BEGIN
                                                                          REPEAT
                                                                            lrecValue.SETCURRENTKEY( "Template Key","Table ID","Record ID");
                                                                            IF ( lrecValue.GET( Key, recRef.NUMBER, recRef.RECORDID ) ) THEN BEGIN
                                                                              lrecValue.Value := "Default Value";
                                                                              lrecValue.MODIFY();
                                                                            END ELSE BEGIN
                                                                              // in this case we need to insert
                                                                              lrecValue.INIT();
                                                                              lrecValue."Template Key" := Key;
                                                                              lrecValue."Table ID" := recRef.NUMBER;
                                                                              lrecValue."Record ID" := recRef.RECORDID;
                                                                              lrecValue.Value := "Default Value";
                                                                              lrecValue.INSERT();

                                                                            END;
                                                                          UNTIL( recRef.NEXT() = 0 );
                                                                        END; // if there are any records in the reference table.

                                                                      END; // end 2
                                                                    3 : // UPDATE only entries that were previous default value to new default value
                                                                      BEGIN
                                                                        lrecValue.RESET();
                                                                        lrecValue.SETRANGE("Template Key", Key );
                                                                        lrecValue.SETRANGE("Table ID", "Table ID" );
                                                                        lrecValue.SETRANGE(Value, xRec."Default Value" ); // xRec PREVIOUS value
                                                                        IF ( lrecValue.FIND('-') ) THEN BEGIN
                                                                          REPEAT
                                                                            lrecValue.Value := "Default Value";
                                                                            lrecValue.MODIFY();
                                                                          UNTIL( lrecValue.NEXT() = 0 );
                                                                        END;
                                                                      END;  // end 3
                                                                  END; // end case
                                                                END; // if we have a table id.
                                                              END;

                                                   CaptionML=[ENU=Default Value;
                                                              ESM=Valor por Defecto;
                                                              FRC=Valeur par d‚faut;
                                                              ENC=Default Value] }
  }
  KEYS
  {
    {    ;Key,Table ID                            ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      tcUpdateOption@1000000000 : TextConst 'ENU=Just a Default,Update All %1 Entries to be %2,Update Only %1 Entries that were %3 to be %2';
      recRef@1000000001 : RecordRef;

    BEGIN
    {
      ************************
      Copyright Notice
      This objects content is copyright of Insight Works 2011.  All rights reserved.
      Any redistribution or reproduction of part or all of the contents in any form is prohibited.
      ************************
    }
    END.
  }
}

