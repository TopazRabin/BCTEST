OBJECT Table 14123803 ES Field Level Security Code
{
  OBJECT-PROPERTIES
  {
    Date=04/30/18;
    Time=12:00:00 PM;
    Version List=ES1.41.51;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               TestEditAllowed(FALSE);

               ESSourceTableSetup.GET("Source Table ID");

               IF NOT NoExtendedInsertDelete THEN BEGIN
                 ESFieldLevelSecurityCode.RESET;
                 ESFieldLevelSecurityCode.SETRANGE("Source Table ID","Source Table ID");
                 ESFieldLevelSecurityCode.SETFILTER(Code,'<>%1',Code);
                 ESFieldLevelSecurityCode.SETRANGE(Default,TRUE);
                 IF ESFieldLevelSecurityCode.FIND('-') THEN
                   IF CONFIRM(Text001,FALSE) THEN
                     CopyObjectsFromDefault(ESFieldLevelSecurityCode.Code);
               END;

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnModify=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               TestEditAllowed(FALSE);

               ESSourceTableSetup.GET("Source Table ID");

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnDelete=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               TestEditAllowed(FALSE);

               ESSourceTableSetup.GET("Source Table ID");

               IF NOT NoExtendedInsertDelete THEN BEGIN
                 ESUserSecuritySetup.RESET;
                 ESUserSecuritySetup.SETRANGE("Source Table ID","Source Table ID");
                 ESUserSecuritySetup.SETRANGE("Field Level Security Code",Code);
                 IF ESUserSecuritySetup.FIND('-') THEN
                   ESUserSecuritySetup.TESTFIELD("Field Level Security Code",'');
               END;

               DeleteRelated;

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnRename=BEGIN
               ERROR(Text020);
             END;

    CaptionML=ENU=Field Level Security Code;
    LookupPageID=Page14123803;
    DrillDownPageID=Page14123803;
  }
  FIELDS
  {
    { 1   ;   ;Source Table ID     ;Integer       ;TableRelation="ES Source Table Setup";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Source Table Caption");
                                                              END;

                                                   CaptionML=ENU=Source Table ID;
                                                   NotBlank=Yes }
    { 2   ;   ;Code                ;Code20        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 11  ;   ;Description         ;Text250       ;CaptionML=ENU=Description }
    { 12  ;   ;Default             ;Boolean       ;OnValidate=BEGIN
                                                                IF Default THEN BEGIN
                                                                  ESFieldLevelSecurityCode.RESET;
                                                                  ESFieldLevelSecurityCode.SETRANGE("Source Table ID","Source Table ID");
                                                                  ESFieldLevelSecurityCode.SETFILTER(Code,'<>%1',Code);
                                                                  ESFieldLevelSecurityCode.SETRANGE(Default,TRUE);
                                                                  IF ESFieldLevelSecurityCode.FIND('-') THEN
                                                                    ESFieldLevelSecurityCode.TESTFIELD(Default,FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Default }
    { 21  ;   ;Objects             ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES Field Level Security Object" WHERE (Source Table ID=FIELD(Source Table ID),
                                                                                                             Field Level Security Code=FIELD(Code)));
                                                   CaptionML=ENU=Objects;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 22  ;   ;User Security Setups;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("ES User Security Setup" WHERE (Source Table ID=FIELD(Source Table ID),
                                                                                                     Field Level Security Code=FIELD(Code)));
                                                   CaptionML=ENU=User Security Setups;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 23  ;   ;From Setup Company  ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES FLADS Copy Setup"."From Setup Company" WHERE (Table ID=CONST(14123803),
                                                                                                                        Source Table ID=FIELD(Source Table ID),
                                                                                                                        Code=FIELD(Code)));
                                                   CaptionML=ENU=From Setup Company;
                                                   Editable=No }
    { 24  ;   ;Source Table Caption;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Source Table ID)));
                                                   CaptionML=ENU=Source Table Caption;
                                                   Editable=No }
    { 25  ;   ;Copy Setup Exists   ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("ES FLADS Copy Setup" WHERE (Table ID=CONST(14123803),
                                                                                                  Source Table ID=FIELD(Source Table ID),
                                                                                                  Code=FIELD(Code)));
                                                   CaptionML=ENU=Copy Setup Exists;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Source Table ID,Code                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Source Table ID,Code,Description,Default,Objects }
  }
  CODE
  {
    VAR
      ESSourceTableSetup@1240520008 : Record 14123802;
      ESUserSecuritySetup@1240520009 : Record 14123805;
      ESFLADSUpdateNo@1240520011 : Record 14123813;
      ESFieldLevelSecurityCode@1240520012 : Record 14123803;
      Text001@1001 : TextConst 'ENU=Copy permissions from Default?';
      ESFLADSSetup@1240520002 : Record 14123801;
      Text003@1240520020 : TextConst 'ENU=File %1 already exists, overwrite?';
      Text004@1240520019 : TextConst 'ENU=Nothing has been exported.';
      Text005@1240520018 : TextConst 'ENU=Exporting %1 to file\';
      Text006@1240520017 : TextConst 'ENU=Process #1################################################';
      Text007@1240520016 : TextConst 'ENU=Updating Version';
      Text008@1240520015 : TextConst 'ENU=Creating File';
      Text011@1240520014 : TextConst 'ENU=Closing File';
      Text012@1240520013 : TextConst 'ENU=%1 %2 %3 exported to %4 with %5 records.';
      Text013@1240520010 : TextConst 'ENU=Nothing imported.';
      Text014@1240520007 : TextConst 'ENU=File Name must be entered.';
      Text015@1240520006 : TextConst 'ENU=Importing %1 from file\';
      Text016@1240520005 : TextConst 'ENU=Process #1################################################\';
      Text017@1240520004 : TextConst 'ENU=Opening file';
      Text018@1240520003 : TextConst 'ENU=%1 records have been imported from %2.';
      NoExtendedInsertDelete@1240520021 : Boolean;
      Text019@1240520022 : TextConst 'ENU=%1 %2 exported to %3 with %4 records.';
      Text002@1002 : TextConst 'ENU=%1 is not valid content for this filetype.';
      Text020@1240520001 : TextConst 'ENU=Rename not allowed.';
      Text101@1003 : TextConst 'ENU=Downloading File.';
      Text102@1000 : TextConst 'ENU=Uploading file.';

    PROCEDURE CopyObjectsFromDefault@1240520008(FromCode@1240520012 : Code[20]);
    VAR
      ESFieldLevelSecurityObject@1240520008 : Record 14123806;
      ESFieldLevelSecurityObject2@1240520010 : Record 14123806;
      ESFieldLevelSecControl@1240520009 : Record 14123807;
      ESFieldLevelSecControl2@1240520011 : Record 14123807;
    BEGIN
      IF ("Source Table ID" = 0) OR (Code = '') OR (FromCode = '') THEN
        EXIT;

      ESFieldLevelSecurityObject.RESET;
      ESFieldLevelSecurityObject.SETRANGE("Source Table ID","Source Table ID");
      ESFieldLevelSecurityObject.SETRANGE("Field Level Security Code",FromCode);
      IF ESFieldLevelSecurityObject.FIND('-') THEN
        REPEAT
          ESFieldLevelSecurityObject2 := ESFieldLevelSecurityObject;
          ESFieldLevelSecurityObject2."Field Level Security Code" := Code;
          ESFieldLevelSecurityObject2.INSERT;
        UNTIL ESFieldLevelSecurityObject.NEXT = 0;

      ESFieldLevelSecControl.RESET;
      ESFieldLevelSecControl.SETRANGE("Source Table ID","Source Table ID");
      ESFieldLevelSecControl.SETRANGE("Field Level Security Code",FromCode);
      IF ESFieldLevelSecControl.FIND('-') THEN
        REPEAT
          ESFieldLevelSecControl2 := ESFieldLevelSecControl;
          ESFieldLevelSecControl2."Field Level Security Code" := Code;
          ESFieldLevelSecControl2.INSERT;
        UNTIL ESFieldLevelSecControl.NEXT = 0;
    END;

    PROCEDURE DeleteRelated@1240520009();
    VAR
      ESFieldLevelSecurityObject@1240520008 : Record 14123806;
      ESFieldLevelSecurityObject2@1240520009 : Record 14123806;
      ESFLADSCopySetup@1240520001 : Record 14123816;
    BEGIN
      ESFieldLevelSecurityObject.RESET;
      ESFieldLevelSecurityObject.SETRANGE("Source Table ID","Source Table ID");
      ESFieldLevelSecurityObject.SETRANGE("Field Level Security Code",Code);
      IF ESFieldLevelSecurityObject.FIND('-') THEN
        REPEAT
          ESFieldLevelSecurityObject2 := ESFieldLevelSecurityObject;
          ESFieldLevelSecurityObject2.DELETE(TRUE);
        UNTIL ESFieldLevelSecurityObject.NEXT = 0;

      IF ESFLADSCopySetup.GET(DATABASE::"ES Field Level Security Code","Source Table ID",Code) THEN
        ESFLADSCopySetup.DELETE;
    END;

    PROCEDURE ExportMultiFieldLevelSecCodes@1240520002(VAR ESFieldLevelSecurityCode@1240520004 : Record 14123803;ShowStatus@1240520021 : Boolean;ExportFileName@1240520001 : Text[250]);
    VAR
      ESEnterFileName@1240520002 : Page 14123560;
      FileMgt@1003 : Codeunit 419;
      TempServerFile@1000 : File;
      Window@1001 : Dialog;
      TempServerFileName@1002 : Text[1000];
      TotalRecords@1240520008 : Integer;
      TotalCodes@1240520003 : Integer;
    BEGIN
      IF ExportFileName = '' THEN BEGIN
        CLEAR(ESEnterFileName);
        ESEnterFileName.LOOKUPMODE(TRUE);
        ESEnterFileName.SelectNewFile(TRUE);
        IF ESEnterFileName.RUNMODAL <> ACTION::LookupOK THEN
          ERROR(Text004);
        ExportFileName := ESEnterFileName.GetFileName;
      END;

      TempServerFile.WRITEMODE(TRUE);
      TempServerFile.TEXTMODE(TRUE);
      TempServerFile.CREATETEMPFILE;
      TempServerFileName := TempServerFile.NAME;
      TempServerFile.CLOSE;

      IF GUIALLOWED THEN
        Window.OPEN(Text006);

      IF ESFieldLevelSecurityCode.FIND('-') THEN
        REPEAT
          IF GUIALLOWED THEN
            Window.UPDATE(
              1,
              STRSUBSTNO(
              '%1 %2',ESFieldLevelSecurityCode."Source Table ID",ESFieldLevelSecurityCode.Code));

          TotalCodes := TotalCodes + 1;

          TotalRecords :=
            TotalRecords +
            ESFieldLevelSecurityCode.ExportFieldLevelSecurityCode(FALSE,TRUE,TRUE,TempServerFileName,FALSE);
        UNTIL ESFieldLevelSecurityCode.NEXT = 0;

      IF GUIALLOWED THEN
        Window.UPDATE(1,Text101);
      FileMgt.DownloadToFile(TempServerFileName,ExportFileName);

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(
          Text019,
          TotalCodes,TABLECAPTION,ExportFileName,TotalRecords);
    END;

    PROCEDURE ExportFieldLevelSecurityCode@1240520014(ShowStatus@1240520021 : Boolean;ConfirmOverwrite@1240520020 : Boolean;AppendToFile@1240520004 : Boolean;ExportFileName@1240520001 : Text[250];ShowProgress@1240520005 : Boolean) : Integer;
    VAR
      ESFieldLevelSecurityObject@1240520012 : Record 14123806;
      ESFieldLevelSecControl@1240520014 : Record 14123807;
      ESEnterFileName@1240520002 : Page 14123560;
      ESFileMgt@1240520003 : Codeunit 14123502;
      FileMgt@1000 : Codeunit 419;
      Window@1240520009 : Dialog;
      OutputFile@1240520019 : File;
      Value@1240520022 : ARRAY [100] OF Text[1000];
      LocalFileName@1001 : Text[1000];
      TotalRecords@1240520008 : Integer;
    BEGIN
      IF NOT AppendToFile THEN BEGIN
        IF ExportFileName = '' THEN BEGIN
          CLEAR(ESEnterFileName);
          ESEnterFileName.LOOKUPMODE(TRUE);
          ESEnterFileName.SelectNewFile(TRUE);
          IF ESEnterFileName.RUNMODAL <> ACTION::LookupOK THEN
            ERROR(Text004);
          ExportFileName := ESEnterFileName.GetFileName;
        END;

        IF ConfirmOverwrite THEN
          IF EXISTS(ExportFileName) THEN
            IF NOT CONFIRM(Text003,FALSE,ExportFileName) THEN
              ERROR(Text004);

        LocalFileName := ExportFileName;
        OutputFile.WRITEMODE(TRUE);
        OutputFile.TEXTMODE(TRUE);
        OutputFile.CREATETEMPFILE;
        ExportFileName := OutputFile.NAME;
        OutputFile.CLOSE;
      END;

      IF GUIALLOWED AND ShowProgress THEN
        Window.OPEN(STRSUBSTNO(ESFLADSSetup.ExtendProgressText(Text005),TABLECAPTION) + Text006);

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,Text007);

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,Text008);
      OutputFile.WRITEMODE(TRUE);
      OutputFile.TEXTMODE(TRUE);
      IF EXISTS(ExportFileName) AND AppendToFile THEN BEGIN
        OutputFile.OPEN(ExportFileName);
        OutputFile.SEEK(OutputFile.LEN);
      END ELSE
        OutputFile.CREATE(ExportFileName);

      CLEAR(ESFileMgt);
      ESFileMgt.SetDelimiters('~"','"~',',');

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,'HEADER');
      Value[1] := ESFileMgt.FormatInteger(0) +'V1';
      Value[2] := 'HEADER';
      Value[3] := ESFileMgt.FormatInteger("Source Table ID") + ' ' + Code;
      Value[4] := Description;
      Value[5] := USERID;
      Value[6] := ESFileMgt.FormatDate(TODAY);
      Value[7] := ESFileMgt.FormatTime(TIME);
      OutputFile.WRITE(ESFileMgt.FormatValues(Value,7));

      Value[1] := ESFileMgt.FormatInteger(DATABASE::"ES Field Level Security Code") + 'V1';
      Value[2] := ESFileMgt.FormatInteger("Source Table ID");
      Value[3] := Code;
      Value[4] := Description;

      OutputFile.WRITE(ESFileMgt.FormatValues(Value,4));

      TotalRecords := TotalRecords + 1;

      ESFieldLevelSecurityObject.RESET;
      ESFieldLevelSecurityObject.SETRANGE("Source Table ID","Source Table ID");
      ESFieldLevelSecurityObject.SETRANGE("Field Level Security Code",Code);
      IF ESFieldLevelSecurityObject.FIND('-') THEN
        REPEAT
          Value[1] := ESFileMgt.FormatInteger(DATABASE::"ES Field Level Security Object") + 'V1';
          Value[2] := ESFileMgt.FormatInteger(ESFieldLevelSecurityObject."Source Table ID");
          Value[3] := ESFieldLevelSecurityObject."Field Level Security Code";
          Value[4] := ESFileMgt.FormatInteger(ESFieldLevelSecurityObject."Object Type");
          Value[5] := ESFileMgt.FormatInteger(ESFieldLevelSecurityObject."Object ID");
          Value[6] := ESFileMgt.FormatInteger(ESFieldLevelSecurityObject."General Permission");
          Value[7] := ESFieldLevelSecurityObject.Comment;
          OutputFile.WRITE(ESFileMgt.FormatValues(Value,7));

          TotalRecords := TotalRecords + 1;
        UNTIL ESFieldLevelSecurityObject.NEXT = 0;

      ESFieldLevelSecControl.RESET;
      ESFieldLevelSecControl.SETRANGE("Source Table ID","Source Table ID");
      ESFieldLevelSecControl.SETRANGE("Field Level Security Code",Code);
      IF ESFieldLevelSecControl.FIND('-') THEN
        REPEAT
          Value[1] := ESFileMgt.FormatInteger(DATABASE::"ES Field Level Sec. Control") + 'V1';
          Value[2] := ESFileMgt.FormatInteger(ESFieldLevelSecControl."Source Table ID");
          Value[3] := ESFieldLevelSecControl."Field Level Security Code";
          Value[4] := ESFileMgt.FormatInteger(ESFieldLevelSecControl."Object Type");
          Value[5] := ESFileMgt.FormatInteger(ESFieldLevelSecControl."Object ID");
          Value[6] := ESFileMgt.FormatInteger(ESFieldLevelSecControl.Type);
          Value[7] := ESFileMgt.FormatInteger(ESFieldLevelSecControl.ID);
          Value[8] := ESFileMgt.FormatInteger(ESFieldLevelSecControl.Permission);
          Value[9] := ESFieldLevelSecControl.Comment;
          OutputFile.WRITE(ESFileMgt.FormatValues(Value,9));

          TotalRecords := TotalRecords + 1;
        UNTIL ESFieldLevelSecControl.NEXT = 0;

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,'TOTALS');
      Value[1] := ESFileMgt.FormatInteger(0) + 'V1';
      Value[2] := 'TOTALS';
      Value[3] := ESFileMgt.FormatInteger("Source Table ID") + ' ' + Code;
      Value[4] := Description;
      Value[5] := USERID;
      Value[6] := ESFileMgt.FormatDate(TODAY);
      Value[7] := ESFileMgt.FormatTime(TIME);
      Value[8] := ESFileMgt.FormatInteger(TotalRecords);
      OutputFile.WRITE(ESFileMgt.FormatValues(Value,8));

      IF GUIALLOWED AND ShowProgress THEN
        Window.UPDATE(1,Text011);
      OutputFile.CLOSE;

      IF NOT AppendToFile THEN BEGIN
        IF GUIALLOWED AND ShowProgress THEN
          Window.UPDATE(1,Text101);
        FileMgt.DownloadToFile(ExportFileName,LocalFileName);
      END;

      IF GUIALLOWED AND ShowProgress THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(
          Text012,
          TABLECAPTION,"Source Table ID",Code,LocalFileName,TotalRecords);

      EXIT(TotalRecords);
    END;

    PROCEDURE ImportFieldLevelSecurityCode@1240520013(ShowStatus@1240520011 : Boolean;ImportFileName@1240520018 : Text[250];OverWriteExisting@1240520001 : Boolean);
    VAR
      ESFieldLevelSecurityCode@1240520002 : Record 14123803;
      ESFieldLevelSecurityCode2@1240520006 : Record 14123803;
      ESFieldLevelSecurityObject@1240520003 : Record 14123806;
      ESFieldLevelSecControl@1240520004 : Record 14123807;
      ESEnterFileName@1240520019 : Page 14123560;
      ESFileMgt@1240520005 : Codeunit 14123502;
      FileMgt@1000 : Codeunit 419;
      InputFile@1240520009 : File;
      Window@1240520012 : Dialog;
      TempText@1240520010 : Text[1000];
      Value@1240520008 : ARRAY [100] OF Text[1000];
      ServerFileName@1001 : Text[1000];
      NextUpdatePos@1240520022 : Integer;
      TotalRecords@1240520023 : Integer;
    BEGIN
      IF ImportFileName = '' THEN BEGIN
        CLEAR(ESEnterFileName);
        ESEnterFileName.LOOKUPMODE(TRUE);
        IF ESEnterFileName.RUNMODAL <> ACTION::LookupOK THEN
          ERROR(Text013);
        ImportFileName := ESEnterFileName.GetFileName;
      END;

      IF ImportFileName = '' THEN
        ERROR(Text014);

      IF GUIALLOWED THEN
        Window.OPEN(
          STRSUBSTNO(ESFLADSSetup.ExtendProgressText(Text015),TABLECAPTION) +
          Text016 + '        @2@@@@@@@@@@@@@@@@@@@@@@@@@@@@');

      CLEAR(ESFileMgt);
      ESFileMgt.SetDelimiters('~"','"~',',');

      IF GUIALLOWED THEN
        Window.UPDATE(1,Text017);
      InputFile.WRITEMODE(FALSE);
      InputFile.TEXTMODE(TRUE);
      IF NOT InputFile.OPEN(ImportFileName) THEN BEGIN
        IF GUIALLOWED THEN
          Window.UPDATE(1,Text102);
        ServerFileName := FileMgt.UploadFileSilent(ImportFileName);
        IF GUIALLOWED THEN
          Window.UPDATE(1,Text017);

        IF NOT InputFile.OPEN(ServerFileName) THEN
          InputFile.OPEN(ImportFileName);
      END;
      WHILE InputFile.POS < InputFile.LEN DO BEGIN
        IF NextUpdatePos < InputFile.POS THEN BEGIN
          IF GUIALLOWED THEN
            Window.UPDATE(2,ROUND(10000 * (InputFile.POS / InputFile.LEN),1));
          NextUpdatePos := ROUND(InputFile.POS + InputFile.LEN / 500,1);
        END;

        InputFile.READ(TempText);
        ESFileMgt.SplitInValues(Value,TempText);

        CASE Value[1] OF
          '','0V1':
            ;
          '14123803V1':
            BEGIN
              IF GUIALLOWED THEN
                Window.UPDATE(1,STRSUBSTNO('%1 %2',Value[2],Value[3]));

              ESFieldLevelSecurityCode.INIT;
              ESFieldLevelSecurityCode."Source Table ID" := ESFileMgt.EvaluateInteger(Value[2]);
              ESFieldLevelSecurityCode.Code := Value[3];
              ESFieldLevelSecurityCode.Description := Value[4];

              IF OverWriteExisting AND
                 ESFieldLevelSecurityCode2.GET(
                   ESFieldLevelSecurityCode."Source Table ID",ESFieldLevelSecurityCode.Code)
              THEN BEGIN
                ESFieldLevelSecurityCode2.SetNoExtendedInsertDelete(TRUE);
                ESFieldLevelSecurityCode2.DELETE(TRUE);
              END;

              ESFieldLevelSecurityCode.SetNoExtendedInsertDelete(TRUE);
              ESFieldLevelSecurityCode.INSERT(TRUE);

              TotalRecords := TotalRecords + 1;
            END;
          '14123806V1':
            BEGIN
              ESFieldLevelSecurityObject.INIT;
              ESFieldLevelSecurityObject."Source Table ID" := ESFileMgt.EvaluateInteger(Value[2]);
              ESFieldLevelSecurityObject."Field Level Security Code" := Value[3];
              ESFieldLevelSecurityObject."Object Type" := ESFileMgt.EvaluateInteger(Value[4]);
              ESFieldLevelSecurityObject."Object ID" := ESFileMgt.EvaluateInteger(Value[5]);
              ESFieldLevelSecurityObject."General Permission" := ESFileMgt.EvaluateInteger(Value[6]);
              ESFieldLevelSecurityObject.Comment := Value[7];
              ESFieldLevelSecurityObject.INSERT(TRUE);

              TotalRecords := TotalRecords + 1;
            END;
          '14123807V1':
            BEGIN
              ESFieldLevelSecControl.INIT;
              ESFieldLevelSecControl."Source Table ID" := ESFileMgt.EvaluateInteger(Value[2]);
              ESFieldLevelSecControl."Field Level Security Code" := Value[3];
              ESFieldLevelSecControl."Object Type" := ESFileMgt.EvaluateInteger(Value[4]);
              ESFieldLevelSecControl."Object ID" := ESFileMgt.EvaluateInteger(Value[5]);
              ESFieldLevelSecControl.Type := ESFileMgt.EvaluateInteger(Value[6]);
              ESFieldLevelSecControl.ID := ESFileMgt.EvaluateInteger(Value[7]);
              ESFieldLevelSecControl.Permission := ESFileMgt.EvaluateInteger(Value[8]);
              ESFieldLevelSecControl.Comment := Value[9];
              ESFieldLevelSecControl.INSERT(TRUE);

              TotalRecords := TotalRecords + 1;
            END;
          ELSE
            ERROR(Text002,Value[1]);
        END;
      END;

      InputFile.CLOSE;
      IF ServerFileName <> '' THEN
        ERASE(ServerFileName);

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ShowStatus THEN
        MESSAGE(Text018,TotalRecords,ImportFileName);
    END;

    PROCEDURE SetNoExtendedInsertDelete@1240520001(NewValue@1240520001 : Boolean);
    BEGIN
      NoExtendedInsertDelete := NewValue;
    END;

    PROCEDURE TestEditAllowed@1240520003(RetrieveSetup@1240520002 : Boolean);
    VAR
      ESLoginSetup@1240520001 : Record 14123540;
    BEGIN
      IF RetrieveSetup THEN
        ESFLADSSetup.GET('');

      IF ESFLADSSetup."Use Login Setup" THEN BEGIN
        ESLoginSetup.GET(USERID);
        ESLoginSetup.TESTFIELD("Edit Field level Sec. Codes");
      END;
    END;

    BEGIN
    END.
  }
}

