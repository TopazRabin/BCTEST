OBJECT Table 14097459 Container Plan Line
{
  OBJECT-PROPERTIES
  {
    Date=03/27/19;
    Time=12:00:00 PM;
    Version List=UBP3.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ContPlanHdr.GET("Container Plan No.");
               "Receiving Location" := ContPlanHdr."Receiving Location";
               "No Lines Selected" := TRUE;
             END;

    OnModify=BEGIN
               IF (xRec."Buy From Vendor No." <> '') AND
                  (xRec."Suggested Order No." <> '')
               THEN BEGIN
                 IF ("Buy From Vendor No." <> xRec."Buy From Vendor No.") OR
                    ("Suggested Order No." <> xRec."Suggested Order No.")
                 THEN BEGIN
                   SugOrderLine.RESET;
                   SugOrderLine.SETRANGE("Buy-from Vendor No.",xRec."Buy From Vendor No.");
                   SugOrderLine.SETRANGE("Document No.",xRec."Suggested Order No.");
                   SugOrderLine.SETRANGE("Container Plan No.",xRec."Container Plan No.");
                   IF SugOrderLine.FIND('-') THEN
                     ERROR(Text001);
                 END;
               END;

               ContPlanHdr.GET("Container Plan No.");
               "Receiving Location" := ContPlanHdr."Receiving Location";
             END;

    OnDelete=BEGIN
               DeselectAllLines;
             END;

    CaptionML=ENU=Container Plan Line;
  }
  FIELDS
  {
    { 1   ;   ;Container Plan No.  ;Code20        ;CaptionML=ENU=Container Plan No. }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=ENU=Line No. }
    { 11  ;   ;Buy From Vendor No. ;Code20        ;TableRelation=Vendor.No.;
                                                   CaptionML=ENU=Buy From Vendor No. }
    { 12  ;   ;Suggested Order No. ;Code20        ;TableRelation="Suggested Order Header".No. WHERE (Buy-from Vendor No.=FIELD(Buy From Vendor No.));
                                                   CaptionML=ENU=Suggested Order No. }
    { 13  ;   ;Vendor Name         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Buy From Vendor No.)));
                                                   CaptionML=ENU=Vendor Name;
                                                   Editable=No }
    { 14  ;   ;Net Weight          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Suggested Order Line"."Line Net Weight" WHERE (Container Plan No.=FIELD(Container Plan No.),
                                                                                                                   Buy-from Vendor No.=FIELD(Buy From Vendor No.),
                                                                                                                   Document No.=FIELD(Suggested Order No.)));
                                                   CaptionML=ENU=Net Weight;
                                                   Editable=No }
    { 15  ;   ;Gross Weight        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Suggested Order Line"."Line Gross Weight" WHERE (Container Plan No.=FIELD(Container Plan No.),
                                                                                                                     Buy-from Vendor No.=FIELD(Buy From Vendor No.),
                                                                                                                     Document No.=FIELD(Suggested Order No.)));
                                                   CaptionML=ENU=Gross Weight;
                                                   Editable=No }
    { 16  ;   ;Volume              ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Suggested Order Line"."Line Volume" WHERE (Container Plan No.=FIELD(Container Plan No.),
                                                                                                               Buy-from Vendor No.=FIELD(Buy From Vendor No.),
                                                                                                               Document No.=FIELD(Suggested Order No.)));
                                                   CaptionML=ENU=Volume;
                                                   Editable=No }
    { 17  ;   ;No Lines Selected   ;Boolean       ;CaptionML=ENU=No Lines Selected;
                                                   Editable=No }
    { 18  ;   ;Receiving Location  ;Code20        ;CaptionML=ENU=Receiving Location }
    { 100 ;   ;Posted Sug. Order No.;Code20       ;CaptionML=ENU=Posted Sug. Order No. }
  }
  KEYS
  {
    {    ;Container Plan No.,Line No.             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ContPlanHdr@1240030002 : Record 14097458;
      ContPlanLine@1240030004 : Record 14097459;
      SugOrderHdr@1240030003 : Record 14000565;
      SugOrderLine@1240030000 : Record 14000566;
      Text001@1240030001 : TextConst 'ENU=Suggested Order Lines are still assigned.  You must Deselect these lines before changing the line.';
      SugOrderLine2@1240030006 : Record 14000566;
      PlanContainerNo@1240030005 : Record 14097457;
      TotalQuantity@1240030007 : Decimal;

    PROCEDURE SelectAllLines@1240030000();
    BEGIN
      ContPlanLine.RESET;
      ContPlanLine.SETRANGE("Container Plan No.","Container Plan No.");
      IF ContPlanLine.FIND('-') THEN
        REPEAT
          SugOrderLine.RESET;
          SugOrderLine.SETRANGE("Buy-from Vendor No.",ContPlanLine."Buy From Vendor No.");
          SugOrderLine.SETRANGE("Document No.",ContPlanLine."Suggested Order No.");
          IF SugOrderLine.FIND('-') THEN
            REPEAT
              SugOrderLine."Container Plan No." := "Container Plan No.";
              SugOrderLine."Container No." := '';
              SugOrderLine.MODIFY;
            UNTIL SugOrderLine.NEXT = 0;
        UNTIL ContPlanLine.NEXT = 0;
      "No Lines Selected" := FALSE;
    END;

    PROCEDURE DeselectAllLines@1240030001();
    BEGIN
      ContPlanHdr.GET("Container Plan No.");
      PlanContainerNo.RESET;
      PlanContainerNo.SETRANGE("Container Plan No.",ContPlanHdr."No.");
      IF PlanContainerNo.FIND('-') THEN
        REPEAT
          SugOrderLine.RESET;
          SugOrderLine.SETCURRENTKEY(
            "Container Plan No.","Buy-from Vendor No.","Document No.","Container No.");
          SugOrderLine.SETRANGE("Container Plan No.",PlanContainerNo."Container Plan No.");
          SugOrderLine.SETRANGE("Container No.",PlanContainerNo."Container No.");
          SugOrderLine.SETRANGE("Split New Line",FALSE);
          IF SugOrderLine.FIND('-') THEN
            REPEAT
              TotalQuantity := SugOrderLine.Quantity;
              SugOrderLine2.RESET;
              SugOrderLine2.SETRANGE("Buy-from Vendor No.",SugOrderLine."Buy-from Vendor No.");
              SugOrderLine2.SETRANGE("Document No.",SugOrderLine."Document No.");
              SugOrderLine2.SETRANGE("No.",SugOrderLine."No.");
              SugOrderLine2.SETRANGE("Variant Code",SugOrderLine."Variant Code");
              SugOrderLine2.SETFILTER("Line No.",'<>%1',SugOrderLine."Line No.");
              IF SugOrderLine2.FIND('-') THEN BEGIN
                REPEAT
                  TotalQuantity := TotalQuantity + SugOrderLine2.Quantity;
                UNTIL SugOrderLine2.NEXT = 0;
                SugOrderLine.VALIDATE(Quantity,TotalQuantity);
                SugOrderLine.MODIFY;
                SugOrderLine2.DELETEALL;
              END;
            UNTIL SugOrderLine.NEXT = 0;
        UNTIL PlanContainerNo.NEXT = 0;

      SugOrderLine.RESET;
      SugOrderLine.SETRANGE("Container Plan No.","Container Plan No.");
      IF SugOrderLine.FIND('-') THEN
        REPEAT
          SugOrderLine."Container Plan No." := '';
          SugOrderLine."Container No." := '';
          SugOrderLine.MODIFY;
        UNTIL SugOrderLine.NEXT = 0;
      "No Lines Selected" := TRUE;
    END;

    PROCEDURE ShowSuggestedOrderLines@1240030002();
    BEGIN
      SugOrderLine.RESET;
      SugOrderLine.SETCURRENTKEY("Container Plan No.");
      SugOrderLine.SETRANGE("Container Plan No.","Container Plan No.");
      PAGE.RUNMODAL(PAGE::"Plan Sug. Order Lines",SugOrderLine);
    END;

    PROCEDURE ShowSuggestedOrder@1240030003();
    BEGIN
      SugOrderHdr.RESET;
      SugOrderHdr.SETRANGE("Buy-from Vendor No.","Buy From Vendor No.");
      SugOrderHdr.SETRANGE("No.","Suggested Order No.");
      PAGE.RUNMODAL(PAGE::"Suggested Order",SugOrderHdr);
    END;

    BEGIN
    END.
  }
}

