OBJECT Table 14123807 ES Field Level Sec. Control
{
  OBJECT-PROPERTIES
  {
    Date=11/01/16;
    Time=12:00:00 PM;
    Version List=ES1.41.22;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit Field level Sec. Codes");
               END;

               ESFieldLevelSecurityCode.GET("Source Table ID","Field Level Security Code");

               ESSourceTableSetup.GET("Source Table ID");

               ESFieldLevelSecurityObject.GET(
                 "Source Table ID","Field Level Security Code","Object Type","Object ID");

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnModify=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit Field level Sec. Codes");
               END;

               ESFieldLevelSecurityCode.GET("Source Table ID","Field Level Security Code");

               ESSourceTableSetup.GET("Source Table ID");

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnDelete=BEGIN
               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFLADSSetup.GET('');

               IF ESFLADSSetup."Use Login Setup" THEN BEGIN
                 ESLoginSetup.GET(USERID);
                 ESLoginSetup.TESTFIELD("Edit Field level Sec. Codes");
               END;

               ESFieldLevelSecurityCode.GET("Source Table ID","Field Level Security Code");

               ESSourceTableSetup.GET("Source Table ID");

               ESFLADSUpdateNo.UpdateNo;
             END;

    OnRename=BEGIN
               xRec.CALCFIELDS("From Setup Company");
               xRec.TESTFIELD("From Setup Company",FALSE);

               CALCFIELDS("From Setup Company");
               TESTFIELD("From Setup Company",FALSE);

               ESFieldLevelSecurityCode.GET("Source Table ID","Field Level Security Code");

               ESSourceTableSetup.GET("Source Table ID");

               ESFLADSUpdateNo.UpdateNo;
             END;

    CaptionML=ENU=Field Level Security Control;
    LookupPageID=Page14123807;
    DrillDownPageID=Page14123807;
  }
  FIELDS
  {
    { 1   ;   ;Source Table ID     ;Integer       ;TableRelation="ES Source Table Setup";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Source Table Caption");
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupObjectID;
                                                            END;

                                                   CaptionML=ENU=Source Table ID;
                                                   NotBlank=Yes }
    { 2   ;   ;Field Level Security Code;Code20   ;TableRelation="ES Field Level Security Code".Code WHERE (Source Table ID=FIELD(Source Table ID));
                                                   OnLookup=BEGIN
                                                              LookupObjectID;
                                                            END;

                                                   CaptionML=ENU=Field Level Security Code;
                                                   NotBlank=Yes }
    { 3   ;   ;Object Type         ;Option        ;OnLookup=BEGIN
                                                              LookupObjectID;
                                                            END;

                                                   CaptionML=ENU=Object Type;
                                                   OptionCaptionML=ENU=" ,,,,,,,,Page";
                                                   OptionString=[ ,,,,,,,,Page];
                                                   NotBlank=Yes }
    { 4   ;   ;Object ID           ;Integer       ;TableRelation="ES FLADS Relation"."Object ID" WHERE (Object Type=FIELD(Object Type),
                                                                                                        Type=FILTER(Source Table|Data Item),
                                                                                                        Relation Object Type=CONST(Table),
                                                                                                        Relation Object ID=FIELD(Source Table ID));
                                                   OnValidate=BEGIN
                                                                IF "Object ID" <> 0 THEN BEGIN
                                                                  ESFLADSSetup.GET;

                                                                  ESFLADSRelation.RESET;
                                                                  IF ESFLADSSetup."Use Form Setup for Pages" OR ESFLADSSetup."Use Page Setup for Forms" THEN
                                                                    ESFLADSRelation.SETFILTER("Object Type",'%1|%2',"Object Type"::"2","Object Type"::Page)
                                                                  ELSE
                                                                    ESFLADSRelation.SETRANGE("Object Type","Object Type");
                                                                  ESFLADSRelation.SETRANGE("Object ID","Object ID");
                                                                  ESFLADSRelation.SETRANGE(Type,ESFLADSRelation.Type::"Source Table");
                                                                  ESFLADSRelation.SETRANGE("Relation Object Type",ESFLADSRelation."Relation Object Type"::Table);
                                                                  ESFLADSRelation.SETRANGE("Relation Object ID","Source Table ID");
                                                                  ESFLADSRelation.FIND('-');
                                                                END;

                                                                CALCFIELDS("Object Caption");
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupObjectID;
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Object ID }
    { 5   ;   ;Type                ;Option        ;CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Field,Action;
                                                   OptionString=Field,Control }
    { 6   ;   ;ID                  ;Integer       ;OnValidate=BEGIN
                                                                CALCFIELDS("Field Caption");
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupID;
                                                            END;

                                                   CaptionML=ENU=ID }
    { 11  ;   ;Permission          ;Option        ;CaptionML=ENU=Permission;
                                                   OptionCaptionML=ENU=Edit,View,Hide;
                                                   OptionString=Edit,View,Hide }
    { 12  ;   ;Source Table Caption;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Source Table ID)));
                                                   CaptionML=ENU=Source Table Caption;
                                                   Editable=No }
    { 13  ;   ;Object Caption      ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FIELD(Object Type),
                                                                                                                Object ID=FIELD(Object ID)));
                                                   CaptionML=ENU=Object Caption;
                                                   Editable=No }
    { 14  ;   ;Field Level Security Descr.;Text250;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Field Level Security Code".Description WHERE (Source Table ID=FIELD(Source Table ID),
                                                                                                                        Code=FIELD(Field Level Security Code)));
                                                   CaptionML=ENU=Field Level Security Descr.;
                                                   Editable=No }
    { 15  ;   ;Field Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Source Table ID),
                                                                                                   No.=FIELD(ID)));
                                                   CaptionML=ENU=Field Caption;
                                                   Editable=No }
    { 16  ;   ;Comment             ;Text250       ;CaptionML=ENU=Comment }
    { 21  ;   ;From Setup Company  ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES FLADS Copy Setup"."From Setup Company" WHERE (Table ID=CONST(14123803),
                                                                                                                        Source Table ID=FIELD(Source Table ID),
                                                                                                                        Code=FIELD(Field Level Security Code)));
                                                   CaptionML=ENU=From Setup Company;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Source Table ID,Field Level Security Code,Object Type,Object ID,Type,ID;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ESFieldLevelSecurityCode@1240520012 : Record 14123803;
      ESFieldLevelSecurityObject@1240520008 : Record 14123806;
      ESFLADSUpdateNo@1240520009 : Record 14123813;
      ESFLADSRelation@1240520010 : Record 14123810;
      Text001@1001 : TextConst 'ENU=<All Fields not specifically controlled.>';
      Text002@1002 : TextConst 'ENU=<All Actions not specifically controlled.>';
      ESSourceTableSetup@1240520013 : Record 14123802;
      ESFLADSSetup@1240520002 : Record 14123801;
      ESLoginSetup@1240520001 : Record 14123540;
      Text003@1003 : TextConst 'ENU=No new Fields selected.';
      Text004@1004 : TextConst 'ENU=Add %1 of %2 selected Fields.';
      Text005@1005 : TextConst 'ENU=No new Actions selected.';
      Text006@1006 : TextConst 'ENU=Add %1 of %2 selected Actions.';
      Text007@1007 : TextConst 'ENU=Edit,View,Hide';

    PROCEDURE AddFields@1240520001();
    VAR
      Field@1240520009 : Record 2000000041;
      Field2@1240520002 : Record 2000000041;
      ESFieldLevelSecControl@1240520005 : Record 14123807;
      ESFields@1240520001 : Page 14123813;
      NewFields@1240520003 : Integer;
      SelectedFields@1240520004 : Integer;
      AccessLevel@1240520006 : Integer;
    BEGIN
      TESTFIELD("Source Table ID");

      Field.FILTERGROUP(2);
      Field.RESET;
      Field.SETRANGE(TableNo,"Source Table ID");
      Field.FILTERGROUP(0);

      Field.TableNo := "Source Table ID";
      Field."No." := ID;
      IF Field.FIND('=><') THEN
        ;

      CLEAR(ESFields);
      ESFields.LOOKUPMODE(TRUE);
      Field.FILTERGROUP(2);
      ESFields.SETTABLEVIEW(Field);
      Field.FILTERGROUP(0);
      IF ESFields.RUNMODAL = ACTION::LookupOK THEN BEGIN
        ESFields.GetSelectionFilter(Field2);
        Field2.FIND('-');
        REPEAT
          Field2.TESTFIELD(TableNo,"Source Table ID");

          IF NOT ESFieldLevelSecControl.GET(
                   "Source Table ID","Field Level Security Code","Object Type","Object ID",
                   Type::Field,Field2."No.")
          THEN
            NewFields := NewFields + 1;
          SelectedFields := SelectedFields + 1;
        UNTIL Field2.NEXT = 0;

        IF NewFields = 0 THEN
          ERROR(Text003);

        AccessLevel := STRMENU(Text007) - 1;

        IF CONFIRM(Text004,TRUE,NewFields,SelectedFields) THEN BEGIN
          Field2.FIND('-');
          REPEAT
            IF NOT ESFieldLevelSecControl.GET(
                     "Source Table ID","Field Level Security Code","Object Type","Object ID",
                     Type::Field,Field2."No.")
            THEN BEGIN
              ESFieldLevelSecControl.INIT;
              ESFieldLevelSecControl."Source Table ID" := "Source Table ID";
              ESFieldLevelSecControl."Field Level Security Code" := "Field Level Security Code";
              ESFieldLevelSecControl."Object Type" := "Object Type";
              ESFieldLevelSecControl."Object ID" := "Object ID";
              ESFieldLevelSecControl.Type := Type::Field;
              ESFieldLevelSecControl.VALIDATE(ID,Field2."No.");
              ESFieldLevelSecControl.VALIDATE(Permission,AccessLevel);
              ESFieldLevelSecControl.INSERT(TRUE);
            END;
          UNTIL Field2.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE AddControls@1240520002();
    VAR
      ESFLADSControl@1240520001 : Record 14123814;
      ESFLADSControl2@1240520003 : Record 14123814;
      ESFieldLevelSecControl@1240520006 : Record 14123807;
      ESFLADSControlsLookup@1240520002 : Page 14123842;
      NewControls@1240520004 : Integer;
      SelectedControls@1240520005 : Integer;
      AccessLevel@1240520007 : Integer;
    BEGIN
      TESTFIELD("Source Table ID");
      TESTFIELD("Object ID");

      ESFLADSControl.FILTERGROUP(2);
      ESFLADSControl.RESET;
      ESFLADSControl.SETRANGE("Object Type","Object Type");
      ESFLADSControl.SETRANGE("Object ID","Object ID");
      CASE "Object Type" OF
        "Object Type"::"2":
          BEGIN
            ESFLADSControl.SETFILTER(
              "Control Type",'%1|%2',
              ESFLADSControl."Control Type"::MenuButton,
              ESFLADSControl."Control Type"::CommandButton);
            ESFLADSControl.SETFILTER("Control Name",'<>%1','');
          END;
        "Object Type"::Page:
          BEGIN
            ESFLADSControl.SETRANGE("Control Type",ESFLADSControl."Control Type"::Action);
          END;
      END;
      ESFLADSControl.FILTERGROUP(0);

      CLEAR(ESFLADSControlsLookup);
      ESFLADSControlsLookup.LOOKUPMODE(TRUE);
      ESFLADSControl.FILTERGROUP(2);
      ESFLADSControlsLookup.SETTABLEVIEW(ESFLADSControl);
      ESFLADSControl.FILTERGROUP(0);
      IF ESFLADSControlsLookup.RUNMODAL = ACTION::LookupOK THEN BEGIN
        ESFLADSControlsLookup.GetSelectionFilter(ESFLADSControl2);
        ESFLADSControl2.FIND('-');
        REPEAT
          ESFLADSControl2.TESTFIELD("Object Type","Object Type");
          ESFLADSControl2.TESTFIELD("Object ID","Object ID");
          IF NOT ESFieldLevelSecControl.GET(
                   "Source Table ID","Field Level Security Code","Object Type","Object ID",
                   Type::Control,ESFLADSControl2."Control ID")
          THEN
            NewControls := NewControls + 1;
          SelectedControls := SelectedControls + 1;
        UNTIL ESFLADSControl2.NEXT = 0;

        IF NewControls = 0 THEN
          ERROR(Text005);

        AccessLevel := STRMENU(Text007) - 1;

        IF CONFIRM(Text006,TRUE,NewControls,SelectedControls) THEN BEGIN
          ESFLADSControl2.FIND('-');
          REPEAT
            ESFLADSControl2.TESTFIELD("Object Type","Object Type");
            ESFLADSControl2.TESTFIELD("Object ID","Object ID");
            IF NOT ESFieldLevelSecControl.GET(
                     "Source Table ID","Field Level Security Code","Object Type","Object ID",
                     Type::Control,ESFLADSControl2."Control ID")
            THEN BEGIN
              ESFieldLevelSecControl.INIT;
              ESFieldLevelSecControl."Source Table ID" := "Source Table ID";
              ESFieldLevelSecControl."Field Level Security Code" := "Field Level Security Code";
              ESFieldLevelSecControl."Object Type" := "Object Type";
              ESFieldLevelSecControl."Object ID" := "Object ID";
              ESFieldLevelSecControl.Type := Type::Control;
              ESFieldLevelSecControl.VALIDATE(ID,ESFLADSControl2."Control ID");
              ESFieldLevelSecControl.VALIDATE(Permission,AccessLevel);
              ESFieldLevelSecControl.INSERT(TRUE);
            END;
          UNTIL ESFLADSControl2.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE LookupObjectID@1240520008();
    BEGIN
      ESFieldLevelSecurityObject.RESET;
      IF "Source Table ID" <> 0 THEN
        ESFieldLevelSecurityObject.SETRANGE("Source Table ID","Source Table ID");
      IF "Field Level Security Code" <> '' THEN
        ESFieldLevelSecurityObject.SETRANGE("Field Level Security Code","Field Level Security Code");
      IF "Object Type" <> 0 THEN
        ESFieldLevelSecurityObject.SETRANGE("Object Type","Object Type");

      ESFieldLevelSecurityObject."Source Table ID" := "Source Table ID";
      ESFieldLevelSecurityObject."Field Level Security Code" := "Field Level Security Code";
      ESFieldLevelSecurityObject."Object Type" := "Object Type";
      ESFieldLevelSecurityObject."Object ID" := "Object ID";
      IF ESFieldLevelSecurityObject.FIND('=><') THEN
        ;

      IF PAGE.RUNMODAL(0,ESFieldLevelSecurityObject) = ACTION::LookupOK THEN BEGIN
        "Source Table ID" := ESFieldLevelSecurityObject."Source Table ID";
        "Field Level Security Code" := ESFieldLevelSecurityObject."Field Level Security Code";
        "Object Type" := ESFieldLevelSecurityObject."Object Type";
        "Object ID" := ESFieldLevelSecurityObject."Object ID";
      END;
    END;

    PROCEDURE LookupID@1240520009();
    VAR
      Field@1240520009 : Record 2000000041;
      ESFLADSControl@1240520008 : Record 14123814;
    BEGIN
      CASE Type OF
        Type::Field:
          BEGIN
            TESTFIELD("Source Table ID");

            Field.FILTERGROUP(2);
            Field.RESET;
            Field.SETRANGE(TableNo,"Source Table ID");
            Field.FILTERGROUP(0);

            Field.TableNo := "Source Table ID";
            Field."No." := ID;
            IF Field.FIND('=><') THEN
              ;
            IF PAGE.RUNMODAL(PAGE::"ES Fields",Field) = ACTION::LookupOK THEN BEGIN
              Field.TESTFIELD(TableNo,"Source Table ID");
              ID := Field."No.";
            END;
          END;
        Type::Control:
          BEGIN
            TESTFIELD("Object ID");

            ESFLADSControl.FILTERGROUP(2);
            ESFLADSControl.RESET;
            ESFLADSControl.SETRANGE("Object Type","Object Type");
            ESFLADSControl.SETRANGE("Object ID","Object ID");
            CASE "Object Type" OF
              "Object Type"::"2":
                BEGIN
                  ESFLADSControl.SETFILTER(
                    "Control Type",'%1|%2',
                    ESFLADSControl."Control Type"::MenuButton,
                    ESFLADSControl."Control Type"::CommandButton);
                  ESFLADSControl.SETFILTER("Control Name",'<>%1','');
                END;
              "Object Type"::Page:
                BEGIN
                  ESFLADSControl.SETRANGE("Control Type",ESFLADSControl."Control Type"::Action);
                END;
            END;
            ESFLADSControl.FILTERGROUP(0);

            IF PAGE.RUNMODAL(0,ESFLADSControl) = ACTION::LookupOK THEN
              ID := ESFLADSControl."Control ID";
          END;
      END;
    END;

    PROCEDURE ControlCaption@1240520012() : Text[250];
    VAR
      ESFLADSControl@1240520008 : Record 14123814;
    BEGIN
      CASE Type OF
        Type::Field:
          BEGIN
            IF ID = 0 THEN
              EXIT(Text001);

            CALCFIELDS("Field Caption");
            EXIT("Field Caption");
          END;
        Type::Control:
          BEGIN
            IF ID = 0 THEN
              EXIT(Text002);

            IF ESFLADSControl.GET("Object Type","Object ID",ID,1) THEN
              EXIT(ESFLADSControl.GetCaption)
            ELSE BEGIN
              ESFLADSControl.RESET;
              ESFLADSControl.SETRANGE("Object Type","Object Type");
              ESFLADSControl.SETRANGE("Object ID","Object ID");
              ESFLADSControl.SETRANGE("Control ID",ID);
              IF ESFLADSControl.FIND('-') THEN
                EXIT(ESFLADSControl.GetCaption);
            END;
          END;
        ELSE
          EXIT('');
      END;
    END;

    PROCEDURE NoOfTimesOnObject@1240520010(DrillDown@1240520009 : Boolean) : Integer;
    VAR
      ESFLADSControl@1240520008 : Record 14123814;
    BEGIN
      IF ("Object ID" = 0) OR (ID = 0) THEN
        EXIT(0);

      ESFLADSControl.RESET;
      ESFLADSControl.SETRANGE("Object Type","Object Type");
      ESFLADSControl.SETRANGE("Object ID","Object ID");
      CASE Type OF
        Type::Field:
          ESFLADSControl.SETRANGE("Field ID",ID);
        Type::Control:
          ESFLADSControl.SETRANGE("Control ID",ID);
      END;

      IF DrillDown THEN
        PAGE.RUNMODAL(0,ESFLADSControl);

      EXIT(ESFLADSControl.COUNT);
    END;

    PROCEDURE OnLookupExists@1240520011(DrillDown@1240520009 : Boolean) : Boolean;
    VAR
      ESFLADSRelation@1240520008 : Record 14123810;
    BEGIN
      IF (Type <> Type::Field) OR (ID = 0) THEN
        EXIT(FALSE);

      ESFLADSRelation.RESET;
      ESFLADSRelation.SETRANGE("Object Type",ESFLADSRelation."Object Type"::Table);
      ESFLADSRelation.SETRANGE("Object ID","Source Table ID");
      ESFLADSRelation.SETRANGE("Field ID",ID);
      ESFLADSRelation.SETRANGE(Type,ESFLADSRelation.Type::OnLookup);
      IF DrillDown THEN
        PAGE.RUNMODAL(0,ESFLADSRelation);

      EXIT(ESFLADSRelation.FIND('-'));
    END;

    BEGIN
    END.
  }
}

