OBJECT Table 7861 MS- PayPal Standard Template
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=MS- PayPal Standard Template;
               ESM=MS - Plantilla de PayPal Standard;
               FRC=MS - Modäle PayPal Standard;
               ENC=MS- PayPal Standard Template];
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              ESM=C¢digo;
                                                              FRC=Code;
                                                              ENC=Code] }
    { 2   ;   ;Name                ;Text250       ;CaptionML=[ENU=Name;
                                                              ESM=Nombre;
                                                              FRC=Nom;
                                                              ENC=Name];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text250       ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description];
                                                   NotBlank=Yes }
    { 8   ;   ;Terms of Service    ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Terms of Service;
                                                              ESM=Condiciones del servicio;
                                                              FRC=Conditions d'utilisation;
                                                              ENC=Terms of Service] }
    { 11  ;   ;Logo                ;BLOB          ;CaptionML=[ENU=Logo;
                                                              ESM=Logotipo;
                                                              FRC=Logo;
                                                              ENC=Logo];
                                                   SubType=Bitmap }
    { 12  ;   ;Target URL          ;BLOB          ;CaptionML=[ENU=Target URL;
                                                              ESM=URL de destino;
                                                              FRC=URL cible;
                                                              ENC=Target URL];
                                                   SubType=Bitmap }
    { 13  ;   ;Logo URL            ;BLOB          ;CaptionML=[ENU=Logo URL;
                                                              ESM=URL del logotipo;
                                                              FRC=URL logo;
                                                              ENC=Logo URL];
                                                   SubType=Bitmap }
    { 14  ;   ;Logo Last Update DateTime;DateTime ;CaptionML=[ENU=Logo Last Update DateTime;
                                                              ESM=Fecha y hora de actualizaci¢n m†s reciente de logotipo;
                                                              FRC=Date et heure de la derniäre mise Ö jour du logo;
                                                              ENC=Logo Last Update DateTime] }
    { 15  ;   ;Logo Update Frequency;Duration     ;CaptionML=[ENU=Logo Update Frequency;
                                                              ESM=Frecuencia de actualizaci¢n de logotipo;
                                                              FRC=FrÇquence de mise Ö jour du logo;
                                                              ENC=Logo Update Frequency] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;Description         ;Description                              }
  }
  CODE
  {
    VAR
      UpdatingPayPalLogoFailedTxt@1000 : TextConst 'ENU=Updating PayPal logo failed.;ESM=Error al actualizar el logotipo de PayPal.;FRC=êchec de la mise Ö jour du logo PayPal.;ENC=Updating PayPal logo failed.';
      PayPalContextTxt@1001 : TextConst 'ENU=PayPal Standard;ESM=PayPal Standard;FRC=PayPal Standard;ENC=PayPal Standard';

    PROCEDURE GetTargetURL@10() : Text;
    VAR
      InStream@1000 : InStream;
      TargetURL@1002 : Text;
    BEGIN
      TargetURL := '';
      CALCFIELDS("Target URL");
      IF "Target URL".HASVALUE THEN BEGIN
        "Target URL".CREATEINSTREAM(InStream);
        InStream.READ(TargetURL);
      END;
      EXIT(TargetURL);
    END;

    PROCEDURE SetTargetURL@20(TargetURL@1000 : Text);
    VAR
      WebRequestHelper@1002 : Codeunit 1299;
      OutStream@1001 : OutStream;
    BEGIN
      WebRequestHelper.IsValidUri(TargetURL);
      WebRequestHelper.IsHttpUrl(TargetURL);
      WebRequestHelper.IsSecureHttpUrl(TargetURL);

      "Target URL".CREATEOUTSTREAM(OutStream);
      OutStream.WRITE(TargetURL);
      MODIFY;
    END;

    PROCEDURE SetTargetURLNoVerification@120(TargetURL@1000 : Text);
    VAR
      OutStream@1001 : OutStream;
    BEGIN
      "Target URL".CREATEOUTSTREAM(OutStream);
      OutStream.WRITE(TargetURL);
      MODIFY;
    END;

    PROCEDURE GetLogoURL@2() : Text;
    VAR
      InStream@1000 : InStream;
      LogoURL@1002 : Text;
    BEGIN
      LogoURL := '';
      CALCFIELDS("Logo URL");
      IF "Logo URL".HASVALUE THEN BEGIN
        "Logo URL".CREATEINSTREAM(InStream);
        InStream.READ(LogoURL);
      END;
      EXIT(LogoURL);
    END;

    PROCEDURE SetLogoURL@1(LogoURL@1000 : Text);
    VAR
      WebRequestHelper@1002 : Codeunit 1299;
      OutStream@1001 : OutStream;
    BEGIN
      WebRequestHelper.IsValidUri(LogoURL);
      WebRequestHelper.IsHttpUrl(LogoURL);
      WebRequestHelper.IsSecureHttpUrl(LogoURL);

      "Logo URL".CREATEOUTSTREAM(OutStream);
      OutStream.WRITE(LogoURL);
      MODIFY;
    END;

    PROCEDURE SetLogoURLNoVerification@131(LogoURL@1000 : Text);
    VAR
      OutStream@1001 : OutStream;
    BEGIN
      "Logo URL".CREATEOUTSTREAM(OutStream);
      OutStream.WRITE(LogoURL);
      MODIFY;
    END;

    PROCEDURE RefreshLogoIfNeeded@5();
    BEGIN
      IF ("Logo Last Update DateTime" <= (CURRENTDATETIME - "Logo Update Frequency")) AND ("Logo Update Frequency" <> 0) THEN
        IF UpdateLogoFromURL(GetLogoURL) THEN
          EXIT;

      CALCFIELDS(Logo);
    END;

    PROCEDURE UpdateLogoFromURL@18(LogoURL@1000 : Text) : Boolean;
    VAR
      TempBlob@1006 : TEMPORARY Record 99008535;
      ActivityLog@1001 : Record 710;
      ResponseInStream@1002 : InStream;
      LogoOutStream@1005 : OutStream;
    BEGIN
      IF LogoURL = '' THEN
        EXIT(FALSE);

      TempBlob.INIT;
      TempBlob.Blob.CREATEINSTREAM(ResponseInStream);
      TempBlob.INSERT;

      IF NOT DownloadLogo(ResponseInStream,LogoURL) THEN BEGIN
        ActivityLog.LogActivity(Rec,ActivityLog.Status::Failed,PayPalContextTxt,UpdatingPayPalLogoFailedTxt,GETLASTERRORTEXT);
        EXIT(FALSE);
      END;

      Logo.CREATEOUTSTREAM(LogoOutStream);
      COPYSTREAM(LogoOutStream,ResponseInStream);
      "Logo Last Update DateTime" := CURRENTDATETIME;
      MODIFY(TRUE);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE DownloadLogo@3(VAR ResponseInStream@1000 : InStream;LogoURL@1004 : Text) : Boolean;
    VAR
      HttpWebRequestMgt@1005 : Codeunit 1297;
      HttpStatusCode@1002 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpStatusCode";
      ResponseHeaders@1001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Specialized.NameValueCollection";
    BEGIN
      HttpWebRequestMgt.Initialize(LogoURL);
      HttpWebRequestMgt.DisableUI;
      EXIT(HttpWebRequestMgt.GetResponse(ResponseInStream,HttpStatusCode,ResponseHeaders));
    END;

    BEGIN
    END.
  }
}

