OBJECT Table 27006 CFDI Relation Document
{
  OBJECT-PROPERTIES
  {
    Date=09/21/19;
    Time=12:00:00 PM;
    Version List=NAVNA11.00.00.36462;
  }
  PROPERTIES
  {
    CaptionML=[ENU=CFDI Relation Document;
               ESM=Documento de relaci¢n CFDI;
               FRC=Document de relation CFDI;
               ENC=CFDI Relation Document];
  }
  FIELDS
  {
    { 1   ;   ;Document Table ID   ;Integer       ;CaptionML=[ENU=Document Table ID;
                                                              ESM=Id. de tabla de documentos;
                                                              FRC=Code de la table document;
                                                              ENC=Document Table ID] }
    { 2   ;   ;Document Type       ;Integer       ;CaptionML=[ENU=Document Type;
                                                              ESM=Tipo de documento;
                                                              FRC=Type de document;
                                                              ENC=Document Type] }
    { 3   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              ESM=N.§ documento;
                                                              FRC=Nø document;
                                                              ENC=Document No.] }
    { 4   ;   ;Customer No.        ;Code20        ;CaptionML=[ENU=Customer No.;
                                                              ESM=N.§ cliente;
                                                              FRC=Nø client;
                                                              ENC=Customer No.];
                                                   Editable=No }
    { 6   ;   ;Related Doc. Type   ;Option        ;InitValue=Invoice;
                                                   CaptionML=[ENU=Related Doc. Type;
                                                              ESM=Tipo doc. relacionado;
                                                              FRC=Type doc. associ‚;
                                                              ENC=Related Doc. Type];
                                                   OptionCaptionML=[ENU=,,Invoice,Credit Memo;
                                                                    ESM=,,Factura,Nota de cr‚dito;
                                                                    FRC=,,Facture,Note de cr‚dit;
                                                                    ENC=,,Invoice,Credit Memo];
                                                   OptionString=,,Invoice,Credit Memo }
    { 7   ;   ;Related Doc. No.    ;Code20        ;TableRelation=IF (Related Doc. Type=CONST(Invoice)) "Cust. Ledger Entry"."Document No." WHERE (Document Type=FILTER(Invoice),
                                                                                                                                                  Customer No.=FIELD(Customer No.))
                                                                                                                                                  ELSE IF (Related Doc. Type=CONST(Credit Memo)) "Cust. Ledger Entry"."Document No." WHERE (Document Type=FILTER(Credit Memo),
                                                                                                                                                                                                                                            Customer No.=FIELD(Customer No.));
                                                   OnValidate=BEGIN
                                                                IF "Related Doc. No." = '' THEN
                                                                  EXIT;

                                                                CheckRelatedDocumentNo;
                                                                UpdateFiscalInvoiceNumber;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Related Doc. No.;
                                                              ESM=N.§ de doc. relacionado;
                                                              FRC=Nøÿdoc. associ‚;
                                                              ENC=Related Doc. No.];
                                                   NotBlank=Yes }
    { 21  ;   ;Fiscal Invoice Number PAC;Text50   ;CaptionML=[ENU=Fiscal Invoice Number PAC;
                                                              ESM=N£mero de factura fiscal de PAC;
                                                              FRC=Num‚ro de facture fiscale PAC;
                                                              ENC=Fiscal Invoice Number PAC];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Document Table ID,Document Type,Document No.,Customer No.,Related Doc. Type,Related Doc. No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE InsertRelatedCreditMemos@1310000();
    BEGIN
      IF "Related Doc. No." = '' THEN
        EXIT;

      CASE "Document Table ID" OF
        DATABASE::"Sales Header",DATABASE::"Sales Invoice Header":
          InsertRelatedSalesCreditMemos;
        DATABASE::"Service Header",DATABASE::"Service Invoice Header":
          InsertRelatedServiceCreditMemos;
      END;
    END;

    LOCAL PROCEDURE InsertRelatedSalesCreditMemos@1310012();
    VAR
      SalesCrMemoHeader@1310000 : Record 114;
      CFDIRelationDocument@1310001 : Record 27006;
    BEGIN
      IF "Related Doc. Type" <> "Related Doc. Type"::Invoice THEN
        EXIT;

      SalesCrMemoHeader.SETRANGE("Bill-to Customer No.","Customer No.");
      SalesCrMemoHeader.SETRANGE("Applies-to Doc. Type",SalesCrMemoHeader."Applies-to Doc. Type"::Invoice);
      SalesCrMemoHeader.SETRANGE("Applies-to Doc. No.","Related Doc. No.");
      IF NOT SalesCrMemoHeader.FINDSET THEN
        EXIT;

      REPEAT
        CFDIRelationDocument := Rec;
        CFDIRelationDocument."Related Doc. Type" := CFDIRelationDocument."Related Doc. Type"::"Credit Memo";
        CFDIRelationDocument."Related Doc. No." := SalesCrMemoHeader."No.";
        CFDIRelationDocument."Fiscal Invoice Number PAC" := SalesCrMemoHeader."Fiscal Invoice Number PAC";
        IF CFDIRelationDocument.INSERT THEN;
      UNTIL SalesCrMemoHeader.NEXT = 0;
    END;

    LOCAL PROCEDURE InsertRelatedServiceCreditMemos@1310009();
    VAR
      ServiceCrMemoHeader@1310004 : Record 5994;
      CFDIRelationDocument@1310001 : Record 27006;
    BEGIN
      IF "Related Doc. Type" <> "Related Doc. Type"::Invoice THEN
        EXIT;

      ServiceCrMemoHeader.SETRANGE("Bill-to Customer No.","Customer No.");
      ServiceCrMemoHeader.SETRANGE("Applies-to Doc. Type",ServiceCrMemoHeader."Applies-to Doc. Type"::Invoice);
      ServiceCrMemoHeader.SETRANGE("Applies-to Doc. No.","Related Doc. No.");
      IF NOT ServiceCrMemoHeader.FINDSET THEN
        EXIT;

      REPEAT
        CFDIRelationDocument := Rec;
        CFDIRelationDocument."Related Doc. Type" := CFDIRelationDocument."Related Doc. Type"::"Credit Memo";
        CFDIRelationDocument."Related Doc. No." := ServiceCrMemoHeader."No.";
        CFDIRelationDocument."Fiscal Invoice Number PAC" := ServiceCrMemoHeader."Fiscal Invoice Number PAC";
        IF CFDIRelationDocument.INSERT THEN;
      UNTIL ServiceCrMemoHeader.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckRelatedDocumentNo@1310001();
    VAR
      CustLedgerEntry@1310000 : Record 21;
    BEGIN
      CustLedgerEntry.SETRANGE("Document Type","Related Doc. Type");
      CustLedgerEntry.SETRANGE("Document No.","Related Doc. No.");
      CustLedgerEntry.SETRANGE("Customer No.","Customer No.");
      CustLedgerEntry.FINDFIRST;
    END;

    LOCAL PROCEDURE UpdateFiscalInvoiceNumber@1310003();
    VAR
      SalesInvoiceHeader@1310002 : Record 112;
      SalesCrMemoHeader@1310000 : Record 114;
      ServiceInvoiceHeader@1310003 : Record 5992;
      ServiceCrMemoHeader@1310001 : Record 5994;
    BEGIN
      IF "Related Doc. No." = '' THEN
        EXIT;

      CASE "Document Table ID" OF
        DATABASE::"Sales Header",DATABASE::"Sales Invoice Header":
          IF "Related Doc. Type" = "Related Doc. Type"::Invoice THEN BEGIN
            SalesInvoiceHeader.GET("Related Doc. No.");
            "Fiscal Invoice Number PAC" := SalesInvoiceHeader."Fiscal Invoice Number PAC";
          END ELSE BEGIN
            SalesCrMemoHeader.GET("Related Doc. No.");
            "Fiscal Invoice Number PAC" := SalesCrMemoHeader."Fiscal Invoice Number PAC";
          END;
        DATABASE::"Service Header",DATABASE::"Service Invoice Header":
          IF "Related Doc. Type" = "Related Doc. Type"::Invoice THEN BEGIN
            ServiceInvoiceHeader.GET("Related Doc. No.");
            "Fiscal Invoice Number PAC" := ServiceInvoiceHeader."Fiscal Invoice Number PAC";
          END ELSE BEGIN
            ServiceCrMemoHeader.GET("Related Doc. No.");
            "Fiscal Invoice Number PAC" := ServiceCrMemoHeader."Fiscal Invoice Number PAC";
          END;
      END;
    END;

    BEGIN
    END.
  }
}

