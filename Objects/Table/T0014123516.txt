OBJECT Table 14123516 ES Rest. Point Role Permission
{
  OBJECT-PROPERTIES
  {
    Date=04/14/15;
    Time=12:00:00 PM;
    Version List=ES1.41;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               Comment(4,'');
             END;

    CaptionML=ENU=Restore Point Permission;
    LookupPageID=Page14123516;
    DrillDownPageID=Page14123516;
  }
  FIELDS
  {
    { 1   ;   ;Role ID             ;Code20        ;TableRelation="ES Restore Point Role"."Role ID" WHERE (Version Code=FIELD(Version Code));
                                                   CaptionML=ENU=Permission Set ID }
    { 2   ;   ;Role Name           ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Restore Point Role"."Role Name" WHERE (Role ID=FIELD(Role ID),
                                                                                                                 Version Code=FIELD(Version Code)));
                                                   CaptionML=ENU=Permission Set Name }
    { 3   ;   ;Object Type         ;Option        ;CaptionML=ENU=Object Type;
                                                   OptionCaptionML=ENU=TableData,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System;
                                                   OptionString=TableData,Table,,Report,,Codeunit,XMLport,MenuSuite,Page,Query,System }
    { 4   ;   ;Object ID           ;Integer       ;TableRelation="ES Restore Point Object Info."."Object ID" WHERE (Version Code=FIELD(Version Code),
                                                                                                                    Object Type=FIELD(Object Type));
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Object ID }
    { 5   ;   ;Object Caption      ;Text249       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ES Restore Point Object Info."."Object Caption" WHERE (Version Code=FIELD(Version Code),
                                                                                                                              Object Type=FIELD(Object Type),
                                                                                                                              Object ID=FIELD(Object ID)));
                                                   CaptionML=ENU=Object Caption;
                                                   Editable=No }
    { 6   ;   ;Read Permission     ;Option        ;InitValue=Yes;
                                                   CaptionML=ENU=Read Permission;
                                                   OptionCaptionML=ENU=" ,Yes,Indirect";
                                                   OptionString=[ ,Yes,Indirect] }
    { 7   ;   ;Insert Permission   ;Option        ;InitValue=Yes;
                                                   CaptionML=ENU=Insert Permission;
                                                   OptionCaptionML=ENU=" ,Yes,Indirect";
                                                   OptionString=[ ,Yes,Indirect] }
    { 8   ;   ;Modify Permission   ;Option        ;InitValue=Yes;
                                                   CaptionML=ENU=Modify Permission;
                                                   OptionCaptionML=ENU=" ,Yes,Indirect";
                                                   OptionString=[ ,Yes,Indirect] }
    { 9   ;   ;Delete Permission   ;Option        ;InitValue=Yes;
                                                   CaptionML=ENU=Delete Permission;
                                                   OptionCaptionML=ENU=" ,Yes,Indirect";
                                                   OptionString=[ ,Yes,Indirect] }
    { 10  ;   ;Execute Permission  ;Option        ;InitValue=Yes;
                                                   CaptionML=ENU=Execute Permission;
                                                   OptionCaptionML=ENU=" ,Yes,Indirect";
                                                   OptionString=[ ,Yes,Indirect] }
    { 11  ;   ;Security Filter     ;TableFilter   ;TableIDExpr="Object ID";
                                                   CaptionML=ENU=Security Filter }
    { 1001;   ;Version Code        ;Code20        ;TableRelation="ES Restore Point".Code;
                                                   CaptionML=ENU=Version Code;
                                                   NotBlank=Yes }
    { 1002;   ;Exists in Live      ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist(Permission WHERE (Role ID=FIELD(Role ID),
                                                                                       Object Type=FIELD(Object Type),
                                                                                       Object ID=FIELD(Object ID)));
                                                   CaptionML=ENU=Exists in Live;
                                                   Editable=No }
    { 1003;   ;Exists in Easy Security;Boolean    ;FieldClass=FlowField;
                                                   CalcFormula=Exist("ES Role Permission" WHERE (Role ID=FIELD(Role ID),
                                                                                                 Object Type=FIELD(Object Type),
                                                                                                 Object ID=FIELD(Object ID)));
                                                   CaptionML=ENU=Exists in Easy Security;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Version Code,Role ID,Object Type,Object ID;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1240520008 : TextConst 'ENU=Permissions';

    PROCEDURE FormatPermissionString@1240520008() : Text[30];
    VAR
      ESFormatPermssion@1240520008 : Codeunit 14123506;
    BEGIN
      EXIT(
        ESFormatPermssion.FormatPermissionString(
          "Read Permission","Insert Permission","Modify Permission","Delete Permission",
          "Execute Permission"));
    END;

    PROCEDURE FormatAsTextLine@1240520037(ShortPermissionText@1240520009 : Boolean;VAR ESTextLine@1240520011 : Record 14123520;NoOfRelatedKeyFields@1240520012 : Integer;CalcFlowFields@1240520013 : Boolean) : Boolean;
    VAR
      ESRolePermission@1240520001 : Record 14123506;
      ESFormatMgt@1240520014 : Codeunit 14123505;
      RecRef@1240520010 : RecordRef;
    BEGIN
      ESFormatMgt.NewRecord(
        ESRolePermission.TABLECAPTION,
        "Version Code","Role ID",FORMAT("Object Type"),FORMAT("Object ID"),
        '','','','','','',4,NoOfRelatedKeyFields);
      IF NOT ShortPermissionText THEN BEGIN
        ESFormatMgt.AddField(FIELDCAPTION("Read Permission"),FORMAT("Read Permission"));
        ESFormatMgt.AddField(FIELDCAPTION("Insert Permission"),FORMAT("Insert Permission"));
        ESFormatMgt.AddField(FIELDCAPTION("Modify Permission"),FORMAT("Modify Permission"));
        ESFormatMgt.AddField(FIELDCAPTION("Delete Permission"),FORMAT("Delete Permission"));
        ESFormatMgt.AddField(FIELDCAPTION("Execute Permission"),FORMAT("Execute Permission"));
      END ELSE
        ESFormatMgt.AddField(Text001,FormatPermissionString);
      ESFormatMgt.AddField(FIELDCAPTION("Security Filter"),FORMAT("Security Filter"));

      IF CalcFlowFields THEN BEGIN
        CALCFIELDS("Role Name","Object Caption");

        ESFormatMgt.AddFlowField(FIELDCAPTION("Role Name"),FORMAT("Role Name"));
        ESFormatMgt.AddFlowField(FIELDCAPTION("Object Caption"),FORMAT("Object Caption"));
      END;

      RecRef.OPEN(DATABASE::"ES Rest. Point Role Permission");
      RecRef.GETTABLE(Rec);
      ESFormatMgt.FormatNewESTextLine(
        ESTextLine,DATABASE::"ES Rest. Point Role Permission",RecRef.RECORDID);

      EXIT(ESFormatMgt.NoTextOverflow);
    END;

    PROCEDURE ObjectWhereUsed@1240520012();
    VAR
      ESObjectWhereUsed@1240520008 : Page 14123562;
    BEGIN
      CLEAR(ESObjectWhereUsed);
      ESObjectWhereUsed.InitObject("Object Type","Object ID");
      ESObjectWhereUsed.RUN;
    END;

    PROCEDURE Comment@1240520009(FunctionNo@1240520008 : ' ,Insert,Show,Exist,DeleteAll';NewText@1240520010 : Text[250]) : Boolean;
    VAR
      ESComment@1240520009 : Record 14123524;
    BEGIN
      ESComment.RESET;
      ESComment.SETRANGE("Table ID",DATABASE::"ES Rest. Point Role Permission");
      ESComment.SETRANGE(Code,"Version Code");
      ESComment.SETRANGE("Role ID","Role ID");
      ESComment.SETRANGE(Type,"Object Type");
      ESComment.SETRANGE("Object ID","Object ID");
      EXIT(ESComment.Functions(FunctionNo,NewText));
    END;

    BEGIN
    END.
  }
}

