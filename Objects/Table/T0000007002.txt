OBJECT Table 7002 Sales Price
{
  OBJECT-PROPERTIES
  {
    Date=05/05/21;
    Time=[ 7:22:22 AM];
    Modified=Yes;
    Version List=NAVW111.00,001;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Sales Type" = "Sales Type"::"All Customers" THEN
                 "Sales Code" := ''
               ELSE
                 TESTFIELD("Sales Code");
               TESTFIELD("Item No.");
             END;

    OnRename=BEGIN
               IF "Sales Type" <> "Sales Type"::"All Customers" THEN
                 TESTFIELD("Sales Code");
               TESTFIELD("Item No.");
             END;

    CaptionML=[ENU=Sales Price;
               ESM=Precio venta;
               FRC=Prix de vente;
               ENC=Sales Price];
    LookupPageID=Page7002;
  }
  FIELDS
  {
    { 1   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                IF "Item No." <> xRec."Item No." THEN BEGIN
                                                                  Item.GET("Item No.");
                                                                  "Unit of Measure Code" := Item."Sales Unit of Measure";
                                                                  "Variant Code" := '';
                                                                END;

                                                                IF "Sales Type" = "Sales Type"::"Customer Price Group" THEN
                                                                  IF CustPriceGr.GET("Sales Code") AND
                                                                     (CustPriceGr."Allow Invoice Disc." = "Allow Invoice Disc.")
                                                                  THEN
                                                                    EXIT;

                                                                UpdateValuesFromItem;
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              ESM=N§ producto;
                                                              FRC=Nø d'article;
                                                              ENC=Item No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Sales Code          ;Code20        ;TableRelation=IF (Sales Type=CONST(Customer Price Group)) "Customer Price Group"
                                                                 ELSE IF (Sales Type=CONST(Customer)) Customer
                                                                 ELSE IF (Sales Type=CONST(Campaign)) Campaign;
                                                   OnValidate=BEGIN
                                                                IF "Sales Code" <> '' THEN
                                                                  CASE "Sales Type" OF
                                                                    "Sales Type"::"All Customers":
                                                                      ERROR(Text001,FIELDCAPTION("Sales Code"));
                                                                    "Sales Type"::"Customer Price Group":
                                                                      BEGIN
                                                                        CustPriceGr.GET("Sales Code");
                                                                        "Price Includes VAT" := CustPriceGr."Price Includes VAT";
                                                                        "VAT Bus. Posting Gr. (Price)" := CustPriceGr."VAT Bus. Posting Gr. (Price)";
                                                                        "Allow Line Disc." := CustPriceGr."Allow Line Disc.";
                                                                        "Allow Invoice Disc." := CustPriceGr."Allow Invoice Disc.";
                                                                      END;
                                                                    "Sales Type"::Customer:
                                                                      BEGIN
                                                                        Cust.GET("Sales Code");
                                                                        "Currency Code" := Cust."Currency Code";
                                                                        "Price Includes VAT" := Cust."Prices Including VAT";
                                                                        "VAT Bus. Posting Gr. (Price)" := Cust."VAT Bus. Posting Group";
                                                                        "Allow Line Disc." := Cust."Allow Line Disc.";
                                                                      END;
                                                                    "Sales Type"::Campaign:
                                                                      BEGIN
                                                                        Campaign.GET("Sales Code");
                                                                        "Starting Date" := Campaign."Starting Date";
                                                                        "Ending Date" := Campaign."Ending Date";
                                                                      END;
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Sales Code;
                                                              ESM=C¢digo ventas;
                                                              FRC=Code de vente;
                                                              ENC=Sales Code] }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              ESM=C¢d. divisa;
                                                              FRC=Code devise;
                                                              ENC=Currency Code] }
    { 4   ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                IF ("Starting Date" > "Ending Date") AND ("Ending Date" <> 0D) THEN
                                                                  ERROR(Text000,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"));

                                                                IF CurrFieldNo = 0 THEN
                                                                  EXIT;

                                                                IF "Starting Date" <> 0D THEN
                                                                  IF "Sales Type" = "Sales Type"::Campaign THEN
                                                                    ERROR(Text002,"Sales Type");
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              ESM=Fecha inicial;
                                                              FRC=Date d‚but;
                                                              ENC=Starting Date] }
    { 5   ;   ;Unit Price          ;Decimal       ;CaptionML=[ENU=Unit Price;
                                                              ESM=Precio unitario;
                                                              FRC=Prix unitaire;
                                                              ENC=Unit Price];
                                                   MinValue=0;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Currency Code" }
    { 7   ;   ;Price Includes VAT  ;Boolean       ;CaptionML=[ENU=Price Includes Tax;
                                                              ESM=Precio IVA incluido;
                                                              FRC=Prix avec TVA;
                                                              ENC=Price Includes VAT] }
    { 10  ;   ;Allow Invoice Disc. ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Allow Invoice Disc.;
                                                              ESM=Permitir dto. factura;
                                                              FRC=Escompte de facture permis;
                                                              ENC=Allow Invoice Disc.] }
    { 11  ;   ;VAT Bus. Posting Gr. (Price);Code20;TableRelation="VAT Business Posting Group";
                                                   CaptionML=[ENU=Tax Bus. Posting Gr. (Price);
                                                              ESM=Gr.regis. IVA negocio (precio);
                                                              FRC=Gr. compta. march‚ TVA (prix);
                                                              ENC=Tax Bus. Posting Gr. (Price)] }
    { 13  ;   ;Sales Type          ;Option        ;OnValidate=BEGIN
                                                                IF "Sales Type" <> xRec."Sales Type" THEN BEGIN
                                                                  VALIDATE("Sales Code",'');
                                                                  UpdateValuesFromItem;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Sales Type;
                                                              ESM=Tipo venta;
                                                              FRC=Type de vente;
                                                              ENC=Sales Type];
                                                   OptionCaptionML=[ENU=Customer,Customer Price Group,All Customers,Campaign;
                                                                    ESM=Cliente,Grupo precio cliente,Todos clientes,Campa¤a;
                                                                    FRC=Client,Groupe de prix du client,Tous les clients,Promotion;
                                                                    ENC=Customer,Customer Price Group,All Customers,Campaign];
                                                   OptionString=Customer,Customer Price Group,All Customers,Campaign }
    { 14  ;   ;Minimum Quantity    ;Decimal       ;CaptionML=[ENU=Minimum Quantity;
                                                              ESM=Cantidad m¡nima;
                                                              FRC=Quantit‚ minimum;
                                                              ENC=Minimum Quantity];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 15  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                IF CurrFieldNo = 0 THEN
                                                                  EXIT;

                                                                VALIDATE("Starting Date");

                                                                IF "Ending Date" <> 0D THEN
                                                                  IF "Sales Type" = "Sales Type"::Campaign THEN
                                                                    ERROR(Text002,"Sales Type");
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              ESM=Fecha final;
                                                              FRC=Date fin;
                                                              ENC=Ending Date] }
    { 5400;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESM=C¢d. unidad medida;
                                                              FRC=Code unit‚ de mesure;
                                                              ENC=Unit of Measure Code] }
    { 5700;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              ESM=C¢d. variante;
                                                              FRC=Code variante;
                                                              ENC=Variant Code] }
    { 7001;   ;Allow Line Disc.    ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Allow Line Disc.;
                                                              ESM=Permite dto. l¡nea;
                                                              FRC=Autoriser remise ligne;
                                                              ENC=Allow Line Disc.] }
    { 50000;  ;Origin              ;Option        ;OptionCaptionML=ENU=" ,Quote,Order,Stock Status";
                                                   OptionString=[ ,Quote,Order,Stock Status];
                                                   Description=TOP010 }
    { 50001;  ;Modified Date       ;Date          ;Description=TOP010 }
    { 50002;  ;Last Price UOM      ;Code10        ;Description=TOP010 }
    { 50003;  ;Last Price Qty      ;Decimal       ;DecimalPlaces=0:5;
                                                   Description=TOP010 }
    { 50004;  ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   Description=TOP010 }
    { 50005;  ;Reason Code Comment ;Text60        ;Description=TOP010 }
    { 50006;  ;Lost Opportunity    ;Boolean       ;Description=TOP010 }
    { 50007;  ;User ID             ;Code50        ;Description=TOP010 }
    { 50008;  ;Modified Time       ;Time          ;Description=TOP010 }
    { 50010;  ;Item Description    ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item.Description WHERE (No.=FIELD(Item No.)));
                                                   Description=TOP070;
                                                   Editable=No }
    { 50023;  ;Low Unit Price      ;Decimal       ;Description=TOP010;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Currency Code" }
    { 50024;  ;High Unit Price     ;Decimal       ;Description=TOP010;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Currency Code" }
    { 50030;  ;Medium Unit Price   ;Decimal       ;CaptionML=ENU=Clearance Price;
                                                   Description=TOP010,TPZ3168;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Currency Code" }
    { 50031;  ;Item Disc. Group    ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item."Item Disc. Group" WHERE (No.=FIELD(Item No.)));
                                                   CaptionML=[ENU=Item Multiplier Group;
                                                              ESM=Grupo dto. producto;
                                                              FRC=Groupe esc. articles/clients;
                                                              ENC=Item Disc. Group];
                                                   Editable=No }
    { 50032;  ;Pricing Logic       ;Option        ;DataClassification=ToBeClassified;
                                                   OptionCaptionML=ENU=" ,Sales Price,Hot Sheet,Sales Line Multiplier(Cust.),Sales Line Multiplier(Cust. Mult. Grp.),Customer Base Multiplier,Item Unit Price";
                                                   OptionString=[ ,Sales Price,Hot Sheet,Sales Line Multiplier(Cust.),Sales Line Multiplier(Cust. Mult. Grp.),Customer Base Multiplier,Item Unit Price];
                                                   Description=TPZ2765;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Item No.,Sales Type,Sales Code,Starting Date,Currency Code,Variant Code,Unit of Measure Code,Minimum Quantity;
                                                   Clustered=Yes }
    {    ;Sales Type,Sales Code,Item No.,Starting Date,Currency Code,Variant Code,Unit of Measure Code,Minimum Quantity }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Sales Type,Sales Code,Item No.,Starting Date,Unit Price,Ending Date }
  }
  CODE
  {
    VAR
      CustPriceGr@1000 : Record 6;
      Text000@1001 : TextConst 'ENU=%1 cannot be after %2;ESM=%1 no puede ser posterior %2;FRC=%1 ne peut pas ˆtre post‚rieur(e) … %2;ENC=%1 cannot be after %2';
      Cust@1002 : Record 18;
      Text001@1003 : TextConst 'ENU=%1 must be blank.;ESM=%1 debe ser blanco.;FRC=%1 doit ˆtre blanc.;ENC=%1 must be blank.';
      Campaign@1005 : Record 5071;
      Item@1004 : Record 27;
      Text002@1006 : TextConst 'ENU="If Sales Type = %1, then you can only change Starting Date and Ending Date from the Campaign Card.";ESM="Si el tipo de ventas es = %1, solo puede cambiar la Fecha de inicio y la Fecha final de la Ficha campa¤a.";FRC="Si Type vente = %1, vous pouvez uniquement modifier Date de d‚but et Date de fin … partir de la fiche promotion.";ENC="If Sales Type = %1, then you can only change Starting Date and Ending Date from the Campaign Card."';

    LOCAL PROCEDURE UpdateValuesFromItem@1();
    BEGIN
      IF Item.GET("Item No.") THEN BEGIN
        "Allow Invoice Disc." := Item."Allow Invoice Disc.";
        IF "Sales Type" = "Sales Type"::"All Customers" THEN BEGIN
          "Price Includes VAT" := Item."Price Includes VAT";
          "VAT Bus. Posting Gr. (Price)" := Item."VAT Bus. Posting Gr. (Price)";
        END;
      END;
    END;

    PROCEDURE CopySalesPriceToCustomersSalesPrice@3(VAR SalesPrice@1000 : Record 7002;CustNo@1002 : Code[20]);
    VAR
      NewSalesPrice@1001 : Record 7002;
    BEGIN
      IF SalesPrice.FINDSET THEN
        REPEAT
          NewSalesPrice := SalesPrice;
          NewSalesPrice."Sales Type" := NewSalesPrice."Sales Type"::Customer;
          NewSalesPrice."Sales Code" := CustNo;
          IF NewSalesPrice.INSERT THEN;
        UNTIL SalesPrice.NEXT = 0;
    END;

    BEGIN
    {

      TOP010 KT ABCSI Stock Status Quick Quote Screen 12082014
       - Added new fields
         # 50000 Origin
         # 50001 Modified Date
         # 50002 Last Price UOM
         # 50003 Last Price Qty
         # 50004 Reason CodeCode
         # 50005 Reason Code Comment
         # 50006 Lost Opportunity
         # 50007 User ID
         # 50008 Modified Time
         # 50023 Low Unit Price
         # 50024 High Unit Price
         # 50030 Medium Unit Price

      TOP100A KT ABCSI Multipliers 02172015
        - Changed the caption of field and changed the InitValue Property to No
          Allow Line Disc. --> Allow Line Multiplier

      TOP070 KT ABCSI Pricing History for Customers - Items 03202015
        - Added a new flow field 50010 Item Description
        - Added code under OnInsert and OnModify triggers to update the User ID, Modified Date and Modified Time fields

      TOP230 KT ABCSI CRP 2 Fixes 06162015
        - Modified the DecimalPlaces property and AutoFormatType property for all the custom fields with Quantity, Price and Amounts

      2015-04-14 TPZHOTFIX VCHERNYA
        User field length has been increased to 50
      2016-02-24 TOP8903 TAKHMETO
        Item Disc. Group field added
      2018-07-31 TPZ2382 UCHOUHAN
        Added code to update USERID on Rename Record.
      2019-05-08 TPZ2542 RTIWARI
        Custom Code Blocked in OnInsert/OnModify/OnRename triggers for moving customization code from table to Events
      001 TPZ3168 GKG 05052021 Change the caption of 'Medium Unit Price' field to 'Clearance Price'
    }
    END.
  }
}

