OBJECT Table 5722 Item Category
{
  OBJECT-PROPERTIES
  {
    Date=04/24/20;
    Time=12:00:00 PM;
    Version List=NAVW111.00.00.42633,SCW19.2.0,UBP2.82;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD(Code);
               UpdateIndentation;
               ItemCategoryManagement.CalcPresentationOrder(Rec);
               "Last Modified Date Time" := CURRENTDATETIME;
             END;

    OnModify=BEGIN
               UpdateIndentation;
               ItemCategoryManagement.CalcPresentationOrder(Rec);
               "Last Modified Date Time" := CURRENTDATETIME;
             END;

    OnDelete=BEGIN
               IF HasChildren THEN
                 ERROR(DeleteWithChildrenErr);
               UpdateDeletedCategoryItems;
               DeleteAssignedAttributes;

               ProductGroup.SETRANGE("Item Category Code",Code);
               ProductGroup.DELETEALL;
             END;

    OnRename=BEGIN
               "Presentation Order" := 0;
               "Last Modified Date Time" := CURRENTDATETIME;
             END;

    CaptionML=[ENU=Item Category;
               ESM=Categor¡a producto;
               FRC=Cat‚gorie d'article;
               ENC=Item Category];
    LookupPageID=Page5730;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              ESM=C¢digo;
                                                              FRC=Code;
                                                              ENC=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Parent Category     ;Code20        ;TableRelation="Item Category";
                                                   OnValidate=VAR
                                                                ItemCategory@1000 : Record 5722;
                                                                ItemAttributeManagement@1002 : Codeunit 7500;
                                                                ParentCategory@1001 : Code[20];
                                                              BEGIN
                                                                ParentCategory := "Parent Category";
                                                                WHILE ItemCategory.GET(ParentCategory) DO BEGIN
                                                                  IF ItemCategory.Code = Code THEN
                                                                    ERROR(CyclicInheritanceErr);
                                                                  ParentCategory := ItemCategory."Parent Category";
                                                                END;
                                                                ItemAttributeManagement.UpdateCategoryAttributesAfterChangingParentCategory(Code,"Parent Category",xRec."Parent Category");
                                                              END;

                                                   CaptionML=[ENU=Parent Category;
                                                              ESM=Categor¡a principal;
                                                              FRC=Cat‚gorie parent;
                                                              ENC=Parent Category] }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 9   ;   ;Indentation         ;Integer       ;CaptionML=[ENU=Indentation;
                                                              ESM=Sangr¡a;
                                                              FRC=Indentation;
                                                              ENC=Indentation];
                                                   MinValue=0 }
    { 10  ;   ;Presentation Order  ;Integer       ;CaptionML=[ENU=Presentation Order;
                                                              ESM=Orden de presentaci¢n;
                                                              FRC=Ordre de pr‚sentation;
                                                              ENC=Presentation Order] }
    { 11  ;   ;Has Children        ;Boolean       ;CaptionML=[ENU=Has Children;
                                                              ESM=Tiene elementos secundarios;
                                                              FRC=A des enfants;
                                                              ENC=Has Children] }
    { 12  ;   ;Last Modified Date Time;DateTime   ;CaptionML=[ENU=Last Modified Date Time;
                                                              ESM=Fecha y hora de £ltima modificaci¢n;
                                                              FRC=Date-heure derniŠre modification;
                                                              ENC=Last Modified Date Time];
                                                   Editable=No }
    { 8000;   ;Id                  ;GUID          ;CaptionML=[ENU=Id;
                                                              ESM=Id.;
                                                              FRC="Code ";
                                                              ENC=Id] }
    { 50001;  ;Zero Inventory Cost ;Boolean       ;Description=TPZ1708 }
    { 70551;  ;Excess Calc. Days   ;Integer       ;CaptionML=ENU=Excess Calc. Days }
    { 70552;  ;Excess Calc. Percent;Decimal       ;CaptionML=ENU=Excess Calc. Percent;
                                                   DecimalPlaces=0:2;
                                                   MinValue=0 }
    { 70553;  ;Review Code         ;Code10        ;TableRelation="AFP Review Code".Code;
                                                   CaptionML=ENU=Review Code }
    { 11123302;;Visible in Webshop ;Boolean       ;CaptionML=ENU=Visible in Webshop;
                                                   Description=TPZ1925 }
    { 11123303;;Sort No.           ;Integer       ;CaptionML=ENU=Sort No.;
                                                   Description=TPZ1925 }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Parent Category                          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ProductGroup@1000 : Record 5723;
      CyclicInheritanceErr@1001 : TextConst 'ENU=An item category cannot be a parent of itself or any of its children.;ESM=Una categor¡a de producto no puede ser un elemento principal de s¡ misma ni de ninguno de sus elementos secundarios.;FRC=Une cat‚gorie d''article ne peut pas ˆtre parent d''elle-mˆme ou de l''un de ses enfants.;ENC=An item category cannot be a parent of itself or any of its children.';
      ItemCategoryManagement@1002 : Codeunit 5722;
      DeleteWithChildrenErr@1003 : TextConst 'ENU=You cannot delete this item category because it has child item categories.;ESM=No puede eliminar esta categor¡a de producto porque tiene categor¡as de producto secundarias.;FRC=Vous ne pouvez pas supprimer cette cat‚gorie d''article, car elle contient des cat‚gories d''article enfants.;ENC=You cannot delete this item category because it has child item categories.';
      DeleteItemInheritedAttributesQst@1004 : TextConst '@@@=%1 - item category code;ENU="One or more items belong to item category ''''%1''''.\\Do you want to delete the inherited item attributes for the items in question? ";ESM="Uno o varios productos pertenecen a la categor¡a de producto ''''%1''''.\\¨Desea eliminar los atributos de producto heredados para los productos en cuesti¢n? ";FRC="Un ou plusieurs articles appartiennent … la cat‚gorie d''article ®ÿ%1ÿ¯.\\Souhaitez-vous supprimer les attributs d''article h‚rit‚s pour les articles en questionÿ? ";ENC="One or more items belong to item category ''''%1''''.\\Do you want to delete the inherited item attributes for the items in question? "';

    [External]
    PROCEDURE HasChildren@1() : Boolean;
    VAR
      ItemCategory@1000 : Record 5722;
    BEGIN
      ItemCategory.SETRANGE("Parent Category",Code);
      EXIT(NOT ItemCategory.ISEMPTY)
    END;

    [External]
    PROCEDURE GetStyleText@3() : Text;
    BEGIN
      IF Indentation = 0 THEN
        EXIT('Strong');

      IF HasChildren THEN
        EXIT('Strong');

      EXIT('');
    END;

    LOCAL PROCEDURE UpdateDeletedCategoryItems@6();
    VAR
      CategoryItem@1001 : Record 27;
      TempCategoryItemAttributeValue@1000 : TEMPORARY Record 7501;
      ItemAttributeManagement@1003 : Codeunit 7500;
      DeleteItemInheritedAttributes@1002 : Boolean;
    BEGIN
      CategoryItem.SETRANGE("Item Category Code",Code);
      IF CategoryItem.ISEMPTY THEN
        EXIT;
      DeleteItemInheritedAttributes := CONFIRM(STRSUBSTNO(DeleteItemInheritedAttributesQst,Code));
      IF DeleteItemInheritedAttributes THEN
        TempCategoryItemAttributeValue.LoadCategoryAttributesFactBoxData(Code);
      IF CategoryItem.FIND('-') THEN
        REPEAT
          CategoryItem.VALIDATE("Item Category Code",'');
          CategoryItem.MODIFY;
          IF DeleteItemInheritedAttributes THEN
            ItemAttributeManagement.DeleteItemAttributeValueMapping(CategoryItem,TempCategoryItemAttributeValue);
        UNTIL CategoryItem.NEXT = 0;
    END;

    LOCAL PROCEDURE DeleteAssignedAttributes@4();
    VAR
      ItemAttributeValueMapping@1000 : Record 7505;
    BEGIN
      ItemAttributeValueMapping.SETRANGE("Table ID",DATABASE::"Item Category");
      ItemAttributeValueMapping.SETRANGE("No.",Code);
      ItemAttributeValueMapping.DELETEALL;
    END;

    LOCAL PROCEDURE UpdateIndentation@5();
    VAR
      ParentItemCategory@1000 : Record 5722;
    BEGIN
      IF ParentItemCategory.GET("Parent Category") THEN
        UpdateIndentationTree(ParentItemCategory.Indentation + 1)
      ELSE
        UpdateIndentationTree(0);
    END;

    PROCEDURE UpdateIndentationTree@7(Level@1000 : Integer);
    VAR
      ItemCategory@1001 : Record 5722;
    BEGIN
      Indentation := Level;

      ItemCategory.SETRANGE("Parent Category",Code);
      IF ItemCategory.FINDSET THEN
        REPEAT
          ItemCategory.UpdateIndentationTree(Level + 1);
          ItemCategory.MODIFY;
        UNTIL ItemCategory.NEXT = 0;
    END;

    BEGIN
    {
      --------------------------------------------------------------------------------
            Sana Commerce
            -------------------------------------------------------------------------------

            Sign  Description
            --------------------------------------------------------------------------------
            SC    Fields "Visible in Webshop", "Sort No." added.
            --------------------------------------------------------------------------------
      2017-08-11 TPZ1925 EBAGIM
        Sana 9.2 Merge
      2017-08-31 TPZ1708 EBAGIM
        add new field calles Zero Inventory Item
    }
    END.
  }
}

