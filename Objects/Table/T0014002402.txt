OBJECT Table 14002402 Routing Request Header
{
  OBJECT-PROPERTIES
  {
    Date=03/06/15;
    Time=12:00:00 PM;
    Version List=SE0.60.06;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 EDISetup.GET;
                 EDISetup.TESTFIELD("Routing Request Nos.");
                 NoSeriesMgt.InitSeries(EDISetup."Routing Request Nos.",xRec."No. Series","Document Date","No.","No. Series");
               END;
               "Document Date" := WORKDATE;
             END;

    OnModify=BEGIN
               TESTFIELD(Released,FALSE);
             END;

    OnDelete=BEGIN
               TESTFIELD(Released,FALSE);

               RoutingReqLine.RESET;
               RoutingReqLine.SETRANGE("Routing Request No.","No.");
               RoutingReqLine.DELETEALL;
             END;

    OnRename=BEGIN
               ERROR(Text001,TABLENAME);
             END;

    CaptionML=ENU=Routing Request Header;
    LookupPageID=Page14002405;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;CaptionML=ENU=No. }
    { 11  ;   ;Trade Partner No.   ;Code20        ;TableRelation="E.D.I. Trade Partner".No.;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS(Name);
                                                              END;

                                                   CaptionML=ENU=Trade Partner No. }
    { 12  ;   ;Name                ;Text40        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("E.D.I. Trade Partner".Name WHERE (No.=FIELD(Trade Partner No.)));
                                                   CaptionML=ENU=Name;
                                                   Editable=No }
    { 13  ;   ;Document Date       ;Date          ;CaptionML=ENU=Document Date }
    { 14  ;   ;No. Series          ;Code10        ;CaptionML=ENU=No. Series }
    { 15  ;   ;EDI Request Generated;Boolean      ;CaptionML=ENU=EDI Request Generated;
                                                   Editable=No }
    { 16  ;   ;EDI Req. Gen. Date  ;Date          ;CaptionML=ENU=EDI Req. Gen. Date;
                                                   Editable=No }
    { 17  ;   ;EDI Routing Received;Boolean       ;CaptionML=ENU=EDI Routing Received;
                                                   Editable=No }
    { 18  ;   ;EDI Routing Rec. Date;Date         ;CaptionML=ENU=EDI Routing Rec. Date;
                                                   Editable=No }
    { 19  ;   ;Released            ;Boolean       ;CaptionML=ENU=Released;
                                                   Editable=No }
    { 20  ;   ;EDI Internal Doc. No. Req.;Code10  ;CaptionML=ENU=EDI Internal Doc. No. Req.;
                                                   Editable=No }
    { 21  ;   ;Ready To Ship Date  ;Date          ;CaptionML=ENU=Ready To Ship Date }
    { 22  ;   ;Ready To Ship Time  ;Code5         ;OnValidate=BEGIN
                                                                IF "Ready To Ship Time" <> '' THEN BEGIN
                                                                  IF STRPOS("Ready To Ship Time",':') = 0 THEN
                                                                    "Ready To Ship Time" := "Ready To Ship Time" + ':00';
                                                                  "Ready To Ship Time" := PADSTR("Ready To Ship Time",(STRPOS("Ready To Ship Time",':') + 2),'0');
                                                                  EVALUATE(TimeVar,"Ready To Ship Time");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Ready To Ship Time }
    { 23  ;   ;EDI Internal Doc. No. Resp.;Code10 ;CaptionML=ENU=EDI Internal Doc. No. Resp. }
    { 24  ;   ;Shipping Time       ;DateFormula   ;OnValidate=BEGIN
                                                                TESTFIELD("Ready To Ship Date");

                                                                ShippingTime := FORMAT("Shipping Time");
                                                                IF ShippingTime = '0D' THEN
                                                                  "Latest Ship Date" := 0D
                                                                ELSE
                                                                  IF STRPOS(ShippingTime,'-') = 0 THEN
                                                                    "Latest Ship Date" := CALCDATE("Shipping Time","Ready To Ship Date")
                                                                  ELSE
                                                                    ERROR(Text002,"Ready To Ship Date");
                                                              END;

                                                   CaptionML=ENU=Shipping Time }
    { 25  ;   ;Latest Ship Date    ;Date          ;CaptionML=ENU=Latest Ship Date;
                                                   Editable=No }
    { 26  ;   ;Latest Ship Time    ;Code5         ;OnValidate=BEGIN
                                                                IF "Latest Ship Time" <> '' THEN BEGIN
                                                                  IF STRPOS("Latest Ship Time",':') = 0 THEN
                                                                    "Latest Ship Time" := "Latest Ship Time" + ':00';
                                                                  "Latest Ship Time" := PADSTR("Latest Ship Time",(STRPOS("Latest Ship Time",':') + 2),'0');
                                                                  EVALUATE(TimeVar,"Latest Ship Time");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Latest Ship Time }
    { 27  ;   ;Routing Type        ;Option        ;CaptionML=ENU=Routing Type;
                                                   OptionCaptionML=ENU=Original,Confirmation,Change,Cancellation;
                                                   OptionString=Original,Confirmation,Change,Cancellation }
    { 28  ;   ;Load Identification ;Text50        ;CaptionML=ENU=Load Identification;
                                                   Editable=Yes }
    { 29  ;   ;EDI Confirmation Gen. Date;Date    ;CaptionML=ENU=EDI Confirmation Gen. Date;
                                                   Editable=No }
    { 30  ;   ;EDI Cancellation Gen. Date;Date    ;CaptionML=ENU=EDI Cancellation Gen. Date;
                                                   Editable=No }
    { 31  ;   ;EDI Change Req. Gen. Date;Date     ;CaptionML=ENU=EDI Change Req. Gen. Date;
                                                   Editable=No }
    { 32  ;   ;Bill of Lading No.  ;Code20        ;TableRelation="Bill of Lading";
                                                   CaptionML=ENU=Bill of Lading No.;
                                                   Editable=Yes }
    { 33  ;   ;Override Pallet Count;Integer      ;CaptionML=ENU=Override Pallet Count }
    { 34  ;   ;Pallet Count        ;Integer       ;CaptionML=ENU=Pallet Count;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;EDI Req. Gen. Date,EDI Request Generated,EDI Routing Received,EDI Routing Rec. Date }
    {    ;Bill of Lading No.                       }
    {    ;Load Identification                      }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EDISetup@1240030000 : Record 14002367;
      RoutingReq@1240030001 : Record 14002402;
      RoutingReqLine@1240030002 : Record 14002403;
      NoSeriesMgt@1240030003 : Codeunit 396;
      TimeVar@1240030004 : Time;
      Text001@1001 : TextConst 'ENU=You cannot rename a %1.';
      ShippingTime@1240020000 : Text[30];
      Text002@1240020001 : TextConst 'ENU=Latest Ship Date cannot occur before Ready To Ship Date.';

    PROCEDURE AssistEdit@1(OldRoutingReq@1240030001 : Record 14002402) Resp@1240030000 : Boolean;
    BEGIN
      WITH RoutingReq DO BEGIN
        RoutingReq := Rec;
        EDISetup.GET;
        EDISetup.TESTFIELD("Routing Request Nos.");
        IF NoSeriesMgt.SelectSeries(EDISetup."Routing Request Nos.",OldRoutingReq."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := RoutingReq;
          EXIT(TRUE);
        END;
      END;
    END;

    BEGIN
    END.
  }
}

