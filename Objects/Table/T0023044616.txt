OBJECT Table 23044616 DSHIP Shipment Options
{
  OBJECT-PROPERTIES
  {
    Date=01/03/19;
    Time=[ 8:37:48 AM];
    Version List=DSHIP2.2.1;
  }
  PROPERTIES
  {
    OnInsert=BEGIN

               bFromInsert := TRUE;
               IF ( (Rec."Document No." <> '') AND
                    (Rec."Entry Type" = Rec."Entry Type"::standard) ) THEN BEGIN
                 IF ( Rec."Document Type" = Rec."Document Type"::"Sales Order" ) THEN
                   Rec."Document Subtype" := 1;
                 getCustomerOptions;
               END;
               updateReturnAddress;
             END;

    CaptionML=[ENU=Dynamic Ship Shipment Options;
               ESM=Dynamic Ship opciones de env°o;
               FRC=Options Dynamic Ship de Livraison;
               ENC=Dynamic Ship Shipment Options];
    LookupPageID=Page23044628;
  }
  FIELDS
  {
    { 1   ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              ESM=Tipo documento;
                                                              FRC=Type de document;
                                                              ENC=Document Type];
                                                   OptionCaptionML=[ENU=Sales Order,Sales Return Order,Purchase Order,Purchase Return Order,Outbound Transfer,Service Order,Warehouse Shipment,Misc. Shipment;
                                                                    ESM=Pedido de venta,devoluci¢n venta,pedido de compra,devoluci¢n compra,transferencia salida,pedido de servicio,env°o,diversos env°o de almacÇn;
                                                                    FRC=Document de vente,Retour Vente,Bon de Commande,Retour Achat,Transfert Sortant,Commande de Service,Livraison,divers Livraison d'Entrepìt;
                                                                    ENC=Sales Order,Sales Return Order,Purchase Order,Purchase Return Order,Outbound Transfer,Service Order,Warehouse Shipment,Misc. Shipment];
                                                   OptionString=Sales Order,Sales Return Order,Purchase Order,Purchase Return Order,Outbound Transfer,Service Order,Warehouse Shipment,Misc. Shipment }
    { 2   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              ESM=Nß documento;
                                                              FRC=N¯ de document;
                                                              ENC=Document No.];
                                                   NotBlank=Yes }
    { 3   ;   ;Document Subtype    ;Integer       ;CaptionML=[ENU=Document Subtype;
                                                              ESM=Subtipo documento;
                                                              FRC=Sous-type document;
                                                              ENC=Document Subtype] }
    { 4   ;   ;Order ID            ;Text100       ;CaptionML=[ENU=Order ID;
                                                              ESM=Id. de pedido;
                                                              FRC=Code commande;
                                                              ENC=Order ID];
                                                   Description=EasyPost Order ID }
    { 5   ;   ;Residential Delivery;Option        ;CaptionML=[ENU=Residential Delivery;
                                                              ESM=Entrega residential;
                                                              FRC=Livraison residential;
                                                              ENC=Residential Delivery];
                                                   OptionString=[ ,No,Yes];
                                                   Description=residential }
    { 6   ;   ;Shipment Type       ;Option        ;OnValidate=BEGIN

                                                                IF ( xRec."Shipment Type" <> "Shipment Type" ) THEN BEGIN
                                                                  VALIDATE("Customer No.", '');
                                                                  updateReturnAddress;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Shipment Type;
                                                              ESM=Tipo env°o;
                                                              FRC=Type de livraison;
                                                              ENC=Shipment Type];
                                                   OptionString=Regular,Blind,Double Blind,Custom Return Address }
    { 7   ;   ;Company             ;Text50        ;CaptionML=[ENU=Company;
                                                              ESM=Empresa;
                                                              FRC=Compagnie;
                                                              ENC=Company];
                                                   NotBlank=Yes;
                                                   Description=company }
    { 8   ;   ;Address             ;Text50        ;CaptionML=[ENU=Address;
                                                              ESM=Direcci¢n;
                                                              FRC=Adresse;
                                                              ENC=Address];
                                                   NotBlank=Yes;
                                                   Description=street1 }
    { 9   ;   ;Address 2           ;Text50        ;CaptionML=[ENU=Address 2;
                                                              ESM=Colonia 2;
                                                              FRC=Adresse (2äme ligne);
                                                              ENC=Address 2];
                                                   Description=street2 }
    { 10  ;   ;City                ;Text30        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code".City
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code".City WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN

                                                                recPostCode.ValidateCity(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=City;
                                                              ESM=Municipio/Ciudad;
                                                              FRC=Ville;
                                                              ENC=City];
                                                   NotBlank=Yes;
                                                   Description=city }
    { 11  ;   ;County              ;Text30        ;CaptionML=[ENU=County;
                                                              ESM=Provincia;
                                                              FRC=ComtÇ;
                                                              ENC=Province/State];
                                                   NotBlank=Yes;
                                                   Description=state }
    { 12  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   OnValidate=BEGIN

                                                                IF ( ("Country/Region Code" <> xRec."Country/Region Code") AND (xRec."Country/Region Code" <> '') ) THEN BEGIN
                                                                  City := '';
                                                                  "Post Code" := '';
                                                                  County := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Country/Region Code;
                                                              ESM=C¢digo de pa°s/regi¢n;
                                                              FRC=Code pays/rÇgion;
                                                              ENC=Country/Region Code];
                                                   NotBlank=Yes;
                                                   Description=country }
    { 13  ;   ;Post Code           ;Code20        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code"
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code" WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN

                                                                recPostCode.ValidatePostCode(City,"Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Post Code;
                                                              ESM=C¢digo postal;
                                                              FRC=Code postal;
                                                              ENC=Postal/ZIP Code];
                                                   NotBlank=Yes;
                                                   Description=zip }
    { 14  ;   ;Phone No.           ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No.;
                                                              ESM=N.ß telÇfono;
                                                              FRC=N¯ tÇlÇphone;
                                                              ENC=Phone No.];
                                                   NotBlank=Yes;
                                                   Description=phone }
    { 15  ;   ;Entry Type          ;Option        ;CaptionML=[ENU=Entry Type;
                                                              ESM=Tipo movimiento;
                                                              FRC=Type d'Çcriture;
                                                              ENC=Entry Type];
                                                   OptionString=standard,template,fromtemplate;
                                                   Description=internal use, differentiate regular and template entries }
    { 16  ;   ;Description         ;Text150       ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description];
                                                   Description=descriptive field for templates }
    { 17  ;   ;Add Freight Line    ;Option        ;CaptionML=[ENU=Add Freight Line;
                                                              ESM=Agregar l°nea flete;
                                                              FRC=Ajouter Ligne de Transport;
                                                              ENC=Add Freight Line];
                                                   OptionString=Automatic,Manual;
                                                   Description=TFS3929 }
    { 18  ;   ;Customer No.        ;Code20        ;TableRelation=Customer.No.;
                                                   OnValidate=BEGIN

                                                                IF ( xRec."Customer No." <> "Customer No." ) THEN
                                                                  "Ship-To Code" := '';
                                                                updateReturnAddress;
                                                              END;

                                                   CaptionML=[ENU=Customer No.;
                                                              ESM=Nß cliente;
                                                              FRC=N¯ client;
                                                              ENC=Customer No.];
                                                   Description=for double blind }
    { 19  ;   ;Ship-To Code        ;Code20        ;TableRelation="Ship-to Address".Code WHERE (Customer No.=FIELD(Customer No.));
                                                   OnValidate=BEGIN

                                                                updateReturnAddress;
                                                              END;

                                                   CaptionML=[ENU=Ship-To Code;
                                                              ESM=Env°o a-C¢digo;
                                                              FRC=Code de livraison;
                                                              ENC=Ship-To Code];
                                                   Description=for double blind }
    { 20  ;   ;Generate Return Label;Option       ;OnValidate=BEGIN

                                                                IF ( xRec."Generate Return Label" = Rec."Generate Return Label" ) THEN
                                                                  EXIT;
                                                                IF ( "Generate Return Label" = "Generate Return Label"::Yes ) THEN
                                                                  setReturnService;
                                                              END;

                                                   CaptionML=[ENU=Generate Return Label;
                                                              ESM=Generar etiqueta devoluci¢n;
                                                              FRC=GÇnÇrer êtiquette de Retour;
                                                              ENC=Generate Return Label];
                                                   OptionString=No,Yes;
                                                   Description=TFS4047 }
    { 21  ;   ;Shipping Agent Code ;Code20        ;TableRelation="Shipping Agent";
                                                   OnValidate=BEGIN

                                                                IF ( xRec."Shipping Agent Code" <> Rec."Shipping Agent Code" ) THEN
                                                                  VALIDATE("Shipping Agent Service Code", '');
                                                              END;

                                                   CaptionML=[ENU=Shipping Agent Code;
                                                              ESM=C¢d. transportista;
                                                              FRC=Code agent de livraison;
                                                              ENC=Shipping Agent Code];
                                                   Description=TFS4047 }
    { 22  ;   ;Shipping Agent Service Code;Code20 ;TableRelation="Shipping Agent Services".Code WHERE (Shipping Agent Code=FIELD(Shipping Agent Code));
                                                   CaptionML=[ENU=Shipping Agent Service Code;
                                                              ESM=C¢d. servicio transportista;
                                                              FRC=Code prestation agent de livraison;
                                                              ENC=Shipping Agent Service Code];
                                                   Description=TFS4047 }
  }
  KEYS
  {
    {    ;Document Type,Document No.              ;Clustered=Yes }
    {    ;Order ID                                 }
    {    ;Entry Type                               }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      recPostCode@1000000000 : Record 225;
      bFromInsert@1000000001 : Boolean;

    LOCAL PROCEDURE getCustomerOptions@1000000003();
    VAR
      lrecDShipSetup@1000000000 : Record 23044600;
      lrecSalesHeader@1000000002 : Record 36;
      lrecCustomerOptions@1000000005 : Record 23044614;
      lrecShipmentOptionsTemplate@1000000006 : Record 23044616;
      lcuPackMgmt@1000000004 : Codeunit 23044601;
      lbTemplateFound@1000000007 : Boolean;
    BEGIN
      //<FUNC>
      //  Description: Apply the defaults / customer options template (if any)
      //  Called From: This table (T23044616)
      //  Side Effect: None
      //</FUNC>

      IF ( Rec."Entry Type" <> Rec."Entry Type"::standard ) THEN
        EXIT;

      IF ( NOT lcuPackMgmt.getSalesHeader(Rec."Document Type", Rec."Document No.", lrecSalesHeader) ) THEN
        EXIT;

      IF ( lcuPackMgmt.getCustomerOptions(lrecSalesHeader, lrecCustomerOptions) ) THEN BEGIN
        lrecShipmentOptionsTemplate.SETRANGE("Document No.", lrecCustomerOptions."Shipment Options Template Code");
        lbTemplateFound := lrecShipmentOptionsTemplate.FINDFIRST;
        IF ( lbTemplateFound ) THEN
          applyTemplate(lrecShipmentOptionsTemplate);
      END;
      IF ( NOT lbTemplateFound ) THEN BEGIN
        lrecDShipSetup.GET;
        Rec."Residential Delivery" := lrecDShipSetup."Residential Delivery Default";
        Rec."Add Freight Line" := lrecDShipSetup."Freight Line Default";
        Rec."Entry Type" := Rec."Entry Type"::standard;
      END;
    END;

    PROCEDURE applyTemplate@1000000006(precShipmentOptionsTemplate@1000000000 : Record 23044616);
    VAR
      liDocSubtype@1000000001 : Integer;
    BEGIN
      //<FUNC>
      //  Description:
      //  Called From:
      //  Side Effect:
      //</FUNC>

      liDocSubtype := Rec."Document Subtype";
      Rec.TRANSFERFIELDS(precShipmentOptionsTemplate, FALSE);
      Rec."Document Subtype" := liDocSubtype;
      Rec."Entry Type" := Rec."Entry Type"::fromtemplate;
      IF ( NOT bFromInsert ) THEN
        Rec.MODIFY(FALSE);
    END;

    LOCAL PROCEDURE updateReturnAddress@1000000001();
    VAR
      lrecWhseShipLine@1000000002 : Record 7321;
      lrecSalesHeader@1000000001 : Record 36;
      lrecTransferHeader@1000000000 : Record 5740;
      lrecCustomer@1000000005 : Record 18;
      lrecShipToAddress@1000000007 : Record 222;
      lcuDShipPackRateMngmt@1000000003 : Codeunit 23044602;
      lasReturnAddress@1000000004 : ARRAY [12] OF Text[100];
      lcodLocationCode@1000000006 : Code[20];
    BEGIN
      //<FUNC>
      //  Description: Using the source document details, sets the address related fields
      //  Called From: This table (T23044616)
      //  Side Effect: None
      //</FUNC>

      // do not alter address data if custom shipment is selected
      // skip if template, or it'll break
      IF ( ("Shipment Type" = "Shipment Type"::"Custom Return Address") OR ("Entry Type" = "Entry Type"::template) ) THEN
        EXIT;

      CASE "Document Type" OF
        "Document Type"::"Warehouse Shipment":
          BEGIN
            lrecWhseShipLine.SETRANGE("No.", "Document No.");
            lrecWhseShipLine.FINDFIRST;
            IF ( lrecWhseShipLine."Source Document" <> lrecWhseShipLine."Source Document"::"Sales Order" ) THEN
              EXIT;
            lrecSalesHeader.GET(lrecWhseShipLine."Source Subtype", lrecWhseShipLine."Source No.");
            lcodLocationCode := lrecWhseShipLine."Location Code";
          END;
        "Document Type"::"Sales Order":
          BEGIN
            lrecSalesHeader.GET("Document Subtype", "Document No.");
            lcodLocationCode := lrecSalesHeader."Location Code";
          END;
        ELSE
          BEGIN
            EXIT;
          END;
      END; // case doc type

      lcuDShipPackRateMngmt.getShipFromAddress(lcodLocationCode, lasReturnAddress);

      CASE "Shipment Type" OF
        "Shipment Type"::Blind:
          BEGIN
            //  return address is bill-to
            lrecCustomer.GET(lrecSalesHeader."Bill-to Customer No.");
            lasReturnAddress[1] := lrecCustomer.Name;
            lasReturnAddress[2] := lrecCustomer.Address;
            lasReturnAddress[3] := lrecCustomer."Address 2";
            lasReturnAddress[4] := lrecCustomer.City;
            lasReturnAddress[5] := lrecCustomer.County;
            lasReturnAddress[6] := lrecCustomer."Country/Region Code";
            lasReturnAddress[7] := lrecCustomer."Post Code";
            lasReturnAddress[8] := lrecCustomer."Phone No.";
          END;
        "Shipment Type"::"Double Blind":
          BEGIN
            IF ( "Customer No." = '') THEN BEGIN
              //  sell-to name + from address
              lrecCustomer.GET(lrecSalesHeader."Sell-to Customer No.");
              lasReturnAddress[1] := lrecCustomer.Name;
            END ELSE IF ( "Ship-To Code" = '' ) THEN BEGIN
              lrecCustomer.GET("Customer No.");
              lasReturnAddress[1] := lrecCustomer.Name;
              lasReturnAddress[2] := lrecCustomer.Address;
              lasReturnAddress[3] := lrecCustomer."Address 2";
              lasReturnAddress[4] := lrecCustomer.City;
              lasReturnAddress[5] := lrecCustomer.County;
              lasReturnAddress[6] := lrecCustomer."Country/Region Code";
              lasReturnAddress[7] := lrecCustomer."Post Code";
              lasReturnAddress[8] := lrecCustomer."Phone No.";
            END ELSE BEGIN
              lrecShipToAddress.GET("Customer No.", "Ship-To Code");
              lasReturnAddress[1] := lrecShipToAddress.Name;
              lasReturnAddress[2] := lrecShipToAddress.Address;
              lasReturnAddress[3] := lrecShipToAddress."Address 2";
              lasReturnAddress[4] := lrecShipToAddress.City;
              lasReturnAddress[5] := lrecShipToAddress.County;
              lasReturnAddress[6] := lrecShipToAddress."Country/Region Code";
              lasReturnAddress[7] := lrecShipToAddress."Post Code";
              lasReturnAddress[8] := lrecShipToAddress."Phone No.";
            END;
          END;
      END; // case ship type

      // this will also cover regular shipments
      Company := lasReturnAddress[1];
      Address := lasReturnAddress[2];
      "Address 2" := lasReturnAddress[3];
      City := lasReturnAddress[4];
      County := lasReturnAddress[5];
      "Country/Region Code" := lasReturnAddress[6];
      "Post Code" := lasReturnAddress[7];
      "Phone No." := lasReturnAddress[8];
    END;

    LOCAL PROCEDURE setReturnService@1000000002();
    VAR
      lrecDShipSetup@1000000000 : Record 23044600;
      lrecWhseShipHeader@1000000001 : Record 7320;
      lrecSalesHeader@1000000002 : Record 36;
      lrecTransferHeader@1000000003 : Record 5740;
    BEGIN
      //<FUNC>
      //  Description: Sets the return service
      //  Called From: This table (T23044616)
      //  Side Effect: None
      //</FUNC>

      lrecDShipSetup.GET;
      CASE lrecDShipSetup."Return Label Service" OF
        lrecDShipSetup."Return Label Service"::"Set Default":
          BEGIN
            "Shipping Agent Code" := lrecDShipSetup."Shipping Agent Code";
            "Shipping Agent Service Code" := lrecDShipSetup."Shipping Agent Service Code";
          END;
        lrecDShipSetup."Return Label Service"::"Same Service":
          BEGIN
            CASE "Document Type" OF
              "Document Type"::"Warehouse Shipment":
                BEGIN
                  IF ( NOT lrecWhseShipHeader.GET("Document No.") ) THEN EXIT;
                  "Shipping Agent Code" := lrecWhseShipHeader."Shipping Agent Code";
                  "Shipping Agent Service Code" := lrecWhseShipHeader."Shipping Agent Service Code";
                END;
              "Document Type"::"Sales Order":
                BEGIN
                  IF ( NOT lrecSalesHeader.GET("Document Subtype", "Document No.") ) THEN EXIT;
                  "Shipping Agent Code" := lrecSalesHeader."Shipping Agent Code";
                  "Shipping Agent Service Code" := lrecSalesHeader."Shipping Agent Service Code";
                END;
              "Document Type"::"Outbound Transfer":
                BEGIN
                  IF ( NOT lrecTransferHeader.GET("Document No.") ) THEN EXIT;
                  "Shipping Agent Code" := lrecTransferHeader."Shipping Agent Code";
                  "Shipping Agent Service Code" := lrecTransferHeader."Shipping Agent Service Code";
                END;
            END;
          END;
      END;
    END;

    BEGIN
    {
      ************************
      Copyright Notice
      This objects content is copyright of Insight Works 2011.  All rights reserved.
      Any redistribution or reproduction of part or all of the contents in any form is prohibited.
      ************************
    }
    END.
  }
}

