OBJECT Table 7700 Miniform Header
{
  OBJECT-PROPERTIES
  {
    Date=11/22/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00,RF1.50.09;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               // >> RF
               CheckChangeMiniform;

               "Changed By" := USERID;
               "Changed Date" := TODAY;
               "Changed Time" := TIME;
               // << RF
             END;

    OnModify=BEGIN
               // >> RF
               CheckChangeMiniform;

               "Changed By" := USERID;
               "Changed Date" := TODAY;
               "Changed Time" := TIME;
               // << RF
             END;

    OnDelete=VAR
               MiniFormLine@1000 : Record 7701;
               MiniFormFunc@1001 : Record 7703;
             BEGIN
               MiniFormLine.RESET;
               MiniFormLine.SETRANGE("Miniform Code",Code);
               MiniFormLine.DELETEALL;

               MiniFormFunc.RESET;
               MiniFormFunc.SETRANGE("Miniform Code",Code);
               MiniFormFunc.DELETEALL;
             END;

    CaptionML=[ENU=Miniform Header;
               ESM=Cabecera miniform.;
               FRC=En-tˆte mini-formulaire;
               ENC=Miniform Header];
    LookupPageID=Page7703;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              ESM=C¢digo;
                                                              FRC=Code;
                                                              ENC=Code];
                                                   NotBlank=Yes }
    { 11  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 12  ;   ;No. of Records in List;Integer     ;CaptionML=[ENU=No. of Records in List;
                                                              ESM=N§ de registro en Lista;
                                                              FRC=Nbre enreg. liste;
                                                              ENC=No. of Records in List] }
    { 13  ;   ;Form Type           ;Option        ;CaptionML=[ENU=Form Type;
                                                              ESM=Form Type;
                                                              FRC=Form Type;
                                                              ENC=Form Type];
                                                   OptionCaptionML=[@@@={Locked};
                                                                    ENU=Card,Selection List,Data List,Data List Input;
                                                                    ESM=Card,Selection List,Data List,Data List Input;
                                                                    FRC=Card,Selection List,Data List,Data List Input;
                                                                    ENC=Card,Selection List,Data List,Data List Input];
                                                   OptionString=Card,Selection List,Data List,Data List Input }
    { 15  ;   ;Start Miniform      ;Boolean       ;OnValidate=VAR
                                                                MiniformHeader@1000 : Record 7700;
                                                              BEGIN
                                                                MiniformHeader.SETFILTER(Code,'<>%1',Code);
                                                                MiniformHeader.SETRANGE("Start Miniform",TRUE);
                                                                IF NOT MiniformHeader.ISEMPTY THEN
                                                                  ERROR(Text002);
                                                              END;

                                                   CaptionML=[ENU=Start Miniform;
                                                              ESM=Iniciar Miniform.;
                                                              FRC=Lancer le mini-formulaire;
                                                              ENC=Start Miniform] }
    { 20  ;   ;Handling Codeunit   ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[ENU=Handling Codeunit;
                                                              ESM=Manejar Codeunit;
                                                              FRC=l‚ment de code de traitement;
                                                              ENC=Handling Codeunit] }
    { 21  ;   ;Next Miniform       ;Code20        ;TableRelation="Miniform Header";
                                                   OnValidate=BEGIN
                                                                IF "Next Miniform" = Code THEN
                                                                  ERROR(Text000);

                                                                // >> RF
                                                                // IF "Form Type" IN ["Form Type"::"Selection List","Form Type"::"Data List Input"] THEN
                                                                //   ERROR(Text001,FIELDCAPTION("Form Type"),"Form Type");
                                                                // << RF
                                                              END;

                                                   CaptionML=[ENU=Next Miniform;
                                                              ESM=Siguiente miniform.;
                                                              FRC=Mini-formulaire suivant;
                                                              ENC=Next Miniform] }
    { 25  ;   ;XMLin               ;BLOB          ;CaptionML=[ENU=XMLin;
                                                              ESM=XMLin;
                                                              FRC=XMLin;
                                                              ENC=XMLin] }
    { 72801;  ;Local Menu Miniform Code;Code20    ;TableRelation="Miniform Header";
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Local Menu Miniform Code }
    { 72803;  ;Minimum Label Length;Integer       ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Minimum Label Length }
    { 72804;  ;Local Menu Miniform ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Miniform Header" WHERE (Local Menu Miniform Code=FIELD(Code)));
                                                   CaptionML=ENU=Local Menu Miniform;
                                                   Editable=No }
    { 72805;  ;Changed By          ;Code50        ;TableRelation=User;
                                                   OnValidate=BEGIN
                                                                // >> RF
                                                                LoginMgt.ValidateUserID("Changed By");
                                                                // << RF
                                                              END;

                                                   OnLookup=BEGIN
                                                              // >> RF
                                                              LoginMgt.LookupUserID("Changed By");
                                                              // << RF
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Changed By;
                                                   Editable=No }
    { 72806;  ;Changed Date        ;Date          ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Changed Date;
                                                   Editable=No }
    { 72807;  ;Changed Time        ;Time          ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Changed Time;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Handling Codeunit                       ;KeyGroups=ADCS-WHSE }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1001 : TextConst 'ENU=Recursion is not allowed.;ESM=No se permiten procesos recursivos.;FRC=R‚cursivit‚ interdite;ENC=Recursion is not allowed.';
      Text001@1000 : TextConst 'ENU=%1 must not be %2.;ESM=%1 no debe ser %2.;FRC=%1 ne doit pas ˆtre %2;ENC=%1 must not be %2.';
      Text002@1002 : TextConst 'ENU=There can only be one login form.;ESM=S¢lo puede haber un formulario de inicio.;FRC=Il ne peut y avoir qu''un seul formulaire d''ouverture de session;ENC=There can only be one login form.';
      RFSetup@1170000001 : Record 14002801;
      LoginMgt@1170000000 : Codeunit 418;
      Text14002802@14002803 : TextConst 'ENU=Translated Miniforms should not be changed manually.';
      Text14002803@14002814 : TextConst 'ENU=New Miniform Code must be entered.';

    [External]
    PROCEDURE SaveXMLin@1(DOMxmlin@1001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument");
    VAR
      InStrm@1000 : InStream;
    BEGIN
      XMLin.CREATEINSTREAM(InStrm);
      DOMxmlin.Save(InStrm);

      // >> RF
      RFSetup.GET('');
      IF RFSetup."Enable RF" THEN BEGIN
        IF RFSetup."Save XML Documents" THEN
          DOMxmlin.Save(RFSetup."Save XML Path" + '\TempVT100.xml');
      END;
      // << RF
    END;

    [External]
    PROCEDURE LoadXMLin@2(VAR DOMxmlin@1001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument");
    VAR
      XMLDOMManagement@1002 : Codeunit 6224;
      OutStrm@1000 : OutStream;
    BEGIN
      XMLin.CREATEOUTSTREAM(OutStrm);
      XMLDOMManagement.LoadXMLDocumentFromOutStream(OutStrm,DOMxmlin);
    END;

    PROCEDURE CopyMiniform@14002801(NewMiniformCode@14002800 : Code[20]);
    VAR
      MiniformHeader2@14002801 : Record 7700;
      MiniformLine@14002802 : Record 7701;
      MiniformLine2@14002803 : Record 7701;
      MiniformFunction@14002804 : Record 7703;
      MiniformFunction2@14002805 : Record 7703;
    BEGIN
      // >> RF
      MiniformHeader2 := Rec;
      MiniformHeader2.Code := NewMiniformCode;
      MiniformHeader2."Changed By" := USERID;
      MiniformHeader2."Changed Date" := TODAY;
      MiniformHeader2."Changed Time" := TIME;
      MiniformHeader2.INSERT(TRUE);

      MiniformLine.RESET;
      MiniformLine.SETRANGE("Miniform Code",Code);
      IF MiniformLine.FIND('-') THEN
        REPEAT
          MiniformLine2 := MiniformLine;
          MiniformLine2."Changed By" := USERID;
          MiniformLine2."Changed Date" := TODAY;
          MiniformLine2."Changed Time" := TIME;
          MiniformLine2."Miniform Code" := MiniformHeader2.Code;
          MiniformLine2.INSERT(TRUE);
        UNTIL MiniformLine.NEXT = 0;

      MiniformFunction.RESET;
      MiniformFunction.SETRANGE("Miniform Code",Code);
      IF MiniformFunction.FIND('-') THEN
        REPEAT
          MiniformFunction2 := MiniformFunction;
          MiniformFunction2."Changed By" := USERID;
          MiniformFunction2."Changed Date" := TODAY;
          MiniformFunction2."Changed Time" := TIME;
          MiniformFunction2."Miniform Code" := MiniformHeader2.Code;
          MiniformFunction2.INSERT(TRUE);
        UNTIL MiniformFunction.NEXT = 0;
      // << RF
    END;

    PROCEDURE CopyMiniformEnterCode@14002804();
    VAR
      EnterNewMiniformCode@14002801 : Page 14002826;
      NewMiniformCode@14002802 : Code[20];
    BEGIN
      // >> RF
      TESTFIELD("Start Miniform",FALSE);

      CLEAR(EnterNewMiniformCode);
      EnterNewMiniformCode.LOOKUPMODE(TRUE);
      IF EnterNewMiniformCode.RUNMODAL = ACTION::LookupOK THEN BEGIN
        EnterNewMiniformCode.GetFields(NewMiniformCode);

        IF NewMiniformCode = '' THEN
          ERROR(Text14002803);
        CopyMiniform(NewMiniformCode);
      END;
      // << RF
    END;

    PROCEDURE ShowFormDesignHelp@14002802();
    BEGIN
      // >> RF
      TESTFIELD("Handling Codeunit");
      CODEUNIT.RUN(CODEUNIT::"RF Show Miniform Design Help",Rec);
      // << RF
    END;

    PROCEDURE CheckChangeMiniform@14002803();
    BEGIN
      // >> RF
      IF STRLEN(Code) > 3 THEN
        IF COPYSTR(Code,3,1) = '-' THEN
          MESSAGE(Text14002802);
      // << RF
    END;

    BEGIN
    END.
  }
}

