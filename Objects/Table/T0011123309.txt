OBJECT Table 11123309 SC - Webshop Filter
{
  OBJECT-PROPERTIES
  {
    Date=07/01/20;
    Time=12:00:00 AM;
    Modified=Yes;
    Version List=SCW19.2.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=SC - Webshop Filter;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code50        ;CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 5   ;   ;Table No.           ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=FILTER(Table)) }
    { 6   ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.));
                                                   NotBlank=Yes }
    { 7   ;   ;Table Name          ;Text30        ;OnValidate=VAR
                                                                AllObjWithCaption@11123302 : Record 2000000058;
                                                              BEGIN
                                                                AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::Table);
                                                                AllObjWithCaption.SETRANGE("Object Name","Table Name");
                                                                AllObjWithCaption.FINDFIRST;
                                                                "Table No." := AllObjWithCaption."Object ID";
                                                              END;
                                                               }
    { 8   ;   ;Field Name          ;Text30        ;OnValidate=VAR
                                                                Field@11123302 : Record 2000000041;
                                                              BEGIN
                                                                Field.SETRANGE(TableNo,"Table No.");
                                                                Field.SETRANGE(FieldName,"Field Name");
                                                                Field.FINDFIRST;
                                                                "Field No." := Field."No.";
                                                              END;
                                                               }
    { 9   ;   ;Filter              ;Text250       ;OnValidate=BEGIN
                                                                ValidateFilter;
                                                              END;

                                                   CaptionML=ENU=Filter }
  }
  KEYS
  {
    {    ;Code,Table No.,Field No.                ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text11123302@11123302 : TextConst 'ENU=Filter value of %1 is not valid.';

    PROCEDURE LookupTableNo@11123303(InTableNo@11123302 : Integer) : Integer;
    VAR
      AllObjWithCaption@11123304 : Record 2000000058;
      TableList@11123303 : Page 11123318;
    BEGIN
      CLEAR(TableList);

      AllObjWithCaption.FILTERGROUP := 2;
      AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::Table);
      AllObjWithCaption.SETFILTER(
        "Object ID",
        '%1|%2|%3|%4',
        DATABASE::Item,
        DATABASE::Customer,
        DATABASE::Contact,
        DATABASE::"Salesperson/Purchaser");
      AllObjWithCaption.FILTERGROUP := 0;

      IF "Table No." <> 0 THEN BEGIN
        AllObjWithCaption."Object Type" := AllObjWithCaption."Object Type"::Table;
        AllObjWithCaption."Object ID" := InTableNo;
        AllObjWithCaption.FIND;
      END ELSE
        AllObjWithCaption.FINDFIRST;

      TableList.LOOKUPMODE := TRUE;
      TableList.SETTABLEVIEW(AllObjWithCaption);
      TableList.SETRECORD(AllObjWithCaption);
      IF TableList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        TableList.GETRECORD(AllObjWithCaption);
        EXIT(AllObjWithCaption."Object ID");
      END;
      EXIT(InTableNo);
    END;

    PROCEDURE LookupTableName@11123302(InTableName@11123302 : Text[30]) : Text[30];
    VAR
      AllObjWithCaption@11123304 : Record 2000000058;
      TableList@11123303 : Page 11123318;
    BEGIN
      CLEAR(TableList);

      AllObjWithCaption.FILTERGROUP := 2;
      AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::Table);
      AllObjWithCaption.SETFILTER(
        "Object ID",
        '%1|%2|%3|%4',
        DATABASE::Item,
        DATABASE::Customer,
        DATABASE::Contact,
        DATABASE::"Salesperson/Purchaser");

      AllObjWithCaption.FILTERGROUP := 0;

      IF "Table No." <> 0 THEN BEGIN
        AllObjWithCaption.SETRANGE("Object ID","Table No.");
        AllObjWithCaption.FINDFIRST;
      END ELSE
        AllObjWithCaption.FINDFIRST;

      TableList.LOOKUPMODE := TRUE;
      TableList.SETTABLEVIEW(AllObjWithCaption);
      TableList.SETRECORD(AllObjWithCaption);
      IF TableList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        TableList.GETRECORD(AllObjWithCaption);
        EXIT(AllObjWithCaption."Object Name");
      END;
      EXIT(InTableName);
    END;

    PROCEDURE ValidateFilter@11123304();
    VAR
      RecRef@11123303 : RecordRef;
      FieldRef@11123304 : FieldRef;
      FilterHelper@11123302 : Codeunit 11123321;
      Expression@11123305 : Text[250];
    BEGIN
      RecRef.OPEN("Table No.",TRUE);
      FieldRef := RecRef.FIELD("Field No.");
      Expression := Filter;
      IF Expression = '' THEN
        ERROR(Text11123302,Filter);

      IF NOT FilterHelper.ValidateExpression(Expression,FieldRef) THEN
        ERROR(Text11123302,Filter);
    END;

    BEGIN
    END.
  }
}

