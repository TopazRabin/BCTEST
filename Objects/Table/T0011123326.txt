OBJECT Table 11123326 SC - Customer Assortment
{
  OBJECT-PROPERTIES
  {
    Date=07/01/20;
    Time=12:00:00 AM;
    Modified=Yes;
    Version List=SCW19.2.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               UpdateItemLastDateTimeModified(Rec);
             END;

    OnModify=BEGIN
               UpdateItemLastDateTimeModified(Rec);
             END;

    OnDelete=BEGIN
               UpdateItemLastDateTimeModified(Rec);
             END;

    OnRename=BEGIN
               UpdateItemLastDateTimeModified(xRec);
               UpdateItemLastDateTimeModified(Rec);
             END;

    CaptionML=ENU=Customer Assortment;
    LookupPageID=Page11123327;
  }
  FIELDS
  {
    { 1   ;   ;Sales Type          ;Option        ;OnValidate=BEGIN
                                                                IF "Sales Type" <> xRec."Sales Type" THEN
                                                                  "Sales Code" := '';
                                                              END;

                                                   CaptionML=ENU=Sales Type;
                                                   OptionCaptionML=ENU=Customer,Customer Price Group;
                                                   OptionString=Customer,Customer Price Group }
    { 2   ;   ;Sales Code          ;Code20        ;TableRelation=IF (Sales Type=CONST(Customer)) Customer
                                                                 ELSE IF (Sales Type=CONST(Customer Price Group)) "Customer Price Group";
                                                   CaptionML=ENU=Sales Code;
                                                   NotBlank=Yes }
    { 3   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Type <> xRec.Type THEN BEGIN
                                                                  Code := '';
                                                                  "Item Category Code (Internal)" := '';
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Type;
                                                   OptionCaptionML=ENU=Item,Product Group,Item Category;
                                                   OptionString=Item,Product Group,Item Category }
    { 4   ;   ;Code                ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Product Group)) "Product Group".Code WHERE (Item Category Code=FIELD("Item Category Code (Internal)"))
                                                                 ELSE IF (Type=CONST(Item Category)) "Item Category";
                                                   OnValidate=VAR
                                                                ProductGroup@11123302 : Record 5723;
                                                                FilterExpr@11123303 : Code[80];
                                                              BEGIN
                                                                IF (Type = Type::"Product Group") AND
                                                                   (Code <> xRec.Code)
                                                                THEN BEGIN
                                                                  ProductGroup.RESET;
                                                                  FilterExpr := STRSUBSTNO('%1*',Code);
                                                                  ProductGroup.SETFILTER(Code,FilterExpr);
                                                                  ProductGroup.FINDFIRST;
                                                                  "Item Category Code (Internal)" := ProductGroup."Item Category Code";
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              Item@11123302 : Record 27;
                                                              ItemCategory@11123303 : Record 5722;
                                                              ProductGroup@11123304 : Record 5723;
                                                            BEGIN
                                                              CASE Type OF
                                                                Type::Item :
                                                                  BEGIN
                                                                    CLEAR(Item);
                                                                    IF Code <> '' THEN
                                                                      Item.GET(Code)
                                                                    ELSE
                                                                      Item.FINDFIRST;
                                                                    IF PAGE.RUNMODAL(PAGE::"Item List",Item) = ACTION::LookupOK THEN
                                                                      Code := Item."No.";
                                                                  END;
                                                                Type::"Product Group" :
                                                                  BEGIN
                                                                    CLEAR(ProductGroup);
                                                                    IF Code <> '' THEN
                                                                      ProductGroup.GET("Item Category Code (Internal)",Code)
                                                                    ELSE
                                                                      ProductGroup.FINDFIRST;

                                                                    IF PAGE.RUNMODAL(PAGE::"Product Groups",ProductGroup) = ACTION::LookupOK THEN BEGIN
                                                                      Code := ProductGroup.Code;
                                                                      "Item Category Code (Internal)" := ProductGroup."Item Category Code";
                                                                    END;
                                                                  END;
                                                                Type::"Item Category" :
                                                                  BEGIN
                                                                    CLEAR(ItemCategory);
                                                                    IF Code <> '' THEN
                                                                      ItemCategory.GET(Code)
                                                                    ELSE
                                                                      ItemCategory.FINDFIRST;
                                                                    IF PAGE.RUNMODAL(PAGE::"Item Categories",ItemCategory) = ACTION::LookupOK THEN
                                                                      Code := ItemCategory.Code;
                                                                  END;
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Code;
                                                   NotBlank=Yes }
    { 5   ;   ;Item Category Code (Internal);Code10;
                                                   TableRelation="Item Category";
                                                   CaptionML=ENU=Item Category Code (Internal);
                                                   Editable=No }
    { 6   ;   ;Mode                ;Option        ;CaptionML=ENU=Mode;
                                                   OptionCaptionML=ENU=Allow,Deny;
                                                   OptionString=Allow,Deny }
  }
  KEYS
  {
    {    ;Sales Type,Sales Code,Type,Code,Item Category Code (Internal);
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    LOCAL PROCEDURE UpdateItemLastDateTimeModified@11123302(CustSpecAssort@11123303 : Record 11123326);
    VAR
      Item@11123302 : Record 27;
      Setup@11123307 : Record 11123305;
      Validation@11123305 : Codeunit 11123310;
      RefreshRequired@11123306 : Boolean;
    BEGIN
      CASE CustSpecAssort.Type OF
        CustSpecAssort.Type::Item :
          IF Item.GET(CustSpecAssort.Code) THEN BEGIN
            Item."Last Date Modified" := TODAY;
            Item.MODIFY;
            RefreshRequired := TRUE;
          END;
        CustSpecAssort.Type::"Product Group" :
          BEGIN
            Item.SETRANGE("Item Category Code",CustSpecAssort."Item Category Code (Internal)");
            Item.SETRANGE("Product Group Code",CustSpecAssort.Code);
            IF NOT Item.ISEMPTY THEN BEGIN
              Item.MODIFYALL("Last Date Modified",TODAY,FALSE);
              RefreshRequired := TRUE;
            END;
          END;
        CustSpecAssort.Type::"Item Category" :
          BEGIN
            Item.SETRANGE("Item Category Code",CustSpecAssort.Code);
            IF NOT Item.ISEMPTY THEN BEGIN
              Item.MODIFYALL("Last Date Modified",TODAY,FALSE);
              RefreshRequired := TRUE;
            END;
         END;
      END;
      IF RefreshRequired THEN
        Validation.SetRefreshCatOverviewRequired(TRUE,Setup);
    END;

    BEGIN
    END.
  }
}

