OBJECT Table 14000614 Receive Command Translation
{
  OBJECT-PROPERTIES
  {
    Date=06/28/17;
    Time=12:00:00 PM;
    Version List=SE0.52.12;
  }
  PROPERTIES
  {
    CaptionML=ENU=Receive Command Translation;
  }
  FIELDS
  {
    { 1   ;   ;Language ID         ;Integer       ;CaptionML=ENU=Language ID;
                                                   NotBlank=Yes }
    { 2   ;   ;Command Code        ;Code10        ;OnValidate=BEGIN
                                                                CheckCommand("Command Code",FALSE);
                                                              END;

                                                   CaptionML=ENU=Command Code;
                                                   NotBlank=Yes }
    { 11  ;   ;Description         ;Text30        ;CaptionML=ENU=Description }
    { 12  ;   ;Replace Command Code;Code10        ;OnValidate=BEGIN
                                                                CheckCommand("Replace Command Code",TRUE);

                                                                IF "Replace Command Code" <> '' THEN BEGIN
                                                                  CommandTranslation.RESET;
                                                                  CommandTranslation.SETCURRENTKEY("Language ID","Replace Command Code");
                                                                  CommandTranslation.SETRANGE("Language ID","Language ID");
                                                                  CommandTranslation.SETRANGE("Replace Command Code","Replace Command Code");
                                                                  CommandTranslation.SETFILTER("Command Code",'<>%1',"Command Code");
                                                                  IF CommandTranslation.FIND('-') THEN
                                                                    MESSAGE(Text001,TABLECAPTION,CommandTranslation."Command Code",CommandTranslation.Description);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Replace Command Code }
  }
  KEYS
  {
    {    ;Language ID,Command Code                ;Clustered=Yes }
    {    ;Language ID,Replace Command Code         }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CommandTranslation@1240030000 : Record 14000614;
      Text001@1240030001 : TextConst 'ENU=%1 %2 %3 already replaces this command.';
      Text002@1240030002 : TextConst 'ENU=Command is less than 3 characters.';
      Text003@1240030003 : TextConst 'ENU=Commands cannot start with //X. This is reserved for Standard Option Setup.';
      Text004@1240030004 : TextConst 'ENU=Commands must start with "//"';
      Text005@1240030005 : TextConst 'ENU=X Commands must be minimum 4 characters.';

    PROCEDURE CheckCommand@1240030000(CommandCode@1240030000 : Code[10];AllowXCommand@1240030001 : Boolean);
    VAR
      StandardOptionSetup@1240030002 : Record 14000610;
    BEGIN
      IF CommandCode = '' THEN
        EXIT;

      IF STRLEN(CommandCode) >= 3 THEN BEGIN
        IF COPYSTR(CommandCode,1,3) = '//X' THEN
          IF AllowXCommand THEN BEGIN
            IF STRLEN(CommandCode) = 3 THEN
              ERROR(Text005);

            StandardOptionSetup.RESET;
            StandardOptionSetup.SETRANGE(
              Code,COPYSTR("Replace Command Code",4,MAXSTRLEN(StandardOptionSetup.Code)));
            StandardOptionSetup.FIND('-');
          END ELSE
            ERROR(Text003);

        IF COPYSTR(CommandCode,1,2) <> '//' THEN
          ERROR(Text004);
      END ELSE
        ERROR(Text002);
    END;

    BEGIN
    END.
  }
}

