OBJECT Table 43 Purch. Comment Line
{
  OBJECT-PROPERTIES
  {
    Date=03/26/20;
    Time=12:00:00 PM;
    Version List=NAVW111.00.00.41920,SE0.60.18;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Purch. Comment Line;
               ESM=L¡n. comentario compra;
               FRC=Ligne commentaire d'achat;
               ENC=Purch. Comment Line];
    LookupPageID=Page68;
    DrillDownPageID=Page68;
  }
  FIELDS
  {
    { 1   ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              ESM=Tipo documento;
                                                              FRC=Type de document;
                                                              ENC=Document Type];
                                                   OptionCaptionML=[ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,Receipt,Posted Invoice,Posted Credit Memo,Posted Return Shipment;
                                                                    ESM=Cotizaci¢n,Pedido,Factura,Nota cr‚dito,Pedido abierto,Devoluci¢n,Recepci¢n,Factura regis.,Nota cr‚dito regis.,Env¡o dev. regis.;
                                                                    FRC=Devis,Commande,Facture,Avoir,Commande permanente,Retour,R‚ception,Facture report‚e,Avoir report‚,Livraison retour report‚e;
                                                                    ENC=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,Receipt,Posted Invoice,Posted Credit Memo,Posted Return Shipment];
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,Receipt,Posted Invoice,Posted Credit Memo,Posted Return Shipment }
    { 2   ;   ;No.                 ;Code20        ;CaptionML=[ENU=No.;
                                                              ESM=N§;
                                                              FRC=Nø;
                                                              ENC=No.] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESM=N§ l¡nea;
                                                              FRC=Nø ligne;
                                                              ENC=Line No.] }
    { 4   ;   ;Date                ;Date          ;CaptionML=[ENU=Date;
                                                              ESM=Fecha;
                                                              FRC=Date;
                                                              ENC=Date] }
    { 5   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              ESM=C¢digo;
                                                              FRC=Code;
                                                              ENC=Code] }
    { 6   ;   ;Comment             ;Text80        ;CaptionML=[ENU=Comment;
                                                              ESM=Comentario;
                                                              FRC=Commentaire;
                                                              ENC=Comment] }
    { 7   ;   ;Document Line No.   ;Integer       ;CaptionML=[ENU=Document Line No.;
                                                              ESM=N§ l¡n. documento;
                                                              FRC=N§ ligne document;
                                                              ENC=Document Line No.] }
    { 50000;  ;Print On Order      ;Boolean       ;Description=TOP160 }
    { 50001;  ;Print On Whse. Rcpt.;Boolean       ;Description=TOP160 }
    { 50002;  ;Print On Whse. Put-Away;Boolean    ;Description=TOP160 }
    { 14000901;;Include in E-Mail  ;Boolean       ;CaptionML=ENU=Include in E-Mail }
  }
  KEYS
  {
    {    ;Document Type,No.,Document Line No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE SetUpNewLine@1();
    VAR
      PurchCommentLine@1000 : Record 43;
    BEGIN
      PurchCommentLine.SETRANGE("Document Type","Document Type");
      PurchCommentLine.SETRANGE("No.","No.");
      PurchCommentLine.SETRANGE("Document Line No.","Document Line No.");
      PurchCommentLine.SETRANGE(Date,WORKDATE);
      IF NOT PurchCommentLine.FINDFIRST THEN
        Date := WORKDATE;
    END;

    [External]
    PROCEDURE CopyComments@22(FromDocumentType@1000 : Integer;ToDocumentType@1001 : Integer;FromNumber@1002 : Code[20];ToNumber@1003 : Code[20]);
    VAR
      PurchCommentLine@1004 : Record 43;
      PurchCommentLine2@1005 : Record 43;
    BEGIN
      PurchCommentLine.SETRANGE("Document Type",FromDocumentType);
      PurchCommentLine.SETRANGE("No.",FromNumber);
      IF PurchCommentLine.FINDSET THEN
        REPEAT
          PurchCommentLine2 := PurchCommentLine;
          PurchCommentLine2."Document Type" := ToDocumentType;
          PurchCommentLine2."No." := ToNumber;
          PurchCommentLine2.INSERT;
        UNTIL PurchCommentLine.NEXT = 0;
    END;

    [External]
    PROCEDURE CopyLineComments@6(FromDocumentType@1000 : Integer;ToDocumentType@1001 : Integer;FromNumber@1002 : Code[20];ToNumber@1003 : Code[20];FromDocumentLineNo@1008 : Integer;ToDocumentLineNo@1007 : Integer);
    VAR
      PurchCommentLineSource@1004 : Record 43;
      PurchCommentLineTarget@1005 : Record 43;
      IsHandled@1006 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeCopyLineComments(
        PurchCommentLineTarget,IsHandled,FromDocumentType,ToDocumentType,FromNumber,ToNumber,FromDocumentLineNo,ToDocumentLineNo);
      IF IsHandled THEN
        EXIT;

      PurchCommentLineSource.SETRANGE("Document Type",FromDocumentType);
      PurchCommentLineSource.SETRANGE("No.",FromNumber);
      PurchCommentLineSource.SETRANGE("Document Line No.",FromDocumentLineNo);
      IF PurchCommentLineSource.FINDSET THEN
        REPEAT
          PurchCommentLineTarget := PurchCommentLineSource;
          PurchCommentLineTarget."Document Type" := ToDocumentType;
          PurchCommentLineTarget."No." := ToNumber;
          PurchCommentLineTarget."Document Line No." := ToDocumentLineNo;
          PurchCommentLineTarget.INSERT;
        UNTIL PurchCommentLineSource.NEXT = 0;
    END;

    [External]
    PROCEDURE CopyLineCommentsFromPurchaseLines@10(FromDocumentType@1000 : Integer;ToDocumentType@1001 : Integer;FromNumber@1002 : Code[20];ToNumber@1003 : Code[20];VAR TempPurchaseLineSource@1008 : TEMPORARY Record 39);
    VAR
      PurchCommentLineSource@1004 : Record 43;
      PurchCommentLineTarget@1005 : Record 43;
      IsHandled@1006 : Boolean;
      NextLineNo@1007 : Integer;
    BEGIN
      IsHandled := FALSE;
      OnBeforeCopyLineCommentsFromPurchaseLines(
        PurchCommentLineTarget,IsHandled,FromDocumentType,ToDocumentType,FromNumber,ToNumber,TempPurchaseLineSource);
      IF IsHandled THEN
        EXIT;

      PurchCommentLineTarget.SETRANGE("Document Type",ToDocumentType);
      PurchCommentLineTarget.SETRANGE("No.",ToNumber);
      PurchCommentLineTarget.SETRANGE("Document Line No.",0);
      IF PurchCommentLineTarget.FINDLAST THEN;
      NextLineNo := PurchCommentLineTarget."Line No." + 10000;
      PurchCommentLineTarget.RESET;

      PurchCommentLineSource.SETRANGE("Document Type",FromDocumentType);
      PurchCommentLineSource.SETRANGE("No.",FromNumber);
      IF TempPurchaseLineSource.FINDSET THEN
        REPEAT
          PurchCommentLineSource.SETRANGE("Document Line No.",TempPurchaseLineSource."Line No.");
          IF PurchCommentLineSource.FINDSET THEN
            REPEAT
              PurchCommentLineTarget := PurchCommentLineSource;
              PurchCommentLineTarget."Document Type" := ToDocumentType;
              PurchCommentLineTarget."No." := ToNumber;
              PurchCommentLineTarget."Document Line No." := 0;
              PurchCommentLineTarget."Line No." := NextLineNo;
              PurchCommentLineTarget.INSERT;
              NextLineNo += 10000;
            UNTIL PurchCommentLineSource.NEXT = 0;
        UNTIL TempPurchaseLineSource.NEXT = 0;
    END;

    [External]
    PROCEDURE CopyHeaderComments@8(FromDocumentType@1000 : Integer;ToDocumentType@1001 : Integer;FromNumber@1002 : Code[20];ToNumber@1003 : Code[20]);
    VAR
      PurchCommentLineSource@1004 : Record 43;
      PurchCommentLineTarget@1005 : Record 43;
      IsHandled@1006 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeCopyHeaderComments(PurchCommentLineTarget,IsHandled,FromDocumentType,ToDocumentType,FromNumber,ToNumber);
      IF IsHandled THEN
        EXIT;

      PurchCommentLineSource.SETRANGE("Document Type",FromDocumentType);
      PurchCommentLineSource.SETRANGE("No.",FromNumber);
      PurchCommentLineSource.SETRANGE("Document Line No.",0);
      IF PurchCommentLineSource.FINDSET THEN
        REPEAT
          PurchCommentLineTarget := PurchCommentLineSource;
          PurchCommentLineTarget."Document Type" := ToDocumentType;
          PurchCommentLineTarget."No." := ToNumber;
          PurchCommentLineTarget.INSERT;
        UNTIL PurchCommentLineSource.NEXT = 0;
    END;

    [External]
    PROCEDURE DeleteComments@2(DocType@1000 : Option;DocNo@1001 : Code[20]);
    BEGIN
      SETRANGE("Document Type",DocType);
      SETRANGE("No.",DocNo);
      IF NOT ISEMPTY THEN
        DELETEALL;
    END;

    PROCEDURE ShowComments@3(DocType@1001 : Option;DocNo@1002 : Code[20];DocLineNo@1003 : Integer);
    VAR
      PurchCommentSheet@1000 : Page 66;
    BEGIN
      SETRANGE("Document Type",DocType);
      SETRANGE("No.",DocNo);
      SETRANGE("Document Line No.",DocLineNo);
      CLEAR(PurchCommentSheet);
      PurchCommentSheet.SETTABLEVIEW(Rec);
      PurchCommentSheet.RUNMODAL;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCopyLineComments@7(VAR PurchCommentLine@1000 : Record 43;VAR IsHandled@1002 : Boolean;FromDocumentType@1005 : Integer;ToDocumentType@1001 : Integer;FromNumber@1004 : Code[20];ToNumber@1003 : Code[20];FromDocumentLineNo@1007 : Integer;ToDocumentLine@1006 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCopyLineCommentsFromPurchaseLines@11(VAR PurchCommentLine@1000 : Record 43;VAR IsHandled@1002 : Boolean;FromDocumentType@1006 : Integer;ToDocumentType@1005 : Integer;FromNumber@1004 : Code[20];ToNumber@1003 : Code[20];VAR TempPurchaseLineSource@1001 : TEMPORARY Record 39);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCopyHeaderComments@9(VAR PurchCommentLine@1000 : Record 43;VAR IsHandled@1002 : Boolean;FromDocumentType@1005 : Integer;ToDocumentType@1001 : Integer;FromNumber@1004 : Code[20];ToNumber@1003 : Code[20]);
    BEGIN
    END;

    BEGIN
    {
      TOP160 KT ABCSI Purchase Order Fields 03312015
        - Addded fields 50000 Print On Order, 50001 Print On Whse. Rcpt., 50002 Print On Whse. Put-Away
    }
    END.
  }
}

