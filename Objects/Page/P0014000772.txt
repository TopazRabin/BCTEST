OBJECT Page 14000772 Edit UPS Hundred Weight
{
  OBJECT-PROPERTIES
  {
    Date=06/28/17;
    Time=12:00:00 PM;
    Version List=SE0.54.10;
  }
  PROPERTIES
  {
    CaptionML=ENU=Edit UPS Hundred Weight;
    SourceTable=Table14000765;
    PageType=List;
    OnOpenPage=BEGIN
                 UPSHundredWeightServiceTmp.DELETEALL;
                 COPYFILTER(Tier,UPSHundredWeightService.Tier);
                 IF UPSHundredWeightService.FIND('-') THEN
                   REPEAT
                     UPSHundredWeightServiceTmp := UPSHundredWeightService;
                     UPSHundredWeightServiceTmp.INSERT;
                   UNTIL UPSHundredWeightService.NEXT = 0;

                 ShippingAgent.RESET;
                 ShippingAgent.SETRANGE("Shipper Type",ShippingAgent."Shipper Type"::UPS);
                 ShippingAgent.FIND('-');
                 EShipAgentService.RESET;
                 EShipAgentService.SETRANGE("Shipping Agent Code",ShippingAgent.Code);
                 EShipAgentService.FIND('-');
                 REPEAT
                   EShipAgentServiceTmp := EShipAgentService;
                   EShipAgentServiceTmp.INSERT;
                 UNTIL EShipAgentService.NEXT = 0;
               END;

    OnClosePage=BEGIN
                  IF SaveChanges THEN BEGIN
                    COPYFILTER(Tier,UPSHundredWeightService.Tier);
                    UPSHundredWeightService.DELETEALL;

                    UPSHundredWeightServiceTmp.RESET;
                    IF UPSHundredWeightServiceTmp.FIND('-') THEN
                      REPEAT
                        UPSHundredWeightService := UPSHundredWeightServiceTmp;
                        UPSHundredWeightService.INSERT(TRUE);

                        UPSHundredWeightServiceTmp.DELETE;
                      UNTIL UPSHundredWeightServiceTmp.NEXT = 0;
                  END;
                END;

    OnFindRecord=BEGIN
                   UPSHundredWeightServiceTmp.COPYFILTERS(Rec);
                   UPSHundredWeightServiceTmp := Rec;
                   IF NOT UPSHundredWeightServiceTmp.FIND(Which) THEN
                     EXIT(FALSE);
                   Rec := UPSHundredWeightServiceTmp;
                   EXIT(TRUE);
                 END;

    OnNextRecord=BEGIN
                   UPSHundredWeightServiceTmp.COPYFILTERS(Rec);
                   UPSHundredWeightServiceTmp := Rec;
                   CurrentSteps := UPSHundredWeightServiceTmp.NEXT(Steps);
                   IF CurrentSteps <> 0 THEN
                     Rec := UPSHundredWeightServiceTmp;
                   EXIT(CurrentSteps);
                 END;

    OnAfterGetRecord=BEGIN
                       GetEShipAgentServiceTmp("Service Indicator");
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  OnAfterGetCurrRecord;
                END;

    OnInsertRecord=BEGIN
                     IF ("Service Indicator" = '') OR
                        (Zone = '')
                     THEN
                       ERROR(Text003,FIELDNAME("Service Indicator"),FIELDNAME(Zone));

                     UPSHundredWeightServiceTmp := Rec;
                     UPSHundredWeightServiceTmp.INSERT;
                     EXIT(FALSE);
                   END;

    OnModifyRecord=BEGIN
                     IF ("Service Indicator" <> xRec."Service Indicator") OR
                        (Zone <> xRec.Zone) OR
                        (Tier <> xRec.Tier) OR
                        ("Minimum Weight" <> xRec."Minimum Weight") OR
                        (Residential <> xRec.Residential)
                     THEN
                       ERROR(Text004);

                     UPSHundredWeightServiceTmp := Rec;
                     UPSHundredWeightServiceTmp.MODIFY;
                     EXIT(FALSE);
                   END;

    OnDeleteRecord=BEGIN
                     UPSHundredWeightServiceTmp := Rec;
                     UPSHundredWeightServiceTmp.DELETE;
                     EXIT(FALSE);
                   END;

    OnQueryClosePage=BEGIN
                       SaveChanges := CONFIRM(Text001);
                       IF NOT SaveChanges THEN
                         IF NOT CONFIRM(Text002) THEN
                           EXIT(FALSE);
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="Service Indicator";
                OnValidate=BEGIN
                             ServiceIndicatorOnAfterValidat;
                           END;

                OnLookup=BEGIN
                           LookupServiceIndicator(Text);
                         END;
                          }

    { 4   ;2   ;Field     ;
                SourceExpr=Zone }

    { 6   ;2   ;Field     ;
                SourceExpr=Tier }

    { 8   ;2   ;Field     ;
                SourceExpr="Minimum Weight" }

    { 10  ;2   ;Field     ;
                SourceExpr=Residential }

    { 25  ;2   ;Field     ;
                Name=EShipAgentService;
                CaptionML=ENU=E-Ship Agent Service;
                SourceExpr=EShipAgentServiceTmp.Code;
                Editable=FALSE;
                OnLookup=BEGIN
                           LookupServiceIndicator("Service Indicator");
                         END;
                          }

    { 12  ;2   ;Field     ;
                SourceExpr="Maximum Weight" }

    { 14  ;2   ;Field     ;
                SourceExpr="Rate per Pound" }

    { 16  ;2   ;Field     ;
                SourceExpr="World Wide Service";
                Visible=FALSE }

    { 18  ;2   ;Field     ;
                SourceExpr="Min. Tier Charge" }

    { 20  ;2   ;Field     ;
                SourceExpr="Max. Tier Charge" }

  }
  CODE
  {
    VAR
      UPSHundredWeightService@1240030000 : Record 14000765;
      UPSHundredWeightServiceTmp@1240030001 : TEMPORARY Record 14000765;
      ShippingAgent@1240030002 : Record 291;
      EShipAgentService@1240030003 : Record 14000708;
      EShipAgentServiceTmp@1240030004 : TEMPORARY Record 14000708;
      CurrentSteps@1240030006 : Integer;
      SaveChanges@1240030007 : Boolean;
      Text001@1001 : TextConst 'ENU=Do you want to save the changes?';
      Text002@1002 : TextConst 'ENU=Leave form?';
      Text003@1003 : TextConst 'ENU=%1 and %2 must be filled in.';
      Text004@1004 : TextConst 'ENU=Cannot be renamed. Please delete the record and insert a new.';

    LOCAL PROCEDURE LookupServiceIndicator@1(VAR Text@1240030000 : Text[260]);
    BEGIN
      GetEShipAgentServiceTmp(Text);

      EShipAgentService.FILTERGROUP(2);
      EShipAgentService.RESET;
      EShipAgentService.SETRANGE("Shipping Agent Code",ShippingAgent.Code);
      EShipAgentService.FILTERGROUP(0);
      IF PAGE.RUNMODAL(0,EShipAgentService) = ACTION::LookupOK THEN
        "Service Indicator" := EShipAgentService."Service Indicator";
    END;

    LOCAL PROCEDURE GetEShipAgentServiceTmp@2(ServiceIndicator@1240030000 : Code[10]);
    BEGIN
      EShipAgentServiceTmp.RESET;
      EShipAgentServiceTmp.SETRANGE("Service Indicator",ServiceIndicator);
      IF NOT EShipAgentServiceTmp.FIND('-') THEN
        CLEAR(EShipAgentServiceTmp);
    END;

    LOCAL PROCEDURE ServiceIndicatorOnAfterValidat@19045637();
    BEGIN
      GetEShipAgentServiceTmp("Service Indicator");
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      GetEShipAgentServiceTmp("Service Indicator");
    END;

    BEGIN
    END.
  }
}

