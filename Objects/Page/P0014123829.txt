OBJECT Page 14123829 ES Select Data Security Codes
{
  OBJECT-PROPERTIES
  {
    Date=04/25/16;
    Time=12:00:00 PM;
    Version List=ES1.41.17;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=ENU=Select Data Security Codes;
    SourceTable=Table14123804;
    PageType=List;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 IF FromCompanyName = '' THEN
                   ERROR(Text001);
               END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1240520001;1;Group  ;
                GroupType=Repeater }

    { 1240520002;2;Field  ;
                SourceExpr="Source Table ID" }

    { 1240520004;2;Field  ;
                SourceExpr=Code }

    { 1240520006;2;Field  ;
                SourceExpr=Description }

    { 1240520008;2;Field  ;
                SourceExpr=Default }

    { 1240520010;2;Field  ;
                CaptionML=ENU=From Setup Company;
                SourceExpr=ESFLADSCompanyMgt.DataSecCodeCompCopied(Rec,FromCompanyName);
                Editable=FALSE }

    { 1240520015;2;Field  ;
                CaptionML=ENU=Already Exists;
                SourceExpr=AlreadyExists }

  }
  CODE
  {
    VAR
      ESDataSecurityCode@1240520004 : Record 14123804;
      ESFLADSCompanyMgt@1240520003 : Codeunit 14123804;
      FromCompanyName@1240520001 : Text[30];
      NoNextExists@1240520002 : Boolean;
      Text001@1001 : TextConst 'ENU=Must be initialized.';

    PROCEDURE InitFromCompany@1240520001(VAR CurrentESDataSecCode@1240520002 : Record 14123804;CurrentFromCompanyName@1240520001 : Text[30];OnlyNotFromSetupComany@1240520003 : Boolean);
    VAR
      ESFLADSCopySetup@1240520004 : Record 14123816;
    BEGIN
      IF CurrentESDataSecCode.FIND('-') THEN BEGIN
        REPEAT
          Rec := CurrentESDataSecCode;
          IF OnlyNotFromSetupComany THEN BEGIN
            ESFLADSCopySetup.CHANGECOMPANY(CurrentFromCompanyName);
            ESFLADSCopySetup.SETRANGE("Table ID",DATABASE::"ES Data Security Code");
            ESFLADSCopySetup.SETRANGE("Source Table ID","Source Table ID");
            ESFLADSCopySetup.SETRANGE(Code,Code);
            ESFLADSCopySetup.SETRANGE("From Setup Company",TRUE);
            IF NOT ESFLADSCopySetup.FIND('-') THEN
              INSERT;
          END ELSE
            INSERT;
        UNTIL CurrentESDataSecCode.NEXT = 0;

        FIND('-');
      END;

      FromCompanyName := CurrentFromCompanyName;
    END;

    PROCEDURE GetFirstSelected@1240520002(VAR CurrentESDataSecCode@1240520002 : Record 14123804) : Boolean;
    BEGIN
      // Only work properly with KB2650787 (build 33002) or later
      CurrPage.SETSELECTIONFILTER(ESDataSecurityCode);
      IF ESDataSecurityCode.FIND('-') THEN BEGIN
        CurrentESDataSecCode := ESDataSecurityCode;
        EXIT(TRUE);
      END ELSE
        IF Code <> '' THEN BEGIN
          CurrentESDataSecCode := Rec;
          NoNextExists := TRUE;
          EXIT(TRUE);
        END ELSE
          EXIT(FALSE);
    END;

    PROCEDURE GetNextSelected@1240520003(VAR CurrentESDataSecCode@1240520001 : Record 14123804) : Boolean;
    BEGIN
      IF NoNextExists THEN
        EXIT(FALSE);

      IF ESDataSecurityCode.NEXT > 0 THEN BEGIN
        CurrentESDataSecCode := ESDataSecurityCode;
        EXIT(TRUE);
      END ELSE
        EXIT(FALSE);
    END;

    PROCEDURE AlreadyExists@1240520004() : Boolean;
    VAR
      ESDataSecurityCode2@1240520001 : Record 14123804;
    BEGIN
      EXIT(ESDataSecurityCode2.GET("Source Table ID",Code));
    END;

    BEGIN
    END.
  }
}

