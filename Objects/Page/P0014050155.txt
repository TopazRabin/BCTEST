OBJECT Page 14050155 EDI Bill of Ladings
{
  OBJECT-PROPERTIES
  {
    Date=02/25/19;
    Time=12:00:00 PM;
    Version List=SE0.61;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=ENU=Bill of Ladings;
    SourceTable=Table14000822;
    PageType=List;
    CardPageID=EDI Bill of Lading;
    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1240020004;1 ;ActionGroup;
                      CaptionML=ENU=&Bill of Lading }
      { 1240020007;2 ;Action    ;
                      ShortCutKey=Shift+F7;
                      CaptionML=ENU=Card;
                      Image=EditLines;
                      OnAction=BEGIN
                                 IF Posted THEN BEGIN
                                   Rec.SETRANGE(Posted);
                                   PAGE.RUN(PAGE::"Posted Bill of Lading",Rec)
                                 END ELSE BEGIN
                                   Rec.SETRANGE(Posted,FALSE);
                                   PAGE.RUN(PAGE::"Bill of Lading",Rec);
                                 END;
                               END;
                                }
      { 1240020008;2 ;Action    ;
                      ShortCutKey=F7;
                      CaptionML=ENU=Worksheet;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=OpenWorksheet;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF Posted THEN
                                   ERROR(Text012,"No.")
                                 ELSE
                                   PAGE.RUN(PAGE::"Bill of Lading Worksheet",Rec);
                               END;
                                }
      { 1240020009;2 ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=ENU=Information Lines;
                      RunObject=Page 14000838;
                      RunPageLink=Bill of Lading No.=FIELD(No.);
                      Image=EditList }
      { 1240020010;2 ;Action    ;
                      CaptionML=ENU=Summary;
                      RunObject=Page 14000826;
                      RunPageLink=Bill of Lading No.=FIELD(No.),
                                  Bill of Lading Type=CONST(Standard);
                      Image=BOMVersions;
                      PromotedCategory=New }
      { 1240020011;2 ;Action    ;
                      CaptionML=ENU=Package Summary;
                      RunObject=Page 14000826;
                      RunPageLink=Bill of Lading No.=FIELD(No.),
                                  Bill of Lading Type=CONST(Package Summary);
                      Image=BOMVersions }
      { 1240020012;2 ;Action    ;
                      CaptionML=ENU=ASN Summary;
                      RunObject=Page 14000832;
                      RunPageLink=Bill of Lading No.=FIELD(No.),
                                  Bill of Lading Type=CONST("ASN (EDI)");
                      Promoted=Yes;
                      Image=BOMVersions;
                      PromotedCategory=Process }
      { 1240020013;2 ;Action    ;
                      CaptionML=ENU=Bill of Lading Packages;
                      RunObject=Page 14000714;
                      RunPageView=SORTING(Source Type,Source Subtype,Posted Source ID,Bill of Lading No.);
                      RunPageLink=Source Type=CONST(14000822),
                                  Source ID=FIELD(No.);
                      Image=List }
      { 1240020014;2 ;Action    ;
                      CaptionML=ENU=Shipping Agent Options;
                      Image=CalculateCrossDock;
                      OnAction=BEGIN
                                 Shipping.ShowOptPageShipBillOfLading(Rec);
                               END;
                                }
      { 1240020015;2 ;Action    ;
                      CaptionML=ENU=E-&Mail List;
                      ToolTipML=ENU=Specifies a list of E-Mail Addresses associated to this record.;
                      Image=SendTo;
                      OnAction=VAR
                                 EMailListEntry@1240030001 : Record 14000908;
                               BEGIN
                                 EMailListEntry.RESET;
                                 EMailListEntry.SETRANGE("Table ID",DATABASE::"Bill of Lading");
                                 EMailListEntry.SETRANGE(Code,"No.");
                                 CASE "Ship-to Type" OF
                                   "Ship-to Type"::Customer:
                                     PAGE.RUNMODAL(PAGE::"E-Mail List Entries",EMailListEntry);
                                   "Ship-to Type"::Vendor:
                                     PAGE.RUNMODAL(PAGE::"E-Mail List Entries (Edit)",EMailListEntry);
                                 END;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1240020005;1 ;ActionGroup;
                      CaptionML=ENU=F&unctions }
      { 1240020016;2 ;ActionGroup;
                      CaptionML=ENU=Bill of Lading;
                      Image=Document }
      { 1240020017;3 ;Action    ;
                      ShortCutKey=Ctrl+F9;
                      CaptionML=ENU=Release Bill of Lading;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ReleaseDoc;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 BOLMgt.CloseBillOfLading(Rec,0);

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1240020018;3 ;Action    ;
                      CaptionML=ENU=Re&open Bill of Lading;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ReOpen;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF Posted THEN BEGIN
                                   IF CONFIRM(Text011,FALSE,"No.") THEN
                                     BOLMgt.UnPostBillOfLading(Rec);
                                 END ELSE
                                   BOLMgt.OpenBillOfLading(Rec);

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1240020019;2 ;ActionGroup;
                      CaptionML=ENU=Summary;
                      Image=BOMVersions }
      { 1240020020;3 ;Action    ;
                      CaptionML=ENU=Update Summary;
                      Promoted=Yes;
                      Image=Refresh;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 BOLMgt.UpdateSummaryLines(Rec);

                                 CurrPage.UPDATE;
                               END;
                                }
      { 1240020021;3 ;Action    ;
                      CaptionML=ENU=Release Summary;
                      Promoted=Yes;
                      Image=ReleaseDoc;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF NOT Released THEN
                                   IF NOT CONFIRM(Text001) THEN
                                     EXIT
                                   ELSE
                                     BOLMgt.CloseBillOfLading(Rec,0);

                                 BOLMgt.CloseBOLSummary(Rec);

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1240020022;3 ;Action    ;
                      CaptionML=ENU=Re&open Summary;
                      Promoted=Yes;
                      Image=Reopen;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 BOLMgt.OpenBOLSummary(Rec);

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1240020023;2 ;Action    ;
                      CaptionML=ENU=Send E-&Mail Notification;
                      Image=Email;
                      OnAction=VAR
                                 EMailMgt@1000000001 : Codeunit 14000903;
                               BEGIN
                                 TESTFIELD("E-Mail Notification Handled",FALSE);

                                 EMailMgt.SendBillOfLadingNotification(Rec,TRUE,FALSE);
                               END;
                                }
      { 1240020024;2 ;Action    ;
                      CaptionML=ENU=Create &VICS Bill of Lading No.;
                      Image=New;
                      OnAction=BEGIN
                                 BOLMgt.CreateVicsBOLNo(Rec);

                                 CurrPage.SAVERECORD;
                               END;
                                }
      { 1240020025;2 ;ActionGroup;
                      CaptionML=ENU=EDI;
                      Image=ElectronicDoc }
      { 1240020026;3 ;Action    ;
                      CaptionML=ENU=Send ASN;
                      Promoted=Yes;
                      Image=ElectronicDoc;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 PackingRule@1240020001 : Record 14000715;
                               BEGIN
                                 TESTFIELD("EDI ASN",TRUE);
                                 TESTFIELD("EDI BSN",FALSE);

                                 PackingRule.GetPackingRule("Ship-to Type","Ship-to No.","Ship-to Code");
                                 IF PackingRule."Auto Post when ASN Send" THEN BEGIN
                                   Rec.SETRANGE(Posted);
                                   CurrPage.UPDATE(FALSE);
                                 END;

                                 IF NOT Released THEN
                                   IF NOT CONFIRM(Text001) THEN
                                     EXIT
                                   ELSE
                                     BOLMgt.CloseBillOfLading(Rec,0);

                                 IF NOT "Summary Released" THEN
                                   IF NOT CONFIRM(Text002) THEN
                                     EXIT
                                   ELSE
                                     BOLMgt.CloseBOLSummary(Rec);

                                 IF "EDI ASN Generated" THEN
                                   IF NOT CONFIRM(Text003,FALSE,"EDI ASN Gen. Date") THEN
                                     EXIT;

                                 EDIIntegration.SendASN(Rec);
                               END;
                                }
      { 1240020027;3 ;Action    ;
                      CaptionML=ENU=Send BSN;
                      Promoted=Yes;
                      Image=ElectronicDoc;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 PackingRule@1240020001 : Record 14000715;
                               BEGIN
                                 TESTFIELD("EDI ASN",TRUE);
                                 TESTFIELD("EDI BSN",TRUE);

                                 PackingRule.GetPackingRule("Ship-to Type","Ship-to No.","Ship-to Code");
                                 IF PackingRule."Auto Post when ASN Send" THEN BEGIN
                                   Rec.SETRANGE(Posted);
                                   CurrPage.UPDATE(FALSE);
                                 END;

                                 IF NOT Released THEN
                                   IF NOT CONFIRM(Text001) THEN
                                     EXIT
                                   ELSE
                                     BOLMgt.CloseBillOfLading(Rec,0);

                                 IF NOT "Summary Released" THEN
                                   IF NOT CONFIRM(Text002) THEN
                                     EXIT
                                   ELSE
                                     BOLMgt.CloseBOLSummary(Rec);

                                 IF "EDI BSN Generated" THEN
                                   IF NOT CONFIRM(Text008,FALSE,"EDI BSN Gen. Date") THEN
                                     EXIT;

                                 EDIIntegration.SendBSN(Rec);
                               END;
                                }
      { 1240020028;3 ;Action    ;
                      CaptionML=ENU=Send Electronic BOL;
                      Promoted=Yes;
                      Image=ElectronicDoc;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 PackingRule@1240020001 : Record 14000715;
                               BEGIN
                                 TESTFIELD("EDI Electronic BOL",TRUE);

                                 IF NOT Released THEN
                                   IF NOT CONFIRM(Text001) THEN
                                     EXIT
                                   ELSE
                                     BOLMgt.CloseBillOfLading(Rec,0);

                                 IF "EDI Electronic BOL Generated" THEN
                                   IF NOT CONFIRM(Text010,FALSE,"EDI Electronic BOL Gen. Date") THEN
                                     EXIT;

                                 EDIIntegration.SendElectronicBOL(Rec);
                               END;
                                }
      { 1240020029;3 ;Action    ;
                      CaptionML=ENU=Trace;
                      ToolTipML=ENU=Select to review all records with the same 'EDI Internal Doc No.';
                      Promoted=Yes;
                      Image=Trace;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 EDITrace@1240020000 : Page 14002386;
                               BEGIN
                                 CLEAR(EDITrace);
                                 EDITrace.SetDoc("EDI Internal Doc. No.");
                                 EDITrace.RUNMODAL;
                               END;
                                }
      { 1240020030;2 ;Action    ;
                      ShortCutKey=Ctrl+F11;
                      CaptionML=ENU=Rate Shop;
                      ToolTipML=ENU=Runs the rate shop process.;
                      Promoted=Yes;
                      Image=Calculate;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Shipping.RateShopBillOfLading(Rec);

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1240020031;2 ;Action    ;
                      CaptionML=ENU=Distribute Shipping Charge;
                      Image=Dimensions;
                      OnAction=BEGIN
                                 IF NOT CONFIRM(Text004,FALSE) THEN
                                   EXIT;

                                 BOLMgt.DistributeShippingCharge(Rec);
                               END;
                                }
      { 1240020032;2 ;Action    ;
                      CaptionML=ENU=Remove Shipping Charge;
                      Image=Delete;
                      OnAction=BEGIN
                                 IF NOT CONFIRM(Text005,FALSE) THEN
                                   EXIT;

                                 BOLMgt.RemoveShippingCharge(Rec);
                               END;
                                }
      { 1240020033;2 ;Action    ;
                      CaptionML=ENU=Track Shipment;
                      Image=Track;
                      OnAction=BEGIN
                                 StartTrackingBOL;
                               END;
                                }
      { 1240020034;2 ;Action    ;
                      CaptionML=ENU=Create Misc. Package;
                      Image=New;
                      OnAction=VAR
                                 Package@1240030002 : Record 14000701;
                                 PostedPackage@1240030003 : Record 14000704;
                                 CreateMiscPackage@1240030000 : Codeunit 14000706;
                               BEGIN
                                 TESTFIELD(Posted,FALSE);

                                 CLEAR(CreateMiscPackage);
                                 CreateMiscPackage.CreatePackageHeader(
                                   "No.","Ship-to Type","Ship-to No.","Ship-to Code","Shipping Agent Code","Shipping Agent Service");
                                 CALCFIELDS("Posted Weight","Posted Value");
                                 CreateMiscPackage.UpdateOverrideInfo("Posted Weight","Posted Value",0,0,0);
                                 CreateMiscPackage.ClosePackage(TRUE);
                                 CreateMiscPackage.PostPackage;

                                 CreateMiscPackage.GetPackage(Package);
                                 PostedPackage.GET(Package."No.");
                                 PostedPackage."Source Type" := DATABASE::"Bill of Lading";
                                 PostedPackage."Source Subtype" := 0;
                                 PostedPackage."Source ID" := "No.";
                                 PostedPackage."Posted Source ID" := "No.";
                                 PostedPackage.MODIFY;

                                 IF NOT Released THEN BEGIN
                                   "Shipping Charge" := "Shipping Charge" + PostedPackage."Shipping Charge";
                                   "Shipping Cost" := "Shipping Cost" + PostedPackage."Shipping Cost";
                                   CurrPage.UPDATE;
                                 END;
                               END;
                                }
      { 1240020035;2 ;Action    ;
                      CaptionML=ENU=Close Orders;
                      Image=PostDocument;
                      OnAction=VAR
                                 BillOfLadingLine@1240030002 : Record 14000823;
                                 CloseBillOfLadingLines@1240030000 : Report 14000721;
                               BEGIN
                                 BillOfLadingLine.RESET;
                                 BillOfLadingLine.SETRANGE("Bill of Lading No.","No.");
                                 BillOfLadingLine.SETRANGE(Type,BillOfLadingLine.Type::Order);

                                 CLEAR(CloseBillOfLadingLines);
                                 CloseBillOfLadingLines.SETTABLEVIEW(BillOfLadingLine);
                                 CloseBillOfLadingLines.RUNMODAL;
                               END;
                                }
      { 1240020036;2 ;Action    ;
                      CaptionML=ENU=Post Shipment Invoices;
                      ToolTipML=ENU=When run, a one-to-one relationship is established between the Posted Sales Shipment and the Posted Sales Invoice as this may be driven by an EDI Trade Partner.;
                      Image=PostDocument;
                      OnAction=VAR
                                 SalesShipmentHeader@1240030001 : Record 110;
                                 PostShipmentInvoices@1240030002 : Report 14000720;
                               BEGIN
                                 SalesShipmentHeader.RESET;
                                 SalesShipmentHeader.SETCURRENTKEY("Bill of Lading No.");
                                 SalesShipmentHeader.SETRANGE("Bill of Lading No.","No.");

                                 CLEAR(PostShipmentInvoices);
                                 PostShipmentInvoices.SETTABLEVIEW(SalesShipmentHeader);
                                 PostShipmentInvoices.RUNMODAL;
                               END;
                                }
      { 1240020037;2 ;Action    ;
                      ShortCutKey=F9;
                      CaptionML=ENU=Post Bill of Lading;
                      Promoted=Yes;
                      Image=PostDocument;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 PackingRule@1240020001 : Record 14000715;
                               BEGIN
                                 Rec.SETRANGE(Posted);
                                 CurrPage.UPDATE(FALSE);

                                 IF NOT Released THEN
                                   IF NOT CONFIRM(Text001) THEN
                                     EXIT
                                   ELSE
                                     BOLMgt.CloseBillOfLading(Rec,0);

                                 IF NOT "Summary Released" THEN
                                   IF NOT CONFIRM(Text002) THEN
                                     EXIT
                                   ELSE
                                     BOLMgt.CloseBOLSummary(Rec);

                                 IF "EDI ASN" THEN BEGIN
                                   PackingRule.GetPackingRule("Ship-to Type","Ship-to No.","Ship-to Code");
                                   IF PackingRule."Allow BOL Post Before ASN Send" = FALSE THEN BEGIN
                                     IF "EDI ASN" AND NOT "EDI BSN" AND NOT "EDI ASN Generated" THEN
                                       IF NOT CONFIRM(Text006) THEN
                                         EXIT
                                       ELSE BEGIN
                                         EDIIntegration.SendASN(Rec);
                                         GET("No.");
                                       END;

                                     IF "EDI ASN" AND "EDI BSN" AND NOT "EDI BSN Generated" THEN
                                       IF NOT CONFIRM(Text009) THEN
                                         EXIT
                                       ELSE BEGIN
                                         EDIASNSend.ExportASN(Rec,'E_SLSBSN',FALSE);
                                         GET("No.");
                                       END;
                                   END;
                                 END;

                                 IF NOT Posted THEN BEGIN
                                   TESTFIELD(Released);
                                   TESTFIELD("Summary Released");
                                   IF CONFIRM(Text007,FALSE) THEN
                                     BOLMgt.PostBillOfLading(Rec,FALSE);
                                 END;
                               END;
                                }
      { 1240020038;2 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=ENU=Batch Change Status;
                      RunObject=Report 14000825;
                      Image=Report }
      { 1240020006;1 ;ActionGroup;
                      CaptionML=ENU=&Print }
      { 1240020039;2 ;Action    ;
                      CaptionML=ENU=Bill of Lading;
                      Image=PrintDocument;
                      OnAction=BEGIN
                                 BillOfLading := Rec;
                                 BillOfLading.SETRECFILTER;
                                 REPORT.RUNMODAL(REPORT::"Bill of Lading",TRUE,TRUE,BillOfLading);
                               END;
                                }
      { 1240020040;2 ;Action    ;
                      CaptionML=ENU=Bill of Lading Advanced;
                      Image=PrintDocument;
                      OnAction=BEGIN
                                 BillOfLading := Rec;
                                 BillOfLading.SETRECFILTER;
                                 REPORT.RUNMODAL(REPORT::"Bill of Lading Advanced",TRUE,TRUE,BillOfLading);
                               END;
                                }
      { 1240020041;2 ;Action    ;
                      CaptionML=ENU=Bill of Lading Info. Lines Advanced;
                      Promoted=Yes;
                      Image=PrintDocument;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 BillOfLading := Rec;
                                 BillOfLading.SETRECFILTER;
                                 REPORT.RUNMODAL(REPORT::"Bill of Lading Info. Lines Adv",TRUE,TRUE,BillOfLading);
                               END;
                                }
      { 1240020042;2 ;Action    ;
                      CaptionML=ENU=Bill of Lading Summary;
                      Image=PrintDocument;
                      OnAction=BEGIN
                                 BillOfLading := Rec;
                                 BillOfLading.SETRECFILTER;
                                 REPORT.RUNMODAL(REPORT::"Bill of Lading Summary",TRUE,TRUE,BillOfLading);
                               END;
                                }
      { 1240020043;2 ;Action    ;
                      CaptionML=ENU=Bill of Lading Summary ASN;
                      Image=PrintDocument;
                      OnAction=BEGIN
                                 BillOfLading := Rec;
                                 BillOfLading.SETRECFILTER;
                                 REPORT.RUNMODAL(REPORT::"Bill of Lading Summary ASN",TRUE,TRUE,BillOfLading);
                               END;
                                }
      { 1240020044;2 ;Action    ;
                      CaptionML=ENU=Bill of Lading &Label;
                      Image=PrintDocument;
                      OnAction=BEGIN
                                 PrintLabel;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="No.";
                OnDrillDown=BEGIN
                              IF Posted THEN BEGIN
                                Rec.SETRANGE(Posted);
                                PAGE.RUN(PAGE::"Posted Bill of Lading",Rec)
                              END ELSE BEGIN
                                Rec.SETRANGE(Posted,FALSE);
                                PAGE.RUN(PAGE::"Bill of Lading",Rec);
                              END;
                            END;
                             }

    { 29  ;2   ;Field     ;
                SourceExpr="Shipping Agent Code" }

    { 31  ;2   ;Field     ;
                SourceExpr="Shipping Agent Service" }

    { 27  ;2   ;Field     ;
                SourceExpr="Ship-to Type" }

    { 13  ;2   ;Field     ;
                SourceExpr="Ship-to No." }

    { 15  ;2   ;Field     ;
                SourceExpr="Ship-to Code" }

    { 4   ;2   ;Field     ;
                SourceExpr=Description }

    { 21  ;2   ;Field     ;
                SourceExpr="Packing Date";
                Visible=FALSE }

    { 19  ;2   ;Field     ;
                SourceExpr="External Tracking No.";
                Visible=FALSE }

    { 6   ;2   ;Field     ;
                ToolTipML=ENU=Enabled when the EDI Document is Released.  The EDI Document must be released in order to send outbound EDI files.;
                SourceExpr=Released }

    { 1240020000;2;Field  ;
                SourceExpr="Summary Released" }

    { 1240020002;2;Field  ;
                SourceExpr=Posted }

  }
  CODE
  {
    VAR
      BillOfLading@1240020004 : Record 14000822;
      BOLMgt@1240020003 : Codeunit 14000821;
      EDIIntegration@1240020002 : Codeunit 14000363;
      Shipping@1240020001 : Codeunit 14000701;
      EDIASNSend@1240020000 : Codeunit 14002358;
      Text001@1240020014 : TextConst 'ENU=Bill of Lading is not relased. Release Bill of Lading?';
      Text002@1240020013 : TextConst 'ENU=Bill of Lading summary is not relased. Release Bill of Lading Summary?';
      Text003@1240020012 : TextConst 'ENU=ASN was generated on %1.  Do you want to send it again?';
      Text004@1240020011 : TextConst 'ENU=Do you want to distribute the cost on the Sales Orders?';
      Text005@1240020010 : TextConst 'ENU=Do you want to remove the cost on the Sales Orders?';
      Text006@1240020009 : TextConst 'ENU=EDI ASN has not been send. Send EDI ASN?';
      Text007@1240020008 : TextConst 'ENU=Do you want to post the Bill of Lading?';
      Text008@1240020007 : TextConst 'ENU=BSN was generated on %1.  Do you want to re-create BSN?';
      Text009@1240020006 : TextConst 'ENU=EDI BSN has not been created. Create EDI BSN?';
      Text010@1240020005 : TextConst 'ENU=EDI Electonic BOL was generated on %1. Do you want to re-create Electronic BOL?';
      Text011@1240020015 : TextConst 'ENU=Are you sure you will reopen the Bill of Lading %1?';
      Text012@1240020016 : TextConst 'ENU=Posted must be No on Bill of Lading %1.';

    BEGIN
    END.
  }
}

