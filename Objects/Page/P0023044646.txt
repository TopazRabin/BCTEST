OBJECT Page 23044646 DSHIP License Plate List
{
  OBJECT-PROPERTIES
  {
    Date=11/03/21;
    Time=11:28:15 AM;
    Modified=Yes;
    Version List=DSHIP2.2.2,3237,3187,3299,3333;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Dynamic Ship License Plate List;
               ENC=Dynamic Ship Package List];
    InsertAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table23044505;
    PageType=List;
    CardPageID=IWX License Plate;
    PromotedActionCategoriesML=ENU=New,Process,Report,Related Information,Print;
    OnAfterGetRecord=BEGIN

                       setAllowedActions;
                     END;

    ActionList=ACTIONS
    {
      { 1000000021;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1000000022;1 ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 ESM=F&unciones;
                                 FRC=F&onctions;
                                 ENC=F&unctions];
                      ActionContainerType=NewDocumentItems }
      { 1000000023;2 ;Action    ;
                      Name=miRemoveUsage;
                      CaptionML=[ENU=&Remove Usage;
                                 ESM=&Retire Uso;
                                 FRC=&Enlever utilisation;
                                 ENC=&Remove Usage];
                      Promoted=No;
                      Image=DeleteQtyToHandle;
                      PromotedCategory=Process;
                      OnAction=BEGIN

                                 Rec.removeUsage(TRUE);
                               END;
                                }
      { 1000000048;2 ;Action    ;
                      Name=miOpenPkgWS;
                      CaptionML=ENU=Package Worksheet;
                      Promoted=Yes;
                      Enabled=bPkgWsAllowed;
                      PromotedIsBig=Yes;
                      Image=Inventory;
                      PromotedCategory=Process;
                      OnAction=BEGIN

                                 openPackageWorksheet;
                               END;
                                }
      { 1000000050;2 ;Action    ;
                      Name=miGetReturnLabel;
                      CaptionML=ENU=Get Return Label;
                      Promoted=Yes;
                      Enabled=bHasOrderID;
                      PromotedIsBig=Yes;
                      Image=ReturnShipment;
                      PromotedCategory=Process;
                      OnAction=BEGIN

                                 getReturnLabel;
                               END;
                                }
      { 1000000020;1 ;ActionGroup;
                      CaptionML=[ENU=&Print;
                                 ESM=Impresi¢n;
                                 FRC=Imprimer;
                                 ENC=&Print];
                      ActionContainerType=NewDocumentItems }
      { 1000000024;2 ;Action    ;
                      Name=miPrintLabel;
                      CaptionML=[ENU=&Label;
                                 ESM=Etiqueta;
                                 FRC=&Etiquette;
                                 ENC=&Label];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=DepositSlip;
                      PromotedCategory=Report;
                      OnAction=VAR
                                 lrecLPHeader@1000000000 : Record 23044505;
                               BEGIN

                                 lrecLPHeader.SETFILTER("No.", Rec."No.");
                                 cuLPCommon.printLPLabel(lrecLPHeader);
                               END;
                                }
      { 1000000049;2 ;Action    ;
                      Name=miPrintShippingLabels;
                      CaptionML=ENU=Carrier Label;
                      Promoted=Yes;
                      Enabled=bCarrierLabelAvailable;
                      PromotedIsBig=Yes;
                      Image=PrintCover;
                      PromotedCategory=Report;
                      OnAction=BEGIN

                                 printShippingLabels;
                               END;
                                }
      { 1000000051;2 ;Action    ;
                      Name=miPrintReturnLabel;
                      CaptionML=ENU=Return Label;
                      Enabled=bCarrierLabelAvailable AND bHasOrderID;
                      Image=PrintCheck;
                      PromotedCategory=Report;
                      OnAction=BEGIN

                                 printReturnLabel;
                               END;
                                }
      { 1000000054;2 ;Action    ;
                      Name=miPrintCommercialInvoice;
                      CaptionML=ENU=Commercial Invoice;
                      Image=Invoice;
                      PromotedCategory=Report;
                      OnAction=BEGIN

                                 printCommercialInvoice;
                               END;
                                }
      { 1000000043;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1000000042;1 ;ActionGroup;
                      CaptionML=[ENU=Documents;
                                 ESM=Documentos;
                                 FRC=Documents;
                                 ENC=Documents];
                      Image=Shipment }
      { 1000000041;2 ;Action    ;
                      Name=miShowOrder;
                      CaptionML=ENU=Show Order;
                      Image=ViewOrder;
                      PromotedCategory=Category4;
                      OnAction=BEGIN

                                 cuPackMgmt.showOrderDocumentLP(Rec);
                               END;
                                }
      { 1000000027;2 ;Action    ;
                      Name=miShowPostedDocument;
                      CaptionML=ENU=Show Posted Document;
                      Image=ViewPostedOrder;
                      PromotedCategory=Category4;
                      OnAction=BEGIN

                                 cuPackMgmt.showPostedDocumentLP(Rec);
                               END;
                                }
      { 1000000044;2 ;Action    ;
                      Name=miShowPackageOptions;
                      CaptionML=ENU=Package Options;
                      Image=ItemVariant;
                      PromotedCategory=Category4;
                      OnAction=BEGIN

                                 openPackageOptions;
                               END;
                                }
      { 1000000045;2 ;Action    ;
                      Name=miShowShipmentOptions;
                      CaptionML=ENU=Shipment Options;
                      Image=Shipment;
                      PromotedCategory=Category4;
                      OnAction=BEGIN

                                 openShipmentOptions;
                               END;
                                }
      { 1000000046;2 ;Action    ;
                      Name=miShowCustoms;
                      CaptionML=ENU=Customs;
                      Enabled=bCustomsAvailable;
                      Image=Voucher;
                      PromotedCategory=Category4;
                      OnAction=BEGIN

                                 openCustoms;
                               END;
                                }
      { 1000000047;2 ;Action    ;
                      Name=miShowManifest;
                      CaptionML=ENU=Manifest;
                      Enabled=bManifestAvailable;
                      Image=ItemTrackingLedger;
                      PromotedCategory=Category4;
                      OnAction=BEGIN

                                 openManifest;
                               END;
                                }
      { 1000000052;2 ;Action    ;
                      Name=miShowTracking;
                      CaptionML=ENU=Shipment Tracking URLs;
                      Promoted=Yes;
                      Enabled=bTrackingAvailable;
                      PromotedIsBig=Yes;
                      Image=OrderTracking;
                      PromotedCategory=Category4;
                      OnAction=BEGIN

                                 openTracking;
                               END;
                                }
      { 1000000053;2 ;Action    ;
                      Name=miShowCost;
                      CaptionML=ENU=Freight Information;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ItemCosts;
                      PromotedCategory=Category4;
                      OnAction=BEGIN

                                 openShippingCosts;
                               END;
                                }
      { 1000000057;2 ;Action    ;
                      Name=Pallet Details;
                      Image=DataEntry;
                      OnAction=VAR
                                 PalletDimension@1000000000 : Page 50090;
                                 PalletEntry@1000000001 : Record 50068;
                               BEGIN
                                 //TPZ3299
                                 PalletEntry.SETRANGE("License Plate No.","No.");
                                 IF PalletEntry.FINDFIRST THEN;
                                 PalletDimension.SETTABLEVIEW(PalletEntry);
                                 PalletDimension.SetValues("No.",'','');
                                 PalletDimension.EDITABLE(FALSE);
                                 PalletDimension.RUN;
                               END;
                                }
      { 1000000056;2 ;Action    ;
                      CaptionML=ENU=Create BOL;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NewWarehouseShipment;
                      OnAction=BEGIN
                                 EDIASNSend.ExportASNLP(Rec,'E_SLSASN',TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1000000002;2;Field  ;
                SourceExpr="No." }

    { 1000000003;2;Field  ;
                SourceExpr=Description }

    { 1000000004;2;Field  ;
                SourceExpr=Status }

    { 1000000026;2;Field  ;
                SourceExpr="Location Code" }

    { 1000000025;2;Field  ;
                SourceExpr="Bin Code";
                Visible=FALSE }

    { 1000000005;2;Field  ;
                SourceExpr="Created By";
                Visible=FALSE }

    { 1000000006;2;Field  ;
                SourceExpr="Created On";
                Visible=FALSE }

    { 1000000007;2;Field  ;
                SourceExpr="Last Modified By" }

    { 1000000008;2;Field  ;
                SourceExpr="Last Modified On" }

    { 1000000009;2;Field  ;
                SourceExpr="Item Quantity Count" }

    { 1000000010;2;Field  ;
                SourceExpr="Line Count" }

    { 1000000011;2;Field  ;
                SourceExpr="Unbuild on Receipt";
                Visible=FALSE }

    { 1000000012;2;Field  ;
                SourceExpr="Source Document" }

    { 1000000013;2;Field  ;
                SourceExpr="Source No." }

    { 1000000014;2;Field  ;
                SourceExpr="Source Company Name";
                Visible=FALSE }

    { 1000000015;2;Field  ;
                SourceExpr="Shipped Source Document" }

    { 1000000016;2;Field  ;
                SourceExpr="Shipped Source No." }

    { 1000000017;2;Field  ;
                SourceExpr="Shipped Company Name";
                Visible=FALSE }

    { 1000000018;2;Field  ;
                SourceExpr="Shipment No." }

    { 1000000019;2;Field  ;
                SourceExpr="Package Tracking Number" }

    { 1000000028;2;Field  ;
                SourceExpr="Shipment Tracking URL";
                Visible=FALSE }

    { 1000000029;2;Field  ;
                SourceExpr="Template Code";
                Visible=FALSE }

    { 1000000030;2;Field  ;
                SourceExpr="Has Carrier Label";
                Visible=FALSE }

    { 1000000031;2;Field  ;
                SourceExpr="Shipment Length";
                Visible=FALSE }

    { 1000000032;2;Field  ;
                SourceExpr="Shipment Width";
                Visible=FALSE }

    { 1000000033;2;Field  ;
                SourceExpr="Shipment Height";
                Visible=FALSE }

    { 1000000034;2;Field  ;
                SourceExpr="Shpt. Dim. Unit of Measure";
                Visible=FALSE }

    { 1000000038;2;Field  ;
                SourceExpr="Shipment Gross Weight";
                Visible=FALSE }

    { 1000000039;2;Field  ;
                SourceExpr="Weight Unit of Measure";
                Visible=FALSE }

    { 1000000035;2;Field  ;
                SourceExpr="Package Order ID";
                Visible=FALSE }

    { 1000000037;2;Field  ;
                SourceExpr="Shipment Date";
                Visible=FALSE }

    { 1000000036;2;Field  ;
                SourceExpr="Customs No.";
                Visible=FALSE }

    { 1000000040;2;Field  ;
                SourceExpr="Manifest No.";
                Visible=FALSE }

    { 1000000055;2;Field  ;
                SourceExpr="Order No.";
                Editable=False }

    { 100000057;2;Field   ;
                SourceExpr="BOL No." }

    { 1000000058;2;Field  ;
                SourceExpr="Freight Cost" }

  }
  CODE
  {
    VAR
      cuPackMgmt@1000000002 : Codeunit 23044601;
      cuRateMgmt@1000000007 : Codeunit 23044602;
      cuLPCommon@1000000000 : Codeunit 23044541;
      bPkgWsAllowed@1000000001 : Boolean;
      bTrackingAvailable@1000000003 : Boolean;
      bManifestAvailable@1000000004 : Boolean;
      bCustomsAvailable@1000000005 : Boolean;
      bCarrierLabelAvailable@1000000006 : Boolean;
      tcReturnLabelExists@1000000008 : TextConst 'ENU=A return shipping label already exists for %1. \Would you like to print it instead?';
      bHasOrderID@1000000009 : Boolean;
      EDIASNSend@1000000010 : Codeunit 14002358;

    LOCAL PROCEDURE setAllowedActions@1000000014();
    BEGIN
      //<FUNC>
      //  Description: Enables/disables actions based on LP properties
      //  Called From: This page (P23044646)
      //  Side Effect: None
      //</FUNC>

      bPkgWsAllowed := Rec."Source Document" IN [Rec."Source Document"::"Sales Order",
                                                 Rec."Source Document"::"Outbound Transfer",
                                                 Rec."Source Document"::Shipment];
      bHasOrderID := Rec."Package Order ID" <> '';
      bCustomsAvailable := Rec."Customs No." <> '';
      bManifestAvailable := Rec."Manifest No." <> '';
      bCarrierLabelAvailable := Rec."Has Carrier Label";
      bTrackingAvailable := Rec."Shipment Tracking URL" <> '';
    END;

    LOCAL PROCEDURE openPackageWorksheet@1000000012();
    VAR
      lrecSalesHeader@1000000001 : Record 36;
      lrecTransferHeader@1000000002 : Record 5740;
      lrecWhseShipHeader@1000000003 : Record 7320;
      varSupportedRec@1000000004 : Variant;
      lcuPackMgmt@1000000000 : Codeunit 23044601;
    BEGIN
      //<FUNC>
      //  Description: Opens the package worksheet for the order associated to the LP
      //  Called From: This page (P23044646)
      //  Side Effect: None
      //</FUNC>

      CASE Rec."Source Document" OF
        Rec."Source Document"::"Sales Order":
          BEGIN
            lrecSalesHeader.GET(lrecSalesHeader."Document Type"::Order, Rec."Source No.");
            varSupportedRec := lrecSalesHeader;
          END;
        Rec."Source Document"::"Outbound Transfer":
          BEGIN
            lrecTransferHeader.GET(Rec."Source No.");
            varSupportedRec := lrecTransferHeader;
          END;
        Rec."Source Document"::Shipment:
          BEGIN
            lrecWhseShipHeader.GET(Rec."Source No.");
            varSupportedRec := lrecWhseShipHeader;
          END;
        ELSE
          EXIT;
      END;

      lcuPackMgmt.openPackageWorksheet(varSupportedRec);
    END;

    LOCAL PROCEDURE openPackageOptions@1000000020();
    VAR
      lrecPackageOptions@1000000000 : Record 23044608;
      loptDocType@1000000001 : Option;
    BEGIN
      //<FUNC>
      //  Description: Opens the package options for the LP
      //  Called From: This page (P23044646)
      //  Side Effect: None
      //</FUNC>

      IF ( NOT lrecPackageOptions.GET(Rec."No.") ) THEN BEGIN
        IF ( (Rec."Source No." = '') OR (Rec."Source Document" = Rec."Source Document"::" ") ) THEN
          EXIT;
        cuPackMgmt.getDShipDocTypeFromLPSrcType(Rec."Source Document", loptDocType);
        cuPackMgmt.createPackOptions(Rec."No.", loptDocType, Rec."Source No.");
        lrecPackageOptions.GET(Rec."No.");
      END;

      PAGE.RUN(PAGE::"DSHIP Package Options Card", lrecPackageOptions);
    END;

    LOCAL PROCEDURE openShipmentOptions@1000000021();
    VAR
      lrecShipmentOptions@1000000000 : Record 23044616;
      loptSourceDoc@1000000001 : Option;
      loptDocType@1000000002 : Option;
      lcodDocNo@1000000003 : Code[20];
    BEGIN
      //<FUNC>
      //  Description: Opens the shipment options for the order associated to the LP
      //  Called From: This page (P23044646)
      //  Side Effect: None
      //</FUNC>

      IF ( (Rec."Source No." = '') OR (Rec."Source Document" = Rec."Source Document"::" ") ) THEN BEGIN
        IF ( (Rec."Shipped Source No." = '') OR (Rec."Shipped Source Document" = Rec."Shipped Source Document"::" ") ) THEN
          EXIT;
        loptSourceDoc := Rec."Shipped Source Document";
        lcodDocNo := Rec."Shipped Source No.";
      END ELSE BEGIN
        loptSourceDoc := Rec."Source Document";
        lcodDocNo := Rec."Source No.";
      END;

      cuPackMgmt.getDShipDocTypeFromLPSrcType(loptSourceDoc, loptDocType);

      IF ( NOT lrecShipmentOptions.GET(loptDocType, lcodDocNo) ) THEN BEGIN
        cuPackMgmt.createShipOptions(loptDocType, lcodDocNo);
        lrecShipmentOptions.GET(loptDocType, lcodDocNo);
      END;

      PAGE.RUN(PAGE::"DSHIP Shipment Options Card", lrecShipmentOptions);
    END;

    LOCAL PROCEDURE openCustoms@1000000022();
    VAR
      lrecCustomsHeader@1000000000 : Record 23044605;
    BEGIN
      //<FUNC>
      //  Description: Opens the customs document for the order associated to the LP
      //  Called From: This page (P23044646)
      //  Side Effect: None
      //</FUNC>

      IF ( lrecCustomsHeader.GET(Rec."Customs No.") ) THEN
        PAGE.RUN(PAGE::"DSHIP Shipment Customs Card", lrecCustomsHeader);
    END;

    LOCAL PROCEDURE openManifest@1000000023();
    VAR
      lrecManifestHeader@1000000000 : Record 23044618;
    BEGIN
      //<FUNC>
      //  Description: Opens the manifest for the LP
      //  Called From: This page (P23044646)
      //  Side Effect: None
      //</FUNC>

      IF ( lrecManifestHeader.GET(Rec."Manifest No.") ) THEN
        PAGE.RUN(PAGE::"DSHIP Shipment Manifest Card", lrecManifestHeader);
    END;

    LOCAL PROCEDURE openTracking@1000000008();
    VAR
      lpgTrackingInfo@1000000000 : Page 23044621;
      lbInitSuccess@1000000001 : Boolean;
    BEGIN
      //<FUNC>
      //  Description: Opens the tracking url page for the LP/Order
      //  Called From: This page (P23044646)
      //  Side Effect: None
      //</FUNC>

      IF ( Rec."Source No." <> '' ) THEN
        lbInitSuccess := lpgTrackingInfo.initPage(Rec."Source Document", Rec."Source No.")
      ELSE IF ( Rec."Shipped Source No." <> '' ) THEN
        lbInitSuccess := lpgTrackingInfo.initPage(Rec."Shipped Source Document", Rec."Shipped Source No.");

      IF ( lbInitSuccess ) THEN
        lpgTrackingInfo.RUN;
    END;

    LOCAL PROCEDURE openShippingCosts@1000000009();
    VAR
      lrecLabelData@1000000001 : Record 23044622;
      lrecLPHeader@1000000003 : Record 23044505;
      lpgPackageCosts@1000000002 : Page 23044647;
      lsLPNoFilter@1000000000 : Text;
    BEGIN
      //<FUNC>
      //  Description: Opens the shipping cost page for the LP/Order
      //  Called From: This page (P23044646)
      //  Side Effect: None
      //</FUNC>

      IF (Rec."Source No." <> '') THEN BEGIN
        lsLPNoFilter := cuPackMgmt.CreateLPNoFilterWithLPSourceDoc(Rec."Source Document", Rec."Source No.");
      END ELSE IF (Rec."Shipped Source No." <> '') THEN BEGIN
        lrecLPHeader.SETRANGE("Shipped Source Document", Rec."Shipped Source Document");
        lrecLPHeader.SETRANGE("Shipped Source No.", Rec."Shipped Source No.");
        IF (lrecLPHeader.FINDSET(FALSE)) THEN BEGIN
        REPEAT
          lsLPNoFilter += '|' + lrecLPHeader."No.";
        UNTIL(lrecLPHeader.NEXT=0);
      END;

        IF (lsLPNoFilter = '') THEN
        EXIT;
      lsLPNoFilter := COPYSTR(lsLPNoFilter, 2, STRLEN(lsLPNoFilter));
      END ELSE
        EXIT;

      IF (lsLPNoFilter = '') THEN
        EXIT;

      lrecLabelData.SETFILTER("License Plate No.", lsLPNoFilter);
      lrecLabelData.SETRANGE("Label Format", lrecLabelData."Label Format"::PNG);
      IF ( lrecLabelData.FINDSET(FALSE) ) THEN BEGIN
        PAGE.RUN(PAGE::"DSHIP LP Freight Information", lrecLabelData);
      END;
    END;

    LOCAL PROCEDURE printShippingLabels@1000000024();
    BEGIN
      //<FUNC>
      //  Description: Prints the shipping labels for the LP
      //  Called From: This page (P23044646)
      //  Side Effect: None
      //</FUNC>

      cuRateMgmt.printCarrierLabel(Rec, TRUE);
    END;

    LOCAL PROCEDURE getReturnLabel@1000000026();
    VAR
      lrecLabelData@1000000000 : Record 23044622;
      lpgOrderPack@1000000002 : Page 23044601;
      loptSourceType@1000000003 : Option;
      loptDocType@1000000004 : Option;
      lcodDocNo@1000000005 : Code[20];
      lbGenerateLabel@1000000001 : Boolean;
    BEGIN
      //<FUNC>
      //  Description: Gets a return label label for the LP
      //  Called From: This page (P23044646)
      //  Side Effect: None
      //</FUNC>

      lrecLabelData.SETRANGE("License Plate No.", Rec."No.");
      lrecLabelData.SETRANGE("Label Type", lrecLabelData."Label Type"::Return);
      IF ( lrecLabelData.FINDFIRST ) THEN BEGIN
        IF ( CONFIRM(tcReturnLabelExists, TRUE, Rec."No.") ) THEN BEGIN
          printReturnLabel;
          EXIT;
        END;
      END;

      IF ( Rec."Source No." <> '' ) THEN BEGIN
        loptSourceType := Rec."Source Document";
        lcodDocNo := Rec."Source No.";
      END ELSE IF ( Rec."Shipped Source No." <> '' ) THEN BEGIN
        loptSourceType :=  Rec."Shipped Source Document";
        lcodDocNo := Rec."Shipped Source No.";
      END ELSE
        EXIT;

      cuPackMgmt.getDShipDocTypeFromLPSrcType(loptSourceType, loptDocType);
      lpgOrderPack.getReturnLabel(loptDocType, lcodDocNo);
    END;

    LOCAL PROCEDURE printReturnLabel@1000000028();
    VAR
      lrecLabelData@1000000000 : Record 23044622;
      lrecLPHeader@1000000001 : Record 23044505;
    BEGIN
      //<FUNC>
      //  Description: Prints the return label for the LP
      //  Called From: This page (P23044646)
      //  Side Effect: None
      //</FUNC>

      lrecLabelData.SETRANGE("License Plate No.", Rec."No.");
      lrecLabelData.SETRANGE("Label Type", lrecLabelData."Label Type"::Return);
      IF ( lrecLabelData.FINDFIRST ) THEN BEGIN
        lrecLPHeader.SETRANGE("No.", Rec."No.");
        cuRateMgmt.printReturnLabel(lrecLPHeader, TRUE);
      END;
    END;

    LOCAL PROCEDURE printCommercialInvoice@1000000000();
    VAR
      loptDocType@1000000000 : Option;
      loptSourceDoc@1000000001 : Option;
      lcodSourceNo@1000000002 : Code[20];
    BEGIN
      //<FUNC>
      //  Description: Prints the commercial invoice
      //  Called From: This page (P23044646)
      //  Side Effect: None
      //</FUNC>

      IF ( Rec."Shipped Source No." <> '' ) THEN BEGIN
        Rec.SETRANGE("Shipped Source Document", Rec."Shipped Source Document");
        Rec.SETRANGE("Shipped Source No.", Rec."Shipped Source No.");
      END ELSE IF ( Rec."Source No." <> '' ) THEN BEGIN
        Rec.CALCFIELDS("Source No.", "Source Document");
        Rec.SETRANGE("Source Document", Rec."Source Document");
        Rec.SETRANGE("Source No.", Rec."Source No.");
      END;

      cuPackMgmt.printCommercialInvoiceByLP(Rec, TRUE);
    END;

    BEGIN
    {
      ************************
      Copyright Notice
      This objects content is copyright of Insight Works 2011.  All rights reserved.
      Any redistribution or reproduction of part or all of the contents in any form is prohibited.
      ************************
      001 TPZ3237 VAH 07162021 Added New Field Order No.
      001 TPZ3187 VAH 10052021 Added new field "BOL No."
      002 TPZ3333 PKS 09072021 Added freight cost field
    }
    END.
  }
}

