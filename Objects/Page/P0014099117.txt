OBJECT Page 14099117 Create OWR Replenishment
{
  OBJECT-PROPERTIES
  {
    Date=08/21/18;
    Time=12:00:00 PM;
    Version List=RF1.50.13;
  }
  PROPERTIES
  {
    SaveValues=Yes;
    LinksAllowed=No;
    PageType=Card;
    ShowFilter=No;
    ActionList=ACTIONS
    {
      { 1240020014;  ;ActionContainer;
                      Name=Replenishment;
                      ActionContainerType=ActionItems }
      { 1240020013;1 ;Action    ;
                      Name=Create OWR Replenishment;
                      CaptionML=ENU=Create OWR Replenishment;
                      ToolTipML=ENU=Generates and OWR Replenishment based on the applied filters.;
                      ApplicationArea=#All;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=InwardEntry;
                      PromotedCategory=Process;
                      PromotedOnly=Yes;
                      OnAction=BEGIN
                                 IF LocationCode <> '' THEN
                                   CreateReplenishment
                                 ELSE
                                   ERROR(Text001);
                               END;
                                }
      { 1240020015;1 ;Action    ;
                      Name=Clear Filters;
                      CaptionML=ENU=Clear Filters;
                      ToolTipML=ENU=Clears all applied filters.;
                      ApplicationArea=#All;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ClearFilter;
                      PromotedCategory=Process;
                      PromotedOnly=Yes;
                      OnAction=BEGIN
                                 ClearFilters;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1240020002;0;Container;
                ContainerType=ContentArea }

    { 1240020001;1;Group  ;
                Name=General;
                CaptionML=ENU=General;
                GroupType=Group }

    { 1240020000;2;Field  ;
                Name=Location Code;
                CaptionML=ENU=Location Code;
                ToolTipML=ENU=Specifies the Location Code to be replenished.;
                ApplicationArea=#All;
                SourceExpr=LocationCode;
                TableRelation=Location }

    { 1240020003;2;Field  ;
                Name=To Zone Filter;
                CaptionML=ENU=To Zone Filter;
                ToolTipML=ENU=Specifies the Zones that are to be replenished.;
                ApplicationArea=#All;
                SourceExpr=ToZoneFilter;
                OnLookup=VAR
                           Zone@1240020000 : Record 7300;
                         BEGIN
                           Zone.SETRANGE("Location Code",LocationCode);
                           IF PAGE.RUNMODAL(PAGE::"Zone List",Zone) = ACTION::LookupOK THEN BEGIN
                             IF ToZoneFilter = '' THEN
                               ToZoneFilter := Zone.Code
                             ELSE
                               ToZoneFilter := ToZoneFilter + '|' + Zone.Code;
                           END;
                         END;
                          }

    { 1240020004;2;Field  ;
                Name=To Bin Filter;
                CaptionML=ENU=To Bin Filter;
                ToolTipML=ENU=Specifies the Bins that are to be replenished.;
                ApplicationArea=#All;
                SourceExpr=ToBinFilter;
                OnLookup=VAR
                           Bin@1240020000 : Record 7354;
                         BEGIN
                           Bin.SETRANGE("Location Code",LocationCode);
                           IF ToZoneFilter <> '' THEN
                             Bin.SETFILTER("Zone Code",ToZoneFilter);
                           IF PAGE.RUNMODAL(PAGE::"Bin List",Bin) = ACTION::LookupOK THEN BEGIN
                             IF ToBinFilter = '' THEN
                               ToBinFilter := Bin.Code
                             ELSE
                               ToBinFilter := ToBinFilter + '|' + Bin.Code;
                           END;
                         END;
                          }

    { 1240020005;2;Field  ;
                Name=To Item Filter;
                CaptionML=ENU=To Item Filter;
                ToolTipML=ENU=Specifies the Items that are to be replenished.;
                ApplicationArea=#All;
                SourceExpr=ToItemFilter;
                OnLookup=VAR
                           Item@1240020000 : Record 27;
                         BEGIN
                           IF PAGE.RUNMODAL(PAGE::"Item List",Item) = ACTION::LookupOK THEN BEGIN
                             IF ToItemFilter = '' THEN
                               ToItemFilter := Item."No."
                             ELSE
                               ToItemFilter := ToItemFilter + '|' + Item."No.";
                           END;
                         END;
                          }

    { 1240020006;2;Field  ;
                Name=To Variant Filter;
                CaptionML=ENU=To Variant Filter;
                ToolTipML=ENU=Specifies the Variants that are to be replenished.;
                ApplicationArea=#All;
                SourceExpr=ToVariantFilter;
                OnLookup=VAR
                           ItemVariant@1240020000 : Record 5401;
                         BEGIN
                           IF ToItemFilter <> '' THEN
                             ItemVariant.SETFILTER("Item No.",ToItemFilter);
                           IF PAGE.RUNMODAL(PAGE::"Item Variants",ItemVariant) = ACTION::LookupOK THEN BEGIN
                             IF ToVariantFilter = '' THEN
                               ToVariantFilter := ItemVariant.Code
                             ELSE
                               ToVariantFilter := ToVariantFilter + '|' + ItemVariant.Code;
                           END;
                         END;
                          }

    { 1240020007;2;Field  ;
                Name=To Unit of Measure Filter;
                CaptionML=ENU=To Unit of Measure Filter;
                ToolTipML=ENU=Specifies the Units of Measure that are to be replenished.;
                ApplicationArea=#All;
                SourceExpr=ToUOMFilter;
                OnLookup=VAR
                           UnitOfMeasure@1240020000 : Record 204;
                         BEGIN
                           IF PAGE.RUNMODAL(PAGE::"Units of Measure",UnitOfMeasure) = ACTION::LookupOK THEN BEGIN
                             IF ToUOMFilter = '' THEN
                               ToUOMFilter := UnitOfMeasure.Code
                             ELSE
                               ToUOMFilter := ToUOMFilter + '|' + UnitOfMeasure.Code;
                           END;
                         END;
                          }

    { 1240020008;2;Field  ;
                Name=Top Off Replenishment;
                CaptionML=ENU=Top Off Replenishment;
                ToolTipML=ENU=Specifies that the replenishment will be a Top Off Replenishment. If this is disabled, then the replenishment will be a Min-Max Replenishment;
                ApplicationArea=#All;
                SourceExpr=TopOffReplenishment }

    { 1240020009;2;Field  ;
                Name=Auto Create Movement;
                CaptionML=ENU=Auto Create Movement;
                ToolTipML=ENU=Set this to automatically create either the Warehouse Movement or Inventory Movement.;
                ApplicationArea=#All;
                SourceExpr=AutoCreateMovement }

    { 1240020010;1;Group  ;
                Name=Whse. Worksheet;
                CaptionML=ENU=Whse. Worksheet;
                GroupType=Group }

    { 1240020011;2;Field  ;
                Name=Whse. Worksheet Template;
                CaptionML=ENU=Whse. Worksheet Template;
                ToolTipML=ENU=This represents the Warehouse Worksheet Template that will be used for the replenishment activity. This option only applies to a Location where Directed Put-away and Pick is enabled.;
                ApplicationArea=#All;
                SourceExpr=WhseWkshTemplate;
                TableRelation="Whse. Worksheet Template" }

    { 1240020012;2;Field  ;
                Name=Whse. Worksheet Name;
                CaptionML=ENU=Whse. Worksheet Name;
                ToolTipML=ENU=This represents the Warehouse Worksheet Name that will be used for the replenishment activity. This option only applies to a Location where Directed Put-away and Pick is enabled.;
                ApplicationArea=#All;
                SourceExpr=WhseWkshName;
                OnLookup=VAR
                           WhseWorksheetName@1240020000 : Record 7327;
                         BEGIN
                           WhseWorksheetName.SETRANGE("Worksheet Template Name",WhseWkshTemplate);
                           WhseWorksheetName.SETRANGE("Location Code",LocationCode);
                           IF PAGE.RUNMODAL(PAGE::"Whse. Worksheet Names",WhseWorksheetName) = ACTION::LookupOK THEN
                             WhseWkshName := WhseWorksheetName.Name;
                         END;
                          }

  }
  CODE
  {
    VAR
      LocationCode@1240020000 : Code[10];
      WhseWkshTemplate@1240020001 : Code[10];
      WhseWkshName@1240020002 : Code[10];
      ToZoneFilter@1240020003 : Code[250];
      ToBinFilter@1240020004 : Code[250];
      ToItemFilter@1240020005 : Code[250];
      ToVariantFilter@1240020006 : Code[250];
      ToUOMFilter@1240020007 : Code[250];
      TopOffReplenishment@1240020008 : Boolean;
      AutoCreateMovement@1240020009 : Boolean;
      Text001@1240020010 : TextConst 'ENU=Location Code must be defined.';

    LOCAL PROCEDURE CreateReplenishment@1240020000();
    VAR
      OWRReplenMgt@1240020000 : Codeunit 14002865;
    BEGIN
      OWRReplenMgt.CreateReplenUsingFilters(
        LocationCode,WhseWkshTemplate,WhseWkshName,'',ToZoneFilter,ToBinFilter,
        ToItemFilter,ToVariantFilter,ToUOMFilter,0,0,TopOffReplenishment,AutoCreateMovement);

      CurrPage.CLOSE;
    END;

    LOCAL PROCEDURE ClearFilters@1240020012();
    BEGIN
      CLEAR(LocationCode);
      CLEAR(ToZoneFilter);
      CLEAR(ToBinFilter);
      CLEAR(ToItemFilter);
      CLEAR(ToVariantFilter);
      CLEAR(ToUOMFilter);
      CLEAR(WhseWkshTemplate);
      CLEAR(WhseWkshName);
      TopOffReplenishment := FALSE;
      AutoCreateMovement := FALSE;
    END;

    BEGIN
    END.
  }
}

