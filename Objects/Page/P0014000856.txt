OBJECT Page 14000856 Open Bill of Ladings
{
  OBJECT-PROPERTIES
  {
    Date=01/28/21;
    Time=[ 3:52:47 PM];
    Modified=Yes;
    Version List=SE0.61;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=ENU=Open Bill of Ladings;
    SourceTable=Table14000822;
    SourceTableView=WHERE(Posted=CONST(No));
    PageType=List;
    CardPageID=Bill of Lading;
    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 11      ;1   ;ActionGroup;
                      CaptionML=ENU=&Bill of Lading }
      { 1024    ;2   ;Action    ;
                      ShortCutKey=F7;
                      CaptionML=ENU=Worksheet;
                      RunObject=Page 14000827;
                      RunPageLink=No.=FIELD(No.);
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=OpenWorksheet;
                      PromotedCategory=Process }
      { 1137    ;2   ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=ENU=Information Lines;
                      RunObject=Page 14000838;
                      RunPageLink=Bill of Lading No.=FIELD(No.) }
      { 1028    ;2   ;Action    ;
                      CaptionML=ENU=Summary;
                      RunObject=Page 14000826;
                      RunPageLink=Bill of Lading No.=FIELD(No.),
                                  Bill of Lading Type=CONST(Standard);
                      Promoted=Yes;
                      Image=BOMVersions;
                      PromotedCategory=Process }
      { 1240031004;2 ;Action    ;
                      CaptionML=ENU=Package Summary;
                      RunObject=Page 14000826;
                      RunPageLink=Bill of Lading No.=FIELD(No.),
                                  Bill of Lading Type=CONST(Package Summary) }
      { 1021    ;2   ;Action    ;
                      CaptionML=ENU=ASN Summary;
                      RunObject=Page 14000832;
                      RunPageLink=Bill of Lading No.=FIELD(No.),
                                  Bill of Lading Type=CONST("ASN (EDI)") }
      { 1240031002;2 ;Action    ;
                      CaptionML=ENU=Bill of Lading Packages;
                      RunObject=Page 14000714;
                      RunPageView=SORTING(Source Type,Source Subtype,Posted Source ID,Bill of Lading No.);
                      RunPageLink=Source Type=CONST(14000822),
                                  Source ID=FIELD(No.) }
      { 1119    ;2   ;Action    ;
                      CaptionML=ENU=Shipping Agent Options;
                      OnAction=BEGIN
                                 Shipping.ShowOptPageShipBillOfLading(Rec);
                               END;
                                }
      { 1143    ;2   ;Action    ;
                      CaptionML=ENU=E-&Mail List;
                      ToolTipML=ENU=Specifies a list of E-Mail Addresses associated to this record.;
                      OnAction=VAR
                                 EMailListEntry@1240030001 : Record 14000908;
                               BEGIN
                                 EMailListEntry.RESET;
                                 EMailListEntry.SETRANGE("Table ID",DATABASE::"Bill of Lading");
                                 EMailListEntry.SETRANGE(Code,"No.");
                                 CASE "Ship-to Type" OF
                                   "Ship-to Type"::Customer:
                                     PAGE.RUNMODAL(PAGE::"E-Mail List Entries",EMailListEntry);
                                   "Ship-to Type"::Vendor:
                                     PAGE.RUNMODAL(PAGE::"E-Mail List Entries (Edit)",EMailListEntry);
                                 END;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1031    ;1   ;ActionGroup;
                      CaptionML=ENU=F&unctions }
      { 1089    ;2   ;ActionGroup;
                      CaptionML=ENU=Bill of Lading }
      { 33      ;3   ;Action    ;
                      ShortCutKey=Ctrl+F9;
                      CaptionML=ENU=Release Bill of Lading;
                      Promoted=Yes;
                      Image=ReleaseDoc;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 BOLMgt.CloseBillOfLading(Rec,0);

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1034    ;3   ;Action    ;
                      CaptionML=ENU=Re&open Bill of Lading;
                      OnAction=BEGIN
                                 BOLMgt.OpenBillOfLading(Rec);

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1088    ;2   ;ActionGroup;
                      CaptionML=ENU=Summary }
      { 1026    ;3   ;Action    ;
                      CaptionML=ENU=Update Summary;
                      OnAction=BEGIN
                                 BOLMgt.UpdateSummaryLines(Rec);

                                 CurrPage.UPDATE;
                               END;
                                }
      { 1086    ;3   ;Action    ;
                      CaptionML=ENU=Release Summary;
                      OnAction=BEGIN
                                 IF NOT Released THEN
                                   IF NOT CONFIRM(Text001) THEN
                                     EXIT
                                   ELSE
                                     BOLMgt.CloseBillOfLading(Rec,0);

                                 BOLMgt.CloseBOLSummary(Rec);

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1099    ;3   ;Action    ;
                      CaptionML=ENU=Re&open Summary;
                      OnAction=BEGIN
                                 BOLMgt.OpenBOLSummary(Rec);

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1102    ;2   ;Action    ;
                      CaptionML=ENU=Send E-&Mail Notification;
                      OnAction=VAR
                                 EMailMgt@1000000001 : Codeunit 14000903;
                               BEGIN
                                 TESTFIELD("E-Mail Notification Handled",FALSE);

                                 EMailMgt.SendBillOfLadingNotification(Rec,TRUE,FALSE);
                               END;
                                }
      { 1144    ;2   ;Action    ;
                      CaptionML=ENU=Create &VICS Bill of Lading No.;
                      OnAction=BEGIN
                                 BOLMgt.CreateVicsBOLNo(Rec);

                                 CurrPage.SAVERECORD;
                               END;
                                }
      { 1100    ;2   ;ActionGroup;
                      CaptionML=ENU=EDI }
      { 1098    ;3   ;Action    ;
                      CaptionML=ENU=Send ASN;
                      OnAction=VAR
                                 PackingRule@1240020001 : Record 14000715;
                               BEGIN
                                 TESTFIELD("EDI ASN",TRUE);
                                 TESTFIELD("EDI BSN",FALSE);

                                 PackingRule.GetPackingRule("Ship-to Type","Ship-to No.","Ship-to Code");
                                 IF PackingRule."Auto Post when ASN Send" THEN BEGIN
                                   Rec.SETRANGE(Posted);
                                   CurrPage.UPDATE(FALSE);
                                 END;

                                 IF NOT Released THEN
                                   IF NOT CONFIRM(Text001) THEN
                                     EXIT
                                   ELSE
                                     BOLMgt.CloseBillOfLading(Rec,0);

                                 IF NOT "Summary Released" THEN
                                   IF NOT CONFIRM(Text002) THEN
                                     EXIT
                                   ELSE
                                     BOLMgt.CloseBOLSummary(Rec);

                                 IF "EDI ASN Generated" THEN
                                   IF NOT CONFIRM(Text003,FALSE,"EDI ASN Gen. Date") THEN
                                     EXIT;

                                 EDIIntegration.SendASN(Rec);
                               END;
                                }
      { 99      ;3   ;Action    ;
                      CaptionML=ENU=Send BSN;
                      OnAction=VAR
                                 PackingRule@1240020001 : Record 14000715;
                               BEGIN
                                 TESTFIELD("EDI ASN",TRUE);
                                 TESTFIELD("EDI BSN",TRUE);

                                 PackingRule.GetPackingRule("Ship-to Type","Ship-to No.","Ship-to Code");
                                 IF PackingRule."Auto Post when ASN Send" THEN BEGIN
                                   Rec.SETRANGE(Posted);
                                   CurrPage.UPDATE(FALSE);
                                 END;

                                 IF NOT Released THEN
                                   IF NOT CONFIRM(Text001) THEN
                                     EXIT
                                   ELSE
                                     BOLMgt.CloseBillOfLading(Rec,0);

                                 IF NOT "Summary Released" THEN
                                   IF NOT CONFIRM(Text002) THEN
                                     EXIT
                                   ELSE
                                     BOLMgt.CloseBOLSummary(Rec);

                                 IF "EDI BSN Generated" THEN
                                   IF NOT CONFIRM(Text008,FALSE,"EDI BSN Gen. Date") THEN
                                     EXIT;

                                 EDIIntegration.SendBSN(Rec);
                               END;
                                }
      { 1101    ;3   ;Action    ;
                      CaptionML=ENU=Send Electronic BOL;
                      OnAction=VAR
                                 PackingRule@1240020001 : Record 14000715;
                               BEGIN
                                 TESTFIELD("EDI Electronic BOL",TRUE);

                                 IF NOT Released THEN
                                   IF NOT CONFIRM(Text001) THEN
                                     EXIT
                                   ELSE
                                     BOLMgt.CloseBillOfLading(Rec,0);

                                 IF "EDI Electronic BOL Generated" THEN
                                   IF NOT CONFIRM(Text010,FALSE,"EDI Electronic BOL Gen. Date") THEN
                                     EXIT;

                                 EDIIntegration.SendElectronicBOL(Rec);
                               END;
                                }
      { 14000351;3   ;Action    ;
                      CaptionML=ENU=Trace;
                      ToolTipML=ENU=Select to review all records with the same 'EDI Internal Doc No.';
                      Promoted=Yes;
                      Image=Trace;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 EDITrace@14000351 : Page 14002386;
                               BEGIN
                                 CLEAR(EDITrace);
                                 EDITrace.SetDoc("EDI Internal Doc. No.");
                                 EDITrace.RUNMODAL;
                               END;
                                }
      { 1103    ;2   ;Action    ;
                      ShortCutKey=Ctrl+F11;
                      CaptionML=ENU=Rate Shop;
                      Promoted=Yes;
                      Image=Calculate;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Shipping.RateShopBillOfLading(Rec);

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1032    ;2   ;Action    ;
                      CaptionML=ENU=Distribute Shipping Charge;
                      OnAction=BEGIN
                                 IF NOT CONFIRM(Text004,FALSE) THEN
                                   EXIT;

                                 BOLMgt.DistributeShippingCharge(Rec);
                               END;
                                }
      { 1136    ;2   ;Action    ;
                      CaptionML=ENU=Remove Shipping Charge;
                      OnAction=BEGIN
                                 IF NOT CONFIRM(Text005,FALSE) THEN
                                   EXIT;

                                 BOLMgt.RemoveShippingCharge(Rec);
                               END;
                                }
      { 1240031005;2 ;Action    ;
                      CaptionML=ENU=Track Shipment;
                      OnAction=BEGIN
                                 StartTrackingBOL;
                               END;
                                }
      { 1240031001;2 ;Action    ;
                      CaptionML=ENU=Create Misc. Package;
                      OnAction=VAR
                                 Package@1240030002 : Record 14000701;
                                 PostedPackage@1240030003 : Record 14000704;
                                 CreateMiscPackage@1240030000 : Codeunit 14000706;
                               BEGIN
                                 CLEAR(CreateMiscPackage);
                                 CreateMiscPackage.CreatePackageHeader(
                                   "No.","Ship-to Type","Ship-to No.","Ship-to Code","Shipping Agent Code","Shipping Agent Service");
                                 CALCFIELDS("Posted Weight","Posted Value");
                                 CreateMiscPackage.UpdateOverrideInfo("Posted Weight","Posted Value",0,0,0);
                                 CreateMiscPackage.ClosePackage(TRUE);
                                 CreateMiscPackage.PostPackage;

                                 CreateMiscPackage.GetPackage(Package);
                                 PostedPackage.GET(Package."No.");
                                 PostedPackage."Source Type" := DATABASE::"Bill of Lading";
                                 PostedPackage."Source Subtype" := 0;
                                 PostedPackage."Source ID" := "No.";
                                 PostedPackage."Posted Source ID" := "No.";
                                 PostedPackage.MODIFY;

                                 IF NOT Released THEN BEGIN
                                   "Shipping Charge" := "Shipping Charge" + PostedPackage."Shipping Charge";
                                   "Shipping Cost" := "Shipping Cost" + PostedPackage."Shipping Cost";
                                   CurrPage.UPDATE;
                                 END;
                               END;
                                }
      { 1240031000;2 ;Action    ;
                      CaptionML=ENU=Close Orders;
                      OnAction=VAR
                                 BillOfLadingLine@1240030002 : Record 14000823;
                                 CloseBillOfLadingLines@1240030000 : Report 14000721;
                               BEGIN
                                 BillOfLadingLine.RESET;
                                 BillOfLadingLine.SETRANGE("Bill of Lading No.","No.");
                                 BillOfLadingLine.SETRANGE(Type,BillOfLadingLine.Type::Order);

                                 CLEAR(CloseBillOfLadingLines);
                                 CloseBillOfLadingLines.SETTABLEVIEW(BillOfLadingLine);
                                 CloseBillOfLadingLines.RUNMODAL;
                               END;
                                }
      { 1240020000;2 ;Action    ;
                      Name=Combine Shipments;
                      CaptionML=ENU=Combine Shipments;
                      OnAction=VAR
                                 PackingRule@1240020000 : Record 14000715;
                               BEGIN
                                 TESTFIELD(Released,TRUE);
                                 PackingRule.GetPackingRule("Ship-to Type","Ship-to No.","Ship-to Code");
                                 PackingRule.TESTFIELD("Shipment Invoicing Required",FALSE);

                                 CombineShipments;
                               END;
                                }
      { 1145    ;2   ;Action    ;
                      CaptionML=ENU=Post Shipment Invoices;
                      ToolTipML=ENU=When run, a one-to-one relationship is established between the Posted Sales Shipment and the Posted Sales Invoice as this may be driven by an EDI Trade Partner.;
                      OnAction=VAR
                                 SalesShipmentHeader@1240030001 : Record 110;
                                 PostShipmentInvoices@1240030002 : Report 14000720;
                               BEGIN
                                 SalesShipmentHeader.RESET;
                                 SalesShipmentHeader.SETCURRENTKEY("Bill of Lading No.");
                                 SalesShipmentHeader.SETRANGE("Bill of Lading No.","No.");

                                 CLEAR(PostShipmentInvoices);
                                 PostShipmentInvoices.SETTABLEVIEW(SalesShipmentHeader);
                                 PostShipmentInvoices.RUNMODAL;
                               END;
                                }
      { 1117    ;2   ;Action    ;
                      ShortCutKey=F9;
                      CaptionML=ENU=Post Bill of Lading;
                      Promoted=Yes;
                      Image=PostDocument;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 PackingRule@1240020001 : Record 14000715;
                               BEGIN
                                 Rec.SETRANGE(Posted);
                                 CurrPage.UPDATE(FALSE);

                                 IF NOT Released THEN
                                   IF NOT CONFIRM(Text001) THEN
                                     EXIT
                                   ELSE
                                     BOLMgt.CloseBillOfLading(Rec,0);

                                 IF NOT "Summary Released" THEN
                                   IF NOT CONFIRM(Text002) THEN
                                     EXIT
                                   ELSE
                                     BOLMgt.CloseBOLSummary(Rec);

                                 IF "EDI ASN" THEN BEGIN
                                   PackingRule.GetPackingRule("Ship-to Type","Ship-to No.","Ship-to Code");
                                   IF PackingRule."Allow BOL Post Before ASN Send" = FALSE THEN BEGIN
                                     IF "EDI ASN" AND NOT "EDI BSN" AND NOT "EDI ASN Generated" THEN
                                       IF NOT CONFIRM(Text006) THEN
                                         EXIT
                                       ELSE BEGIN
                                         EDIIntegration.SendASN(Rec);
                                         GET("No.");
                                       END;

                                     IF "EDI ASN" AND "EDI BSN" AND NOT "EDI BSN Generated" THEN
                                       IF NOT CONFIRM(Text009) THEN
                                         EXIT
                                       ELSE BEGIN
                                         EDIASNSend.ExportASN(Rec,'E_SLSBSN',FALSE);
                                         GET("No.");
                                       END;
                                   END;
                                 END;

                                 IF NOT Posted THEN BEGIN
                                   TESTFIELD(Released);
                                   TESTFIELD("Summary Released");
                                   IF CONFIRM(Text007,FALSE) THEN
                                     BOLMgt.PostBillOfLading(Rec,FALSE);
                                 END;
                               END;
                                }
      { 1118    ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=ENU=Batch Change Status;
                      RunObject=Report 14000825 }
      { 1018    ;1   ;ActionGroup;
                      CaptionML=ENU=&Print }
      { 1090    ;2   ;Action    ;
                      CaptionML=ENU=Bill of Lading;
                      OnAction=BEGIN
                                 BillOfLading := Rec;
                                 BillOfLading.SETRECFILTER;
                                 REPORT.RUNMODAL(REPORT::"Bill of Lading",TRUE,TRUE,BillOfLading);
                               END;
                                }
      { 1112    ;2   ;Action    ;
                      CaptionML=ENU=Bill of Lading Advanced;
                      Visible=false;
                      OnAction=BEGIN
                                 BillOfLading := Rec;
                                 BillOfLading.SETRECFILTER;
                                 REPORT.RUNMODAL(REPORT::"Bill of Lading Advanced",TRUE,TRUE,BillOfLading);
                               END;
                                }
      { 1140    ;2   ;Action    ;
                      CaptionML=ENU=Bill of Lading Info. Lines Advanced;
                      Promoted=Yes;
                      Image=PrintDocument;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 PrintFedEx@1240020000 : Boolean;
                               BEGIN
                                 BillOfLading := Rec;
                                 BillOfLading.SETRECFILTER;
                                 PrintFedEx := FALSE;
                                 IF (STRPOS(BillOfLading."Service Indicator",'FEF') <> 0) THEN
                                   PrintFedEx := CONFIRM('Do you wish to create the FedEx Freight BOL?',TRUE);
                                 IF PrintFedEx THEN
                                   BOLMgt.CreateFedExBOL(BillOfLading,'INFO_LINES')
                                 ELSE
                                 REPORT.RUNMODAL(REPORT::"Bill of Lading Info. Lines Adv",TRUE,TRUE,BillOfLading);
                               END;
                                }
      { 1110    ;2   ;Action    ;
                      CaptionML=ENU=Bill of Lading Summary;
                      OnAction=BEGIN
                                 BillOfLading := Rec;
                                 BillOfLading.SETRECFILTER;
                                 REPORT.RUNMODAL(REPORT::"Bill of Lading Summary",TRUE,TRUE,BillOfLading);
                               END;
                                }
      { 1111    ;2   ;Action    ;
                      CaptionML=ENU=Bill of Lading Summary ASN;
                      OnAction=BEGIN
                                 BillOfLading := Rec;
                                 BillOfLading.SETRECFILTER;
                                 REPORT.RUNMODAL(REPORT::"Bill of Lading Summary ASN",TRUE,TRUE,BillOfLading);
                               END;
                                }
      { 1240031003;2 ;Action    ;
                      CaptionML=ENU=Bill of Lading &Label;
                      OnAction=BEGIN
                                 PrintLabel;
                               END;
                                }
      { 1170000000;2 ;Action    ;
                      CaptionML=ENU=Test;
                      OnAction=BEGIN

                                 BillOfLading := Rec;
                                 BillOfLading.SETRECFILTER;
                                 REPORT.RUNMODAL(51149,TRUE,TRUE,BillOfLading);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="No." }

    { 29  ;2   ;Field     ;
                SourceExpr="Shipping Agent Code" }

    { 31  ;2   ;Field     ;
                SourceExpr="Shipping Agent Service" }

    { 27  ;2   ;Field     ;
                SourceExpr="Ship-to Type" }

    { 13  ;2   ;Field     ;
                SourceExpr="Ship-to No." }

    { 15  ;2   ;Field     ;
                SourceExpr="Ship-to Code" }

    { 4   ;2   ;Field     ;
                SourceExpr=Description }

    { 21  ;2   ;Field     ;
                SourceExpr="Packing Date";
                Visible=FALSE }

    { 19  ;2   ;Field     ;
                SourceExpr="External Tracking No.";
                Visible=FALSE }

    { 6   ;2   ;Field     ;
                SourceExpr=Released }

  }
  CODE
  {
    VAR
      Text001@1240030013 : TextConst 'ENU=Bill of Lading is not relased. Release Bill of Lading?';
      Text002@1240030012 : TextConst 'ENU=Bill of Lading summary is not relased. Release Bill of Lading Summary?';
      Text003@1240030011 : TextConst 'ENU=ASN was generated on %1.  Do you want to send it again?';
      Text004@1240030010 : TextConst 'ENU=Do you want to distribute the cost on the Sales Orders?';
      Text005@1240030009 : TextConst 'ENU=Do you want to remove the cost on the Sales Orders?';
      Text006@1240030008 : TextConst 'ENU=EDI ASN has not been send. Send EDI ASN?';
      Text007@1240030007 : TextConst 'ENU=Do you want to post the Bill of Lading?';
      BillOfLading@1240030006 : Record 14000822;
      BOLMgt@1240030004 : Codeunit 14000821;
      EDIIntegration@1240030003 : Codeunit 14000363;
      Shipping@1240030002 : Codeunit 14000701;
      Text008@1240030001 : TextConst 'ENU=BSN was generated on %1.  Do you want to re-create BSN?';
      Text009@1240030000 : TextConst 'ENU=EDI BSN has not been created. Create EDI BSN?';
      EDIASNSend@1240030005 : Codeunit 14002358;
      Text010@1240020000 : TextConst 'ENU=EDI Electonic BOL was generated on %1. Do you want to re-create Electronic BOL?';

    BEGIN
    END.
  }
}

