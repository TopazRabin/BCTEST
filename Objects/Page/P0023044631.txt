OBJECT Page 23044631 DSHIP Order Comments Factbox
{
  OBJECT-PROPERTIES
  {
    Date=01/21/19;
    Time=[ 1:54:19 PM];
    Version List=DSHIP2.0.0;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=ENU=Dynamic Ship Order Comments Factbox;
    SourceTable=Table23044617;
    PageType=ListPart;
    SourceTableTemporary=Yes;
    ActionList=ACTIONS
    {
      { 1000000007;0 ;ActionContainer;
                      CaptionML=[ESM=Editar;
                                 FRC=Modifier;
                                 ENC=Edit];
                      ActionContainerType=ActionItems }
      { 1000000006;1 ;Action    ;
                      Name=acEdit;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Edit;
                                 ESM=Editar;
                                 FRC=Modifier;
                                 ENC=Edit];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Edit;
                      PromotedCategory=New;
                      OnAction=BEGIN
                                 editComments;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1000000002;2;Field  ;
                SourceExpr=Date }

    { 1000000003;2;Field  ;
                SourceExpr=Code;
                Visible=FALSE }

    { 1000000004;2;Field  ;
                SourceExpr=Comment }

  }
  CODE
  {
    VAR
      tcUnknownDoc@1000000000 : TextConst 'ENU=Unknown source document for packaging.;ESM=Documento de origen desconocido para el embalaje.;FRC=Document source inconnue pour l''emballage.;ENC=Unknown source document for packaging.';
      recrRecRef@1000000001 : RecordRef;

    PROCEDURE setOrder@1000000002(VAR precrRecRef@1000000000 : RecordRef);
    VAR
      lrecWhseShipHeader@1000000006 : Record 7320;
      lrecSalesHeader@1000000005 : Record 36;
      lrecTransferHeader@1000000004 : Record 5740;
      lrecWhseCommentLine@1000000003 : Record 5770;
      lrecSalesCommentLine@1000000002 : Record 44;
      lrecTransferCommentLine@1000000001 : Record 5748;
    BEGIN
      //<FUNC>
      //  Description: Init page table
      //  Called From: Page DSHIP Package Worksheet (P23044602)
      //  Side Effect: None
      //</FUNC>

      recrRecRef := precrRecRef;
      getComments;
    END;

    LOCAL PROCEDURE getComments@1000000015();
    VAR
      lrecWhseShipHeader@1000000005 : Record 7320;
      lrecSalesHeader@1000000004 : Record 36;
      lrecTransferHeader@1000000003 : Record 5740;
      lrecWhseCommentLine@1000000002 : Record 5770;
      lrecSalesCommentLine@1000000001 : Record 44;
      lrecTransferCommentLine@1000000000 : Record 5748;
    BEGIN
      //<FUNC>
      //  Description: Gets comments related to the table
      //  Called From: This page (P23044631)
      //  Side Effect: None
      //</FUNC>

      CLEAR(Rec);
      Rec.RESET;
      Rec.DELETEALL;

      CASE recrRecRef.NUMBER OF
        DATABASE::"Warehouse Shipment Header":
          BEGIN
            recrRecRef.SETTABLE(lrecWhseShipHeader);
            lrecWhseCommentLine.SETRANGE("Table Name", lrecWhseCommentLine."Table Name"::"Whse. Shipment");
            lrecWhseCommentLine.SETRANGE("No.", lrecWhseShipHeader."No.");
            IF ( lrecWhseCommentLine.FINDSET(FALSE) ) THEN BEGIN
              REPEAT
                addComment("Document Type"::"Warehouse Shipment", lrecWhseShipHeader."No.", 0,
                           lrecWhseCommentLine."Line No.", lrecWhseCommentLine.Date,
                           lrecWhseCommentLine.Code, lrecWhseCommentLine.Comment);
              UNTIL(lrecWhseCommentLine.NEXT=0);
            END; // if findset
          END;
        DATABASE::"Sales Header":
          BEGIN
            recrRecRef.SETTABLE(lrecSalesHeader);
            lrecSalesCommentLine.SETRANGE("Document Type", lrecSalesCommentLine."Document Type"::Order);
            lrecSalesCommentLine.SETRANGE("No.", lrecSalesHeader."No.");
            IF ( lrecSalesCommentLine.FINDSET(FALSE) ) THEN BEGIN
              REPEAT
                addComment("Document Type"::"Sales Order", lrecSalesCommentLine."No.", 1,
                           lrecSalesCommentLine."Line No.", lrecSalesCommentLine.Date,
                           lrecSalesCommentLine.Code, lrecSalesCommentLine.Comment);
              UNTIL(lrecSalesCommentLine.NEXT=0);
            END; // if findset
          END;
        DATABASE::"Transfer Header":
          BEGIN
            recrRecRef.SETTABLE(lrecTransferHeader);
            lrecTransferCommentLine.SETRANGE("Document Type", lrecTransferCommentLine."Document Type"::"Transfer Order");
            lrecTransferCommentLine.SETRANGE("No.", lrecTransferHeader."No.");
            IF ( lrecTransferCommentLine.FINDSET(FALSE) ) THEN BEGIN
              REPEAT
                addComment("Document Type"::"Outbound Transfer", lrecTransferCommentLine."No.", 0,
                           lrecTransferCommentLine."Line No.", lrecTransferCommentLine.Date,
                           lrecTransferCommentLine.Code, lrecTransferCommentLine.Comment);
              UNTIL(lrecTransferCommentLine.NEXT=0);
            END; // if findset
          END;
      END;

      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE addComment@1000000006(poptDocType@1000000000 : Option;pcodDocNo@1000000001 : Code[20];piDocSubtype@1000000002 : Integer;piLineNo@1000000003 : Integer;pdtDate@1000000004 : Date;pcodCode@1000000005 : Code[10];psComment@1000000006 : Text[80]);
    BEGIN
      //<FUNC>
      //  Description: Adds parameters to record
      //  Called From: This page (P23044631)
      //  Side Effect: None
      //</FUNC>

      Rec.RESET;
      Rec."Document Type" := poptDocType;
      Rec."Document No." := pcodDocNo;
      Rec."Document Subtype" := piDocSubtype;
      Rec."Line No." := piLineNo;
      Rec.Date := pdtDate;
      Rec.Code := pcodCode;
      Rec.Comment := psComment;
      Rec.INSERT;
    END;

    LOCAL PROCEDURE editComments@1000000013();
    VAR
      lrecWhseShipHeader@1000000005 : Record 7320;
      lrecSalesHeader@1000000004 : Record 36;
      lrecTransferHeader@1000000003 : Record 5740;
      lrecWhseCommentLine@1000000002 : Record 5770;
      lrecSalesCommentLine@1000000001 : Record 44;
      lrecTransferCommentLine@1000000000 : Record 5748;
    BEGIN
      //<FUNC>
      //  Description: Opens the comments page
      //  Called From: This page (P23044631)
      //  Side Effect: None
      //</FUNC>

      CASE   recrRecRef.NUMBER OF
        DATABASE::"Warehouse Shipment Header":
          BEGIN
            recrRecRef.SETTABLE(lrecWhseShipHeader);
            lrecWhseCommentLine.SETRANGE("Table Name", lrecWhseCommentLine."Table Name"::"Whse. Shipment");
            lrecWhseCommentLine.SETRANGE("No.", lrecWhseShipHeader."No.");
            PAGE.RUN(PAGE::"Warehouse Comment Sheet", lrecWhseCommentLine);
          END;
        DATABASE::"Sales Header":
          BEGIN
            recrRecRef.SETTABLE(lrecSalesHeader);
            lrecSalesCommentLine.SETRANGE("Document Type", lrecSalesCommentLine."Document Type"::Order);
            lrecSalesCommentLine.SETRANGE("No.", lrecSalesHeader."No.");
            PAGE.RUNMODAL(PAGE::"Sales Comment Sheet", lrecSalesCommentLine);
          END;
        DATABASE::"Transfer Header":
          BEGIN
            recrRecRef.SETTABLE(lrecTransferHeader);
            lrecTransferCommentLine.SETRANGE("Document Type", lrecTransferCommentLine."Document Type"::"Transfer Order");
            lrecTransferCommentLine.SETRANGE("No.", lrecTransferHeader."No.");
            PAGE.RUNMODAL(PAGE::"Inventory Comment Sheet", lrecTransferCommentLine);
          END;
      END;

      getComments;
    END;

    BEGIN
    {
      ************************
      Copyright Notice
      This objects content is copyright of Insight Works 2011.  All rights reserved.
      Any redistribution or reproduction of part or all of the contents in any form is prohibited.
      ************************
    }
    END.
  }
}

