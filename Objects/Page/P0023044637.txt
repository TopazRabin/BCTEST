OBJECT Page 23044637 DSHIP Misc. Shipment Card
{
  OBJECT-PROPERTIES
  {
    Date=03/26/19;
    Time=[ 2:41:14 PM];
    Version List=DSHIP2.2.2;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Dynamic Ship Miscellaneous Shipment Card;
               ESM=Ficha de env°o varios Dynamic Ship;
               FRC=Fiche de Livraison Pour Dynamic Ship;
               ENC=Dynamic Ship Miscellaneous Shipment Card];
    SourceTable=Table23044621;
    PageType=Card;
    PromotedActionCategoriesML=[ENU=New,Process,Report,Packing,Shipping,Print;
                                ESM=Nuevo, procesar, informar, empaquetado, env°o, imprimir;
                                FRC=Nouveau, Traitement, Rapport, Packing, Livraison, Imprimer;
                                ENC=New,Process,Report,Packing,Shipping,Print];
    OnFindRecord=BEGIN
                   EXIT(Rec.LocationAllowedOnFindRecord(Which));
                 END;

    OnNextRecord=BEGIN
                   EXIT(Rec.LocationAllowedOnNextRecord(Steps));
                 END;

    OnAfterGetRecord=BEGIN

                       setFieldVisibility();
                     END;

    ActionList=ACTIONS
    {
      { 1000000031;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1000000044;1 ;ActionGroup;
                      CaptionML=[ENU=Packing;
                                 ESM=Creaci¢n de kits;
                                 FRC=Emballage;
                                 ENC=Packing];
                      Image=Action }
      { 1000000032;2 ;Action    ;
                      Name=acAddPackage;
                      CaptionML=[ENU=Add Package;
                                 ESM=Agregar paquete;
                                 FRC=Ajouter Package;
                                 ENC=Add Package];
                      Promoted=Yes;
                      Enabled=NOT bShipped;
                      PromotedIsBig=Yes;
                      Image=NewItem;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 addPackage('');
                               END;
                                }
      { 1000000033;2 ;Action    ;
                      Name=acAddPackageFromLPT;
                      CaptionML=[ENU=Add Package from Template;
                                 ESM=Agregue el paquete de plantilla;
                                 FRC=Ajouter le Package Ö partir du Modäle;
                                 ENC=Add Package from Template];
                      Promoted=Yes;
                      Enabled=NOT bShipped;
                      PromotedIsBig=Yes;
                      Image=NewItemNonStock;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 addPackageFromLPT;
                               END;
                                }
      { 1000000043;1 ;ActionGroup;
                      CaptionML=[ENU=&Print;
                                 ESM=Imprimir;
                                 FRC=Imprimer;
                                 ENC=&Print];
                      Image=Print }
      { 1000000042;2 ;Action    ;
                      Name=acPrintLP;
                      CaptionML=[ENU=Internal Package Label;
                                 ESM=Etiqueta de paquete interno;
                                 FRC=êtiquette de Package. internes;
                                 ENC=Internal Package Label];
                      Promoted=Yes;
                      Enabled=bHasNo;
                      PromotedIsBig=Yes;
                      Image=Print;
                      PromotedCategory=Category6;
                      OnAction=BEGIN
                                 printInternalPackLabel;
                               END;
                                }
      { 1000000041;2 ;Action    ;
                      Name=acPrintCarrierLabel;
                      CaptionML=[ENU=Carrier Label;
                                 ESM=Etiqueta de soporte;
                                 FRC=êtiquette carrier;
                                 ENC=Carrier Label];
                      Promoted=Yes;
                      Enabled=bHasNo;
                      PromotedIsBig=Yes;
                      Image=PrintCover;
                      PromotedCategory=Category6;
                      OnAction=BEGIN
                                 printCarrierLabel;
                               END;
                                }
      { 1000000025;2 ;Action    ;
                      Name=acPrintReturnLabel;
                      CaptionML=ENU=Return Label;
                      Promoted=Yes;
                      Enabled=bHasNo;
                      Image=PrintCheck;
                      PromotedCategory=Category6;
                      OnAction=BEGIN
                                 printReturnLabel;
                               END;
                                }
      { 1000000039;1 ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 ESM=Funciones;
                                 FRC=Fonctions;
                                 ENC=F&unctions];
                      Image=Action }
      { 1000000038;2 ;Action    ;
                      Name=acGetRates;
                      CaptionML=[ENU=Get &Label;
                                 ESM=Obtener etiqueta;
                                 FRC=Extraire êtiquette;
                                 ENC=Get &Label];
                      Promoted=Yes;
                      Enabled=NOT bShipped;
                      PromotedIsBig=Yes;
                      Image=SuggestItemPrice;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 getRates;
                               END;
                                }
      { 1000000037;2 ;Action    ;
                      Name=acOptions;
                      CaptionML=[ENU=Package Options;
                                 ESM=Opciones de paquete;
                                 FRC=Options du package;
                                 ENC=Package Options];
                      Promoted=Yes;
                      Enabled=NOT bShipped;
                      PromotedIsBig=Yes;
                      Image=ItemVariant;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 doOptions;
                               END;
                                }
      { 1000000036;2 ;Action    ;
                      Name=acShpOptions;
                      CaptionML=[ENU=Shipment Options;
                                 ESM=Opciones de env°o;
                                 FRC=Options de livraison;
                                 ENC=Shipment Options];
                      Promoted=Yes;
                      Enabled=NOT bShipped;
                      PromotedIsBig=Yes;
                      Image=Shipment;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 ltrecPackOrderBuffer@1000000000 : TEMPORARY Record 23044601;
                               BEGIN
                                 pgPackageWorksheet.doShipOptions(ltrecPackOrderBuffer."Document Type"::"Misc. Shipment", "No.", 0);
                               END;
                                }
      { 1000000035;2 ;Action    ;
                      Name=acCustoms;
                      CaptionML=[ENU=Customs;
                                 ESM=Aduana;
                                 FRC=Aupräs des administrations douaniäres;
                                 ENC=Customs];
                      Promoted=Yes;
                      Enabled=NOT bShipped;
                      Image=Voucher;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 lrecCustomsHeader@1000000001 : Record 23044605;
                                 lcuCustomsMgmt@1000000000 : Codeunit 23044603;
                               BEGIN
                                 lcuCustomsMgmt.showCustomsPage(lrecCustomsHeader."Document Type"::"Misc. Shipment", "No.");
                               END;
                                }
      { 1000000034;2 ;Action    ;
                      Name=acInsurance;
                      CaptionML=[ENU=Insurance;
                                 ESM=Seguros;
                                 FRC=Assurance;
                                 ENC=Insurance];
                      Promoted=Yes;
                      Enabled=NOT bShipped;
                      Image=Insurance;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 doInsurance;
                               END;
                                }
      { 1000000024;2 ;Action    ;
                      Name=acGetReturnLabel;
                      CaptionML=[ENU=Get Return Label;
                                 ESM=Obtener etiqueta devoluci¢n;
                                 FRC=Extraire êtiquette de Retour;
                                 ENC=Get Return Label];
                      Promoted=Yes;
                      Enabled=bHasNo;
                      PromotedIsBig=Yes;
                      Image=ReturnShipment;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 getReturnLabel;
                               END;
                                }
      { 1000000045;1 ;ActionGroup;
                      CaptionML=[ENU=P&osting;
                                 ESM=Registro;
                                 FRC=Report;
                                 ENC=P&osting];
                      Image=Post }
      { 1000000040;2 ;Action    ;
                      Name=Post;
                      ShortCutKey=F9;
                      Ellipsis=Yes;
                      CaptionML=[ENU=P&ost;
                                 ESM=Registrar;
                                 FRC=Reporter;
                                 ENC=P&ost];
                      Promoted=Yes;
                      Enabled=NOT bShipped;
                      PromotedIsBig=Yes;
                      Image=PostOrder;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 doPost;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=General;
                GroupType=Group }

    { 1000000002;2;Field  ;
                SourceExpr="No.";
                Enabled=NOT bShipped;
                Editable=NOT bHasNo }

    { 1000000003;2;Field  ;
                SourceExpr=Description;
                Enabled=NOT bShipped;
                OnValidate=BEGIN

                             setFieldVisibility();
                           END;
                            }

    { 1000000046;2;Field  ;
                SourceExpr="Ship-To Type";
                Enabled=NOT bShipped;
                OnValidate=BEGIN

                             setFieldVisibility();
                           END;
                            }

    { 1000000005;2;Field  ;
                SourceExpr="Ship-To No.";
                Enabled=NOT bManualEntry AND NOT bShipped }

    { 1000000006;2;Field  ;
                SourceExpr="Ship-To Code";
                Enabled=NOT bShipped;
                Editable=bShipToCode AND NOT bShipped }

    { 1000000047;2;Field  ;
                SourceExpr="Location Code";
                Enabled=NOT bShipped }

    { 1000000007;2;Field  ;
                SourceExpr="Shipping Agent Code";
                Enabled=NOT bShipped }

    { 1000000008;2;Field  ;
                SourceExpr="Shipping Agent Service Code";
                Enabled=NOT bShipped }

    { 1000000009;2;Field  ;
                SourceExpr="Package Tracking No.";
                Enabled=NOT bShipped }

    { 1000000022;2;Field  ;
                SourceExpr="Shipping Cost";
                Enabled=NOT bShipped }

    { 1000000010;2;Field  ;
                SourceExpr=Shipped }

    { 1000000011;1;Group  ;
                Name=Address Details;
                GroupType=Group }

    { 1000000012;2;Field  ;
                SourceExpr=Company;
                Enabled=NOT bShipped }

    { 1000000020;2;Field  ;
                SourceExpr=Name }

    { 1000000013;2;Field  ;
                SourceExpr=Address;
                Enabled=NOT bShipped }

    { 1000000014;2;Field  ;
                SourceExpr="Address 2";
                Enabled=NOT bShipped }

    { 1000000015;2;Field  ;
                SourceExpr=City;
                Enabled=NOT bShipped }

    { 1000000016;2;Field  ;
                SourceExpr=County;
                Enabled=NOT bShipped }

    { 1000000017;2;Field  ;
                SourceExpr="Country/Region Code";
                Enabled=NOT bShipped }

    { 1000000018;2;Field  ;
                SourceExpr="Post Code";
                Enabled=NOT bShipped }

    { 1000000019;2;Field  ;
                SourceExpr="Phone No.";
                Enabled=NOT bShipped }

    { 1000000023;2;Field  ;
                SourceExpr="E-Mail" }

    { 1000000021;1;Part   ;
                Name=pgLPLines;
                CaptionML=[ENU=Package List;
                           ESM=Lista de paquete;
                           FRC=Liste Des package;
                           ENC=Package List];
                SubPageLink=Source No.=FIELD(No.),
                            Source Document=CONST(Misc. Shipment);
                PagePartID=Page23044639;
                Visible=NOT bShipped;
                PartType=Page;
                ShowFilter=No }

    { 1000000004;1;Part   ;
                Name=pgLPShippedLines;
                CaptionML=[ENU=Shipped Package List;
                           ESM=Lista enviados paquete;
                           FRC=Liste Des Package livrÇe;
                           ENC=Shipped Package List];
                SubPageLink=Shipped Source No.=FIELD(No.);
                PagePartID=Page23044639;
                Visible=bShipped;
                Editable=FALSE;
                PartType=Page;
                ShowFilter=No }

  }
  CODE
  {
    VAR
      cuPackMgmt@1000000007 : Codeunit 23044601;
      cuRateMgmt@1000000006 : Codeunit 23044602;
      pgPackageWorksheet@1000000005 : Page 23044602;
      codLicensePlateNo@1000000000 : Code[20];
      codLocationCode@1000000008 : Code[20];
      tcValidScans@1000000003 : TextConst 'ENU=new, %lpt%tmpl, %lp%lp;ESM=nuevo, % lpt % plant, % lp % lp;FRC=nouveau, % lpt % modäle interaction, % lp % lp;ENC=new, %lpt%tmpl, %lp%lp';
      bManualEntry@1000000001 : Boolean;
      bShipToCode@1000000010 : Boolean;
      bShipped@1000000011 : Boolean;
      bHasNo@1000000013 : Boolean;

    LOCAL PROCEDURE addPackage@1000000023(pcodLPTemplateCode@1000000000 : Code[20]);
    VAR
      lrecLP@1000000004 : Record 23044505;
      lrecLPLine@1000000001 : Record 23044506;
      lrecLPLineUsage@1000000002 : Record 23044507;
      lrecLPSetup@1000000003 : Record 23044523;
    BEGIN
      //<FUNC>
      //  Description: Adds a new LP to the misc shipment
      //  Called From: This page (P23044637)
      //  Side Effect: None
      //</FUNC>

      codLicensePlateNo := cuPackMgmt.createLicensePlate(codLocationCode, '', pcodLPTemplateCode);

      lrecLPSetup.GET;
      lrecLP.GET(codLicensePlateNo);
      IF ( lrecLP."Shpt. Dim. Unit of Measure" = '' ) THEN
        lrecLP."Shpt. Dim. Unit of Measure" := lrecLPSetup."Dimensions Unit of Measure";
      IF ( lrecLP."Weight Unit of Measure" = '' ) THEN
          lrecLP."Weight Unit of Measure" := lrecLPSetup."Weight Unit of Measure";
      lrecLP.MODIFY;

      lrecLPLine.INIT;
      lrecLPLine."License Plate No." := codLicensePlateNo;
      lrecLPLine."Line No." := 10000;
      lrecLPLine."Item No." := codLicensePlateNo;
      lrecLPLine.Quantity := 1;
      lrecLPLine.INSERT;
      lrecLPLineUsage.INIT;
      lrecLPLineUsage."License Plate No." := codLicensePlateNo;
      lrecLPLineUsage."License Plate Line No." := 10000;
      lrecLPLineUsage.Quantity := 1;
      lrecLPLineUsage."Source Document" := lrecLPLineUsage."Source Document"::"Misc. Shipment";
      lrecLPLineUsage."Source No." := "No.";
      lrecLPLineUsage."Source Company Name" := COMPANYNAME;
      lrecLPLineUsage.INSERT;
    END;

    LOCAL PROCEDURE addPackageFromLPT@1000000024();
    VAR
      lrecLPTmpl@1000000000 : Record 23044536;
    BEGIN
      //<FUNC>
      //  Description: Adds a new LP to the misc shipment based on an LPT
      //  Called From: This page (P23044637)
      //  Side Effect: None
      //</FUNC>

      IF ( PAGE.RUNMODAL(PAGE::"IWX License Plate Templates", lrecLPTmpl) = ACTION::LookupOK ) THEN BEGIN
        addPackage(lrecLPTmpl.Code);
      END;
    END;

    LOCAL PROCEDURE printInternalPackLabel@1000000029();
    VAR
      lrecLPHeader@1000000000 : Record 23044505;
      lrecLPHeaderShipped@1000000001 : Record 23044505;
    BEGIN
      //<FUNC>
      //  Description: Prints the package labels
      //  Called From: This page (P23044637)
      //  Side Effect: None
      //</FUNC>

      setLPHeader(lrecLPHeader);
      IF ( NOT lrecLPHeader.FINDFIRST ) THEN BEGIN
        lrecLPHeader.RESET;
        lrecLPHeader.SETRANGE("Shipped Source Document", lrecLPHeaderShipped."Shipped Source Document"::"Misc. Shipment");
        lrecLPHeader.SETRANGE("Shipped Source No.", "No.");
        IF ( lrecLPHeader.FINDFIRST ) THEN BEGIN
          lrecLPHeaderShipped."Package Order ID" := lrecLPHeader."Package Order ID";
          cuPackMgmt.printInternalPackageLabel(lrecLPHeaderShipped, 2);
        END;
      END ELSE
        cuPackMgmt.printInternalPackageLabel(lrecLPHeader, 2);
    END;

    LOCAL PROCEDURE printCarrierLabel@1000000030();
    VAR
      lrecLPHeader@1000000000 : Record 23044505;
    BEGIN
      //<FUNC>
      //  Description: Prints the carrier's shipping labels
      //  Called From: This page (P23044637)
      //  Side Effect: None
      //</FUNC>

      setLPHeader(lrecLPHeader);
      IF ( NOT lrecLPHeader.FINDFIRST ) THEN BEGIN
        lrecLPHeader.RESET;
        lrecLPHeader.SETRANGE("Shipped Source Document", lrecLPHeader."Shipped Source Document"::"Misc. Shipment");
        lrecLPHeader.SETRANGE("Shipped Source No.", "No.");
        IF ( lrecLPHeader.FINDFIRST ) THEN
          cuRateMgmt.printCarrierLabelByOrder(lrecLPHeader."Package Order ID", TRUE);
      END ELSE
        cuRateMgmt.printCarrierLabel(lrecLPHeader, TRUE);
    END;

    LOCAL PROCEDURE setLPHeader@1000000032(VAR precLPHeader@1000000000 : Record 23044505);
    BEGIN
      //<FUNC>
      //  Description: Set the LP Header range
      //  Called From: This page (P23044637)
      //  Side Effect: None
      //</FUNC>

      precLPHeader.SETFILTER("No.", cuPackMgmt.CreateLPNoFilterWithLPSourceDoc(precLPHeader."Source Document"::"Misc. Shipment", Rec."No."));
    END;

    LOCAL PROCEDURE getRates@1000000033();
    VAR
      ltrecPackOrderBuffer@1000000000 : TEMPORARY Record 23044601;
      lrecLPHeader@1000000001 : Record 23044505;
    BEGIN
      //<FUNC>
      //  Description: Gets rates for the shipment
      //  Called From: This page (P23044637)
      //  Side Effect: None
      //</FUNC>

      setLPHeader(lrecLPHeader);
      IF ( NOT lrecLPHeader.FINDSET(FALSE) ) THEN
        ERROR('This shipment has no packages.');
      REPEAT
        lrecLPHeader.TESTFIELD(Description);
        lrecLPHeader.TESTFIELD("Shipment Length");
        lrecLPHeader.TESTFIELD("Shipment Width");
        lrecLPHeader.TESTFIELD("Shipment Height");
        lrecLPHeader.TESTFIELD("Shpt. Dim. Unit of Measure");
        lrecLPHeader.TESTFIELD("Shipment Gross Weight");
        lrecLPHeader.TESTFIELD("Weight Unit of Measure");
      UNTIL(lrecLPHeader.NEXT=0);

      ltrecPackOrderBuffer."Document Type" := ltrecPackOrderBuffer."Document Type"::"Misc. Shipment";
      ltrecPackOrderBuffer."Document Subtype" := 0;
      ltrecPackOrderBuffer."Document No." := "No.";
      ltrecPackOrderBuffer.INSERT;
      deleteBadCustomsInfo(ltrecPackOrderBuffer);
      cuRateMgmt.getShippingLabel(ltrecPackOrderBuffer);
    END;

    LOCAL PROCEDURE doPost@1000000034();
    VAR
      lrecLPHeader@1000000000 : Record 23044505;
      lrecLPLineUsage@1000000001 : Record 23044507;
      lbHasLabel@1000000002 : Boolean;
    BEGIN
      //<FUNC>
      //  Description: Marks the LP as Shipped.
      //  Called From:
      //  Side Effect:
      //</FUNC>

      setLPHeader(lrecLPHeader);
      IF (lrecLPHeader.FINDSET(FALSE)) THEN
      REPEAT
        lbHasLabel := lrecLPHeader."Has Carrier Label";
      UNTIL((lrecLPHeader.NEXT=0) OR (lbHasLabel));

      IF ( NOT lbHasLabel ) THEN
        ERROR('This shipment hasn''t been labelled.');

      lrecLPLineUsage.SETFILTER("Posting Date",'%1',0D);
      lrecLPLineUsage.SETRANGE("Source No.", Rec."No.");
      lrecLPLineUsage.SETRANGE("Source Document", lrecLPLineUsage."Source Document"::"Misc. Shipment");
      IF (lrecLPLineUsage.FINDSET(FALSE)) THEN
        REPEAT
          lrecLPLineUsage.VALIDATE("Posting Date",TODAY);
          lrecLPLineUsage.MODIFY(TRUE);
          lrecLPHeader.GET(lrecLPLineUsage."License Plate No.");

          lrecLPHeader.Status := lrecLPHeader.Status::Shipped;
          lrecLPHeader.VALIDATE("Shipped Company Name", COMPANYNAME);
          lrecLPHeader.VALIDATE("Shipped Source Document", lrecLPHeader."Shipped Source Document"::"Misc. Shipment");
          lrecLPHeader.VALIDATE("Shipped Source No.", lrecLPLineUsage."Source No.");

          lrecLPHeader.VALIDATE("Shipped Item Count", lrecLPHeader."Current Item Count");
          lrecLPHeader.VALIDATE("Shipment Date", TODAY);
          lrecLPHeader.VALIDATE("Shipped Transaction DateTime", CURRENTDATETIME);
          lrecLPHeader.VALIDATE("Shipment Cubage", lrecLPHeader."Current Cubage");

          lrecLPHeader."Location Code" := '';
          lrecLPHeader."Bin Code" := '';

          lrecLPHeader.Locked := TRUE;
          lrecLPHeader.MODIFY(TRUE);

        UNTIL(lrecLPLineUsage.NEXT=0);

      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE setLP@1000000011();
    VAR
      lcodLPNo@1000000000 : Code[20];
    BEGIN

      IF ( Shipped ) THEN
        lcodLPNo := CurrPage.pgLPShippedLines.PAGE.getCurrentLP
      ELSE
        lcodLPNo := CurrPage.pgLPLines.PAGE.getCurrentLP;

      IF ( lcodLPNo <> '' ) THEN
        codLicensePlateNo := lcodLPNo;
    END;

    LOCAL PROCEDURE doInsurance@1000000000();
    BEGIN

      setLP;
      IF ( codLicensePlateNo <> '' ) THEN
        pgPackageWorksheet.doInsurance(codLicensePlateNo);
    END;

    LOCAL PROCEDURE doOptions@1000000009();
    BEGIN

      setLP;
      IF ( codLicensePlateNo <> '' ) THEN
        pgPackageWorksheet.doPackOptions(codLicensePlateNo);
    END;

    LOCAL PROCEDURE deleteBadCustomsInfo@1000000002(ptrecPackOrderBuffer@1000000000 : TEMPORARY Record 23044601);
    VAR
      lrecCustomsHeader@1000000001 : Record 23044605;
      lcuCustomsMgmt@1000000002 : Codeunit 23044603;
    BEGIN
      //<FUNC>
      //  Description: Delete associated customs if not an international shipment
      //  Called From: This Page (P23044637)
      //  Side Effect: None
      //</FUNC>

      IF ( NOT lcuCustomsMgmt.checkInternationalPackageByOrder(ptrecPackOrderBuffer."Document Type", ptrecPackOrderBuffer."Document No.") ) THEN BEGIN
        lrecCustomsHeader.SETRANGE("Document Type", ptrecPackOrderBuffer."Document Type");
        lrecCustomsHeader.SETRANGE("Document No.", ptrecPackOrderBuffer."Document No.");
        lrecCustomsHeader.DELETEALL(TRUE);
      END;
    END;

    LOCAL PROCEDURE setFieldVisibility@1000000001();
    BEGIN

      bManualEntry := Rec."Ship-To Type" = Rec."Ship-To Type"::"Manual Entry";
      bShipToCode := Rec."Ship-To Type" IN [Rec."Ship-To Type"::Customer, Rec."Ship-To Type"::Vendor, Rec."Ship-To Type"::Contact];
      bShipped := Rec.Shipped;
      bHasNo := Rec."No." <> '';
    END;

    LOCAL PROCEDURE getReturnLabel@1000000039();
    VAR
      ltrecOrderBuder@1000000001 : TEMPORARY Record 23044601;
      lpgOrderPack@1000000000 : Page 23044601;
    BEGIN
      //<FUNC>
      //  Description: Makes a request for a return label
      //  Called From: This page (P23044601)
      //  Side Effect: None
      //</FUNC>

      lpgOrderPack.getReturnLabel(ltrecOrderBuder."Document Type"::"Misc. Shipment", Rec."No.");
    END;

    LOCAL PROCEDURE printReturnLabel@1000000038();
    VAR
      ltrecOrderBuder@1000000001 : TEMPORARY Record 23044601;
      lpgOrderPack@1000000000 : Page 23044601;
    BEGIN
      //<FUNC>
      //  Description: Prints the return label
      //  Called From: This page (P23044601)
      //  Side Effect: None
      //</FUNC>

      lpgOrderPack.printReturnLabel(ltrecOrderBuder."Document Type"::"Misc. Shipment", Rec."No.", TRUE);
    END;

    BEGIN
    {
      ************************
      Copyright Notice
      This objects content is copyright of Insight Works 2011.  All rights reserved.
      Any redistribution or reproduction of part or all of the contents in any form is prohibited.
      ************************
    }
    END.
  }
}

