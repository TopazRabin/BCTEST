OBJECT Page 11123327 SC - Customer Assortment
{
  OBJECT-PROPERTIES
  {
    Date=04/18/17;
    Time=[ 1:00:00 PM];
    Version List=SCW19.2.0;
  }
  PROPERTIES
  {
    CaptionML=ENU=Customer Assortment;
    SaveValues=Yes;
    SourceTable=Table11123326;
    DelayedInsert=Yes;
    PageType=Worksheet;
    OnInit=BEGIN
             CodeFilterCtrlEnable := TRUE;
             SalesCodeFilterCtrlEnable := TRUE;
             SalesTypeFilter := SalesTypeFilter::All;
             TypeFilter := TypeFilter::All;
             ModeFilter := ModeFilter::All;
           END;

    OnOpenPage=BEGIN
                 GetRecFilters;
                 SetRecFilters;
               END;

  }
  CONTROLS
  {
    { 11123315;0;Container;
                ContainerType=ContentArea }

    { 11123314;1;Group    ;
                CaptionML=ENU=General }

    { 11123313;2;Field    ;
                Name=SalesTypeFilterCtrl;
                CaptionML=ENU=Sales Type Filter;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SalesTypeFilter;
                OnValidate=BEGIN
                             SalesTypeFilterOnAfterValidate;
                           END;
                            }

    { 11123312;2;Field    ;
                Name=SalesCodeFilterCtrl;
                CaptionML=ENU=Sales Code Filter;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SalesCodeFilter;
                Enabled=SalesCodeFilterCtrlEnable;
                OnValidate=BEGIN
                             SalesCodeFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           CustList@11123303 : Page 22;
                           CustPriceGrList@11123302 : Page 7;
                         BEGIN
                           IF SalesTypeFilter = SalesTypeFilter::All THEN EXIT;

                           CASE SalesTypeFilter OF
                             SalesTypeFilter::Customer:
                               BEGIN
                                 CustList.LOOKUPMODE := TRUE;
                                 IF CustList.RUNMODAL = ACTION::LookupOK THEN
                                   Text := CustList.GetSelectionFilter
                                 ELSE
                                   EXIT(FALSE);
                               END;
                             SalesTypeFilter::"Customer Price Group":
                               BEGIN
                                 CustPriceGrList.LOOKUPMODE := TRUE;
                                 IF CustPriceGrList.RUNMODAL = ACTION::LookupOK THEN
                                   Text := CustPriceGrList.GetSelectionFilter
                                 ELSE
                                   EXIT(FALSE);
                               END;
                           END;

                           EXIT(TRUE);
                         END;
                          }

    { 11123311;2;Field    ;
                Name=ModeFilterCtrl;
                CaptionML=ENU=Mode Filter;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ModeFilter;
                OnValidate=BEGIN
                             ModeFilterOnAfterValidate;
                           END;
                            }

    { 11123310;2;Field    ;
                Name=TypeFilterCtrl;
                CaptionML=ENU=Type Filter;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=TypeFilter;
                OnValidate=BEGIN
                             TypeFilterOnAfterValidate;
                           END;
                            }

    { 11123309;2;Field    ;
                Name=CodeFilterCtrl;
                CaptionML=ENU=Code Filter;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CodeFilter;
                Enabled=CodeFilterCtrlEnable;
                OnValidate=BEGIN
                             CodeFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           ProductGroup@11123305 : Record 5723;
                           ItemCategory@11123306 : Record 5722;
                           ItemList@11123302 : Page 31;
                         BEGIN
                           ItemCategoryFilter := '';
                           CASE Type OF
                             Type::Item :
                               BEGIN
                                 ItemList.LOOKUPMODE := TRUE;
                                 IF ItemList.RUNMODAL = ACTION::LookupOK THEN
                                   Text := ItemList.GetSelectionFilter
                                 ELSE
                                   EXIT(FALSE);
                               END;
                             Type::"Product Group" :
                               BEGIN
                                 IF PAGE.RUNMODAL(PAGE::"Product Groups",ProductGroup) = ACTION::LookupOK THEN BEGIN
                                   Text := ProductGroup.Code;
                                   ItemCategoryFilter := ProductGroup."Item Category Code";
                                 END ELSE
                                   EXIT(FALSE);
                               END;
                             Type::"Item Category" :
                               BEGIN
                                 IF PAGE.RUNMODAL(PAGE::"Item Categories",ItemCategory) = ACTION::LookupOK THEN
                                   Text := ItemCategory.Code
                                 ELSE
                                   EXIT(FALSE);
                               END;
                           END;

                           EXIT(TRUE);
                         END;
                          }

    { 11123308;1;Group    ;
                GroupType=Repeater }

    { 11123307;2;Field    ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sales Type" }

    { 11123306;2;Field    ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sales Code" }

    { 11123305;2;Field    ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Type }

    { 11123304;2;Field    ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Code }

    { 11123303;2;Field    ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Item Category Code (Internal)";
                Visible=FALSE }

    { 11123302;2;Field    ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Mode }

  }
  CODE
  {
    VAR
      SalesTypeFilter@11123302 : 'Customer,Customer Price Group,All';
      SalesCodeFilter@11123303 : Text[250];
      TypeFilter@11123304 : 'Item,Product Group,Item Category,All';
      CodeFilter@11123305 : Text[250];
      ModeFilter@11123306 : 'Allow,Deny,All';
      ItemCategoryFilter@11123307 : Text[250];
      SalesCodeFilterCtrlEnable@19013164 : Boolean INDATASET;
      CodeFilterCtrlEnable@19026169 : Boolean INDATASET;

    PROCEDURE GetRecFilters@11123302();
    BEGIN
      IF GETFILTERS <> '' THEN BEGIN
        IF GETFILTER("Sales Type") <> '' THEN
          SalesTypeFilter := "Sales Type"
        ELSE
          SalesTypeFilter := SalesTypeFilter::All;

        IF GETFILTER(Type) <> '' THEN
          TypeFilter := Type
        ELSE
          TypeFilter := TypeFilter::All;

        IF GETFILTER(Mode) <> '' THEN
          ModeFilter := Mode
        ELSE
          ModeFilter := ModeFilter::All;

        SalesCodeFilter := GETFILTER("Sales Code");
        CodeFilter := GETFILTER(Code);
        ItemCategoryFilter := GETFILTER("Item Category Code (Internal)");
      END;
    END;

    PROCEDURE SetRecFilters@11123303();
    BEGIN
      SalesCodeFilterCtrlEnable := TRUE;
      CodeFilterCtrlEnable := TRUE;

      IF SalesTypeFilter <> SalesTypeFilter::All THEN
        SETRANGE("Sales Type",SalesTypeFilter)
      ELSE
        SETRANGE("Sales Type");

      IF SalesTypeFilter IN [SalesTypeFilter::All] THEN BEGIN
        SalesCodeFilterCtrlEnable := FALSE;
        SalesCodeFilter := '';
      END;

      IF SalesCodeFilter <> '' THEN
        SETFILTER("Sales Code",SalesCodeFilter)
      ELSE
        SETRANGE("Sales Code");

      IF TypeFilter <> TypeFilter::All THEN
        SETRANGE(Type,TypeFilter)
      ELSE
        SETRANGE(Type);

      IF TypeFilter = TypeFilter::All THEN BEGIN
        CodeFilterCtrlEnable := FALSE;
        CodeFilter := '';
      END;

      IF CodeFilter <> '' THEN BEGIN
        SETFILTER(Code,CodeFilter);
      END ELSE
        SETRANGE(Code);

      IF ItemCategoryFilter <> '' THEN BEGIN
        SETFILTER("Item Category Code (Internal)",ItemCategoryFilter);
      END ELSE
        SETRANGE("Item Category Code (Internal)");

      IF ModeFilter <> ModeFilter::All THEN
        SETRANGE(Mode,ModeFilter)
      ELSE
        SETRANGE(Mode);

      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE SalesTypeFilterOnAfterValidate@19024044();
    BEGIN
      CurrPage.SAVERECORD;
      SalesCodeFilter := '';
      SetRecFilters;
    END;

    LOCAL PROCEDURE SalesCodeFilterOnAfterValidate@19067727();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE TypeFilterOnAfterValidate@19042848();
    BEGIN
      CurrPage.SAVERECORD;
      CodeFilter := '';
      SetRecFilters;
    END;

    LOCAL PROCEDURE CodeFilterOnAfterValidate@19025728();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE ModeFilterOnAfterValidate@19023907();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    BEGIN
    END.
  }
}

