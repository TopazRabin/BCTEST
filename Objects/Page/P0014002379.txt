OBJECT Page 14002379 E.D.I. File Cabinet Subform
{
  OBJECT-PROPERTIES
  {
    Date=12/28/17;
    Time=12:00:00 PM;
    Version List=SE0.60.18;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=ENU=E.D.I. File Cabinet Subform;
    SourceTable=Table14002368;
    PageType=ListPart;
    OnFindRecord=BEGIN
                   EDIFileTmp := Rec;
                   IF NOT EDIFileTmp.FIND(Which) THEN
                     EXIT(FALSE);
                   Rec := EDIFileTmp;
                   EXIT(TRUE);
                 END;

    OnNextRecord=BEGIN
                   EDIFileTmp := Rec;
                   CurrentSteps := EDIFileTmp.NEXT(Steps);
                   IF CurrentSteps <> 0 THEN
                     Rec := EDIFileTmp;
                   EXIT(CurrentSteps);
                 END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                ToolTipML=ENU=Temporary table/fields used for internal processing.;
                SourceExpr=Path;
                Visible=FALSE }

    { 4   ;2   ;Field     ;
                ToolTipML=ENU=Temporary table/fields used for internal processing.;
                SourceExpr="Is a File";
                Visible=FALSE }

    { 6   ;2   ;Field     ;
                ToolTipML=ENU=Temporary table/fields used for internal processing.;
                SourceExpr=Name }

    { 8   ;2   ;Field     ;
                ToolTipML=ENU=Temporary table/fields used for internal processing.;
                SourceExpr=Size }

    { 10  ;2   ;Field     ;
                ToolTipML=ENU=Temporary table/fields used for internal processing.;
                SourceExpr=Date }

    { 12  ;2   ;Field     ;
                ToolTipML=ENU=Temporary table/fields used for internal processing.;
                SourceExpr=Time }

  }
  CODE
  {
    VAR
      EDIFunctAckSendAll@1240030000 : Codeunit 14002366;
      EDIImport@1240030001 : Codeunit 14002354;
      EDIFileTmp@1240030002 : TEMPORARY Record 14002368;
      FileObject@1240030003 : Record 2000000022;
      EDiSetup@1240030004 : Record 14002367;
      TestFile@1240030005 : File;
      CurrentSteps@1240030006 : Integer;
      FromName@1240030007 : Text[150];
      ToName@1240030008 : Text[150];
      LastDirectory@1240030009 : Text[250];
      Text001@1001 : TextConst 'ENU=Archive %1?';
      Text002@1002 : TextConst 'ENU=Restore %1 from Archive?';

    PROCEDURE GetFileLines@2(EDITemplate@1240030000 : Record 14002350;VAR Path@1240030001 : Text[150]);
    BEGIN
      EDITemplate.TESTFIELD("Interface File Path");
      EDITemplate."Interface File Path" := DELCHR(EDITemplate."Interface File Path",'>','\');

      EDiSetup.GET;
      EDiSetup.TESTFIELD("Common Receive Path");
      EDiSetup."Common Receive Path" := DELCHR(EDiSetup."Common Receive Path",'>','\');

      CASE EDITemplate."View Source" OF
        EDITemplate."View Source"::Inbox:
          Path := EDITemplate."Interface File Path" + '\Inbox\';
        EDITemplate."View Source"::"Inbox Archive":
          Path := EDITemplate."Interface File Path" + '\Inbox\Archive\';
        EDITemplate."View Source"::Outbox:
          Path := EDITemplate."Interface File Path" + '\Outbox\';
        EDITemplate."View Source"::"Outbox Archive":
          Path := EDITemplate."Interface File Path" + '\Outbox\Archive\';
        EDITemplate."View Source"::"Common Rec. Dir.":
          Path := EDiSetup."Common Receive Path" + '\';
        EDITemplate."View Source"::"Common Rec. Dir. Archive":
          Path := EDiSetup."Common Receive Path" + '\Archive\';
      END;

      CreateFileLines(Path);

      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE CreateFileLines@1(Directory@1240030000 : Text[150]);
    VAR
      TempPath@1240030001 : Text[100];
    BEGIN
      IF (Directory <> LastDirectory) AND (Directory <> '') THEN
        LastDirectory := Directory;
      CLEAR(FileObject);
      EDIFileTmp.DELETEALL;

      TempPath := COPYSTR(EDiSetup."Common Receive Path",1,STRPOS(EDiSetup."Common Receive Path",'\'));
      FileObject.RESET;
      FileObject.SETFILTER(Path,TempPath);
      FileObject.FIND('-');

      FileObject.RESET;
      FileObject.SETFILTER(Path,'%1',LastDirectory);
      FileObject.SETRANGE("Is a file",TRUE);
      IF FileObject.FIND('-') THEN
        REPEAT
          EDIFileTmp.INIT;
          EDIFileTmp.Path := FileObject.Path;
          EDIFileTmp."Is a File" := FileObject."Is a file";
          EDIFileTmp.Name := FileObject.Name;
          EDIFileTmp.Size := FileObject.Size;
          EDIFileTmp.Date := FileObject.Date;
          EDIFileTmp.Time := FileObject.Time;
          EDIFileTmp.INSERT;
        UNTIL FileObject.NEXT = 0;

      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE ProcessAllFiles@6();
    BEGIN
      EDIFileTmp.RESET;
      IF EDIFileTmp.FIND('-') THEN
        REPEAT
          CLEAR(EDIImport);
          EDIImport.ImportByFile(EDIFileTmp);
        UNTIL EDIFileTmp.NEXT = 0;

      COMMIT;

      IF EDiSetup."Auto Export Send Doc." THEN
        EDIFunctAckSendAll.RUN;

      CreateFileLines('');
    END;

    PROCEDURE ProcessFile@5();
    BEGIN
      CLEAR(EDIImport);
      EDIImport.ImportByFile(Rec);
      EDIFileTmp.DELETE;

      COMMIT;

      IF EDiSetup."Auto Export Send Doc." THEN
        EDIFunctAckSendAll.RUN;

      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE ArchiveFile@4(Directory@1240030000 : Text[100]);
    BEGIN
      FromName := Path + Name;
      IF NOT CONFIRM(Text001,FALSE,FromName) THEN
        EXIT;
      ToName := Path + '\archive\' + Name;
      FILE.COPY(FromName,ToName);
      IF FILE.EXISTS(ToName) THEN
        FILE.ERASE(FromName);
    END;

    PROCEDURE RestoreFile@7(Directory@1240030000 : Text[100]);
    BEGIN
      FromName := Path + Name;
      IF NOT CONFIRM(Text002,FALSE,FromName) THEN
        EXIT;
      ToName := DELSTR(FromName,STRPOS(FromName,'\ARCHIVE\'),8);
      FILE.COPY(FromName,ToName);
    END;

    BEGIN
    END.
  }
}

