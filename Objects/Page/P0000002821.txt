OBJECT Page 2821 Native - PDFs
{
  OBJECT-PROPERTIES
  {
    Date=12/21/17;
    Time=12:00:00 PM;
    Version List=NAVW111.00.00.19846;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[@@@={Locked};
               ENU=nativeInvoicingPDFs;
               ESM=nativeInvoicingPDFs;
               FRC=nativeInvoicingPDFs;
               ENC=nativeInvoicingPDFs];
    SourceTable=Table5509;
    PageType=List;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 BINDSUBSCRIPTION(NativeAPILanguageHandler);
               END;

    OnFindRecord=VAR
                   DocumentId@1005 : GUID;
                   DocumentIdFilter@1001 : Text;
                   FilterView@1000 : Text;
                 BEGIN
                   IF NOT PdfGenerated THEN BEGIN
                     FilterView := GETVIEW;
                     DocumentIdFilter := GETFILTER("Document Id");
                     IF DocumentIdFilter = '' THEN
                       DocumentIdFilter := GETFILTER(Id);
                     SETVIEW(FilterView);
                     DocumentId := GetDocumentId(DocumentIdFilter);
                     IF ISNULLGUID(DocumentId) THEN
                       EXIT(FALSE);
                     GeneratePdf(DocumentId);
                   END;
                   EXIT(TRUE);
                 END;

    ODataKeyFields=Document Id;
  }
  CONTROLS
  {
    { 18  ;0   ;Container ;
                ContainerType=ContentArea }

    { 17  ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 1   ;2   ;Field     ;
                Name=documentId;
                CaptionML=[@@@={Locked};
                           ENU=documentId;
                           ESM=documentId;
                           FRC=documentId;
                           ENC=documentId];
                ApplicationArea=#All;
                SourceExpr="Document Id" }

    { 7   ;2   ;Field     ;
                Name=fileName;
                CaptionML=[@@@={Locked};
                           ENU=fileName;
                           ESM=fileName;
                           FRC=fileName;
                           ENC=fileName];
                ApplicationArea=#All;
                SourceExpr="File Name" }

    { 4   ;2   ;Field     ;
                Name=content;
                CaptionML=[@@@={Locked};
                           ENU=content;
                           ESM=content;
                           FRC=content;
                           ENC=content];
                ApplicationArea=#All;
                SourceExpr=Content }

  }
  CODE
  {
    VAR
      CannotFindDocumentErr@1006 : TextConst '@@@=%1 - Error Message;ENU=The document %1 cannot be found.;ESM=No se encuentra el documento %1.;FRC=Impossible de trouver le document %1.;ENC=The document %1 cannot be found.';
      CannotOpenFileErr@1005 : TextConst '@@@=%1 - Error Message;ENU=Opening the file failed because of the following error: \%1.;ESM=Error al abrir el archivo debido al siguiente error: \%1.;FRC=L''ouverture du fichier a ‚chou‚ en raison de l''erreur suivanteÿ: \%1.;ENC=Opening the file failed because of the following error: \%1.';
      DocumentIDNotSpecifiedForAttachmentsErr@1010 : TextConst 'ENU=You must specify a document ID to get the PDF.;ESM=Debe especificar un id. de documento para obtener el archivo PDF.;FRC=Vous devez sp‚cifier un code de document pour obtenir le PDF.;ENC=You must specify a document ID to get the PDF.';
      DocumentDoesNotExistErr@1009 : TextConst 'ENU=No document with the specified ID exists.;ESM=No existe ning£n documento con el identificador especificado.;FRC=Il n''existe aucun document avec le code sp‚cifi‚.;ENC=No document with the specified ID exists.';
      NativeAPILanguageHandler@1000 : Codeunit 2850;
      PdfGenerated@1002 : Boolean;

    LOCAL PROCEDURE GetDocumentId@5(DocumentIdFilter@1001 : Text) : GUID;
    VAR
      SalesHeader@1003 : Record 36;
      SalesInvoiceHeader@1004 : Record 112;
      DataTypeManagement@1006 : Codeunit 701;
      DocumentRecordRef@1000 : RecordRef;
      DocumentIdFieldRef@1005 : FieldRef;
      DocumentId@1002 : GUID;
    BEGIN
      IF DocumentIdFilter = '' THEN
        ERROR(DocumentIDNotSpecifiedForAttachmentsErr);

      SalesHeader.SETFILTER(Id,DocumentIdFilter);
      IF SalesHeader.FINDFIRST THEN
        DocumentRecordRef.GETTABLE(SalesHeader)
      ELSE BEGIN
        SalesInvoiceHeader.SETFILTER(Id,DocumentIdFilter);
        IF SalesInvoiceHeader.FINDFIRST THEN
          DocumentRecordRef.GETTABLE(SalesInvoiceHeader)
        ELSE
          ERROR(DocumentDoesNotExistErr);
      END;

      DataTypeManagement.FindFieldByName(DocumentRecordRef,DocumentIdFieldRef,SalesHeader.FIELDNAME(Id));
      EVALUATE(DocumentId,FORMAT(DocumentIdFieldRef.VALUE));

      EXIT(DocumentId);
    END;

    LOCAL PROCEDURE GeneratePdf@21(DocumentId@1009 : GUID);
    VAR
      CompanyInformation@1004 : Record 79;
      SalesInvoiceHeader@1002 : Record 112;
      SalesHeader@1006 : Record 36;
      ReportSelections@1005 : Record 77;
      NativeReports@1011 : Codeunit 2822;
      DocumentMailing@1003 : Codeunit 260;
      GraphIntBusinessProfile@1013 : Codeunit 5442;
      File@1007 : File;
      InStream@1008 : InStream;
      OutStream@1010 : OutStream;
      Path@1001 : Text[250];
      Name@1000 : Text[250];
      ReportId@1012 : Integer;
    BEGIN
      GraphIntBusinessProfile.SyncFromGraphSynchronously;

      CompanyInformation.GET;
      SalesHeader.SETRANGE(Id,DocumentId);
      IF SalesHeader.FINDFIRST THEN
        CASE SalesHeader."Document Type" OF
          SalesHeader."Document Type"::Invoice:
            BEGIN
              ReportId := NativeReports.DraftSalesInvoiceReportId;
              ReportSelections.GetPdfReport(Path,ReportId,SalesHeader,SalesHeader."Sell-to Customer No.");
              DocumentMailing.GetAttachmentFileName(Name,SalesHeader."No.",SalesHeader.GetDocTypeTxt,ReportId);
            END;
          SalesHeader."Document Type"::Quote:
            BEGIN
              ReportId := NativeReports.SalesQuoteReportId;
              ReportSelections.GetPdfReport(Path,ReportId,SalesHeader,SalesHeader."Sell-to Customer No.");
              DocumentMailing.GetAttachmentFileName(Name,SalesHeader."No.",SalesHeader.GetDocTypeTxt,ReportId);
            END;
          ELSE
            ERROR(CannotFindDocumentErr,DocumentId);
        END
      ELSE BEGIN
        SalesInvoiceHeader.SETRANGE(Id,DocumentId);
        IF SalesInvoiceHeader.FINDFIRST THEN BEGIN
          ReportId := NativeReports.PostedSalesInvoiceReportId;
          ReportSelections.GetPdfReport(Path,ReportId,SalesInvoiceHeader,SalesInvoiceHeader."Sell-to Customer No.");
          DocumentMailing.GetAttachmentFileName(Name,SalesInvoiceHeader."No.",SalesInvoiceHeader.GetDocTypeTxt,ReportId);
        END ELSE
          ERROR(CannotFindDocumentErr,DocumentId);
      END;

      IF NOT File.OPEN(Path) THEN
        ERROR(CannotOpenFileErr,GETLASTERRORTEXT);

      INIT;
      Id := DocumentId;
      "Document Id" := DocumentId;
      "File Name" := Name;
      Type := Type::PDF;
      Content.CREATEOUTSTREAM(OutStream);
      File.CREATEINSTREAM(InStream);
      COPYSTREAM(OutStream,InStream);
      File.CLOSE;
      IF ERASE(Path) THEN;
      INSERT(TRUE);

      PdfGenerated := TRUE;
    END;

    BEGIN
    END.
  }
}

