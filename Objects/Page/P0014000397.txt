OBJECT Page 14000397 E.D.I. Alert Worksheet
{
  OBJECT-PROPERTIES
  {
    Date=02/25/19;
    Time=12:00:00 PM;
    Version List=SE0.61;
  }
  PROPERTIES
  {
    CaptionML=ENU=EDI Alert Worksheet;
    InsertAllowed=No;
    SourceTable=Table14000380;
    DelayedInsert=No;
    PageType=Worksheet;
    OnOpenPage=BEGIN
                 RESET;
                 SETCURRENTKEY("Alert Date","Trade Partner No.",Type);
                 SETRANGE("Review Alert",TRUE);
                 "View Status" := 1;
                 ViewStatus := FORMAT("View Status");
               END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1240020009;1 ;ActionGroup;
                      CaptionML=ENU=&Line }
      { 1240020010;2 ;Action    ;
                      CaptionML=ENU=&Receive Document;
                      ToolTipML=ENU=Displays the EDI Receive Document list for the selected Alert.;
                      Promoted=Yes;
                      Image=View;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ViewReceiveDocument;
                               END;
                                }
      { 1240020011;2 ;Action    ;
                      CaptionML=ENU=&Send Document;
                      ToolTipML=ENU=Displays the EDI Send Document list for the selected Alert.;
                      Promoted=Yes;
                      Image=View;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ViewSendDocument;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1240030031;1 ;ActionGroup;
                      CaptionML=ENU=F&unctions }
      { 1240030024;2 ;Action    ;
                      CaptionML=ENU=&View All Alerts;
                      ToolTipML=ENU=Shows all Alerts including those that have already been reviewed.;
                      Promoted=Yes;
                      Image=View;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 AlertView := 1;
                                 ApplyFilters(AlertView);
                                 "Alert Date Filter" := DateFilter;

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1240030025;2 ;Action    ;
                      CaptionML=ENU=View A&lerts;
                      ToolTipML=ENU=Gives a filtered list of Alerts where Review Alert is TRUE.;
                      Promoted=Yes;
                      Image=View;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 AlertView := 2;
                                 ApplyFilters(AlertView);
                                 "Alert Date Filter" := DateFilter;

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1240030038;2 ;Action    ;
                      CaptionML=ENU=&Reviewed Alerts;
                      ToolTipML=ENU=Displays a list of Alerts where Review Alert is FALSE.;
                      Promoted=Yes;
                      Image=View;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 AlertView := 3;
                                 ApplyFilters(AlertView);
                                 "Alert Date Filter" := DateFilter;

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1240030032;2 ;Action    ;
                      CaptionML=ENU=Remove Review &Status;
                      ToolTipML=ENU=Removes the 'Review Alert' flag from the highlighted Alert.;
                      Promoted=Yes;
                      Image=View;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 RemoveAlert(Rec);
                               END;
                                }
      { 1240020000;2 ;ActionGroup;
                      CaptionML=ENU=&Create Manual Alerts }
      { 1240020001;3 ;Action    ;
                      CaptionML=ENU=&Generic Document;
                      ToolTipML=ENU=Create a Manual Alert for a Generic Document.;
                      OnAction=BEGIN
                                 REPORT.RUNMODAL(REPORT::"E.D.I. Generic Doc. Alert",TRUE,FALSE);

                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 1240020018;3 ;Action    ;
                      CaptionML=ENU=Receive &Document Error;
                      ToolTipML=ENU=Create a Manual Alert for a Receive Document with data error.;
                      OnAction=BEGIN
                                 REPORT.RUNMODAL(REPORT::"E.D.I. Receive Doc Error Alert",TRUE,FALSE);

                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 1240020025;3 ;Action    ;
                      CaptionML=ENU=&Send Document Error;
                      ToolTipML=ENU=Create a Manual Alert for a Send Document with data error.;
                      OnAction=BEGIN
                                 REPORT.RUNMODAL(REPORT::"E.D.I. Send Doc. Error Alert",TRUE,FALSE);

                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 1240030039;2 ;Action    ;
                      CaptionML=ENU=&Trace;
                      ToolTipML=ENU=Lists all system and EDI records containing the same 'EDI Internal Doc No.' or associated records.;
                      Promoted=Yes;
                      Image=Trace;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF ("Line No." > 0) AND ("Internal Doc. No." <> '') THEN BEGIN
                                   CLEAR(EDITrace);
                                   EDITrace.SetDoc("Internal Doc. No.");
                                   EDITrace.RUNMODAL;
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1240030036;1;Field  ;
                CaptionML=ENU=Trade Partner No.;
                SourceExpr=TradePartnerFilter;
                OnValidate=BEGIN
                             TradePartnerFilterOnAfterValid;
                           END;

                OnLookup=BEGIN
                           EDITradePartner.RESET;
                           IF PAGE.RUNMODAL(PAGE::"E.D.I. Trade Partner List",EDITradePartner) = ACTION::LookupOK THEN BEGIN
                             TradePartnerFilter := EDITradePartner."No.";
                             SETRANGE("Trade Partner No.",TradePartnerFilter);
                             CurrPage.UPDATE(FALSE);
                           END;
                         END;
                          }

    { 1240020014;1;Field  ;
                CaptionML=ENU=Alert Date Filter;
                SourceExpr=AlertDateFilter;
                OnValidate=BEGIN
                             DateFilter := '';

                             IF AlertDateFilter <> '' THEN
                               CASE AlertDateFilter OF
                                 't','T':
                                   BEGIN
                                     AlertDateFilter := FORMAT(TODAY);
                                     DateFilter := FORMAT(TODAY);
                                     SETFILTER("Alert Date",DateFilter);
                                    END;
                                 'w','W':
                                   BEGIN
                                     AlertDateFilter := FORMAT(WORKDATE);
                                     DateFilter := FORMAT(WORKDATE);
                                     SETFILTER("Alert Date",DateFilter);
                                   END;
                                 'c','C':
                                   MESSAGE(Text002,AlertDateFilter);
                                 ELSE BEGIN
                                   DateFilter := AlertDateFilter;
                                   SETFILTER("Alert Date",AlertDateFilter);
                                 END;
                               END
                             ELSE BEGIN
                               CurrTradePtnrFilter := '';
                               CurrAlertFilter := GETFILTER("Review Alert");
                               CurrTradePtnrFilter := GETFILTER("Trade Partner No.");
                               RESET;
                               IF CurrTradePtnrFilter <>  '' THEN
                                 SETRANGE("Trade Partner No.",CurrTradePtnrFilter);
                               IF CurrAlertFilter = 'Yes' THEN
                                 SETRANGE("Review Alert",TRUE)
                               ELSE BEGIN
                                 SETRANGE("Review Alert",FALSE) ;
                                 DateFilter := '..' + FORMAT(TODAY);
                               END;
                             END;
                               AlertDateFilterOnAfterValidate;
                           END;
                            }

    { 1240030000;1;Group  ;
                GroupType=Repeater }

    { 1240030030;2;Field  ;
                ToolTipML=ENU=Same as the EDI Receive Doc's 'Date Received.';
                SourceExpr="Alert Date" }

    { 1240020012;2;Field  ;
                ToolTipML=ENU=Specifies the Trade Partner "No." from the Trade Partner page.;
                SourceExpr="Trade Partner No." }

    { 1240030034;2;Field  ;
                ToolTipML=ENU=List the Type of Alert that created the entry.;
                SourceExpr=Type }

    { 1240030019;2;Field  ;
                ToolTipML=ENU=Specifies the message generated by the Alert.;
                SourceExpr="Alert Message" }

    { 1240030003;2;Field  ;
                ToolTipML=ENU=It is set to TRUE until the Alert has been printed or previewed by the Formatted Receive Document report.;
                SourceExpr="Review Alert";
                OnValidate=BEGIN
                             ReviewAlertOnAfterValidate;
                           END;
                            }

    { 1240030040;2;Field  ;
                ToolTipML=ENU=Where applicable, diplays the process action such as Request Cancelled or Order Created.;
                SourceExpr="Process Action" }

    { 1240030001;2;Field  ;
                ToolTipML=ENU=Specifies the EDI Internal Doc No that is a link back to the EDI Receive Document.;
                SourceExpr="Internal Doc. No.";
                Visible=TRUE;
                Enabled=TRUE }

    { 1240020016;2;Field  ;
                ToolTipML=ENU=Specifies the EDI Send Document No. that created the Alert.;
                SourceExpr="Send Document No." }

    { 1240030027;2;Field  ;
                ToolTipML=ENU=Specifies the source Document Type associated to the Alert (ex. Quote, Blanket Order).;
                SourceExpr="Document Type";
                Visible=TRUE }

    { 1240030007;2;Field  ;
                ToolTipML=ENU=Specifies the source Document No. associated to the Alert.;
                SourceExpr="Document No.";
                Visible=TRUE }

    { 1240030005;2;Field  ;
                ToolTipML=ENU=Specifies any associated source Document Type for the Alert (ex. Quote, Blanket Order).;
                SourceExpr="Associated Document Type" }

    { 1240030009;2;Field  ;
                ToolTipML=ENU=Specifies any associated source Document No. for the Alert.;
                SourceExpr="Associated Document No." }

    { 1240030011;2;Field  ;
                ToolTipML=ENU=Specifies the Line No. of any associated source documents for the Alert.;
                SourceExpr="Associated Document Line No." }

    { 1240030013;2;Field  ;
                ToolTipML=ENU=Specifies any associated source Document Type 2 for the Alert (ex. Quote, Blanket Order).;
                SourceExpr="Associated Document Type 2";
                Visible=FALSE }

    { 1240030015;2;Field  ;
                ToolTipML=ENU=Specifies any associated source Document No. 2 for the Alert.;
                SourceExpr="Associated Document No. 2";
                Visible=FALSE }

    { 1240030017;2;Field  ;
                ToolTipML=ENU=Specifies the Line No. 2 of any associated source documents for the Alert.;
                SourceExpr="Associated Document Line No. 2";
                Visible=FALSE }

    { 1240020002;2;Field  ;
                ToolTipML=ENU=Set to TRUE when the Alert is manually created.;
                SourceExpr="Manual Alert";
                Visible=FALSE }

    { 1240020019;2;Field  ;
                ToolTipML=ENU=Specifies the date of which the Alert was created.;
                SourceExpr="Date Created";
                Visible=FALSE }

    { 1240020021;2;Field  ;
                ToolTipML=ENU=Specifies the time of which the Alert was created.;
                SourceExpr="Time Created";
                Visible=FALSE }

    { 1240020023;2;Field  ;
                ToolTipML=ENU=Populates with the User ID of who created the Alert.;
                SourceExpr=User;
                Visible=FALSE }

    { 1240020006;1;Group   }

    { 1240020007;2;Field  ;
                CaptionClass=Text19010345 }

    { 1240020008;2;Field  ;
                CaptionML=ENU=Status;
                SourceExpr=ViewStatus;
                Editable=FALSE }

    { 1240020026;2;Field  ;
                CaptionClass=Text19029964 }

    { 1240020005;2;Field  ;
                Name=<Alert Message2>;
                ToolTipML=ENU=Specifies the message generated by the Alert.;
                SourceExpr="Alert Message";
                Editable=FALSE }

  }
  CODE
  {
    VAR
      EDITradePartner@1240030003 : Record 14002360;
      EDIAlert@1240020012 : Record 14000380;
      EDITrace@1240030004 : Page 14002386;
      Text001@1240030001 : TextConst 'ENU=Do you wish to remove the Reveiw Alert status from the selected record(s)?';
      TradePartnerFilter@1240030002 : Code[20];
      DateFilter@1240020004 : Text[250];
      ViewStatus@1240020003 : Text[60];
      AlertView@1240020001 : Integer;
      Text002@1240020010 : TextConst 'ENU=''%1'' is not a supported date format.';
      AlertDateFilter@1240020011 : Text[250];
      CurrAlertFilter@1240020000 : Text[250];
      CurrTradePtnrFilter@1240020002 : Text[250];
      Text19010345@19038008 : TextConst 'ENU=View Status';
      Text19029964@19061301 : TextConst 'ENU=Alert Message';

    PROCEDURE RemoveAlert@1240030000(EDIAlert@1240030000 : Record 14000380);
    BEGIN
      IF CONFIRM(Text001,TRUE) THEN BEGIN
        CurrPage.SETSELECTIONFILTER(EDIAlert);
        EDIAlert.FIND('-');
        REPEAT
          IF EDIAlert.MARK THEN BEGIN
            EDIAlert."Review Alert" := FALSE;
            EDIAlert.MARK(FALSE);
            EDIAlert.MODIFY;
          END;
        UNTIL EDIAlert.NEXT = 0;
      END;

      CurrPage.UPDATE(TRUE);
    END;

    PROCEDURE GetAlertFilters@1240020001() FiltersFound : Boolean;
    BEGIN
      IF (TradePartnerFilter <> '') OR (DateFilter <> '') THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    PROCEDURE ApplyFilters@1240020000(AlertView@1240020000 : Integer);
    VAR
      AlertFilter@1240020001 : Boolean;
    BEGIN
      AlertFilter := GetAlertFilters;

      CASE AlertView OF
        1:
          BEGIN
            IF AlertFilter THEN BEGIN
              RESET;
              IF TradePartnerFilter <> '' THEN
                SETRANGE("Trade Partner No.",TradePartnerFilter);
              IF DateFilter <> '' THEN
                SETFILTER("Alert Date",DateFilter)
            END ELSE
              RESET;
            "View Status" := 0;
            ViewStatus := FORMAT("View Status");
          END;
        2:
          BEGIN
            IF AlertFilter THEN BEGIN
              RESET;
              IF TradePartnerFilter <> '' THEN
                SETRANGE("Trade Partner No.",TradePartnerFilter);
              IF DateFilter <> '' THEN
                SETFILTER("Alert Date",DateFilter);
              SETRANGE("Review Alert",TRUE);
            END ELSE BEGIN
              RESET;
              SETRANGE("Review Alert",TRUE);
            END;
            "View Status" := 1;
            ViewStatus := FORMAT("View Status");
          END;
        3:
          BEGIN
            IF AlertFilter THEN BEGIN
              RESET;
              IF TradePartnerFilter <> '' THEN
                SETRANGE("Trade Partner No.",TradePartnerFilter);
              IF DateFilter <> '' THEN
                SETFILTER("Alert Date",DateFilter);
              SETRANGE("Review Alert",FALSE);
            END ELSE BEGIN
              RESET;
              SETRANGE("Review Alert",FALSE);
            END;
            "View Status" := 2;
            ViewStatus := FORMAT("View Status");
          END;
      END;
    END;

    LOCAL PROCEDURE ReviewAlertOnAfterValidate@19035015();
    BEGIN
      CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE TradePartnerFilterOnAfterValid@19015273();
    BEGIN
      IF TradePartnerFilter = '' THEN
        SETRANGE("Trade Partner No.")
      ELSE
        SETRANGE("Trade Partner No.",TradePartnerFilter);
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE AlertDateFilterOnAfterValidate@19045964();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    BEGIN
    END.
  }
}

