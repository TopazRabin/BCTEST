OBJECT Page 23044621 DSHIP Tracking Information
{
  OBJECT-PROPERTIES
  {
    Date=01/21/19;
    Time=[ 1:34:44 PM];
    Version List=DSHIP2.0.0;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=ENU=Dynamic Ship Tracking Information;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table23044505;
    DataCaptionExpr=sCaption;
    PageType=StandardDialog;
    ShowFilter=No;
  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 1000000007;1;Group  ;
                GroupType=Group }

    { 1000000008;2;Field  ;
                Name=Source Document;
                Lookup=Yes;
                SourceExpr=sDocType;
                Enabled=FALSE;
                Editable=FALSE }

    { 1000000009;2;Field  ;
                Name=Source No.;
                Lookup=Yes;
                SourceExpr=codDocNo;
                Enabled=FALSE;
                Editable=FALSE }

    { 1000000001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1000000002;2;Field  ;
                SourceExpr="No." }

    { 1000000003;2;Field  ;
                SourceExpr=Status }

    { 1000000004;2;Field  ;
                Name=Carrier;
                SourceExpr=sCarrierName }

    { 1000000005;2;Field  ;
                SourceExpr="Package Tracking Number" }

    { 1000000006;2;Field  ;
                SourceExpr="Shipment Tracking URL" }

  }
  CODE
  {
    VAR
      recLPHeader@1000000001 : Record 23044505;
      sCarrierName@1000000000 : Text[50];
      sDocType@1000000002 : Text;
      sCaption@1000000006 : Text;
      optDocType@1000000004 : Option;
      codDocNo@1000000005 : Code[20];

    PROCEDURE initPage@1000000000(poptSrcDocType@1000000000 : Option;pcodSrcDocNo@1000000001 : Code[20]) : Boolean;
    VAR
      loptDocType@1000000002 : Option;
    BEGIN
      //<FUNC>
      //  Description: Inits page
      //  Called From: PAGE DSHIP Order Packaging (23044601)
      //               PAGE DSHIP Package Worksheet (23044602)
      //  Side Effect: None
      //</FUNC>

      optDocType := poptSrcDocType;
      codDocNo := pcodSrcDocNo;

      RESET;
      SETRANGE("Source Document", poptSrcDocType);
      SETRANGE("Source No.", pcodSrcDocNo);
      IF ( NOT FINDSET(FALSE) ) THEN  BEGIN
        RESET;
        SETRANGE("Shipped Source Document", poptSrcDocType);
        SETRANGE("Shipped Source No.", pcodSrcDocNo);
        IF ( FINDSET(FALSE) ) THEN ;
      END;

      EXIT( setCarrier );
    END;

    LOCAL PROCEDURE setCarrier@1000000002() : Boolean;
    VAR
      lrecSalesHeader@1000000000 : Record 36;
      lrecTransferHeader@1000000001 : Record 5740;
      lrecWhseShipHeader@1000000002 : Record 7320;
      lrecShippingAgent@1000000004 : Record 291;
      lcodShipAgentCode@1000000003 : Code[10];
    BEGIN
      //<FUNC>
      //  Description: Sets the carrier field
      //  Called From: This page (23044621)
      //  Side Effect:
      //</FUNC>

      CASE optDocType OF
        "Source Document"::"Sales Order":
          BEGIN
            sDocType := 'Sales Order';
            lrecSalesHeader.GET(lrecSalesHeader."Document Type"::Order, codDocNo);
            lcodShipAgentCode := lrecSalesHeader."Shipping Agent Code";
          END;
        "Source Document"::"Outbound Transfer":
          BEGIN
            sDocType := 'Outbound Transfer';
            lrecTransferHeader.GET(codDocNo);
            lcodShipAgentCode := lrecTransferHeader."Shipping Agent Code";
          END;
        "Source Document"::Shipment:
          BEGIN
            sDocType := 'Warehouse Shipment';
            lrecWhseShipHeader.GET(codDocNo);
            lcodShipAgentCode := lrecWhseShipHeader."Shipping Agent Code";
          END;
      END; // case "Document Type"

      sCaption := sDocType + ' ' + codDocNo;

      IF ( lrecShippingAgent.GET(lcodShipAgentCode) ) THEN BEGIN
        sCarrierName := lrecShippingAgent.Name;
        EXIT(TRUE);
      END ELSE BEGIN
        EXIT(FALSE);
      END;
    END;

    BEGIN
    END.
  }
}

