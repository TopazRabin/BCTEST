OBJECT Report 14000561 Create Procurement Units
{
  OBJECT-PROPERTIES
  {
    Date=07/01/20;
    Time=12:00:00 AM;
    Modified=Yes;
    Version List=UBP2.98;
  }
  PROPERTIES
  {
    CaptionML=ENU=Create Procurement Units;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  InventoryManagementSetup.GET;
                END;

  }
  DATASET
  {
    { 8129;    ;DataItem;                    ;
               DataItemTable=Table27;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               DialogWindow.OPEN(
                                 Text000 +
                                 Text001 +
                                 Text002);
                             END;

               OnPostDataItem=BEGIN
                                DialogWindow.CLOSE;
                              END;

               ReqFilterFields=No.,Manufacturer Code,Item Category Code,Product Group Code,Variant Filter,Location Filter }

    { 6004;1   ;DataItem;                    ;
               DataItemTable=Table14;
               DataItemTableView=SORTING(Code)
                                 ORDER(Ascending)
                                 WHERE(Use As In-Transit=CONST(No),
                                       Enable Adv. Forecasting=CONST(Yes));
               OnAfterGetRecord=BEGIN
                                  IF (Item.GETFILTER("Variant Filter") <> '') THEN
                                    ItemVariant.SETFILTER(Code, Item.GETFILTER("Variant Filter"))
                                  ELSE
                                  ItemVariant.SETFILTER(Code,'<>%1','');
                                  ItemVariant.SETRANGE("Item No.",Item."No.");
                                  IF ItemVariant.FIND('-') THEN BEGIN
                                    REPEAT
                                      CreateProcUnit(Location.Code,Item."No.",ItemVariant.Code);
                                    UNTIL ItemVariant.NEXT = 0;
                                    CASE InventoryManagementSetup."Summarize Variant Detail" OF
                                      InventoryManagementSetup."Summarize Variant Detail"::Always:
                                        BEGIN
                                          CreateProcUnit(Location.Code,Item."No.",'');
                                          ProcUnit.VALIDATE("Summarize Variant Detail",TRUE);
                                          ProcUnit.MODIFY;
                                        END;
                                      InventoryManagementSetup."Summarize Variant Detail"::"Specify Item":
                                        CreateProcUnit(Location.Code,Item."No.",'');
                                    END;
                                  END ELSE
                                    CreateProcUnit(Location.Code,Item."No.",'');
                                END;

               DataItemLink=Code=FIELD(Location Filter) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      InventoryManagementSetup@1240030002 : Record 14000551;
      ItemVariant@1000000000 : Record 5401;
      ItemLedgerEntry@1240030001 : Record 32;
      ProcUnit@1000000002 : Record 14000555;
      Text000@1000000005 : TextConst 'ENU=Item No.       #1##################\';
      Text001@1000000004 : TextConst 'ENU=Location Code  #2########\';
      Text002@1000000003 : TextConst 'ENU=Variant Code   #3########\';
      DialogWindow@1000000006 : Dialog;
      OnlyWithLedgerEntries@1240030000 : Boolean;

    PROCEDURE CreateProcUnit@1000000000(LocationCode@1000000000 : Code[10];ItemNo@1000000001 : Code[20];VariantCode@1000000002 : Code[10]);
    BEGIN
      IF NOT ProcUnit.GET(LocationCode,ItemNo,VariantCode) THEN BEGIN
        IF OnlyWithLedgerEntries THEN BEGIN
          ItemLedgerEntry.RESET;
          ItemLedgerEntry.SETRANGE("Location Code",LocationCode);
          ItemLedgerEntry.SETRANGE("Item No.",ItemNo);
          ItemLedgerEntry.SETRANGE("Variant Code",VariantCode);
          ItemLedgerEntry.SETFILTER("Posting Date",'>=%1',InventoryManagementSetup."Usage Start Date");
          IF NOT ItemLedgerEntry.FIND('-') THEN
            EXIT;
        END;
        //DialogWindow.UPDATE(1,ItemNo);
        //DialogWindow.UPDATE(2,LocationCode);
        //DialogWindow.UPDATE(3,VariantCode);
        ProcUnit.INIT;
        ProcUnit."Location Code" := LocationCode;
        ProcUnit."Item No." := ItemNo;
        ProcUnit."Variant Code" := VariantCode;
        ProcUnit.INSERT(TRUE);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

