OBJECT Report 14000715 Delete E-Ship Documents
{
  OBJECT-PROPERTIES
  {
    Date=12/28/17;
    Time=12:00:00 PM;
    Version List=SE0.60.18;
  }
  PROPERTIES
  {
    CaptionML=ENU=Delete E-Ship Documents;
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   Window.CLOSE;
                 END;

  }
  DATASET
  {
    { 9934;    ;DataItem;PostedPackages      ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               IF DeleteToDate = 0D THEN
                                 ERROR(Text001);

                               IF DeleteToDate > CALCDATE('-3M') THEN
                                 ERROR(Text002);

                               IF NOT CONFIRM(Text003) THEN
                                 ERROR(Text004);

                               Window.OPEN(
                                 Text005 +
                                 Text006);

                               IF NOT DeletePostedPackages THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=VAR
                                  ShippingAgent@1240030000 : Record 291;
                                  PostedPackage@1240030001 : Record 14000704;
                                  PostedPackageLine@1240030002 : Record 14000705;
                                  UPSPostedOptionPage@1240030003 : Record 14000762;
                                  UPSManifest@1240030004 : Record 14000768;
                                  UPSManifest2@1240030005 : Record 14000771;
                                  FedExPostedOptionPage@1240030006 : Record 14000782;
                                  FedExShipment@1240030007 : Record 14000783;
                                  FedExShipment2@1240030008 : Record 14000789;
                                  LTLPostedOptionPage@1240030009 : Record 14000802;
                                  GenericPostedOptionPage@1240030010 : Record 14000862;
                                  USPSPostedOptionPage@1240030011 : Record 14000882;
                                  USPS2PostedOptionPage@1240030015 : Record 14050502;
                                BEGIN
                                  Window.UPDATE(1,PostedPackage.TABLENAME);

                                  PostedPackage.RESET;
                                  PostedPackage.SETRANGE("Packing Date",0D,DeleteToDate);
                                  IF PostedPackage.FIND('-') THEN
                                    REPEAT
                                      Window.UPDATE(2,PostedPackage."No.");

                                      GetShippingAgent(ShippingAgent,PostedPackage."Shipping Agent Code");
                                      IF (ShippingAgent."Shipper Type" IN
                                          [ShippingAgent."Shipper Type"::UPS,ShippingAgent."Shipper Type"::FEDEX,
                                           ShippingAgent."Shipper Type"::"4",ShippingAgent."Shipper Type"::"5",
                                           ShippingAgent."Shipper Type"::Generic]) AND
                                         (NOT PostedPackage."Manual Shipment")
                                      THEN
                                        PostedPackage.TESTFIELD("Manifest No.");

                                      PostedPackage.DELETE;

                                      PostedPackageLine.RESET;
                                      PostedPackageLine.SETRANGE("Package No.",PostedPackage."No.");
                                      PostedPackageLine.DELETEALL;

                                      UPSPostedOptionPage.RESET;
                                      UPSPostedOptionPage.SETRANGE("No.",PostedPackage."No.");
                                      IF UPSPostedOptionPage.FIND('-') THEN
                                        UPSPostedOptionPage.DELETEALL;

                                      UPSManifest.RESET;
                                      UPSManifest.SETCURRENTKEY("Package No.");
                                      UPSManifest.SETRANGE("Package No.",PostedPackage."No.");
                                      IF UPSManifest.FIND('-') THEN
                                        REPEAT
                                          IF UPSManifest2.GET(UPSManifest."Entry No.") THEN
                                            UPSManifest2.DELETE;

                                          UPSManifest.DELETE;
                                        UNTIL UPSManifest.NEXT = 0;

                                      FedExPostedOptionPage.RESET;
                                      FedExPostedOptionPage.SETRANGE("No.",PostedPackage."No.");
                                      IF FedExPostedOptionPage.FIND('-') THEN
                                        FedExPostedOptionPage.DELETEALL;

                                      FedExShipment.RESET;
                                      FedExShipment.SETCURRENTKEY("Package No.");
                                      FedExShipment.SETRANGE("Package No.",PostedPackage."No.");
                                      IF FedExShipment.FIND('-') THEN
                                        REPEAT
                                          IF FedExShipment2.GET(FedExShipment."Transaction Type",FedExShipment."Transaction ID") THEN
                                            FedExShipment2.DELETE;

                                          FedExShipment.DELETE;
                                        UNTIL FedExShipment.NEXT = 0;

                                      LTLPostedOptionPage.RESET;
                                      LTLPostedOptionPage.SETRANGE("No.",PostedPackage."No.");
                                      IF LTLPostedOptionPage.FIND('-') THEN
                                        LTLPostedOptionPage.DELETEALL;

                                      GenericPostedOptionPage.RESET;
                                      GenericPostedOptionPage.SETRANGE("No.",PostedPackage."No.");
                                      IF GenericPostedOptionPage.FIND('-') THEN
                                        GenericPostedOptionPage.DELETEALL;

                                      USPSPostedOptionPage.RESET;
                                      USPSPostedOptionPage.SETRANGE("No.",PostedPackage."No.");
                                      IF USPSPostedOptionPage.FIND('-') THEN
                                        USPSPostedOptionPage.DELETEALL;

                                      USPS2PostedOptionPage.RESET;
                                      USPS2PostedOptionPage.SETRANGE("No.",PostedPackage."No.");
                                      IF USPS2PostedOptionPage.FIND('-') THEN
                                        USPS2PostedOptionPage.DELETEALL;

                                    UNTIL PostedPackage.NEXT = 0;
                                END;
                                 }

    { 2841;    ;DataItem;PostedManifests     ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               IF NOT DeletePostedManifests THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=VAR
                                  ManifestHeader@1240030000 : Record 14000712;
                                  ManifestLine@1240030001 : Record 14000713;
                                BEGIN
                                  Window.UPDATE(1,ManifestHeader.TABLENAME);

                                  ManifestHeader.RESET;
                                  ManifestHeader.SETRANGE("Pickup Date",0D,DeleteToDate);
                                  ManifestHeader.SETRANGE(Posted,TRUE);
                                  ManifestHeader.SETRANGE("UPS Processing Required",FALSE);
                                  ManifestHeader.SETRANGE("FedEx Processing Required",FALSE);
                                  ManifestHeader.SETRANGE("LTL Processing Required",FALSE);
                                  ManifestHeader.SETRANGE("Generic Processing Required",FALSE);
                                  ManifestHeader.SETRANGE("USPS Processing Required",FALSE);
                                  ManifestHeader.SETRANGE("Airborne Processing Required",FALSE);
                                  IF ManifestHeader.FIND('-') THEN
                                    REPEAT
                                      Window.UPDATE(2,ManifestHeader."No.");

                                      ManifestHeader.DELETE;

                                      ManifestLine.RESET;
                                      ManifestLine.SETRANGE("Manifest No.",ManifestHeader."No.");
                                      ManifestLine.DELETEALL;
                                    UNTIL ManifestHeader.NEXT = 0;
                                END;
                                 }

    { 3487;    ;DataItem;LabelFiles          ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               IF NOT DeleteLabelFiles THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=VAR
                                  LabelFile@1240030000 : Record 14000710;
                                BEGIN
                                  Window.UPDATE(1,LabelFile.TABLENAME);
                                  Window.UPDATE(2,Text007);

                                  LabelFile.RESET;
                                  LabelFile.SETCURRENTKEY("Date Created");
                                  LabelFile.SETRANGE("Date Created",0D,DeleteToDate);
                                  LabelFile.DELETEALL;
                                END;
                                 }

    { 1759;    ;DataItem;PostedBillOfLadings ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               IF NOT DeletePostedBillOfLadings THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=VAR
                                  BillOfLading@1240030000 : Record 14000822;
                                  BillOfLadingLine@1240030001 : Record 14000823;
                                  BillOfLadingSummaryLine@1240030003 : Record 14000825;
                                  BillOfLadingInfoLine@1240030004 : Record 14000827;
                                  SalesShipmentHeader@1240030005 : Record 110;
                                  ReturnShipmentHeader@1240030007 : Record 6650;
                                  TransferShipmentHeader@1240030008 : Record 5744;
                                  AfterDateFound@1240030006 : Boolean;
                                BEGIN
                                  Window.UPDATE(1,BillOfLading.TABLENAME);

                                  BillOfLading.RESET;
                                  BillOfLading.SETRANGE(Released,TRUE);
                                  BillOfLading.SETRANGE("Summary Released",TRUE);
                                  BillOfLading.SETRANGE(Posted,TRUE);
                                  BillOfLading.SETRANGE("Packing Date",0D,DeleteToDate);
                                  IF BillOfLading.FIND('-') THEN
                                    REPEAT
                                      AfterDateFound := FALSE;

                                      BillOfLadingLine.RESET;
                                      BillOfLadingLine.SETRANGE("Bill of Lading No.",BillOfLading."No.");
                                      REPEAT
                                        CASE BillOfLadingLine."Source Type" OF
                                          DATABASE::"Sales Header":
                                            IF SalesShipmentHeader.GET(BillOfLadingLine."Posted Source ID") THEN
                                              IF SalesShipmentHeader."Posting Date" > DeleteToDate THEN
                                                AfterDateFound := TRUE;
                                          DATABASE::"Purchase Header":
                                            IF ReturnShipmentHeader.GET(BillOfLadingLine."Posted Source ID") THEN
                                              IF ReturnShipmentHeader."Posting Date" > DeleteToDate THEN
                                                AfterDateFound := TRUE;
                                          DATABASE::"Transfer Header":
                                            IF SalesShipmentHeader.GET(BillOfLadingLine."Posted Source ID") THEN
                                              IF TransferShipmentHeader."Posting Date" > DeleteToDate THEN
                                                AfterDateFound := TRUE;
                                        END;
                                      UNTIL (BillOfLadingLine.NEXT = 0) OR AfterDateFound;

                                      IF NOT AfterDateFound THEN BEGIN
                                        Window.UPDATE(2,BillOfLading."No.");

                                        IF BillOfLading."EDI ASN" THEN
                                          BillOfLading.TESTFIELD("EDI ASN Generated",TRUE);

                                        BillOfLading.DELETE;

                                        BillOfLadingLine.DELETEALL;

                                        BillOfLadingSummaryLine.RESET;
                                        BillOfLadingSummaryLine.SETRANGE("Bill of Lading No.",BillOfLading."No.");
                                        BillOfLadingSummaryLine.DELETEALL;

                                        BillOfLadingInfoLine.RESET;
                                        BillOfLadingInfoLine.SETRANGE("Bill of Lading No.",BillOfLading."No.");
                                        BillOfLadingInfoLine.DELETEALL;
                                      END;
                                    UNTIL BillOfLading.NEXT = 0;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF DeleteToDate = 0D THEN
                     DeleteToDate := CALCDATE('-1Y');
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1   ;2   ;Field     ;
                  CaptionML=ENU=Delete to Date;
                  SourceExpr=DeleteToDate }

      { 3   ;2   ;Field     ;
                  CaptionML=ENU=Delete Posted Packages;
                  SourceExpr=DeletePostedPackages }

      { 5   ;2   ;Field     ;
                  CaptionML=ENU=Delete Posted Manifests;
                  SourceExpr=DeletePostedManifests }

      { 7   ;2   ;Field     ;
                  CaptionML=ENU=Delete Label Files;
                  SourceExpr=DeleteLabelFiles }

      { 9   ;2   ;Field     ;
                  CaptionML=ENU=Delete Posted Bill of Ladings;
                  SourceExpr=DeletePostedBillOfLadings }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ShippingAgentTmp@1240030000 : TEMPORARY Record 291;
      Window@1240030001 : Dialog;
      DeleteToDate@1240030002 : Date;
      DeletePostedPackages@1240030003 : Boolean;
      DeletePostedManifests@1240030004 : Boolean;
      DeleteLabelFiles@1240030005 : Boolean;
      DeletePostedBillOfLadings@1240030006 : Boolean;
      Text001@1001 : TextConst 'ENU=A date must be entered.';
      Text002@1002 : TextConst 'ENU=Cannot delete within the last 3 months.';
      Text003@1003 : TextConst 'ENU=This Report will delete Data, Be sure to have a backup. Continue?';
      Text004@1004 : TextConst 'ENU=Nothing deleted.';
      Text005@1005 : TextConst 'ENU=Table:   #1##################\';
      Text006@1006 : TextConst 'ENU=Record:  #2##################';
      Text007@1007 : TextConst 'ENU=Deleting Records';

    PROCEDURE GetShippingAgent@4(VAR NewShippingAgent@1240030000 : Record 291;NewShippingAgentCode@1240030001 : Code[10]);
    VAR
      ShippingAgent@1240030002 : Record 291;
    BEGIN
      IF NewShippingAgent.Code = NewShippingAgentCode THEN
        EXIT;

      IF ShippingAgentTmp.GET(NewShippingAgentCode) THEN BEGIN
        NewShippingAgent := ShippingAgentTmp;
        EXIT;
      END;

      IF NOT ShippingAgent.GET(NewShippingAgentCode) THEN BEGIN
        ShippingAgent.INIT;
        ShippingAgent.Code := NewShippingAgentCode;
      END;

      ShippingAgentTmp := ShippingAgent;
      ShippingAgentTmp.INSERT;
      NewShippingAgent := ShippingAgent;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

