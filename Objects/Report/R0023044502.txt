OBJECT Report 23044502 WHI Copy Application
{
  OBJECT-PROPERTIES
  {
    Date=04/16/19;
    Time=[ 3:19:16 PM];
    Version List=WHI2.4.7684.0;
  }
  PROPERTIES
  {
    CaptionML=[ENU=WHI Copy Application;
               ESM=WHI Copiar solicitud;
               FRC=WHI Copier l'application;
               ENC=WHI Copy Application];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  CopyApplication();
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               bCopyMenus := TRUE;
               bCopyScanBehaviors := TRUE;
             END;

    }
    CONTROLS
    {
      { 1000000001;0;Container;
                  ContainerType=ContentArea }

      { 1000000000;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 1000000002;2;Field  ;
                  Name=FromApplication;
                  CaptionML=[ENU=From Application;
                             ESM=De la aplicaci¢n;
                             FRC=Ú partir de l'application;
                             ENC=From Application];
                  SourceExpr=codFromAppName;
                  TableRelation="WMDM Application" }

      { 1000000003;2;Field  ;
                  Name=ToApplication;
                  CaptionML=[ENU=To Application;
                             ESM=A la aplicaci¢n;
                             FRC=Ú l'application;
                             ENC=To Application];
                  SourceExpr=codToAppName;
                  OnLookup=VAR
                             lrecApplication@1000000000 : Record 23044527;
                           BEGIN
                             IF( PAGE.RUNMODAL(PAGE::"WHI Application List", lrecApplication) = ACTION::LookupOK ) THEN
                               codToAppName := lrecApplication.Code;
                           END;
                            }

      { 1000000004;2;Field  ;
                  Name=CopyMenus;
                  CaptionML=[ENU=Copy Menus;
                             ESM=Copiar men£s;
                             FRC=Copier les menus;
                             ENC=Copy Menus];
                  SourceExpr=bCopyMenus }

      { 1000000005;2;Field  ;
                  Name=CopyScanBehaviors;
                  CaptionML=[ENU=Copy Scan Behaviors;
                             ESM=Copiar los comportamientos de escaneo;
                             FRC=Comportements de num‚risation;
                             ENC=Copy Scan Behaviors];
                  SourceExpr=bCopyScanBehaviors }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      codFromAppName@1000000000 : Code[20];
      codToAppName@1000000001 : Code[20];
      bCopyMenus@1000000002 : Boolean;
      bCopyScanBehaviors@1000000003 : Boolean;
      cuTranslationMgmt@1000000004 : Codeunit 23044526;

    PROCEDURE CopyApplication@1000000004();
    VAR
      lrecFromApplication@1000000000 : Record 23044527;
      lrecToApplication@1000000001 : Record 23044527;
    BEGIN
      //<FUNC>
      //  Description: Copies the application
      //  Side Effect: If copy to application does not exist it will be created
      //</FUNC>

      lrecFromApplication.GET(codFromAppName);

      IF( NOT lrecToApplication.GET(codToAppName) ) THEN BEGIN
        lrecToApplication.VALIDATE(Code,codToAppName);
        lrecToApplication.INSERT(TRUE);
      END;

      lrecFromApplication.CALCFIELDS("Application Blob");

      //lrecToApplication.COPY(lrecFromApplication);
      lrecToApplication.Description := lrecFromApplication.Description;
      lrecToApplication.Events := lrecFromApplication.Events;
      lrecToApplication."Application Blob" := lrecFromApplication."Application Blob";
      lrecToApplication.MODIFY(TRUE);

      IF bCopyMenus THEN
        CopyMenus(lrecFromApplication, lrecToApplication);

      IF bCopyScanBehaviors THEN
        CopyScanBehaviors(lrecFromApplication,lrecToApplication);

      MESSAGE(cuTranslationMgmt.getTranslatedString('MSG_APPLICATION_COPIED', ''));
    END;

    PROCEDURE CopyMenus@1000000001(precFromApplication@1000000000 : Record 23044527;precToApplication@1000000001 : Record 23044527);
    VAR
      lrecPDAMenuFrom@1000000002 : Record 23044510;
      lrecPDAMenuTo@1000000003 : Record 23044510;
    BEGIN
      //<FUNC>
      //  Description: Copies the application menus
      //</FUNC>

      lrecPDAMenuFrom.SETRANGE("Form ID", lrecPDAMenuFrom."Form ID"::"Custom Application");
      lrecPDAMenuFrom.SETRANGE("Application Screen",precFromApplication.Code);
      IF lrecPDAMenuFrom.FINDSET(FALSE) THEN BEGIN
        REPEAT
          CLEAR(lrecPDAMenuTo);
          lrecPDAMenuTo.COPY(lrecPDAMenuFrom);
          lrecPDAMenuTo."Application Screen" := precToApplication.Code;
          lrecPDAMenuTo.INSERT;
        UNTIL(lrecPDAMenuFrom.NEXT=0);
      END;
    END;

    PROCEDURE CopyScanBehaviors@1000000002(precFromApplication@1000000001 : Record 23044527;precToApplication@1000000000 : Record 23044527);
    VAR
      lrecScanBehaviorFrom@1000000002 : Record 23044521;
      lrecScanBehaviorTo@1000000003 : Record 23044521;
    BEGIN
      //<FUNC>
      //  Description: Copies the application scan behaviors
      //</FUNC>


      lrecScanBehaviorFrom.SETRANGE("Application Screen",precFromApplication.Code);
      IF lrecScanBehaviorFrom.FINDSET(FALSE) THEN BEGIN
        REPEAT
          CLEAR(lrecScanBehaviorTo);
          lrecScanBehaviorTo.COPY(lrecScanBehaviorFrom);
          lrecScanBehaviorTo."Application Screen" := precToApplication.Code;
          lrecScanBehaviorTo.INSERT;
        UNTIL(lrecScanBehaviorFrom.NEXT=0);
      END;
    END;

    PROCEDURE InitFromRecord@1000000008(precFromApplication@1000000000 : Record 23044527);
    BEGIN
      //<FUNC>
      //  Description: Initalize the report if being called from another object
      //</FUNC>

      codFromAppName := precFromApplication.Code;
    END;

    BEGIN
    {
      ************************
      Copyright Notice
      This objects content is copyright of Insight Works 2011.  All rights reserved.
      Any redistribution or reproduction of part or all of the contents in any form is prohibited.
      ************************
    }
    END.
  }
  RDLDATA
  {
  }
}

