OBJECT Report 295 Combine Shipments
{
  OBJECT-PROPERTIES
  {
    Date=09/24/20;
    Time=[ 5:34:49 AM];
    Modified=Yes;
    Version List=NAVW111.00,SE0.60.18;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Combine Shipments;
               ESM=Fact. autom tica;
               FRC=Combiner les livraisons;
               ENC=Combine Shipments];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 2495;    ;DataItem;SalesOrderHeader    ;
               DataItemTable=Table36;
               DataItemTableView=SORTING(Document Type,Combine Shipments,Bill-to Customer No.,Currency Code,EU 3-Party Trade,Dimension Set ID)
                                 WHERE(Document Type=CONST(Order),
                                       Combine Shipments=CONST(Yes));
               ReqFilterHeadingML=[ENU=Sales Order;
                                   ESM=Pedido venta;
                                   FRC=Document de vente;
                                   ENC=Sales Order];
               OnPreDataItem=BEGIN
                               IF PostingDateReq = 0D THEN
                                 ERROR(Text000);
                               IF DocDateReq = 0D THEN
                                 ERROR(Text001);

                               Window.OPEN(
                                 Text002 +
                                 Text003 +
                                 Text004 +
                                 Text005);
                             END;

               OnAfterGetRecord=BEGIN
                                  CurrReport.LANGUAGE := Language.GetLanguageID("Language Code");

                                  Window.UPDATE(1,"Bill-to Customer No.");
                                  Window.UPDATE(2,"No.");
                                END;

               OnPostDataItem=BEGIN
                                CurrReport.LANGUAGE := GLOBALLANGUAGE;
                                Window.CLOSE;
                                IF SalesHeader."No." <> '' THEN BEGIN // Not the first time
                                  FinalizeSalesInvHeader;
                                  IF (NoOfSalesInvErrors = 0) AND NOT HideDialog THEN BEGIN
                                    IF NoOfskippedShiment > 0 THEN BEGIN
                                      MESSAGE(
                                        Text011,
                                        NoOfSalesInv,NoOfskippedShiment)
                                    END ELSE
                                      MESSAGE(
                                        Text010,
                                        NoOfSalesInv);
                                  END ELSE
                                    IF NOT HideDialog THEN
                                      IF PostInv THEN
                                        MESSAGE(
                                          Text007,
                                          NoOfSalesInvErrors)
                                      ELSE
                                        MESSAGE(
                                          NotAllInvoicesCreatedMsg,
                                          NoOfSalesInvErrors)
                                END ELSE
                                  IF NOT HideDialog THEN
                                    MESSAGE(Text008);
                              END;

               ReqFilterFields=Sell-to Customer No.,Bill-to Customer No. }

    { 3595;1   ;DataItem;                    ;
               DataItemTable=Table110;
               DataItemTableView=SORTING(Order No.);
               ReqFilterHeadingML=[ENU=Posted Sales Shipment;
                                   ESM=Hist¢rico remisiones venta;
                                   FRC=Livraison vente report‚e;
                                   ENC=Posted Sales Shipment];
               OnAfterGetRecord=VAR
                                  DueDate@1000 : Date;
                                  PmtDiscDate@1001 : Date;
                                  PmtDiscPct@1002 : Decimal;
                                BEGIN
                                  Window.UPDATE(3,"No.");

                                  IF IsCompletlyInvoiced THEN
                                    CurrReport.SKIP;

                                  IF OnlyStdPmtTerms THEN BEGIN
                                    Cust.GET("Bill-to Customer No.");
                                    PmtTerms.GET(Cust."Payment Terms Code");
                                    IF PmtTerms.Code = "Payment Terms Code" THEN BEGIN
                                      DueDate := CALCDATE(PmtTerms."Due Date Calculation","Document Date");
                                      PmtDiscDate := CALCDATE(PmtTerms."Discount Date Calculation","Document Date");
                                      PmtDiscPct := PmtTerms."Discount %";
                                      IF (DueDate <> "Due Date") OR
                                         (PmtDiscDate <> "Pmt. Discount Date") OR
                                         (PmtDiscPct <> "Payment Discount %")
                                      THEN BEGIN
                                        NoOfskippedShiment := NoOfskippedShiment + 1;
                                        CurrReport.SKIP;
                                      END;
                                    END ELSE BEGIN
                                      NoOfskippedShiment := NoOfskippedShiment + 1;
                                      CurrReport.SKIP;
                                    END;
                                  END;
                                END;

               ReqFilterFields=Posting Date;
               DataItemLink=Order No.=FIELD(No.) }

    { 2502;2   ;DataItem;                    ;
               DataItemTable=Table111;
               DataItemTableView=SORTING(Document No.,Line No.);
               OnAfterGetRecord=BEGIN
                                  IF Type = 0 THEN BEGIN
                                    IF (NOT CopyTextLines) OR ("Attached to Line No." <> 0) THEN
                                      CurrReport.SKIP;
                                  END;

                                  IF "Authorized for Credit Card" THEN
                                    CurrReport.SKIP;

                                  IF ("Qty. Shipped Not Invoiced" <> 0) OR (Type = 0) THEN BEGIN
                                    IF ("Bill-to Customer No." <> Cust."No.") AND
                                       ("Sell-to Customer No." <> '')
                                    THEN
                                      Cust.GET("Bill-to Customer No.");
                                    IF NOT (Cust.Blocked IN [Cust.Blocked::All,Cust.Blocked::Invoice]) THEN BEGIN
                                      IF (SalesOrderHeader."Bill-to Customer No." <> SalesHeader."Bill-to Customer No.") OR
                                         (SalesOrderHeader."Currency Code" <> SalesHeader."Currency Code") OR
                                         (SalesOrderHeader."EU 3-Party Trade" <> SalesHeader."EU 3-Party Trade") OR
                                         (SalesOrderHeader."Dimension Set ID" <> SalesHeader."Dimension Set ID")
                                      THEN BEGIN
                                        IF SalesHeader."No." <> '' THEN
                                          FinalizeSalesInvHeader;
                                        InsertSalesInvHeader;
                                        SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
                                        SalesLine.SETRANGE("Document No.",SalesHeader."No.");
                                        SalesLine."Document Type" := SalesHeader."Document Type";
                                        SalesLine."Document No." := SalesHeader."No.";
                                      END;
                                      SalesShptLine := "Sales Shipment Line";
                                      HasAmount := HasAmount OR ("Qty. Shipped Not Invoiced" <> 0);
                                      SalesShptLine.InsertInvLineFromShptLine(SalesLine);
                                    END ELSE
                                      NoOfSalesInvErrors := NoOfSalesInvErrors + 1;
                                  END;
                                END;

               OnPostDataItem=VAR
                                SalesShipmentLine@1000 : Record 111;
                                SalesLineInvoice@1002 : Record 37;
                                SalesGetShpt@1001 : Codeunit 64;
                              BEGIN
                                SalesShipmentLine.SETRANGE("Document No.","Document No.");
                                SalesShipmentLine.SETRANGE(Type,Type::"Charge (Item)");
                                IF SalesShipmentLine.FINDSET THEN
                                  REPEAT
                                    SalesLineInvoice.SETRANGE("Document Type",SalesLineInvoice."Document Type"::Invoice);
                                    SalesLineInvoice.SETRANGE("Document No.",SalesHeader."No.");
                                    SalesLineInvoice.SETRANGE("Shipment Line No.",SalesShipmentLine."Line No.");
                                    IF SalesLineInvoice.FINDFIRST THEN
                                      SalesGetShpt.GetItemChargeAssgnt(SalesShipmentLine,SalesLineInvoice."Qty. to Invoice");
                                  UNTIL SalesShipmentLine.NEXT = 0;
                              END;

               DataItemLink=Document No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF PostingDateReq = 0D THEN
                     PostingDateReq := WORKDATE;
                   IF DocDateReq = 0D THEN
                     DocDateReq := WORKDATE;
                   SalesSetup.GET;
                   CalcInvDisc := SalesSetup."Calc. Inv. Discount";
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 1   ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[ENU=Posting Date;
                             ESM=Fecha registro;
                             FRC=Date de report;
                             ENC=Posting Date];
                  ToolTipML=[ENU=Specifies the posting date for the invoice(s) that the batch job creates. This field must be filled in.;
                             ESM=Especifica la fecha de la factura o facturas que va a crear el proceso. Este campo debe rellenarse.;
                             FRC=Sp‚cifie la date de report de la ou des factures cr‚‚es par le traitement en lot. Ce champ doit ˆtre renseign‚.;
                             ENC=Specifies the posting date for the invoice(s) that the batch job creates. This field must be filled in.];
                  ApplicationArea=#Advanced;
                  SourceExpr=PostingDateReq }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Document Date;
                             ESM=Fecha emisi¢n documento;
                             FRC=Date document;
                             ENC=Document Date];
                  ToolTipML=[ENU=Specifies the document date for the invoice(s) that the batch job creates. This field must be filled in.;
                             ESM=Especifica la fecha de emisi¢n de la factura o facturas que va a crear el proceso. Este campo debe cumplimentarse.;
                             FRC=Sp‚cifie la date document des factures cr‚‚es par le traitement en lot. Ce champ doit ˆtre renseign‚.;
                             ENC=Specifies the document date for the invoice(s) that the batch job creates. This field must be filled in.];
                  ApplicationArea=#Advanced;
                  SourceExpr=DocDateReq }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Calc. Inv. Discount;
                             ESM=Calc. dto. factura;
                             FRC=Calculer escompte facture;
                             ENC=Calc. Inv. Discount];
                  ToolTipML=[ENU=Specifies if you want the invoice discount amount to be automatically calculated on the shipment.;
                             ESM=Especifica si desea que el importe de descuento en factura se calcule autom ticamente en el env¡o.;
                             FRC=Sp‚cifie si vous souhaitez que le montant d'escompte de la facture soit calcul‚ automatiquement … la livraison.;
                             ENC=Specifies if you want the invoice discount amount to be automatically calculated on the shipment.];
                  ApplicationArea=#Advanced;
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               SalesSetup.GET;
                               SalesSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Post Invoices;
                             ESM=Registrar facturas;
                             FRC=Reporter factures;
                             ENC=Post Invoices];
                  ToolTipML=[ENU=Specifies if you want to have the invoices posted immediately.;
                             ESM=Especifica si desea que las facturas se registren inmediatamente.;
                             FRC=Sp‚cifie si vous souhaitez que les factures soient imm‚diatement report‚es.;
                             ENC=Specifies if you want to have the invoices posted immediately.];
                  ApplicationArea=#Advanced;
                  SourceExpr=PostInv }

      { 6   ;2   ;Field     ;
                  CaptionML=[ENU=Only Std. Payment Terms;
                             ESM=S¢lo plazos de pago est ndar;
                             FRC=Seulement les conditions de paiement standard;
                             ENC=Only Std. Payment Terms];
                  ToolTipML=[ENU=Specifies if you want to include shipments with standard payments terms. If you select this option, you must manually invoice all other shipments.;
                             ESM=Especifica si desea incluir remisiones con plazos de pago est ndar. Si selecciona esta opci¢n, deber  facturar manualmente el resto de las remisiones.;
                             FRC=Sp‚cifie si vous souhaitez inclure les livraisons ayant des conditions de paiement standard. Si vous s‚lectionnez cette option, vous devez facturer manuellement toutes les autres livraisons.;
                             ENC=Specifies if you want to include shipments with standard payments terms. If you select this option, you must manually invoice all other shipments.];
                  ApplicationArea=#Advanced;
                  SourceExpr=OnlyStdPmtTerms }

      { 8   ;2   ;Field     ;
                  CaptionML=[ENU=Copy Text Lines;
                             ESM=Copiar l¡neas texto;
                             FRC=Copier les lignes de texte;
                             ENC=Copy Text Lines];
                  ToolTipML=[ENU=Specifies if you want manually written text on the shipment lines to be copied to the invoice.;
                             ESM=Especifica si desea que el texto escrito manualmente en las l¡neas de env¡o se copie en la factura.;
                             FRC=Sp‚cifie si vous souhaitez que le texte r‚dig‚ manuellement sur les lignes livraison soit copi‚ sur la facture.;
                             ENC=Specifies if you want manually written text on the shipment lines to be copied to the invoice.];
                  ApplicationArea=#Advanced;
                  SourceExpr=CopyTextLines }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Enter the posting date.;ESM=Introduzca la fecha de registro.;FRC=Entrez une date de report.;ENC=Enter the posting date.';
      Text001@1001 : TextConst 'ENU=Enter the document date.;ESM=Introduzca la fecha de documento.;FRC=Entrez la date du document.;ENC=Enter the document date.';
      Text002@1002 : TextConst 'ENU=Combining shipments...\\;ESM=Agrupando remisiones...\\;FRC=Regrouper livraisons...\\;ENC=Combining shipments...\\';
      Text003@1003 : TextConst 'ENU=Customer No.    #1##########\;ESM=N§ cliente      #1##########\;FRC=Nø client       #1##########\;ENC=Customer No.    #1##########\';
      Text004@1004 : TextConst 'ENU=Order No.       #2##########\;ESM=N§ pedido       #2##########\;FRC=Nø commande     #2##########\;ENC=Order No.       #2##########\';
      Text005@1005 : TextConst 'ENU=Shipment No.    #3##########;ESM=N§ revisi¢n.    #3##########;FRC=Nø envoi        #3##########;ENC=Shipment No.    #3##########';
      Text007@1006 : TextConst 'ENU=Not all the invoices were posted. A total of %1 invoices were not posted.;ESM=No se registraron todas las facturas. %1 facturas est n pendientes de registrar.;FRC=Ce ne sont pas toutes les factures qui ont ‚t‚ report‚es. Un total de %1 factures n''a pas ‚t‚ report‚.;ENC=Not all the invoices were posted. A total of %1 invoices were not posted.';
      Text008@1007 : TextConst 'ENU=There is nothing to combine.;ESM=No hay remisiones para agrupar.;FRC=Il n''y a rien … combiner.;ENC=There is nothing to combine.';
      Text010@1008 : TextConst 'ENU=The shipments are now combined and the number of invoices created is %1.;ESM=Se han agrupado los env¡os y el n£mero de facturas creadas es %1.;FRC=Les exp‚ditions sont maintenant regroup‚es, et le nombre de factures cr‚‚es est de %1.;ENC=The shipments are now combined and the number of invoices created is %1.';
      SalesHeader@1010 : Record 36;
      SalesLine@1011 : Record 37;
      SalesShptLine@1012 : Record 111;
      SalesSetup@1013 : Record 311;
      Cust@1018 : Record 18;
      Language@1009 : Record 8;
      PmtTerms@1017 : Record 3;
      SalesCalcDisc@1019 : Codeunit 60;
      SalesPost@1020 : Codeunit 80;
      Window@1021 : Dialog;
      PostingDateReq@1022 : Date;
      DocDateReq@1023 : Date;
      CalcInvDisc@1024 : Boolean;
      PostInv@1025 : Boolean;
      OnlyStdPmtTerms@1015 : Boolean;
      HasAmount@1102601000 : Boolean;
      HideDialog@1034 : Boolean;
      NoOfSalesInvErrors@1027 : Integer;
      NoOfSalesInv@1028 : Integer;
      Text011@1029 : TextConst '@@@=%1-Number of invoices,%2-Number Of shipments;ENU=The shipments are now combined, and the number of invoices created is %1.\%2 Shipments with nonstandard payment terms have not been combined.;ESM=Se han agrupado los env¡os y el n£mero de facturas creadas es %1.\No se combinaron %2 env¡os con condiciones de pago no est ndar.;FRC=Les exp‚ditions sont maintenant regroup‚es, et le nombre de factures cr‚‚es est de %1.\%2 exp‚ditions avec des modalit‚s de paiement non standard n''ont pas ‚t‚ combin‚es.;ENC=The shipments are now combined, and the number of invoices created is %1.\%2 Shipments with nonstandard payment terms have not been combined.';
      NoOfskippedShiment@1030 : Integer;
      CopyTextLines@1035 : Boolean;
      NotAllInvoicesCreatedMsg@1014 : TextConst 'ENU=Not all the invoices were created. A total of %1 invoices were not created.;ESM=No se crearon todas las facturas. %1 facturas est n pendientes de crearse.;FRC=Les factures n''ont pas toutes ‚t‚ cr‚‚es. Au total, %1 factures n''ont pas ‚t‚ cr‚‚es.;ENC=Not all the invoices were created. A total of %1 invoices were not created.';

    LOCAL PROCEDURE FinalizeSalesInvHeader@2();
    BEGIN
      WITH SalesHeader DO BEGIN
        IF NOT HasAmount THEN BEGIN
          DELETE(TRUE);
          EXIT;
        END;
        IF CalcInvDisc THEN
          SalesCalcDisc.RUN(SalesLine);
        FIND;
        COMMIT;
        CLEAR(SalesCalcDisc);
        CLEAR(SalesPost);
        NoOfSalesInv := NoOfSalesInv + 1;
        IF PostInv THEN BEGIN
          CLEAR(SalesPost);
          IF NOT SalesPost.RUN(SalesHeader) THEN
            NoOfSalesInvErrors := NoOfSalesInvErrors + 1;
        END;
      END;
    END;

    LOCAL PROCEDURE InsertSalesInvHeader@1();
    BEGIN
      CLEAR(SalesHeader);
      WITH SalesHeader DO BEGIN
        INIT;
        "Document Type" := "Document Type"::Invoice;
        "No." := '';
        INSERT(TRUE);
        VALIDATE("Sell-to Customer No.",SalesOrderHeader."Bill-to Customer No.");
        IF "Bill-to Customer No." <> "Sell-to Customer No." THEN
          VALIDATE("Bill-to Customer No.",SalesOrderHeader."Bill-to Customer No.");
        VALIDATE("Posting Date",PostingDateReq);
        VALIDATE("Document Date",DocDateReq);
        VALIDATE("Currency Code",SalesOrderHeader."Currency Code");
        VALIDATE("EU 3-Party Trade",SalesOrderHeader."EU 3-Party Trade");
        "Salesperson Code" := SalesOrderHeader."Salesperson Code";
        "Shortcut Dimension 1 Code" := SalesOrderHeader."Shortcut Dimension 1 Code";
        "Shortcut Dimension 2 Code" := SalesOrderHeader."Shortcut Dimension 2 Code";
        "Dimension Set ID" := SalesOrderHeader."Dimension Set ID";

        // >> Shipping
        SalesSetup.GET;
        IF ("Sales Shipment Header".GETFILTER("External Document No.") <> '') AND
           ("Sales Shipment Header".GETFILTER("Bill of Lading No.") <> '') AND
           SalesSetup."Ext. Doc. No. Mandatory"
        THEN BEGIN
          IF ("Sales Shipment Header".GETFILTER("External Document No.") =
              "Sales Shipment Header"."External Document No.") AND
             ("Sales Shipment Header".GETFILTER("Bill of Lading No.") =
              "Sales Shipment Header"."Bill of Lading No.")
          THEN
            SetEShipFields(SalesHeader,FALSE)
        END ELSE
          SetEShipFields(SalesHeader,TRUE);
        // << Shipping

        MODIFY;
        COMMIT;
        HasAmount := FALSE;
      END;
    END;

    PROCEDURE InitializeRequest@5(NewPostingDate@1002 : Date;NewDocDate@1003 : Date;NewCalcInvDisc@1000 : Boolean;NewPostInv@1001 : Boolean;NewOnlyStdPmtTerms@1004 : Boolean;NewCopyTextLines@1005 : Boolean);
    BEGIN
      PostingDateReq := NewPostingDate;
      DocDateReq := NewDocDate;
      CalcInvDisc := NewCalcInvDisc;
      PostInv := NewPostInv;
      OnlyStdPmtTerms := NewOnlyStdPmtTerms;
      CopyTextLines := NewCopyTextLines;
    END;

    PROCEDURE SetHideDialog@7(NewHideDialog@1000 : Boolean);
    BEGIN
      HideDialog := NewHideDialog;
    END;

    PROCEDURE SetEShipFields@14000701(VAR CurrentSalesInvHeader@14000900 : Record 36;ByTradePartner@1240020000 : Boolean);
    VAR
      SalesShipmentHeader@14000901 : Record 110;
      EDISetup@1240020001 : Record 14002367;
      EDIRecDocHdr@1240020004 : Record 14002358;
      EDIRecordAssocDocument@1240020003 : Codeunit 14002380;
      OverrideExternalDocNo@1240020002 : Boolean;
      RecRef@1240020005 : RecordRef;
      OverrideInternalDocNo@1240020006 : Boolean;
    BEGIN
      OverrideExternalDocNo := FALSE;
      OverrideInternalDocNo := FALSE;
      WITH CurrentSalesInvHeader DO BEGIN
        IF ByTradePartner THEN BEGIN
          VALIDATE("External Document No.","Sales Shipment Header".GETFILTER("External Document No."));
          VALIDATE(
            "Invoice for Bill of Lading No.","Sales Shipment Header".GETFILTER("Bill of Lading No."));
          IF NOT EDISetup.READPERMISSION THEN
            EXIT;
          IF "Sales Shipment Header"."EDI Order" THEN BEGIN
            "EDI Internal Doc. No." := "Sales Shipment Header"."EDI Internal Doc. No.";
            VALIDATE("EDI Trade Partner","Sales Shipment Header"."EDI Trade Partner");
            SalesShipmentHeader.COPYFILTERS("Sales Shipment Header");
            IF SalesShipmentHeader.FIND('-') THEN BEGIN
              "EDI Order" := TRUE;
              "EDI Invoice" := TRUE;
              "EDI Sell-to Code" := SalesShipmentHeader."EDI Sell-to Code";
              "EDI Ship-to Code" := SalesShipmentHeader."EDI Ship-to Code";
              IF SalesShipmentHeader."Ship-to Code" <> '' THEN
                VALIDATE("Ship-to Code",SalesShipmentHeader."Ship-to Code");
              VALIDATE("Shipping Agent Code",SalesShipmentHeader."Shipping Agent Code");
              VALIDATE("E-Ship Agent Service",SalesShipmentHeader."E-Ship Agent Service");
              REPEAT
                IF "EDI Trade Partner" <> SalesShipmentHeader."EDI Trade Partner" THEN BEGIN
                  "EDI Order" := FALSE;
                  "EDI Internal Doc. No." := '';
                  "EDI Invoice" := FALSE;
                END;
                IF "Ship-to Code" <> "Sales Shipment Header"."Ship-to Code" THEN BEGIN
                  CLEAR("Ship-to Code");
                  "EDI Ship-to Code" := '';
                END;
                IF OverrideExternalDocNo = FALSE THEN
                  IF "External Document No." <> SalesShipmentHeader."External Document No." THEN
                    OverrideExternalDocNo := TRUE;
                IF EDIRecDocHdr.GET(SalesShipmentHeader."EDI Internal Doc. No.") THEN BEGIN
                  RecRef.OPEN(110);
                  IF RecRef.FIND('-') THEN BEGIN
                    RecRef.GETTABLE(SalesShipmentHeader);
                    EDIRecordAssocDocument.RecordAssocPostedDoc(
                      EDIRecDocHdr,RecRef,TRUE,"EDI Internal Doc. No.",'810');
                  END;
                  RecRef.CLOSE;
                END;
                IF ("Shipping Agent Code" <> SalesShipmentHeader."Shipping Agent Code") AND
                   ("E-Ship Agent Service" <> SalesShipmentHeader."E-Ship Agent Service")
                THEN BEGIN
                  "Shipping Agent Code" := '';
                  "E-Ship Agent Service" := '';
                END;
                IF "EDI Internal Doc. No." <> SalesShipmentHeader."EDI Internal Doc. No." THEN
                  IF OverrideInternalDocNo = FALSE THEN BEGIN
                    EDISetup.LOCKTABLE;
                    EDISetup.GET;
                    EDISetup."Last Internal Doc. No." := INCSTR(EDISetup."Last Internal Doc. No.");
                    "EDI Internal Doc. No." := EDISetup."Last Internal Doc. No.";
                    EDISetup.MODIFY;
                    OverrideInternalDocNo := TRUE;
                  END;
              UNTIL (SalesShipmentHeader.NEXT = 0) OR ("EDI Invoice" = FALSE);
            END;
            IF OverrideExternalDocNo THEN
              VALIDATE("External Document No.",('COMBINED' +' ' + FORMAT("Posting Date")));
          END;
        END ELSE BEGIN
          IF "Sales Shipment Header".GETFILTER("Ship-to Code") <> '' THEN
            VALIDATE("Ship-to Code","Sales Shipment Header".GETFILTER("Ship-to Code"));
          VALIDATE("External Document No.","Sales Shipment Header".GETFILTER("External Document No."));
          VALIDATE(
            "Invoice for Bill of Lading No.","Sales Shipment Header".GETFILTER("Bill of Lading No."));
          IF NOT EDISetup.READPERMISSION THEN
            EXIT;
          IF "Sales Shipment Header"."EDI Order" THEN BEGIN
            SalesShipmentHeader.COPYFILTERS("Sales Shipment Header");
            IF SalesShipmentHeader.FIND('-') THEN BEGIN
              "EDI Order" := TRUE;
              "EDI Internal Doc. No." := SalesShipmentHeader."EDI Internal Doc. No.";
              "EDI Trade Partner" := SalesShipmentHeader."EDI Trade Partner";
              "EDI Sell-to Code" := SalesShipmentHeader."EDI Sell-to Code";
              "EDI Ship-to Code" := SalesShipmentHeader."EDI Ship-to Code";
              "EDI Invoice" := TRUE;
              VALIDATE("Shipping Agent Code",SalesShipmentHeader."Shipping Agent Code");
              VALIDATE("E-Ship Agent Service",SalesShipmentHeader."E-Ship Agent Service");
              REPEAT
                IF "EDI Internal Doc. No." <> SalesShipmentHeader."EDI Internal Doc. No." THEN BEGIN
                  "EDI Order" := FALSE;
                  "EDI Internal Doc. No." := '';
                  "EDI Invoice" := FALSE;
                END;
                IF ("Shipping Agent Code" <> SalesShipmentHeader."Shipping Agent Code") AND
                   ("E-Ship Agent Service" <> SalesShipmentHeader."E-Ship Agent Service")
                THEN BEGIN
                  "Shipping Agent Code" := '';
                  "E-Ship Agent Service" := '';
                END;
              UNTIL SalesShipmentHeader.NEXT = 0;
            END;
          END;
        END;
      END;
    END;

    PROCEDURE SetVariables@1240020000();
    BEGIN
      IF PostingDateReq = 0D THEN
        PostingDateReq := WORKDATE;
      IF DocDateReq = 0D THEN
        DocDateReq := WORKDATE;
      SalesSetup.GET;
      CalcInvDisc := SalesSetup."Calc. Inv. Discount";
      PostInv := TRUE;
      HideDialog := TRUE;
      CurrReport.USEREQUESTPAGE := FALSE;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

