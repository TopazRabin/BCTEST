OBJECT Report 51016 Test - Create EDI Sales Order
{
  OBJECT-PROPERTIES
  {
    Date=07/01/20;
    Time=12:00:00 AM;
    Modified=Yes;
    Version List=TPZ000.00.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Test - Create EDI Sales Order;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  SalesSetup.GET;
                  SalesSetup."Enable Mail" := FALSE;
                  SalesSetup."Enable Fax" := FALSE;
                  SalesSetup."Enable E-Mail" := FALSE;
                  SalesSetup.MODIFY;
                  COMMIT;

                  SetParameters;
                END;

    OnPostReport=BEGIN
                   MESSAGE('Completed');
                 END;

  }
  DATASET
  {
    { 1000000000;;DataItem;Customer          ;
               DataItemTable=Table18;
               DataItemTableView=WHERE(Blocked=FILTER(' '),
                                       EDI Trade Partner=FILTER(<>''),
                                       EDI Invoice=FILTER(Always),
                                       Bill-to Customer No.=FILTER(<>''));
               OnAfterGetRecord=BEGIN
                                  CLEAR(ItemNo);

                                  IF "External No." = '' THEN
                                    "External No." := '123456';
                                  MODIFY;
                                END;

               ReqFilterFields=No.,EDI Trade Partner }

    { 1000000001;1;DataItem;CustomerDivision ;
               DataItemTable=Table50007;
               OnAfterGetRecord=BEGIN
                                  FOR i := 1 TO OrderQty DO BEGIN
                                    SalesHeader.INIT;
                                    SalesHeader.SetHideValidationDialog(TRUE);
                                    SalesHeader.VALIDATE("Document Type",SalesHeader."Document Type"::Order);
                                    SalesHeader.VALIDATE("Sell-to Customer No.",CustomerDivision."Customer No.");
                                    SalesHeader.INSERT(TRUE);
                                    COMMIT;
                                    SalesHeader.VALIDATE("Shortcut Dimension 5 Code",CustomerDivision."Shortcut Dimension 5 Code");
                                    SalesHeader."External Document No." := 'Test' + CustomerDivision."Customer No." + CustomerDivision."Shortcut Dimension 5 Code" + FORMAT(i);
                                    SalesHeader.VALIDATE("Location Code",LocationCode);
                                    IF SalesHeader."Shipping Agent Code" = '' THEN
                                      SalesHeader.VALIDATE("Shipping Agent Code",'PICKUP');
                                    SalesHeader.MODIFY(TRUE);

                                    LineNo := 0;

                                    FOR j := 1 TO ItemQty DO BEGIN
                                      LineNo := LineNo + 10000;
                                      SalesLine.INIT;
                                      SalesLine.SetHideValidationDialog(TRUE);
                                      SalesLine."Document Type" := SalesHeader."Document Type";
                                      SalesLine."Document No." := SalesHeader."No.";
                                      SalesLine."Line No." := LineNo;
                                      SalesLine.INSERT(TRUE);
                                      SalesLine.VALIDATE("Sell-to Customer No.",SalesHeader."Sell-to Customer No.");
                                      SalesLine.Type := SalesLine.Type::Item;
                                      ItemNo := GetItem;
                                      SalesLine.VALIDATE("No.",ItemNo);
                                      SalesLine.VALIDATE("Location Code",SalesHeader."Location Code");
                                      SalesLine.VALIDATE(Quantity,1);
                                      SalesLine.MODIFY(TRUE);
                                    END;

                                    COMMIT;

                                    IF CODEUNIT.RUN(CODEUNIT::"Release Sales Document",SalesHeader) THEN BEGIN
                                      Released := TRUE;
                                      COMMIT;
                                    END;

                                    IF Released THEN BEGIN
                                      SalesHeader.Ship := TRUE;
                                      SalesHeader.Invoice := TRUE;
                                      IF CODEUNIT.RUN(CODEUNIT::"Sales-Post",SalesHeader) THEN
                                        Posted := TRUE;
                                    END;
                                    COMMIT;
                                  END;
                                END;

               DataItemLink=Customer No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      SalesHeader@1000000000 : Record 36;
      SalesHeader2@1000000015 : Record 36;
      SalesHeader3@1000000016 : Record 36;
      SalesLine@1000000001 : Record 37;
      Item@1000000002 : Record 27;
      Location@1000000003 : Record 14;
      OrderQty@1000000004 : Integer;
      ItemQty@1000000006 : Integer;
      i@1000000005 : Integer;
      j@1000000009 : Integer;
      LocationCode@1000000007 : Code[2];
      ItemNo@1000000008 : Code[20];
      ReleaseSalesOrder@1000000010 : Codeunit 414;
      Released@1000000011 : Boolean;
      Posted@1000000012 : Boolean;
      SalesSetup@1000000013 : Record 311;
      LineNo@1000000014 : Integer;

    PROCEDURE SetParameters@1000000000();
    BEGIN
      OrderQty := 1;
      ItemQty := 5;
      LocationCode := '26';
    END;

    PROCEDURE GetItem@1000000004() : Code[20];
    VAR
      SalesLine3@1000000000 : Record 37;
    BEGIN
      Item.RESET;
      Item.SETRANGE(Blocked,FALSE);//Item.Blocked::"0"); TPZ2785
      Item.SETFILTER("No.",'<>%1',ItemNo);
      Item.SETRANGE("Inventory Posting Group",CustomerDivision."Shortcut Dimension 5 Code");
      Item.FINDSET;
      REPEAT
        Item.SETFILTER("Location Filter",LocationCode);
        Item.CALCFIELDS(Inventory);
        IF Item.Inventory > 0 THEN BEGIN
          EXIT(Item."No.");
        END;
      UNTIL Item.NEXT = 0;
    END;

    BEGIN
    {
      TPZ2785 05112020 GGUPTA Remove Item blocking Topaz Customization
    }
    END.
  }
  RDLDATA
  {
  }
}

