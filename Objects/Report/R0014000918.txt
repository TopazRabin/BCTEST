OBJECT Report 14000918 Issued Fin. Charge Memo E-Mail
{
  OBJECT-PROPERTIES
  {
    Date=06/28/17;
    Time=12:00:00 PM;
    Version List=SE0.60.11;
  }
  PROPERTIES
  {
    CaptionML=ENU=Issued Fin. Charge Memo E-Mail;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 5444;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               ShowStatus := FALSE;

                               IF ShowStatus THEN
                                 Window.OPEN(
                                   Text001 +
                                   Text002);
                             END;

               OnAfterGetRecord=BEGIN
                                  CLEAR(FieldValue);

                                  EMailSetup.GET;
                                  EMailSetup.TESTFIELD("Issued Fin.Charge Memo E-Mail");

                                  IF NOT GetInitialRecords THEN BEGIN
                                    ModifyRecords;
                                    EXIT;
                                  END;

                                  IF EMailHeader."Line Break Character" <> '' THEN
                                    MailInt.LineFeedChar := EMailHeader."Line Break Character";

                                  // Create Subject Line
                                  IF ShowStatus THEN
                                    Window.UPDATE(1,Text003);
                                  MailInt.InitializeMail(FieldValue.SubstituteHeader(EMailHeader));

                                  // Address the mail
                                  IF ShowStatus THEN
                                    Window.UPDATE(1,Text004);

                                  ReceiverTo := '';
                                  ReceiverCC := '';
                                  ReceiverBCC := '';
                                  EMailListEntryTmp.RemoveEntriesWithSameEMail(EMailListEntryTmp);
                                  REPEAT
                                    CASE EMailListEntryTmp."Issued Fin. Charge Memo E-Mail" OF
                                      EMailListEntryTmp."Issued Fin. Charge Memo E-Mail"::"To":
                                        CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'TO');
                                      EMailListEntryTmp."Issued Fin. Charge Memo E-Mail"::CC:
                                        CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'CC');
                                      EMailListEntryTmp."Issued Fin. Charge Memo E-Mail"::BCC:
                                        CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'BCC');
                                    END;
                                  UNTIL EMailListEntryTmp.NEXT = 0;

                                  IF ReceiverTo <> '' THEN
                                    MailInt.AddReceiverTo(ReceiverTo);

                                  IF ReceiverCC <> '' THEN
                                    MailInt.AddReceiverCC(ReceiverCC);

                                  IF ReceiverBCC <> '' THEN
                                    MailInt.AddReceiverBCC(ReceiverBCC);

                                  // Create the body of the E-Mail
                                  RecordFound[1] := TRUE;
                                  LastLevel := 1;
                                  LastType := 0;
                                  EMailLine.RESET;
                                  EMailLine.SETRANGE("E-Mail Code",EMailHeader.Code);
                                  IF EMailLine.FIND('-') THEN
                                    REPEAT
                                      IF ShowStatus THEN
                                        Window.UPDATE(1,EMailLine.Text);
                                      EMailLine.TESTFIELD(Level);

                                      // Handle Higher Level
                                      IF LastLevel < EMailLine.Level THEN BEGIN
                                        GetFirstRecordInLoop(EMailLine.Level);
                                        FirstOnLevel[EMailLine.Level] := TRUE;
                                        EMailLineFirstOnLevel[EMailLine.Level] := EMailLine;
                                      END;

                                      // Handle Skip Level if no records
                                      IF FirstOnLevel[EMailLine.Level] AND
                                         NOT RecordFound[EMailLine.Level] AND
                                         EMailLine."Include only level if details"
                                      THEN BEGIN
                                        EMailLine.SETFILTER(Level,'<%1',EMailLine.Level);
                                        Finished := EMailLine.NEXT = 0;
                                        EMailLine.SETRANGE(Level);
                                      END ELSE BEGIN
                                        // Substitute Values in Line and add to E-Mail
                                        IF (EMailLine.Type <> EMailLine.Type::Loop) OR
                                           RecordFound[EMailLine.Level]
                                        THEN
                                          MailInt.AddtoBody(FieldValue.SubstituteLine(EMailLine));

                                        // Get Next E-Mail Line
                                        LastType := EMailLine.Type;
                                        LastLevel := EMailLine.Level;
                                        Finished := EMailLine.NEXT = 0;

                                        // Handle Loop End or Return to Lower Level
                                        IF (LastType = EMailLine.Type::Loop) AND
                                           (((EMailLine.Type <> EMailLine.Type::Loop) AND
                                             (LastLevel = EMailLine.Level)) OR
                                            (LastLevel > EMailLine.Level)) AND
                                           RecordFound[LastLevel]
                                        THEN BEGIN
                                          GetNextRecordInLoop(LastLevel);

                                          // Repeat Loop if Record found
                                          IF RecordFound[LastLevel] THEN BEGIN
                                            EMailLine := EMailLineFirstOnLevel[LastLevel];
                                            EMailLine.SETRANGE(Type,EMailLine.Type::Loop);
                                            EMailLine.NEXT;
                                            EMailLine.SETRANGE(Type);
                                          END;
                                        END;
                                      END;

                                      FirstOnLevel[EMailLine.Level] := FALSE;
                                    UNTIL Finished;

                                  // Create and add attachment
                                  IF ShowStatus THEN
                                    Window.UPDATE(1,Text005);
                                  EMailAttachmentTmp.DELETEALL;
                                  EMailAttachment.RESET;
                                  EMailAttachment.SETRANGE("E-Mail Code",EMailHeader.Code);
                                  IF EMailAttachment.FIND('-') THEN BEGIN
                                    EMailSetup.TESTFIELD("E-Mail Buffer Directory");

                                    REPEAT
                                      CASE EMailAttachment.Type OF
                                        EMailAttachment.Type::Report:
                                          BEGIN
                                            EMailAttachment.TESTFIELD("Attachment Name");
                                            EMailAttachment.TESTFIELD("Attachment Report ID");

                                            TempFilename := EMailSetup."E-Mail Buffer Directory";
                                            IF COPYSTR(TempFilename,STRLEN(TempFilename),1) <> '\' THEN
                                              TempFilename := TempFilename + '\';
                                            TempFilename := TempFilename + FieldValue.SubstituteAttachment(EMailAttachment);

                                            SaveReport(EMailAttachment."Attachment Report ID",TempFilename);
                                            MailInt.AddAttachment(TempFilename);

                                            EMailAttachmentTmp := EMailAttachment;
                                            EMailAttachmentTmp."Use Attachment File Name" := TempFilename;
                                            EMailAttachmentTmp.INSERT;
                                          END;
                                        EMailAttachment.Type::"Fixed File":
                                          BEGIN
                                            IF NOT EXISTS(EMailAttachment."Fixed File Name") THEN
                                              ERROR(Text006,EMailAttachment."Fixed File Name");

                                            MailInt.AddAttachment(EMailAttachment."Fixed File Name");
                                          END;
                                      END;
                                    UNTIL EMailAttachment.NEXT = 0;
                                  END;

                                  // Send e-mail
                                  IF ShowStatus THEN
                                    Window.UPDATE(1,Text007);
                                  MailInt.Send(EMailHeader."Confirm E-Mail manually");

                                  CLEAR(MailInt);

                                  IF EMailAttachmentTmp.FIND('-') THEN
                                    REPEAT
                                      MailInt.DeleteAttachment(EMailAttachmentTmp."Use Attachment File Name");
                                    UNTIL EMailAttachmentTmp.NEXT = 0;

                                  // Update the Records
                                  ModifyRecords;
                                END;

               OnPostDataItem=BEGIN
                                IF ShowStatus THEN
                                  Window.CLOSE;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      IssuedFinChargeMemoHeader@1000000000 : Record 304;
      IssuedFinChargeMemoLine@1000000001 : Record 305;
      FinChargeMemoCommentLine@1000000002 : Record 306;
      CompanyInformation@1000000003 : Record 79;
      Customer@1000000004 : Record 18;
      PaymentTerms@1000000005 : Record 3;
      EMailSetup@1000000009 : Record 14000905;
      EMailRule@1000000010 : Record 14000907;
      EMailHeader@1000000011 : Record 14000901;
      EMailLine@1000000012 : Record 14000902;
      EMailLineFirstOnLevel@1000000013 : ARRAY [10] OF Record 14000902;
      EMailListEntryTmp@1000000014 : TEMPORARY Record 14000908;
      EMailAttachment@1000000015 : Record 14000909;
      EMailAttachmentTmp@1000000016 : TEMPORARY Record 14000909;
      FieldValue@1000000017 : Codeunit 14000901;
      MailInt@1000000018 : Codeunit 14000905;
      Window@1000000019 : Dialog;
      RecordFound@1000000020 : ARRAY [10] OF Boolean;
      FirstOnLevel@1000000021 : ARRAY [10] OF Boolean;
      Finished@1000000022 : Boolean;
      ShowStatus@1000000023 : Boolean;
      BatchProcessing@1240020004 : Boolean;
      LastLevel@1000000024 : Integer;
      LastType@1000000025 : Integer;
      IssuedFinChargeMemoNo@1000000026 : Code[20];
      TempFilename@1000000027 : Text[250];
      Text001@1001 : TextConst 'ENU=Creating E-Mail\';
      Text002@1002 : TextConst 'ENU=Current Line #1##################';
      Text003@1003 : TextConst 'ENU=Subject';
      Text004@1004 : TextConst 'ENU=Addressing';
      Text005@1005 : TextConst 'ENU=Attachments';
      Text006@1006 : TextConst 'ENU=Attachment file %1 do not exists.';
      Text007@1007 : TextConst 'ENU=Sending';
      Text008@1008 : TextConst 'ENU=Level   Table\';
      Text009@1009 : TextConst 'ENU="  1     Issued Fin. Charge Memo Header (304)\"';
      Text010@1010 : TextConst 'ENU="  2       Issued Finance Charge Memo Line (305)\"';
      Text011@1011 : TextConst 'ENU="  3       Fin. Charge Comment Line (306)\\"';
      Text012@1012 : TextConst 'ENU=Other tables\';
      Text013@1013 : TextConst 'ENU=Payment Terms (3), Customer (18), Company Information (79)';
      Text014@1014 : TextConst 'ENU=%1 is not a supported level.';
      Text015@1015 : TextConst 'ENU=Get E-Mail List must be empty.';
      ReceiverTo@1240020003 : Text[250];
      ReceiverCC@1240020002 : Text[250];
      ReceiverBCC@1240020001 : Text[250];

    PROCEDURE CreateEmailAddressStr@1240020000(EmailAddressStr@1240020000 : Text[250];Type@1240020001 : Text[10]);
    BEGIN
      CASE Type OF
        'TO':
          IF ReceiverTo = '' THEN
            ReceiverTo := EmailAddressStr
          ELSE
            ReceiverTo := ReceiverTo + ';' + EmailAddressStr;
        'CC':
          IF ReceiverCC = '' THEN
            ReceiverCC := EmailAddressStr
          ELSE
            ReceiverCC := ReceiverCC + ';' + EmailAddressStr;
        'BCC':
          IF ReceiverBCC = '' THEN
            ReceiverBCC := EmailAddressStr
          ELSE
            ReceiverBCC := ReceiverBCC + ';' + EmailAddressStr;
      END;
    END;

    PROCEDURE InitializeRequest@1(NewIssuedFinChargeMemoNo@1000000000 : Code[20];BatchSending@1240020000 : Boolean);
    BEGIN
      IssuedFinChargeMemoNo := NewIssuedFinChargeMemoNo;
      BatchProcessing := BatchSending;

      IF NewIssuedFinChargeMemoNo = 'SHOWHELP' THEN
        MESSAGE(
          Text008 +
          Text009 +
          Text010 +
          Text011 +
          Text012 +
          Text013);
    END;

    PROCEDURE GetLevelName@5(LevelNo@1000000000 : Integer) : Text[30];
    BEGIN
      CASE LevelNo OF
        1:
          EXIT(IssuedFinChargeMemoHeader.TABLENAME);
        2:
          EXIT(IssuedFinChargeMemoLine.TABLENAME);
        3:
          EXIT(FinChargeMemoCommentLine.TABLENAME);
        ELSE
          EXIT('');
      END;
    END;

    PROCEDURE GetTableFilter@6() : Text[250];
    BEGIN
      EXIT('3|18|79|304|305|306|14000901|14000907');
    END;

    PROCEDURE GetRecordNoStrMenu@9(CurrentTableNo@1000000000 : Integer) : Text[250];
    BEGIN
      CASE CurrentTableNo OF
        ELSE
          EXIT('');
      END;
    END;

    LOCAL PROCEDURE GetInitialRecords@2() : Boolean;
    VAR
      EMailListEntry@1240030000 : Record 14000908;
      SendMail@1000000000 : Boolean;
    BEGIN
      // Get and Load Header values
      IssuedFinChargeMemoHeader.GET(IssuedFinChargeMemoNo);
      FieldValue.IssuedFinChargeMemoHeader_304(IssuedFinChargeMemoHeader,0);
      CompanyInformation.GET;
      FieldValue.CompanyInformation_79(CompanyInformation,0);
      Customer.GET(IssuedFinChargeMemoHeader."Customer No.");
      FieldValue.Customer_18(Customer,0);
      IF NOT PaymentTerms.GET(Customer."Payment Terms Code") THEN
        CLEAR(PaymentTerms);
      FieldValue.PaymentTerms_3(PaymentTerms,0);

      // Find E-Mail receivers
      EMailListEntryTmp.RESET;
      EMailListEntryTmp.DELETEALL;

      EMailListEntry.RESET;
      EMailListEntry.SETRANGE("Table ID",DATABASE::Customer);
      EMailListEntry.SETRANGE(Code,Customer."No.");
      IF EMailListEntry.FIND('-') THEN
        REPEAT
          EMailListEntryTmp := EMailListEntry;
          EMailListEntryTmp.INSERT;
        UNTIL EMailListEntry.NEXT = 0;

      EMailListEntry.RESET;
      EMailListEntry.SETRANGE("Table ID",DATABASE::"Issued Fin. Charge Memo Header");
      EMailListEntry.SETRANGE(Code,IssuedFinChargeMemoHeader."No.");
      IF EMailListEntry.FIND('-') THEN
        REPEAT
          EMailListEntryTmp := EMailListEntry;
          EMailListEntryTmp.INSERT;
        UNTIL EMailListEntry.NEXT = 0;

      EMailListEntryTmp.SETRANGE("Issued Fin. Charge Memo E-Mail",1,3);
      EMailListEntryTmp.SETFILTER("E-Mail",'<>%1','');

      // Test That a Mail should be sent
      SendMail := EMailRule.GetEMailRule(0,Customer."No.",'');

      IF NOT SendMail THEN
        EXIT(FALSE);

      SendMail := EMailHeader.GET(EMailRule."Issued Fin Ch Memo E-Mail Code");

      IF NOT SendMail THEN
        EXIT(FALSE);

      FieldValue.EMailHeader_14000901(EMailHeader,0);
      FieldValue.EMailRule_14000907(EMailRule,0);

      EXIT(EMailListEntryTmp.FIND('-'));
    END;

    LOCAL PROCEDURE ModifyRecords@7();
    BEGIN
      IssuedFinChargeMemoHeader.GET(IssuedFinChargeMemoNo);
      IssuedFinChargeMemoHeader."E-Mail Confirmation Handled" := TRUE;
      IssuedFinChargeMemoHeader.MODIFY;
      COMMIT;
    END;

    LOCAL PROCEDURE GetFirstRecordInLoop@3(CurrentLevel@1000000000 : Integer);
    BEGIN
      CASE CurrentLevel OF
        2:
          BEGIN
            IssuedFinChargeMemoLine.RESET;
            IssuedFinChargeMemoLine.SETRANGE("Finance Charge Memo No.",IssuedFinChargeMemoHeader."No.");
            IF EMailHeader."Line Type Filter" <> '' THEN
              IssuedFinChargeMemoLine.SETFILTER(Type,EMailHeader."Line Type Filter");
            IF EMailHeader."Only Lines with Content" THEN
              IssuedFinChargeMemoLine.SETFILTER(Amount,'<>0');
            RecordFound[CurrentLevel] := IssuedFinChargeMemoLine.FIND('-');
            FieldValue.IssuedFinChargeMemoLine_305(IssuedFinChargeMemoLine,0);
          END;
        3:
          BEGIN
            FinChargeMemoCommentLine.RESET;
            FinChargeMemoCommentLine.SETRANGE(
              Type,FinChargeMemoCommentLine.Type::"Issued Finance Charge Memo");
            FinChargeMemoCommentLine.SETRANGE("No.",IssuedFinChargeMemoHeader."No.");
            FinChargeMemoCommentLine.SETRANGE("Include in E-Mail",TRUE);
            RecordFound[CurrentLevel] := FinChargeMemoCommentLine.FIND('-');
            FieldValue.FinChargeCommentLine_306(FinChargeMemoCommentLine,0);
          END;
        ELSE
          ERROR(Text014,CurrentLevel);
      END;
    END;

    LOCAL PROCEDURE GetNextRecordInLoop@4(CurrentLevel@1000000000 : Integer);
    BEGIN
      CASE CurrentLevel OF
        2:
          BEGIN
            RecordFound[CurrentLevel] := IssuedFinChargeMemoLine.NEXT <> 0;
            FieldValue.IssuedFinChargeMemoLine_305(IssuedFinChargeMemoLine,0);
          END;
        3:
          BEGIN
            RecordFound[CurrentLevel] := FinChargeMemoCommentLine.NEXT <> 0;
            FieldValue.FinChargeCommentLine_306(FinChargeMemoCommentLine,0);
          END;
        ELSE
          ERROR(Text014,CurrentLevel);
      END;
    END;

    PROCEDURE SaveReport@8(ReportNo@1000000000 : Integer;VAR SaveFileName@1000000001 : Text[250]);
    BEGIN
      IssuedFinChargeMemoHeader.SETRECFILTER;
      IF ISSERVICETIER THEN BEGIN
        EMailSetup.ChangeFilenameToPDF(SaveFileName);
        REPORT.SAVEASPDF(ReportNo,SaveFileName,IssuedFinChargeMemoHeader);
      END ELSE
        REPORT.SAVEASHTML(ReportNo,SaveFileName,IssuedFinChargeMemoHeader);
    END;

    PROCEDURE GetReceiverList@10(VAR GetEMailListEntryTmp@1240030000 : Record 14000908);
    BEGIN
      GetInitialRecords;

      GetEMailListEntryTmp.RESET;
      IF GetEMailListEntryTmp.FIND('-') THEN
        ERROR(Text015);

      EMailListEntryTmp.RESET;
      IF EMailListEntryTmp.FIND('-') THEN
        REPEAT
          GetEMailListEntryTmp := EMailListEntryTmp;
          GetEMailListEntryTmp.INSERT;
        UNTIL EMailListEntryTmp.NEXT = 0;
    END;

    BEGIN
    {
      The Code that customize the running of the report should be done in the 4 triggers
      GetInitialRecords
        This is where all the values needed for the general information in the E-Mail are retrived.
        Additional tables could be added in the information was needed.

      ModifyRecords
        This is the final step, where the status of some records are modified. This could be used to
        change a field telling that the E-Mail has been sent.

      GetFirstRecordInLoop
        This will get the first record in each level. This also adjust some variables and contol that the
        flow of multilevel tables are correct. Like the printing of Posted Container/Posted Container Line.

      GetNextRecordInLoop
        This will retrieve the next record for each level.

      SaveReport
        This will run and save a specific report in HTML or PDF(only for NAV 2009 RTC and higher).
    }
    END.
  }
  RDLDATA
  {
  }
}

