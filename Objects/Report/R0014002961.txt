OBJECT Report 14002961 Calculate Pallet Phys. Jnl.
{
  OBJECT-PROPERTIES
  {
    Date=01/22/18;
    Time=12:00:00 PM;
    Version List=RF1.50.11;
  }
  PROPERTIES
  {
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   DeleteExistingLines := TRUE;
                 END;

    OnPostReport=BEGIN
                   PalletJournalLine.SETRANGE("Journal Template Name",TemplateCode);
                   PalletJournalLine.SETRANGE("Journal Batch Name",BatchCode);
                   PalletJournalLine.SETRANGE("Qty. (Calculated)",0);
                   PalletJournalLine.DELETEALL;
                 END;

  }
  DATASET
  {
    { 3330;    ;DataItem;PalletBinContent    ;
               DataItemTable=Table14002963;
               OnPreDataItem=BEGIN
                               IF DeleteExistingLines THEN BEGIN
                                 PalletJournalLine.SETRANGE("Journal Template Name",TemplateCode);
                                 PalletJournalLine.SETRANGE("Journal Batch Name",BatchCode);
                                 PalletJournalLine.SETRANGE("Location Code",LocationCode);
                                 PalletJournalLine.DELETEALL;
                               END;

                               IF LocationCode <> '' THEN
                                 PalletBinContent.SETRANGE(Location,LocationCode);
                               IF BinCode <> '' THEN
                                 PalletBinContent.SETRANGE(Bin,BinCode);
                               IF ItemCode <> '' THEN
                                 PalletBinContent.SETRANGE("Item No.",ItemCode);
                               IF LotNo <> '' THEN
                                 PalletBinContent.SETRANGE("Lot No.",LotNo);
                             END;

               OnAfterGetRecord=VAR
                                  PalletJournalLine2@1240020001 : Record 14002966;
                                  Bin@1240020000 : Record 7354;
                                BEGIN
                                  IF NOT EmptyPalletCounting THEN
                                    IF PalletBinContent.Quantity = 0 THEN
                                      CurrReport.SKIP;

                                  PalletJournalLine2.RESET;
                                  PalletJournalLine2.SETRANGE("Journal Template Name",TemplateCode);
                                  PalletJournalLine2.SETRANGE("Journal Batch Name",BatchCode);
                                  PalletJournalLine2.SETRANGE("Location Code",PalletBinContent.Location);
                                  PalletJournalLine2.SETRANGE("Bin Code",PalletBinContent.Bin);
                                  IF PalletBinContent."Pallet No." <> '' THEN
                                    PalletJournalLine2.SETRANGE("Pallet No.",PalletBinContent."Pallet No.");
                                  IF PalletBinContent."Box No." <> '' THEN
                                    PalletJournalLine2.SETRANGE("Box No.",PalletBinContent."Box No.");
                                  IF PalletBinContent."Item No." <> '' THEN
                                    PalletJournalLine2.SETRANGE("Item No.",PalletBinContent."Item No.");
                                  IF PalletBinContent."Variant No." <> '' THEN
                                    PalletJournalLine2.SETRANGE("Variant Code",PalletBinContent."Variant No.")
                                  ELSE
                                    PalletJournalLine2.SETFILTER("Variant Code",'=%1','');
                                  IF PalletBinContent."Lot No." <> '' THEN
                                    PalletJournalLine2.SETRANGE("Lot No.",PalletBinContent."Lot No.");
                                  IF PalletBinContent."Serial No." <> '' THEN
                                    PalletJournalLine2.SETRANGE("Serial No.",PalletBinContent."Serial No.");
                                  IF PalletJournalLine2.FIND('-') THEN
                                    CurrReport.SKIP;

                                  PalletJournalLine2.RESET;
                                  PalletJournalLine2.LOCKTABLE(TRUE);
                                  PalletJournalLine2.SETRANGE("Journal Template Name",TemplateCode);
                                  PalletJournalLine2.SETRANGE("Journal Batch Name",BatchCode);
                                  PalletJournalLine2.SETRANGE("Location Code",PalletBinContent.Location);
                                  IF PalletJournalLine2.FINDLAST THEN ;

                                  PalletJournalLine.INIT;
                                  PalletJournalLine."Journal Template Name" := TemplateCode;
                                  PalletJournalLine."Journal Batch Name" := BatchCode;
                                  PalletJournalLine."Line No." := PalletJournalLine2."Line No." + 10000;

                                  PalletJournalLine."Registering Date" := WORKDATE;
                                  PalletJournalLine."Location Code" := PalletBinContent.Location;
                                  PalletJournalLine."Bin Code" := PalletBinContent.Bin;
                                  PalletJournalLine."Pallet No." := PalletBinContent."Pallet No.";
                                  PalletJournalLine."Box No." := PalletBinContent."Box No.";
                                  PalletJournalLine."Item No." := PalletBinContent."Item No.";
                                  PalletJournalLine."Variant Code" := PalletBinContent."Variant No.";
                                  PalletJournalLine."Unit of Measure Code" := PalletBinContent."Unit of Measure";
                                  PalletJournalLine."Qty. (Calculated)" := PalletBinContent.Quantity;
                                  PalletJournalLine."Qty. (Phys. Inventory)" := PalletBinContent.Quantity;
                                  PalletJournalLine."Qty. (Calculated) (Base)" := PalletBinContent."Quantity (Base)";
                                  PalletJournalLine."Qty. Per Unit of Measure" := PalletBinContent."Qty per UOM";
                                  PalletJournalLine."Phys. Inventory" := TRUE;
                                  PalletJournalLine."Lot No." := PalletBinContent."Lot No.";
                                  PalletJournalLine."Serial No." := PalletBinContent."Serial No.";
                                  PalletJournalLine."Original Qty. (Calculated)" := PalletBinContent.Quantity;
                                  PalletJournalLine.INSERT;
                                END;

               ReqFilterFields=Location,Bin,Pallet No.,Item No.,Last Handled Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PalletJournalLine@1240020010 : Record 14002966;
      BatchCode@1240020008 : Code[20];
      TemplateCode@1240020007 : Code[20];
      LocationCode@1240020006 : Code[20];
      BinCode@1240020005 : Code[20];
      ItemCode@1240020004 : Code[20];
      LotNo@1240020003 : Code[20];
      DeleteExistingLines@1240020002 : Boolean;
      ConsumptionBin@1240020001 : Boolean;
      EmptyPalletCounting@1240020012 : Boolean;
      LineNo@1240020000 : Integer;

    PROCEDURE SetTemplate@1240020000(TemplateCodeLoc@1240020000 : Code[20]);
    BEGIN
      TemplateCode := TemplateCodeLoc;
    END;

    PROCEDURE SetBatch@1240020001(BatchCodeLoc@1240020000 : Code[20]);
    BEGIN
      BatchCode := BatchCodeLoc;
    END;

    PROCEDURE SetLocation@1240020002(LocationCodeLoc@1240020000 : Code[20]);
    BEGIN
      LocationCode := LocationCodeLoc;
    END;

    PROCEDURE SetBin@1240020003(BinCodeLoc@1240020000 : Code[20]);
    BEGIN
      BinCode := BinCodeLoc;
    END;

    PROCEDURE SetItem@1240020004(ItemCodeLoc@1240020000 : Code[20]);
    BEGIN
      ItemCode := ItemCodeLoc;
    END;

    PROCEDURE SetLotNo@1240020005(LotLoc@1240020000 : Code[20]);
    BEGIN
      LotNo := LotLoc;
    END;

    PROCEDURE SetInitialRun@1240020006(InitialLoc@1240020000 : Boolean);
    BEGIN
      DeleteExistingLines := InitialLoc;
    END;

    PROCEDURE SetPhysInvLine@1240020007(LineNoLoc@1240020000 : Integer);
    BEGIN
      LineNo := LineNoLoc;
    END;

    PROCEDURE SetConsumptionBin@1240020008(ConsumptionBinLoc@1240020000 : Boolean);
    BEGIN
      ConsumptionBin := ConsumptionBinLoc;
    END;

    PROCEDURE SetEmptyPalletCount@1240020009(EmptyPalletCountingLoc@1240020000 : Boolean);
    BEGIN
      EmptyPalletCounting := EmptyPalletCountingLoc;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

