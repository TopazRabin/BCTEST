OBJECT Report 14000842 Customer Label
{
  OBJECT-PROPERTIES
  {
    Date=06/28/17;
    Time=12:00:00 PM;
    Version List=SE0.55.12;
  }
  PROPERTIES
  {
    CaptionML=ENU=Customer Label;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 6836;    ;DataItem;                    ;
               DataItemTable=Table18;
               OnPreDataItem=BEGIN
                               PackingStation.GetPackingStation;

                               LabelHeader.GET(LabelHeader.Code);

                               Window.OPEN(
                                 Text001 +
                                 Text002);
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"No.");
                                  Window.UPDATE(2,Name);

                                  PrintCustomer;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                              END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 3   ;2   ;Field     ;
                  CaptionML=ENU=Number of Copies;
                  SourceExpr=NoOfCopies }

      { 5   ;2   ;Field     ;
                  CaptionML=ENU=Label Code;
                  SourceExpr=LabelHeader.Code;
                  TableRelation="Label Header" WHERE (Label Usage=CONST(Customer)) }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PackingStation@1000000000 : Record 14000709;
      LabelHeader@1000000001 : Record 14000841;
      LabelLine@1000000002 : Record 14000842;
      LabelMgt@1000000003 : Codeunit 14000841;
      FieldValue@1000000004 : Codeunit 14000843;
      Window@1000000005 : Dialog;
      NoOfCopies@1000000006 : Integer;
      Text001@1001 : TextConst 'ENU=No.  #1######################\';
      Text002@1002 : TextConst 'ENU=Name #2######################';
      Text003@1003 : TextConst 'ENU=Only the Customer table (18) is available';

    PROCEDURE PrintCustomer@1();
    VAR
      OutputFile@1000000000 : File;
      Value@1000000005 : Text[1000];
      LabelFileName@1240030000 : Text[250];
    BEGIN
      CLEAR(FieldValue);
      FieldValue.Customer_18(Customer,0);
      // Additional tables can be loaded here.

      PackingStation.TESTFIELD("Label Buffer File");
      LabelFileName := PackingStation.CalcLabelFileName(PackingStation."Label Buffer File");

      OutputFile.TEXTMODE(TRUE);
      OutputFile.CREATE(LabelFileName);

      FieldValue.LabelHeader_14000841(LabelHeader,0);
      LabelMgt.LabelFileTop(LabelHeader,OutputFile);

      LabelLine.RESET;
      LabelLine.SETRANGE("Label Code",LabelHeader.Code);
      IF LabelLine.FIND('-') THEN
        REPEAT
          FieldValue.LabelLine_14000842(LabelLine,0);
          Value := FieldValue.SubstituteLine(LabelLine);

          LabelMgt.LabelFileLine(LabelHeader,LabelLine,Value,OutputFile);
        UNTIL LabelLine.NEXT = 0;

      LabelMgt.LabelFileBottom(LabelHeader,OutputFile,NoOfCopies);

      OutputFile.CLOSE;

      PackingStation.TESTFIELD("Std. Pack. Label Printer Port");

      IF NOT PackingStation."No Label Printer" THEN
        PackingStation.PrintLabel(
          LabelFileName,PackingStation."Std. Pack. Label Printer Port",
          PackingStation."No Label Printer",PackingStation."Auto Delete Label Buffer File",
          Customer."No.",LabelHeader);
    END;

    PROCEDURE LabelFormattingHelp@2();
    BEGIN
      MESSAGE(Text003);
    END;

    PROCEDURE GetTableFilter@3() : Text[250];
    BEGIN
      EXIT('18|14000841|14000842');
    END;

    PROCEDURE GetRecordNoStrMenu@4(CurrentTableNo@1000000000 : Integer) : Text[250];
    BEGIN
      CASE CurrentTableNo OF
        ELSE
          EXIT('');
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

