OBJECT Report 14050301 Send E.D.I. Sales Invoice Job
{
  OBJECT-PROPERTIES
  {
    Date=11/11/13;
    Time=12:00:00 PM;
    Version List=SE0.60;
  }
  PROPERTIES
  {
    Permissions=TableData 112=rimd;
    CaptionML=ENU=Send E.D.I. Sales Invoice Job;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  CompanyInformation.GET;
                END;

    UseRequestPage=No;
  }
  DATASET
  {
    { 2284;    ;DataItem;                    ;
               DataItemTable=Table14050305 }

    { 2527;    ;DataItem;EDITradePartner     ;
               DataItemTable=Table14002360;
               PrintOnlyIfDetail=Yes;
               OnPreDataItem=BEGIN
                               EDITradePartner.SETFILTER("No.","E.D.I. Job Routine"."Trade Partner Filter");
                               EDITradePartner.SETFILTER("Company Name","E.D.I. Job Routine"."Company Filter");
                             END;

               ReqFilterFields=No. }

    { 5581;1   ;DataItem;                    ;
               DataItemTable=Table112;
               DataItemTableView=SORTING(EDI Order,EDI Invoice Generated,EDI Trade Partner,EDI Internal Doc. No.)
                                 ORDER(Ascending)
                                 WHERE(EDI Invoice=CONST(Yes),
                                       EDI Invoice Generated=CONST(No));
               OnAfterGetRecord=BEGIN
                                  NavisionDocument := '';
                                  EDIDocument.SETRANGE("Trade Partner No.",EDITradePartner."No.");
                                  EDIDocument.SETFILTER(
                                    Status,'%1|%2',EDIDocument.Status::Test,EDIDocument.Status::Production);
                                  EDIDocument.SETRANGE("Navision Document",'E_SLSINV');
                                  EDIDocument.SETRANGE(Type,EDIDocument.Type::Export);
                                  IF EDIDocument.FIND('-') THEN
                                    NavisionDocument := EDIDocument."Navision Document"
                                  ELSE BEGIN
                                    EDIDocument.INIT;
                                    NavisionDocument := 'E_SLSINV';
                                  END;

                                  EDIManagement.CreateOutboundLogEntry(
                                    Text001,LogEntryNo,'SALES INVOICE',"Sales Invoice Header"."No.",EDITradePartner."No.",
                                    NavisionDocument,EDIDocument."EDI Document No.");
                                  COMMIT;

                                  CLEARLASTERROR;
                                  CLEAR(EDISalesInvoiceSend);
                                  IF EDISalesInvoiceSend.RUN("Sales Invoice Header") THEN
                                    DeleteLog := TRUE
                                  ELSE
                                    DeleteLog := FALSE;
                                  COMMIT;

                                  IF DeleteLog THEN
                                    EDIManagement.DeleteOutboundLogEntry(LogEntryNo)
                                  ELSE BEGIN
                                    ErrorText := '';
                                    ErrorText := COPYSTR(GETLASTERRORTEXT,1,250);
                                    EDIManagement.CloseOutboundLogEntry(LogEntryNo,ErrorText);
                                  END;
                                  COMMIT;
                                END;

               ReqFilterFields=Bill-to Customer No.,No.;
               DataItemLink=EDI Trade Partner=FIELD(No.) }

    { 1534;1   ;DataItem;                    ;
               DataItemTable=Table14002353;
               DataItemTableView=SORTING(Trade Partner No.,Navision Document,EDI Document No.,Version,Type)
                                 ORDER(Ascending)
                                 WHERE(Navision Document=CONST(E_SLSINV),
                                       Type=CONST(Export),
                                       Status=FILTER(Test|Production));
               OnPreDataItem=BEGIN
                               EDISetup.GET;
                               "E.D.I. Document".SETFILTER("EDI Document No.","E.D.I. Job Routine"."EDI Document No. Filter");
                             END;

               OnAfterGetRecord=BEGIN
                                  IF EDISetup."Auto Export Send Doc." THEN BEGIN
                                    EDIManagement.CreateOutboundLogEntry(Text002,LogEntryNo,'SEND DOCUMENT','',EDITradePartner."No.",
                                    "E.D.I. Document"."Navision Document","E.D.I. Document"."EDI Document No.");
                                    COMMIT;

                                    CLEARLASTERROR;
                                    IF EDISend.RUN("E.D.I. Document") THEN
                                      DeleteLog := TRUE
                                    ELSE
                                      DeleteLog := FALSE;
                                    COMMIT;

                                    IF DeleteLog THEN
                                      EDIManagement.DeleteOutboundLogEntry(LogEntryNo)
                                    ELSE BEGIN
                                      ErrorText := '';
                                      ErrorText := COPYSTR(GETLASTERRORTEXT,1,250);
                                      EDIManagement.CloseOutboundLogEntry(LogEntryNo,ErrorText);
                                    END;
                                    COMMIT;
                                  END;
                                END;

               DataItemLink=Trade Partner No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CompanyInformation@1240030000 : Record 79;
      EDISetup@1240030001 : Record 14002367;
      EDIDocument@1240020005 : Record 14002353;
      EDISalesInvoiceSend@1240030002 : Codeunit 14002360;
      EDISend@1240030003 : Codeunit 14002356;
      EDIManagement@1240020000 : Codeunit 14002379;
      ReCreate@1240030011 : Boolean;
      Text001@1001 : TextConst 'ENU=Create EDI Sales Invoice';
      LogEntryNo@1240020001 : Integer;
      Text002@1240020002 : TextConst 'ENU=Send EDI Invoice';
      DeleteLog@1240020003 : Boolean;
      ErrorText@1240020004 : Text[250];
      NavisionDocument@1240020006 : Code[10];

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

