OBJECT Report 14000914 Purchase E-Mail
{
  OBJECT-PROPERTIES
  {
    Date=06/28/17;
    Time=12:00:00 PM;
    Version List=SE0.60.11;
  }
  PROPERTIES
  {
    CaptionML=ENU=Purchase E-Mail;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 5444;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               ShowStatus := FALSE;

                               IF ShowStatus THEN
                                 Window.OPEN(
                                   Text001 +
                                   Text002);
                             END;

               OnAfterGetRecord=BEGIN
                                  CLEAR(FieldValue);

                                  EMailSetup.GET;
                                  CASE DocumentType OF
                                    PurchaseHeader."Document Type"::Quote:
                                      EMailSetup.TESTFIELD("Purchase Quote Conf. E-Mail");
                                    PurchaseHeader."Document Type"::Order:
                                      EMailSetup.TESTFIELD("Purchase Order Conf. E-Mail");
                                    PurchaseHeader."Document Type"::Invoice:
                                      EMailSetup.TESTFIELD("Purchase Invoice Conf. E-Mail");
                                    PurchaseHeader."Document Type"::"Credit Memo":
                                      EMailSetup.TESTFIELD("Purchase Cr. Memo Conf. E-Mail");
                                    PurchaseHeader."Document Type"::"Blanket Order":
                                      EMailSetup.TESTFIELD("Purch. Bl. Order Conf. E-Mail");
                                    PurchaseHeader."Document Type"::"Return Order":
                                      EMailSetup.TESTFIELD("Purch. Ret. Order Conf. E-Mail");
                                  END;

                                  IF NOT GetInitialRecords THEN BEGIN
                                    ModifyRecords;
                                    EXIT;
                                  END;

                                  IF EMailHeader."Line Break Character" <> '' THEN
                                    MailInt.LineFeedChar := EMailHeader."Line Break Character";

                                  // Create Subject Line
                                  IF ShowStatus THEN
                                    Window.UPDATE(1,Text003);
                                  MailInt.InitializeMail(FieldValue.SubstituteHeader(EMailHeader));

                                  // Address the mail
                                  IF ShowStatus THEN
                                    Window.UPDATE(1,Text004);

                                  ReceiverTo := '';
                                  ReceiverCC := '';
                                  ReceiverBCC := '';
                                  EMailListEntryTmp.RemoveEntriesWithSameEMail(EMailListEntryTmp);
                                  REPEAT
                                    CASE DocumentType OF
                                      PurchaseHeader."Document Type"::Quote:
                                        CASE EMailListEntryTmp."Purchase Quote Conf. E-Mail" OF
                                          EMailListEntryTmp."Purchase Quote Conf. E-Mail"::"To":
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'TO');
                                          EMailListEntryTmp."Purchase Quote Conf. E-Mail"::CC:
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'CC');
                                          EMailListEntryTmp."Purchase Quote Conf. E-Mail"::BCC:
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'BCC');
                                        END;
                                      PurchaseHeader."Document Type"::Order:
                                        CASE EMailListEntryTmp."Purchase Order Conf. E-Mail" OF
                                          EMailListEntryTmp."Purchase Order Conf. E-Mail"::"To":
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'TO');
                                          EMailListEntryTmp."Purchase Order Conf. E-Mail"::CC:
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'CC');
                                          EMailListEntryTmp."Purchase Order Conf. E-Mail"::BCC:
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'BCC');
                                        END;
                                      PurchaseHeader."Document Type"::Invoice:
                                        CASE EMailListEntryTmp."Purchase Invoice Conf. E-Mail" OF
                                          EMailListEntryTmp."Purchase Invoice Conf. E-Mail"::"To":
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'TO');
                                          EMailListEntryTmp."Purchase Invoice Conf. E-Mail"::CC:
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'CC');
                                          EMailListEntryTmp."Purchase Invoice Conf. E-Mail"::BCC:
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'BCC');
                                        END;
                                      PurchaseHeader."Document Type"::"Credit Memo":
                                        CASE EMailListEntryTmp."Purchase Cr. Memo Conf. E-Mail" OF
                                          EMailListEntryTmp."Purchase Cr. Memo Conf. E-Mail"::"To":
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'TO');
                                          EMailListEntryTmp."Purchase Cr. Memo Conf. E-Mail"::CC:
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'CC');
                                          EMailListEntryTmp."Purchase Cr. Memo Conf. E-Mail"::BCC:
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'BCC');
                                        END;
                                      PurchaseHeader."Document Type"::"Blanket Order":
                                        CASE EMailListEntryTmp."Purch. Bl. Order Conf. E-Mail" OF
                                          EMailListEntryTmp."Purch. Bl. Order Conf. E-Mail"::"To":
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'TO');
                                          EMailListEntryTmp."Purch. Bl. Order Conf. E-Mail"::CC:
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'CC');
                                          EMailListEntryTmp."Purch. Bl. Order Conf. E-Mail"::BCC:
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'BCC');
                                        END;
                                      PurchaseHeader."Document Type"::"Return Order":
                                        CASE EMailListEntryTmp."Purch. Ret. Order Conf. E-Mail" OF
                                          EMailListEntryTmp."Purch. Ret. Order Conf. E-Mail"::"To":
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'TO');
                                          EMailListEntryTmp."Purch. Ret. Order Conf. E-Mail"::CC:
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'CC');
                                          EMailListEntryTmp."Purch. Ret. Order Conf. E-Mail"::BCC:
                                            CreateEmailAddressStr(EMailListEntryTmp."E-Mail",'BCC');
                                        END;
                                    END;
                                  UNTIL EMailListEntryTmp.NEXT = 0;

                                  IF ReceiverTo <> '' THEN
                                    MailInt.AddReceiverTo(ReceiverTo);

                                  IF ReceiverCC <> '' THEN
                                    MailInt.AddReceiverCC(ReceiverCC);

                                  IF ReceiverBCC <> '' THEN
                                    MailInt.AddReceiverBCC(ReceiverBCC);

                                  // Create the body of the E-Mail
                                  RecordFound[1] := TRUE;
                                  LastLevel := 1;
                                  LastType := 0;
                                  EMailLine.RESET;
                                  EMailLine.SETRANGE("E-Mail Code",EMailHeader.Code);
                                  IF EMailLine.FIND('-') THEN
                                    REPEAT
                                      IF ShowStatus THEN
                                        Window.UPDATE(1,EMailLine.Text);
                                      EMailLine.TESTFIELD(Level);

                                      // Handle Higher Level
                                      IF LastLevel < EMailLine.Level THEN BEGIN
                                        GetFirstRecordInLoop(EMailLine.Level);
                                        FirstOnLevel[EMailLine.Level] := TRUE;
                                        EMailLineFirstOnLevel[EMailLine.Level] := EMailLine;
                                      END;

                                      // Handle Skip Level if no records
                                      IF FirstOnLevel[EMailLine.Level] AND
                                         NOT RecordFound[EMailLine.Level] AND
                                         EMailLine."Include only level if details"
                                      THEN BEGIN
                                        EMailLine.SETFILTER(Level,'<%1',EMailLine.Level);
                                        Finished := EMailLine.NEXT = 0;
                                        EMailLine.SETRANGE(Level);
                                      END ELSE BEGIN
                                        // Substitute Values in Line and add to E-Mail
                                        IF (EMailLine.Type <> EMailLine.Type::Loop) OR
                                           RecordFound[EMailLine.Level]
                                        THEN
                                          MailInt.AddtoBody(FieldValue.SubstituteLine(EMailLine));

                                        // Get Next E-Mail Line
                                        LastType := EMailLine.Type;
                                        LastLevel := EMailLine.Level;
                                        Finished := EMailLine.NEXT = 0;

                                        // Handle Loop End or Return to Lower Level
                                        IF (LastType = EMailLine.Type::Loop) AND
                                           (((EMailLine.Type <> EMailLine.Type::Loop) AND
                                             (LastLevel = EMailLine.Level)) OR
                                            (LastLevel > EMailLine.Level)) AND
                                           RecordFound[LastLevel]
                                        THEN BEGIN
                                          GetNextRecordInLoop(LastLevel);

                                          // Repeat Loop if Record found
                                          IF RecordFound[LastLevel] THEN BEGIN
                                            EMailLine := EMailLineFirstOnLevel[LastLevel];
                                            EMailLine.SETRANGE(Type,EMailLine.Type::Loop);
                                            EMailLine.NEXT;
                                            EMailLine.SETRANGE(Type);
                                          END;
                                        END;
                                      END;

                                      FirstOnLevel[EMailLine.Level] := FALSE;
                                    UNTIL Finished;

                                  // Create and add attachment
                                  IF ShowStatus THEN
                                    Window.UPDATE(1,Text005);
                                  EMailAttachmentTmp.DELETEALL;
                                  EMailAttachment.RESET;
                                  EMailAttachment.SETRANGE("E-Mail Code",EMailHeader.Code);
                                  IF EMailAttachment.FIND('-') THEN BEGIN
                                    EMailSetup.TESTFIELD("E-Mail Buffer Directory");

                                    REPEAT
                                      CASE EMailAttachment.Type OF
                                        EMailAttachment.Type::Report:
                                          BEGIN
                                            EMailAttachment.TESTFIELD("Attachment Name");
                                            EMailAttachment.TESTFIELD("Attachment Report ID");

                                            TempFilename := EMailSetup."E-Mail Buffer Directory";
                                            IF COPYSTR(TempFilename,STRLEN(TempFilename),1) <> '\' THEN
                                              TempFilename := TempFilename + '\';
                                            TempFilename := TempFilename + FieldValue.SubstituteAttachment(EMailAttachment);

                                            SaveReport(EMailAttachment."Attachment Report ID",TempFilename);
                                            MailInt.AddAttachment(TempFilename);

                                            EMailAttachmentTmp := EMailAttachment;
                                            EMailAttachmentTmp."Use Attachment File Name" := TempFilename;
                                            EMailAttachmentTmp.INSERT;
                                          END;
                                        EMailAttachment.Type::"Fixed File":
                                          BEGIN
                                            IF NOT EXISTS(EMailAttachment."Fixed File Name") THEN
                                              ERROR(Text006,EMailAttachment."Fixed File Name");

                                            MailInt.AddAttachment(EMailAttachment."Fixed File Name");
                                          END;
                                      END;
                                    UNTIL EMailAttachment.NEXT = 0;
                                  END;

                                  // Send e-mail
                                  IF ShowStatus THEN
                                    Window.UPDATE(1,Text007);
                                  MailInt.Send(EMailHeader."Confirm E-Mail manually");

                                  CLEAR(MailInt);

                                  IF EMailAttachmentTmp.FIND('-') THEN
                                    REPEAT
                                      MailInt.DeleteAttachment(EMailAttachmentTmp."Use Attachment File Name");
                                    UNTIL EMailAttachmentTmp.NEXT = 0;

                                  // Update the Records
                                  ModifyRecords;
                                END;

               OnPostDataItem=BEGIN
                                IF ShowStatus THEN
                                  Window.CLOSE;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PurchaseHeader@1000000000 : Record 38;
      PurchaseLine@1000000001 : Record 39;
      PurchaseCommentLine@1000000002 : Record 43;
      CompanyInformation@1000000003 : Record 79;
      Vendor@1000000004 : Record 23;
      OrderAddress@1000000005 : Record 224;
      PaymentTerms@1000000006 : Record 3;
      SalesPersonPurchaser@1000000007 : Record 13;
      Location@1000000008 : Record 14;
      EMailSetup@1000000011 : Record 14000905;
      EMailRule@1000000012 : Record 14000907;
      EMailHeader@1000000013 : Record 14000901;
      EMailLine@1000000014 : Record 14000902;
      EMailLineFirstOnLevel@1000000015 : ARRAY [10] OF Record 14000902;
      EMailListEntryTmp@1000000016 : TEMPORARY Record 14000908;
      EMailAttachment@1000000017 : Record 14000909;
      EMailAttachmentTmp@1000000018 : TEMPORARY Record 14000909;
      FieldValue@1000000019 : Codeunit 14000901;
      MailInt@1000000020 : Codeunit 14000905;
      Window@1000000021 : Dialog;
      RecordFound@1000000022 : ARRAY [10] OF Boolean;
      FirstOnLevel@1000000023 : ARRAY [10] OF Boolean;
      Finished@1000000024 : Boolean;
      ShowStatus@1000000025 : Boolean;
      BatchProcessing@1240020004 : Boolean;
      LastLevel@1000000026 : Integer;
      LastType@1000000027 : Integer;
      DocumentType@1000000031 : 'Quote,Order,Invoice,Credit Memo,Blanket Order';
      OrderNo@1000000029 : Code[20];
      TempFilename@1000000030 : Text[250];
      Text001@1001 : TextConst 'ENU=Creating E-Mail\';
      Text002@1002 : TextConst 'ENU=Current Line #1##################';
      Text003@1003 : TextConst 'ENU=Subject';
      Text004@1004 : TextConst 'ENU=Addressing';
      Text005@1005 : TextConst 'ENU=Attachments';
      Text006@1006 : TextConst 'ENU=Attachment file %1 do not exists.';
      Text007@1007 : TextConst 'ENU=Sending';
      Text008@1008 : TextConst 'ENU=Level   Table\';
      Text009@1009 : TextConst 'ENU="  1     Purchase Header (38)\"';
      Text010@1010 : TextConst 'ENU="  2       Purchase Line (39)\"';
      Text011@1011 : TextConst 'ENU="  3       Purchase Comment Line (43)\\"';
      Text012@1012 : TextConst 'ENU=Other tables\';
      Text013@1013 : TextConst 'ENU=Payment Terms (3), Purchase Person (13), Location (14),\';
      Text014@1014 : TextConst 'ENU=Vendor (18) (0 Buy-from Vendor No., 1 Pay-to Vendor No.),\';
      Text015@1015 : TextConst 'ENU=Company Information (79), Order Address (224)';
      Text016@1016 : TextConst 'ENU=Buy-from Vendor,Pay-to Vendor';
      Text017@1017 : TextConst 'ENU=%1 is not a supported level.';
      Text018@1018 : TextConst 'ENU=Get E-Mail List must be empty.';
      ReceiverTo@1240020003 : Text[250];
      ReceiverCC@1240020002 : Text[250];
      ReceiverBCC@1240020001 : Text[250];

    PROCEDURE CreateEmailAddressStr@1240020000(EmailAddressStr@1240020000 : Text[250];Type@1240020001 : Text[10]);
    BEGIN
      CASE Type OF
        'TO':
          IF ReceiverTo = '' THEN
            ReceiverTo := EmailAddressStr
          ELSE
            ReceiverTo := ReceiverTo + ';' + EmailAddressStr;
        'CC':
          IF ReceiverCC = '' THEN
            ReceiverCC := EmailAddressStr
          ELSE
            ReceiverCC := ReceiverCC + ';' + EmailAddressStr;
        'BCC':
          IF ReceiverBCC = '' THEN
            ReceiverBCC := EmailAddressStr
          ELSE
            ReceiverBCC := ReceiverBCC + ';' + EmailAddressStr;
      END;
    END;

    PROCEDURE InitializeRequest@1(NewDocumentType@1000000000 : Integer;NewOrderNo@1000000001 : Code[20];BatchSending@1240020000 : Boolean);
    BEGIN
      DocumentType := NewDocumentType;
      OrderNo := NewOrderNo;
      BatchProcessing := BatchSending;

      IF NewOrderNo = 'SHOWHELP' THEN
        MESSAGE(
          Text008 +
          Text009 +
          Text010 +
          Text011 +
          Text012 +
          Text013 +
          Text014 +
          Text015);
    END;

    PROCEDURE GetLevelName@5(LevelNo@1000000000 : Integer) : Text[30];
    BEGIN
      CASE LevelNo OF
        1:
          EXIT(PurchaseHeader.TABLENAME);
        2:
          EXIT(PurchaseLine.TABLENAME);
        3:
          EXIT(PurchaseCommentLine.TABLENAME);
        ELSE
          EXIT('');
      END;
    END;

    PROCEDURE GetTableFilter@6() : Text[250];
    BEGIN
      EXIT('3|13|14|18|38|39|43|79|224|14000901|14000907');
    END;

    PROCEDURE GetRecordNoStrMenu@9(CurrentTableNo@1000000000 : Integer) : Text[250];
    BEGIN
      CASE CurrentTableNo OF
        DATABASE::Vendor:
          EXIT(Text016);
        ELSE
          EXIT('');
      END;
    END;

    LOCAL PROCEDURE GetInitialRecords@2() : Boolean;
    VAR
      EMailListEntry@1240030000 : Record 14000908;
      SendMail@1000000000 : Boolean;
    BEGIN
      // Get and Load Header values
      PurchaseHeader.GET(DocumentType,OrderNo);
      FieldValue.PurchaseHeader_38(PurchaseHeader,0);
      CompanyInformation.GET;
      FieldValue.CompanyInformation_79(CompanyInformation,0);
      Vendor.GET(PurchaseHeader."Buy-from Vendor No.");
      FieldValue.Vendor_23(Vendor,0);
      Vendor.GET(PurchaseHeader."Pay-to Vendor No.");
      FieldValue.Vendor_23(Vendor,1);
      IF PurchaseHeader."Order Address Code" <> '' THEN BEGIN
        OrderAddress.GET(Vendor."No.",PurchaseHeader."Order Address Code");
        FieldValue.OrderAddress_224(OrderAddress,0);
      END;
      IF NOT PaymentTerms.GET(PurchaseHeader."Payment Terms Code") THEN
        CLEAR(PaymentTerms);
      FieldValue.PaymentTerms_3(PaymentTerms,0);
      IF NOT SalesPersonPurchaser.GET(PurchaseHeader."Purchaser Code") THEN
        CLEAR(SalesPersonPurchaser);
      FieldValue.SalespersonPurchaser_13(SalesPersonPurchaser,0);
      IF NOT Location.GET(PurchaseHeader."Location Code") THEN
        CLEAR(Location);
      FieldValue.Location_14(Location,0);

      // Find E-Mail receivers
      EMailListEntryTmp.RESET;
      EMailListEntryTmp.DELETEALL;

      EMailListEntry.RESET;
      EMailListEntry.SETRANGE("Table ID",DATABASE::Vendor);
      EMailListEntry.SETRANGE(Code,Vendor."No.");
      IF PurchaseHeader."Ship-to Code" <> '' THEN
        EMailListEntry.SETRANGE("Use for Ship-to/Order Address",TRUE);
      IF EMailListEntry.FIND('-') THEN
        REPEAT
          EMailListEntryTmp := EMailListEntry;
          EMailListEntryTmp.INSERT;
        UNTIL EMailListEntry.NEXT = 0;

      IF PurchaseHeader."Ship-to Code" <> '' THEN BEGIN
        EMailListEntry.RESET;
        EMailListEntry.SETRANGE("Table ID",DATABASE::"Order Address");
        EMailListEntry.SETRANGE(Code,Vendor."No.");
        EMailListEntry.SETRANGE("Code 2",OrderAddress.Code);
        IF EMailListEntry.FIND('-') THEN
          REPEAT
            EMailListEntryTmp := EMailListEntry;
            EMailListEntryTmp.INSERT;
          UNTIL EMailListEntry.NEXT = 0;
      END;

      EMailListEntry.RESET;
      EMailListEntry.SETRANGE("Table ID",DATABASE::"Purchase Header");
      EMailListEntry.SETRANGE(Type,PurchaseHeader."Document Type");
      EMailListEntry.SETRANGE(Code,PurchaseHeader."No.");
      IF EMailListEntry.FIND('-') THEN
        REPEAT
          EMailListEntryTmp := EMailListEntry;
          EMailListEntryTmp.INSERT;
        UNTIL EMailListEntry.NEXT = 0;

      // Test That a Mail should be sent
      SendMail :=
        EMailRule.GetEMailRule(
          1,PurchaseHeader."Buy-from Vendor No.",PurchaseHeader."Order Address Code");

      IF NOT SendMail THEN
        EXIT(FALSE);

      EMailListEntryTmp.SETFILTER("E-Mail",'<>%1','');
      CASE DocumentType OF
        PurchaseHeader."Document Type"::Quote:
          BEGIN
            EMailListEntryTmp.SETRANGE("Purchase Quote Conf. E-Mail",1,3);

            SendMail := EMailHeader.GET(EMailRule."Purch. Quote Conf. E-Mail Code");
          END;
        PurchaseHeader."Document Type"::Order:
          BEGIN
            EMailListEntryTmp.SETRANGE("Purchase Order Conf. E-Mail",1,3);

            SendMail := EMailHeader.GET(EMailRule."Purch. Order Conf. E-Mail Code");
          END;
        PurchaseHeader."Document Type"::Invoice:
          BEGIN
            EMailListEntryTmp.SETRANGE("Purchase Invoice Conf. E-Mail",1,3);

            SendMail := EMailHeader.GET(EMailRule."Purch Invoice Conf E-Mail Code");
          END;
        PurchaseHeader."Document Type"::"Credit Memo":
          BEGIN
            EMailListEntryTmp.SETRANGE("Purchase Cr. Memo Conf. E-Mail",1,3);

            SendMail := EMailHeader.GET(EMailRule."Purch Cr Memo Conf E-Mail Code");
          END;
        PurchaseHeader."Document Type"::"Blanket Order":
          BEGIN
            EMailListEntryTmp.SETRANGE("Purch. Bl. Order Conf. E-Mail",1,3);

            SendMail := EMailHeader.GET(EMailRule."Purch Bl. Ord Conf E-Mail Code");
          END;
        PurchaseHeader."Document Type"::"Return Order":
          BEGIN
            EMailListEntryTmp.SETRANGE("Purch. Ret. Order Conf. E-Mail",1,3);

            SendMail := EMailHeader.GET(EMailRule."Purch Ret Ord Conf E-Mail Code");
          END;
      END;

      IF NOT SendMail THEN
        EXIT(FALSE);

      FieldValue.EMailHeader_14000901(EMailHeader,0);
      FieldValue.EMailRule_14000907(EMailRule,0);

      EXIT(EMailListEntryTmp.FIND('-'));
    END;

    LOCAL PROCEDURE ModifyRecords@7();
    BEGIN
      PurchaseHeader.GET(DocumentType,OrderNo);
      PurchaseHeader."E-Mail Confirmation Handled" := TRUE;
      PurchaseHeader.MODIFY;
      COMMIT;
    END;

    LOCAL PROCEDURE GetFirstRecordInLoop@3(CurrentLevel@1000000000 : Integer);
    BEGIN
      CASE CurrentLevel OF
        2:
          BEGIN
            PurchaseLine.RESET;
            PurchaseLine.SETRANGE("Document Type",PurchaseHeader."Document Type");
            PurchaseLine.SETRANGE("Document No.",PurchaseHeader."No.");
            IF EMailHeader."Line Type Filter" <> '' THEN
              PurchaseLine.SETFILTER(Type,EMailHeader."Line Type Filter");
            IF EMailHeader."Only Lines with Content" THEN
              PurchaseLine.SETFILTER(Quantity,'<>%1',0);
            RecordFound[CurrentLevel] := PurchaseLine.FIND('-');
            FieldValue.PurchaseLine_39(PurchaseLine,0);
          END;
        3:
          BEGIN
            PurchaseCommentLine.RESET;
            CASE DocumentType OF
              PurchaseHeader."Document Type"::Quote:
                PurchaseCommentLine.SETRANGE("Document Type",PurchaseCommentLine."Document Type"::Quote);
              PurchaseHeader."Document Type"::Order:
                PurchaseCommentLine.SETRANGE("Document Type",PurchaseCommentLine."Document Type"::Order);
              PurchaseHeader."Document Type"::Invoice:
                PurchaseCommentLine.SETRANGE("Document Type",PurchaseCommentLine."Document Type"::Invoice);
              PurchaseHeader."Document Type"::"Credit Memo":
                PurchaseCommentLine.SETRANGE(
                  "Document Type",PurchaseCommentLine."Document Type"::"Credit Memo");
              PurchaseHeader."Document Type"::"Blanket Order":
                PurchaseCommentLine.SETRANGE(
                  "Document Type",PurchaseCommentLine."Document Type"::"Blanket Order");
              PurchaseHeader."Document Type"::"Return Order":
                PurchaseCommentLine.SETRANGE(
                  "Document Type",PurchaseCommentLine."Document Type"::Receipt);
            END;
            PurchaseCommentLine.SETRANGE("No.",PurchaseHeader."No.");
            PurchaseCommentLine.SETRANGE("Include in E-Mail",TRUE);
            RecordFound[CurrentLevel] := PurchaseCommentLine.FIND('-');
            FieldValue.PurchCommentLine_43(PurchaseCommentLine,0);
          END;
        ELSE
          ERROR(Text017,CurrentLevel);
      END;
    END;

    LOCAL PROCEDURE GetNextRecordInLoop@4(CurrentLevel@1000000000 : Integer);
    BEGIN
      CASE CurrentLevel OF
        2:
          BEGIN
            RecordFound[CurrentLevel] := PurchaseLine.NEXT <> 0;
            FieldValue.PurchaseLine_39(PurchaseLine,0);
          END;
        3:
          BEGIN
            RecordFound[CurrentLevel] := PurchaseCommentLine.NEXT <> 0;
            FieldValue.PurchCommentLine_43(PurchaseCommentLine,0);
          END;
        ELSE
          ERROR(Text017,CurrentLevel);
      END;
    END;

    PROCEDURE SaveReport@8(ReportNo@1000000000 : Integer;VAR SaveFileName@1000000001 : Text[250]);
    BEGIN
      PurchaseHeader.SETRECFILTER;
      IF ISSERVICETIER THEN BEGIN
        EMailSetup.ChangeFilenameToPDF(SaveFileName);
        REPORT.SAVEASPDF(ReportNo,SaveFileName,PurchaseHeader);
      END ELSE
        REPORT.SAVEASHTML(ReportNo,SaveFileName,PurchaseHeader);
    END;

    PROCEDURE GetReceiverList@10(VAR GetEMailListEntryTmp@1240030000 : Record 14000908);
    BEGIN
      GetInitialRecords;

      GetEMailListEntryTmp.RESET;
      IF GetEMailListEntryTmp.FIND('-') THEN
        ERROR(Text018);

      EMailListEntryTmp.RESET;
      IF EMailListEntryTmp.FIND('-') THEN
        REPEAT
          GetEMailListEntryTmp := EMailListEntryTmp;
          GetEMailListEntryTmp.INSERT;
        UNTIL EMailListEntryTmp.NEXT = 0;
    END;

    BEGIN
    {
      The Code that customize the running of the report should be done in the 4 triggers
      GetInitialRecords
        This is where all the values needed for the general information in the E-Mail are retrived.
        Additional tables could be added in the information was needed.

      ModifyRecords
        This is the final step, where the status of some records are modified. This could be used to
        change a field telling that the E-Mail has been sent.

      GetFirstRecordInLoop
        This will get the first record in each level. This also adjust some variables and contol that the
        flow of multilevel tables are correct. Like the printing of Posted Container/Posted Container Line.

      GetNextRecordInLoop
        This will retrieve the next record for each level.

      SaveReport
        This will run and save a specific report in HTML or PDF(only for NAV 2009 RTC and higher).
    }
    END.
  }
  RDLDATA
  {
  }
}

