OBJECT Report 14000825 Bill of Lading Change Status
{
  OBJECT-PROPERTIES
  {
    Date=06/28/17;
    Time=12:00:00 PM;
    Version List=SE0.60.06;
  }
  PROPERTIES
  {
    CaptionML=ENU=Bill of Lading Change Status;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 6966;    ;DataItem;                    ;
               DataItemTable=Table14000822;
               DataItemTableView=SORTING(Released,Summary Released,Posted)
                                 WHERE(Posted=CONST(No));
               OnPreDataItem=BEGIN
                               CASE SetSelectionOption OF
                                 SetSelectionOption::" ": SetSelection := 0;
                                 SetSelectionOption::"Create New Bill of Lading for Orders": SetSelection := 1;
                                 SetSelectionOption::"Blank Bill of Lading No. on Orders": SetSelection := 2;
                                 SetSelectionOption::"Leave Bill of Lading No. on Orders": SetSelection := 3;
                               END;

                               IF SetReleased AND SetReopen THEN
                                ERROR(Text002);
                               IF SetReleased THEN
                                 IF SetSelection = 0 THEN
                                   ERROR(Text001);

                               BillOfLadingTmp.DELETEALL;
                             END;

               OnAfterGetRecord=BEGIN
                                  BillOfLadingTmp := "Bill of Lading";
                                  BillOfLadingTmp.INSERT;
                                END;

               OnPostDataItem=BEGIN
                                IF BillOfLadingTmp.FIND('-') THEN
                                  REPEAT
                                    BillOfLading.GET(BillOfLadingTmp."No.");
                                    IF SetReopen AND BillOfLading.Released THEN BEGIN
                                      BOLMgt.OpenBOLSummary(BillOfLading);
                                      BOLMgt.OpenBillOfLading(BillOfLading);
                                    END ELSE BEGIN
                                      IF SetReleased AND NOT BillOfLading.Released THEN BEGIN
                                        BOLMgt.CloseBillOfLading(BillOfLading,SetSelection);
                                        BillOfLading.FIND;
                                      END;

                                      IF SetSummaryReleased AND NOT BillOfLading."Summary Released" THEN BEGIN
                                        BOLMgt.CloseBOLSummary(BillOfLading);
                                        BillOfLading.FIND;
                                      END;

                                      IF SetPosted AND NOT BillOfLading.Posted THEN BEGIN
                                        BOLMgt.PostBillOfLading(BillOfLading,FALSE);
                                        BillOfLading.FIND;
                                      END;
                                    END;
                                  UNTIL BillOfLadingTmp.NEXT = 0;
                              END;

               ReqFilterFields=No.,Ship-to No.,Ship-to Code,Pickup Date,Released,Summary Released }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1   ;2   ;Field     ;
                  CaptionML=ENU=Release;
                  SourceExpr=SetReleased }

      { 1240020000;2;Field  ;
                  CaptionML=ENU=Existing Orders;
                  SourceExpr=SetSelectionOption }

      { 3   ;2   ;Field     ;
                  CaptionML=ENU=Release Summary;
                  SourceExpr=SetSummaryReleased }

      { 5   ;2   ;Field     ;
                  CaptionML=ENU=Post;
                  SourceExpr=SetPosted }

      { 1240020002;2;Field  ;
                  CaptionML=ENU=Reopen;
                  SourceExpr=SetReopen }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      BillOfLading@1000000000 : Record 14000822;
      BillOfLadingTmp@1000000001 : TEMPORARY Record 14000822;
      BOLMgt@1000000002 : Codeunit 14000821;
      SetReleased@1000000003 : Boolean;
      SetSummaryReleased@1000000005 : Boolean;
      SetPosted@1000000006 : Boolean;
      Text001@1001 : TextConst 'ENU=Existing Orders must be handled.';
      SetSelection@1240020000 : Integer;
      SetSelectionOption@1240020001 : ' ,Create New Bill of Lading for Orders,Blank Bill of Lading No. on Orders,Leave Bill of Lading No. on Orders';
      Text002@1240020002 : TextConst 'ENU=Batch Change Status cannot be Release and Reopen.\Select Release or Reopen.';
      SetReopen@1240020003 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

