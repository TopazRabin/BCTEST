OBJECT Report 14000848 Bin Label
{
  OBJECT-PROPERTIES
  {
    Date=06/28/17;
    Time=12:00:00 PM;
    Version List=SE0.55.12;
  }
  PROPERTIES
  {
    CaptionML=ENU=Bin Label;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   UsePackingStationSetup := TRUE;
                 END;

  }
  DATASET
  {
    { 7020;    ;DataItem;                    ;
               DataItemTable=Table7354;
               OnPreDataItem=BEGIN
                               IF UsePackingStationSetup THEN
                                 PackingStation.GetPackingStation
                               ELSE
                                 ReceiveStation.GetReceiveStation;

                               LabelHeader.GET(LabelHeader.Code);

                               Window.OPEN(
                                 Text001 +
                                 Text002);
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,Code);
                                  Window.UPDATE(2,Description);

                                  PrintBin;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                              END;

               ReqFilterFields=Location Code,Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 3   ;2   ;Field     ;
                  CaptionML=ENU=Number of Copies;
                  SourceExpr=NoOfCopies }

      { 5   ;2   ;Field     ;
                  CaptionML=ENU=Label Code;
                  SourceExpr=LabelHeader.Code;
                  TableRelation="Label Header" WHERE (Label Usage=CONST(Bin)) }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PackingStation@1000000000 : Record 14000709;
      ReceiveStation@1240030000 : Record 14000608;
      LabelHeader@1000000001 : Record 14000841;
      LabelLine@1000000002 : Record 14000842;
      LabelMgt@1000000003 : Codeunit 14000841;
      FieldValue@1000000004 : Codeunit 14000843;
      Window@1000000005 : Dialog;
      NoOfCopies@1000000006 : Integer;
      UsePackingStationSetup@1240030001 : Boolean;
      Text001@1001 : TextConst 'ENU=Code        #1######################\';
      Text002@1002 : TextConst 'ENU=Description #2######################';
      Text004@1004 : TextConst 'ENU=Only Location table (14) Bin table (5400/7354) and Company Information (79) is available.\';

    PROCEDURE PrintBin@1();
    VAR
      PackingRule@1240030000 : Record 14000715;
      CompanyInfo@1240030001 : Record 79;
      OutputFile@1000000000 : File;
      Value@1000000005 : Text[1000];
      LabelFileName@1240030002 : Text[250];
    BEGIN
      CLEAR(FieldValue);
      FieldValue.Bin_7354(Bin,0);

      CompanyInfo.GET;
      FieldValue.CompanyInformation_79(CompanyInfo,0);

      // Additional tables can be loaded here.

      IF UsePackingStationSetup THEN BEGIN
        PackingStation.TESTFIELD("Label Buffer File");
        LabelFileName := PackingStation.CalcLabelFileName(PackingStation."Label Buffer File");
      END ELSE BEGIN
        ReceiveStation.TESTFIELD("Label Buffer File");
        LabelFileName := ReceiveStation.CalcLabelFileName(ReceiveStation."Label Buffer File");
      END;

      OutputFile.TEXTMODE(TRUE);
      OutputFile.CREATE(LabelFileName);

      FieldValue.LabelHeader_14000841(LabelHeader,0);
      LabelMgt.LabelFileTop(LabelHeader,OutputFile);

      LabelLine.RESET;
      LabelLine.SETRANGE("Label Code",LabelHeader.Code);
      IF LabelLine.FIND('-') THEN
        REPEAT
          FieldValue.LabelLine_14000842(LabelLine,0);
          Value := FieldValue.SubstituteLine(LabelLine);

          LabelMgt.LabelFileLine(LabelHeader,LabelLine,Value,OutputFile);
        UNTIL LabelLine.NEXT = 0;

      LabelMgt.LabelFileBottom(LabelHeader,OutputFile,NoOfCopies);

      OutputFile.CLOSE;

      IF UsePackingStationSetup THEN BEGIN
        PackingStation.TESTFIELD("Std. Pack. Label Printer Port");

        PackingStation.PrintLabel(
          LabelFileName,PackingStation."Std. Pack. Label Printer Port",
          PackingStation."No Label Printer",PackingStation."Auto Delete Label Buffer File",
          Bin.Code,LabelHeader);
      END ELSE BEGIN
        ReceiveStation.TESTFIELD("Label Printer Port");

        ReceiveStation.PrintLabel(
          LabelFileName,ReceiveStation."Item Label Printer Port",
          ReceiveStation."No Label Printer",ReceiveStation."Auto Delete Label Buffer File",
          Bin.Code,LabelHeader);
      END;
    END;

    PROCEDURE LabelFormattingHelp@2();
    BEGIN
      MESSAGE(Text004);
    END;

    PROCEDURE GetTableFilter@3() : Text[250];
    BEGIN
      EXIT('14|79|5400|7354|14000841|14000842');
    END;

    PROCEDURE GetRecordNoStrMenu@4(CurrentTableNo@1000000000 : Integer) : Text[250];
    BEGIN
      CASE CurrentTableNo OF
        ELSE
          EXIT('');
      END;
    END;

    PROCEDURE InitializeRequest@6(NewLabelCode@1240030000 : Code[10];NewNoOfCopies@1240030001 : Integer;NewUsePackingStationSetup@1240030002 : Boolean);
    BEGIN
      LabelHeader.Code := NewLabelCode;
      NoOfCopies := NewNoOfCopies;
      UsePackingStationSetup := NewUsePackingStationSetup;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

