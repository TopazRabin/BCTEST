OBJECT Report 14123502 ES Compare Role
{
  OBJECT-PROPERTIES
  {
    Date=04/30/18;
    Time=12:00:00 PM;
    Version List=ES1.41.51;
  }
  PROPERTIES
  {
    CaptionML=ENU=Compare Permission Set;
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   IF FullPrimaryKey THEN
                     Factor := 0
                   ELSE
                     Factor := 1;

                   ESTextLineTmp.RESET;
                   ESTextLineTmp.DELETEALL;

                   IF Compare THEN BEGIN
                     IF ESRole.GETFILTERS = '' THEN
                       ERROR(Text003,ESRole.TABLECAPTION);
                     IF ESRole.FIND('-') THEN BEGIN
                       IF ESRole.NEXT <> 0 THEN
                         ERROR(Text004,ESRole.TABLECAPTION);
                     END ELSE
                       ERROR(Text004,ESRole.TABLECAPTION);

                     IF ESRoleCompare.GETFILTERS = '' THEN
                       ERROR(Text005,ESRoleCompare.TABLECAPTION);
                     IF ESRoleCompare.FIND('-') THEN BEGIN
                       IF ESRoleCompare.NEXT <> 0 THEN
                         ERROR(Text006,ESRoleCompare.TABLECAPTION);
                     END ELSE
                       ERROR(Text006,ESRoleCompare.TABLECAPTION);

                     ESTextLineOldTmp.RESET;
                     ESTextLineOldTmp.DELETEALL;

                     ESTextLineNewTmp.RESET;
                     ESTextLineNewTmp.DELETEALL;
                   END;

                   CASE ESReportFormat OF
                     ESReportFormat::Basic:
                       IF Compare THEN BEGIN
                         BasicFormat(ESTextLineOldTmp,ESRoleCompare);
                         BasicFormat(ESTextLineNewTmp,ESRole);
                         ESTextLineMgt.Compare(ESTextLineOldTmp,ESTextLineNewTmp,ESTextLineTmp);
                       END ELSE
                         BasicFormat(ESTextLineTmp,ESRole);
                     ESReportFormat::"Role Builder":
                       IF Compare THEN BEGIN
                       DetailedFormat(ESTextLineOldTmp,ESRoleCompare);
                       DetailedFormat(ESTextLineNewTmp,ESRole);
                         ESTextLineMgt.Compare(ESTextLineOldTmp,ESTextLineNewTmp,ESTextLineTmp);
                       END ELSE
                       DetailedFormat(ESTextLineTmp,ESRole);
                   END;

                   IF ISSERVICETIER THEN BEGIN
                     CLEAR(ESTextLinesRTC);
                     ESTextLinesRTC.InitVersion(
                       ESTextLineTmp,STRSUBSTNO('%1 %2',ESRole.TABLECAPTION,FORMAT(ESReportFormat)),Compare);
                     ESTextLinesRTC.RUNMODAL;
                   END ELSE BEGIN
                     CLEAR(ESTextLines);
                     ESTextLines.InitVersion(
                       ESTextLineTmp,STRSUBSTNO('%1 %2',ESRole.TABLECAPTION,FORMAT(ESReportFormat)),Compare);
                     ESTextLines.RUNMODAL;
                   END;
                 END;

  }
  DATASET
  {
    { 2813;    ;DataItem;ESRole              ;
               DataItemTable=Table14123505;
               DataItemTableView=SORTING(Role ID);
               OnPreDataItem=BEGIN
                               CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  CurrReport.BREAK;
                                END;

               ReqFilterFields=Role ID }

    { 8291;    ;DataItem;ESRoleCompare       ;
               DataItemTable=Table14123505;
               DataItemTableView=SORTING(Role ID);
               ReqFilterHeadingML=ENU=Compare to Permission Set;
               OnPreDataItem=BEGIN
                               CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  CurrReport.BREAK;
                                END;

               ReqFilterFields=Role ID }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1240520008;2;Field  ;
                  CaptionML=ENU=Report Format;
                  OptionCaptionML=ENU=Basic,Permission Set Builder;
                  SourceExpr=ESReportFormat }

      { 1240520020;2;Field  ;
                  CaptionML=ENU=Full Primary Key;
                  SourceExpr=FullPrimaryKey }

      { 1240520026;2;Field  ;
                  CaptionML=ENU=Origin Details;
                  SourceExpr=OriginDetails }

      { 1240520021;2;Field  ;
                  CaptionML=ENU=User Date Time Details;
                  SourceExpr=UserDateTimeDetails }

      { 1240520024;2;Field  ;
                  CaptionML=ENU=Compare;
                  SourceExpr=Compare }

      { 1240520015;2;Field  ;
                  CaptionML=ENU=Permissions;
                  SourceExpr=RolePermissions }

      { 1240520011;2;Field  ;
                  CaptionML=ENU=Permission Set Recordings;
                  SourceExpr=RoleRecordings }

      { 1240520013;2;Field  ;
                  CaptionML=ENU=Recordings;
                  SourceExpr=Recordings }

      { 1240520017;2;Field  ;
                  CaptionML=ENU=Recording Permissions;
                  SourceExpr=RecordingPermissions }

      { 1240520019;2;Field  ;
                  CaptionML=ENU=Builder Permissions;
                  SourceExpr=RoleBuilderPermissions }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ESTextLineTmp@1240520009 : TEMPORARY Record 14123520;
      ESTextLineOldTmp@1240520024 : TEMPORARY Record 14123520;
      ESTextLineNewTmp@1240520023 : TEMPORARY Record 14123520;
      ESSecuritySetup@1240520001 : Record 14123521;
      ESTextLines@1240520011 : Page 14123524;
      ESTextLinesRTC@1240520010 : Page 14123555;
      ESTextLineMgt@1240520030 : Codeunit 14123503;
      Window@1240520014 : Dialog;
      ESReportFormat@1240520008 : 'Basic,Role Builder';
      Text001@1240520013 : TextConst 'ENU=Creating Text Lines\';
      Text002@1240520012 : TextConst 'ENU=Process #1################################################';
      RolePermissions@1240520015 : Boolean;
      RoleRecordings@1240520017 : Boolean;
      Recordings@1240520019 : Boolean;
      RecordingPermissions@1240520016 : Boolean;
      RoleBuilderPermissions@1240520018 : Boolean;
      UserDateTimeDetails@1240520020 : Boolean;
      OriginDetails@1240520031 : Boolean;
      FullPrimaryKey@1240520021 : Boolean;
      Compare@1240520025 : Boolean;
      Factor@1240520022 : Integer;
      Text003@1240520029 : TextConst 'ENU=%1 must be filtered.';
      Text004@1240520028 : TextConst 'ENU=Filter on %1 must be a simple filter.';
      Text005@1240520027 : TextConst 'ENU=Compare to %1 must be filtered.';
      Text006@1240520026 : TextConst 'ENU=Filter on Compare to %1 must be a simple filter.';

    PROCEDURE BasicFormat@1240520008(VAR ESTextLine@1240520010 : Record 14123520;VAR CurrentESRole@1240520011 : Record 14123505);
    VAR
      ESRole@1240520008 : Record 14123505;
      ESRolePermission@1240520009 : Record 14123506;
    BEGIN
      IF GUIALLOWED THEN
        Window.OPEN(ESSecuritySetup.ExtendProgressText(Text001) + Text002);

      IF GUIALLOWED THEN
        Window.UPDATE(1,ESRole.TABLECAPTION);
      ESRole.COPYFILTERS(CurrentESRole);
      IF ESRole.FIND('-') THEN
        REPEAT
          ESRole.FormatAsTextLine(ESTextLine,0,TRUE,UserDateTimeDetails);
          ESTextLine.Level := 0;
          ESTextLine.INSERT;

          IF RolePermissions THEN BEGIN
            ESRolePermission.RESET;
            ESRolePermission.SETRANGE("Role ID",ESRole."Role ID");
            IF ESRolePermission.FIND('-') THEN
              REPEAT
                ESRolePermission.FormatAsTextLine(
                  TRUE,ESTextLine,Factor * 1,TRUE,UserDateTimeDetails,OriginDetails);
                ESTextLine.Level := 1;
                ESTextLine.INSERT;
              UNTIL ESRolePermission.NEXT = 0;
          END;
        UNTIL ESRole.NEXT = 0;

      IF GUIALLOWED THEN
        Window.CLOSE;
    END;

    PROCEDURE DetailedFormat@1240520009(VAR ESTextLine@1240520014 : Record 14123520;VAR CurrentESRole@1240520015 : Record 14123505);
    VAR
      ESRole@1240520008 : Record 14123505;
      ESRolePermission@1240520009 : Record 14123506;
      ESRoleRecording@1240520010 : Record 14123654;
      ESRecording@1240520013 : Record 14123651;
      ESRecordingPermission@1240520011 : Record 14123652;
      ESRoleBuilderPermission@1240520012 : Record 14123655;
    BEGIN
      IF GUIALLOWED THEN
        Window.OPEN(ESSecuritySetup.ExtendProgressText(Text001) + Text002);

      IF GUIALLOWED THEN
        Window.UPDATE(1,ESRole.TABLECAPTION);
      ESRole.COPYFILTERS(CurrentESRole);
      IF ESRole.FIND('-') THEN
        REPEAT
          ESRole.FormatAsTextLine(ESTextLine,0,TRUE,UserDateTimeDetails);
          ESTextLine.Level := 0;
          ESTextLine.INSERT;

          IF RolePermissions THEN BEGIN
            ESRolePermission.RESET;
            ESRolePermission.SETRANGE("Role ID",ESRole."Role ID");
            IF ESRolePermission.FIND('-') THEN
              REPEAT
                ESRolePermission.FormatAsTextLine(
                  TRUE,ESTextLine,Factor * 1,TRUE,UserDateTimeDetails,OriginDetails);
                ESTextLine.Level := 1;
                ESTextLine.INSERT;
              UNTIL ESRolePermission.NEXT = 0;
          END;

          IF RoleRecordings THEN BEGIN
            ESRoleRecording.RESET;
            ESRoleRecording.SETRANGE("Role ID",ESRole."Role ID");
            IF ESRoleRecording.FIND('-') THEN
              REPEAT
                ESRoleRecording.FormatAsTextLine(ESTextLine,Factor * 1,TRUE,UserDateTimeDetails);
                ESTextLine.Level := 1;
                ESTextLine.INSERT;

                IF Recordings THEN BEGIN
                  ESRecording.GET(ESRoleRecording."Recording Code");
                  ESRecording.FormatAsTextLine(ESTextLine,0,TRUE,UserDateTimeDetails);
                  ESTextLine.Level := 2;
                  ESTextLine.INSERT;

                  IF RecordingPermissions THEN BEGIN
                    ESRecordingPermission.RESET;
                    ESRecordingPermission.SETRANGE("Recording Code",ESRecording.Code);
                    IF ESRecordingPermission.FIND('-') THEN
                      REPEAT
                        ESRecordingPermission.FormatAsTextLine(
                          TRUE,ESTextLine,Factor * 1,TRUE,UserDateTimeDetails,OriginDetails);
                        ESTextLine.Level := 3;
                        ESTextLine.INSERT;
                      UNTIL ESRecordingPermission.NEXT = 0;
                  END;
                END;
              UNTIL ESRoleRecording.NEXT = 0;
          END;

          IF RoleBuilderPermissions THEN BEGIN
            ESRoleBuilderPermission.RESET;
            ESRoleBuilderPermission.SETRANGE("Role ID",ESRole."Role ID");
            IF ESRoleBuilderPermission.FIND('-') THEN
              REPEAT
                ESRoleBuilderPermission.FormatAsTextLine(
                  TRUE,ESTextLine,Factor * 1,TRUE,UserDateTimeDetails,OriginDetails);
                ESTextLine.Level := 1;
                ESTextLine.INSERT;
              UNTIL ESRoleBuilderPermission.NEXT = 0;
          END;
        UNTIL ESRole.NEXT = 0;

      IF GUIALLOWED THEN
        Window.CLOSE;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

