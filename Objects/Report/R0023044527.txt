OBJECT Report 23044527 WHI Export All Tables
{
  OBJECT-PROPERTIES
  {
    Date=10/03/19;
    Time=[ 9:04:23 AM];
    Version List=WHI2.4.7684.0;
  }
  PROPERTIES
  {
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   txtSafeCompanyName := DELCHR( COMPANYNAME(),'=', '=+-_<>,.!~*& ');
                   txtZipBackup := STRSUBSTNO('%1WHIConfigurationBackup.zip',TEMPORARYPATH() );


                   dnZipArchiveMode := dnZipArchiveMode.Update;
                   dnZipArchive :=  dnZipFile.Open( txtZipBackup, dnZipArchiveMode );
                 END;

  }
  DATASET
  {
    { 1000000000;;DataItem;WHITables         ;
               DataItemTable=Table2000000001;
               DataItemTableView=SORTING(Type,Company Name,ID)
                                 ORDER(Ascending)
                                 WHERE(Type=CONST(Table),
                                       Name=FILTER(<>Usage&<>Entry),
                                       Version List=FILTER(*WMDM*),
                                       ID=FILTER(23044000..23044999));
               OnAfterGetRecord=VAR
                                  lrecRefTestContents@1000000000 : RecordRef;
                                  rptUniversalExport@1000000001 : Report 23044526;
                                BEGIN
                                  lrecRefTestContents.OPEN( WHITables.ID );
                                  IF ( lrecRefTestContents.FINDFIRST() ) THEN BEGIN
                                    // only export if there are values in the table
                                    txtFileName := STRSUBSTNO('%3WHI_Table_%1_%2.txt',
                                      WHITables.ID,
                                      txtSafeCompanyName,
                                      TEMPORARYPATH()  );
                                    rptUniversalExport.setDefaults(txtFileName, WHITables.ID,'');
                                    rptUniversalExport.USEREQUESTPAGE(FALSE);
                                    rptUniversalExport.RUNMODAL();
                                    dnZipAdder.CreateEntryFromFile(
                                      dnZipArchive,
                                      txtFileName,
                                      STRSUBSTNO('WHITable%1.txt', WHITables.ID));

                                  END;
                                END;

               OnPostDataItem=BEGIN
                                CLEAR( dnZipAdder );

                                CLEAR( dnZipFile );
                                CLEAR( dnZipArchiveMode );
                                dnZipArchive.Dispose(); // work around NAV bug with disposing objects.
                                CLEAR( dnZipArchive );

                                txtLocalFileName := STRSUBSTNO('WHISettingsBackup.zip');
                                cuCompatibility.PromptAndDownloadFile(
                                  'Zip Files|*.zip',
                                  txtZipBackup,
                                  txtLocalFileName);
                              END;

               ReqFilterFields=Type,Name,Version List }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      txtFileName@1000000001 : Text[1024];
      txtSafeCompanyName@1000000002 : Text;
      dnZipFile@1000000003 : DotNet "'System.IO.Compression.FileSystem, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.Compression.ZipFile";
      dnZipArchive@1000000004 : DotNet "'System.IO.Compression, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.Compression.ZipArchive";
      dnZipArchiveMode@1000000006 : DotNet "'System.IO.Compression, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.Compression.ZipArchiveMode";
      dnZipAdder@1000000000 : DotNet "'System.IO.Compression.FileSystem, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.Compression.ZipFileExtensions";
      txtZipBackup@1000000005 : Text;
      cuCompatibility@1000000007 : Codeunit 23044530;
      txtLocalFileName@1000000008 : Text;

    BEGIN
    {
      ************************
      Copyright Notice
      This objects content is copyright of Insight Works 2011.  All rights reserved.
      Any redistribution or reproduction of part or all of the contents in any form is prohibited.
      ************************
    }
    END.
  }
  RDLDATA
  {
  }
}

