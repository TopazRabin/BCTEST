OBJECT Report 14050302 Send Purch. Order Ack. Job
{
  OBJECT-PROPERTIES
  {
    Date=11/11/13;
    Time=12:00:00 PM;
    Version List=SE0.60;
  }
  PROPERTIES
  {
    Permissions=TableData 112=rimd;
    CaptionML=ENU=Send Purch. Order Ack. Job;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  CompanyInformation.GET;
                END;

    UseRequestPage=No;
  }
  DATASET
  {
    { 2284;    ;DataItem;                    ;
               DataItemTable=Table14050305 }

    { 2527;    ;DataItem;EDITradePartner     ;
               DataItemTable=Table14002360;
               PrintOnlyIfDetail=Yes;
               OnPreDataItem=BEGIN
                               EDITradePartner.SETFILTER("No.","E.D.I. Job Routine"."Trade Partner Filter");
                               EDITradePartner.SETFILTER("Company Name","E.D.I. Job Routine"."Company Filter");
                             END;

               ReqFilterFields=No. }

    { 6640;1   ;DataItem;                    ;
               DataItemTable=Table36;
               DataItemTableView=SORTING(EDI Order,EDI Trade Partner,EDI Internal Doc. No.)
                                 ORDER(Ascending)
                                 WHERE(EDI Order=CONST(Yes),
                                       EDI Released=CONST(Yes),
                                       EDI Ack. Generated=CONST(No));
               OnAfterGetRecord=BEGIN
                                  NavisionDocument := '';
                                  EDIDocument.SETRANGE("Trade Partner No.",EDITradePartner."No.");
                                  EDIDocument.SETFILTER(
                                    Status,'%1|%2',EDIDocument.Status::Test,EDIDocument.Status::Production);
                                  EDIDocument.SETRANGE("Navision Document",'E_SLSPOA');
                                  EDIDocument.SETRANGE(Type,EDIDocument.Type::Export);
                                  IF EDIDocument.FIND('-') THEN
                                    NavisionDocument := EDIDocument."Navision Document"
                                  ELSE BEGIN
                                    EDIDocument.INIT;
                                    NavisionDocument := 'E_SLSPOA';
                                  END;

                                  EDIManagement.CreateOutboundLogEntry(
                                    Text001,LogEntryNo,'SALES ORDER',"Sales Header"."No.",EDITradePartner."No.",
                                    NavisionDocument,EDIDocument."EDI Document No.");
                                  COMMIT;

                                  CLEARLASTERROR;
                                  CLEAR(EDISalesOrderSend);
                                  EDISalesOrderSend.SetProcessID(1);
                                  IF EDISalesOrderSend.RUN("Sales Header") THEN
                                    DeleteLog := TRUE
                                  ELSE
                                    DeleteLog := FALSE;
                                  COMMIT;

                                  IF DeleteLog THEN
                                    EDIManagement.DeleteOutboundLogEntry(LogEntryNo)
                                  ELSE BEGIN
                                    ErrorText := '';
                                    ErrorText := COPYSTR(GETLASTERRORTEXT,1,250);
                                    EDIManagement.CloseOutboundLogEntry(LogEntryNo,ErrorText);
                                  END;
                                  COMMIT;
                                END;

               ReqFilterFields=Bill-to Customer No.,No.;
               DataItemLink=EDI Trade Partner=FIELD(No.) }

    { 1534;1   ;DataItem;                    ;
               DataItemTable=Table14002353;
               DataItemTableView=SORTING(Trade Partner No.,Navision Document,EDI Document No.,Version,Type)
                                 ORDER(Ascending)
                                 WHERE(Navision Document=CONST(E_SLSPOA),
                                       Type=CONST(Export),
                                       Status=FILTER(Test|Production));
               OnPreDataItem=BEGIN
                               EDISetup.GET;
                               "E.D.I. Document".SETFILTER("EDI Document No.","E.D.I. Job Routine"."EDI Document No. Filter");
                             END;

               OnAfterGetRecord=BEGIN
                                  IF EDISetup."Auto Export Send Doc." THEN BEGIN
                                    EDIManagement.CreateOutboundLogEntry(Text002,LogEntryNo,'SEND DOCUMENT','',EDITradePartner."No.",
                                    "E.D.I. Document"."Navision Document","E.D.I. Document"."EDI Document No.");
                                    COMMIT;

                                    CLEARLASTERROR;
                                    IF EDISend.RUN("E.D.I. Document") THEN
                                      DeleteLog := TRUE
                                    ELSE
                                      DeleteLog := FALSE;
                                    COMMIT;

                                    IF DeleteLog THEN
                                      EDIManagement.DeleteOutboundLogEntry(LogEntryNo)
                                    ELSE BEGIN
                                      ErrorText := '';
                                      ErrorText := COPYSTR(GETLASTERRORTEXT,1,250);
                                      EDIManagement.CloseOutboundLogEntry(LogEntryNo,ErrorText);
                                    END;
                                    COMMIT;
                                  END;
                                END;

               DataItemLink=Trade Partner No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CompanyInformation@1240030000 : Record 79;
      EDISetup@1240030001 : Record 14002367;
      EDIDocument@1240020006 : Record 14002353;
      EDISalesOrderSend@1240030002 : Codeunit 14002359;
      EDISend@1240030003 : Codeunit 14002356;
      EDIManagement@1240020000 : Codeunit 14002379;
      Text001@1001 : TextConst 'ENU=Create EDI Purchase Order Ack.';
      Text002@1002 : TextConst 'ENU=Send EDI Purchase Order Ack.';
      LogEntryNo@1240020001 : Integer;
      DeleteLog@1240020002 : Boolean;
      EDILog@1240020003 : Record 14050302;
      ErrorText@1240020004 : Text[250];
      NavisionDocument@1240020005 : Code[10];

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

