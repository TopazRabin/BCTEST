OBJECT Report 14123505 ES Copy User Groups
{
  OBJECT-PROPERTIES
  {
    Date=12/14/18;
    Time=12:00:00 PM;
    Version List=ES1.41.56;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy User Groups;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1   ;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               MaxIteration=1;
               OnAfterGetRecord=BEGIN
                                  IF CleanUserGroupData THEN
                                    CleanUserGroups(TRUE);

                                  IF CopyUserGroupsData THEN
                                    CopyNAVUserGroups(TRUE,TRUE);

                                  IF DeleteUserGroupAccessCtrlsData THEN
                                    DeleteNAVUserGroupAccessCtrls(TRUE);

                                  IF DeleteUserGroupsData THEN
                                    DeleteNAVUserGroups(TRUE);
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1   ;    ;Container ;
                  Name=Request;
                  ContainerType=ContentArea }

      { 2   ;1   ;Group     ;
                  Name=Options;
                  CaptionML=ENU=Options;
                  GroupType=Group }

      { 6   ;2   ;Field     ;
                  CaptionML=ENU=Clean User Groups;
                  SourceExpr=CleanUserGroupData }

      { 3   ;2   ;Field     ;
                  CaptionML=ENU=Copy User Groups;
                  SourceExpr=CopyUserGroupsData }

      { 4   ;2   ;Field     ;
                  CaptionML=ENU=Delete User Group Members;
                  SourceExpr=DeleteUserGroupAccessCtrlsData }

      { 5   ;2   ;Field     ;
                  CaptionML=ENU=Delete User Groups;
                  SourceExpr=DeleteUserGroupsData }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text201@1002 : TextConst 'ENU=%1 for %2 is not unique.';
      Text202@1001 : TextConst 'ENU=Inserted %1 %2 and %3 %4.';
      Text203@1000 : TextConst 'ENU=Deleted %1 Duplicate %2.';
      CleanUserGroupData@1006 : Boolean;
      CopyUserGroupsData@1003 : Boolean;
      DeleteUserGroupAccessCtrlsData@1004 : Boolean;
      DeleteUserGroupsData@1005 : Boolean;
      Text001@1007 : TextConst 'ENU=No inconsistent data exists.';
      Text002@1008 : TextConst 'ENU=Delete %1 %2 and %3 %4 inconsistent data?';
      Text003@1009 : TextConst 'ENU=%1 %2 and %3 %4 has been deleted.';
      Text004@1010 : TextConst 'ENU=No %1 exists.';
      Text005@1011 : TextConst 'ENU=User Groups has been copied to Permission Groups.';
      Text006@1012 : TextConst 'ENU=Delete User Group Members';
      Text007@1013 : TextConst 'ENU=%1 and %2 has been deleted.';
      Text008@1014 : TextConst 'ENU=%1 exists.';

    PROCEDURE CleanUserGroups@3(ShowStatus@1001 : Boolean);
    VAR
      UserGroupMember@1000 : Record 9001;
      UserGroupMember2@1008 : Record 9001;
      Company@1005 : Record 2000000006;
      UserGroupPermissionSet@1006 : Record 9003;
      UserGroupPermissionSet2@1009 : Record 9003;
      PermissionSet@1003 : Record 2000000004;
      TenantPermissionSet@1004 : Record 2000000165;
      WrongUserGroupMembers@1002 : Integer;
      WrongUserGroupPermissionSets@1007 : Integer;
    BEGIN
      UserGroupMember.RESET;
      UserGroupMember.SETFILTER("Company Name",'<>%1','');
      IF UserGroupMember.FIND('-') THEN
        REPEAT
          IF NOT Company.GET(UserGroupMember."Company Name") THEN
            WrongUserGroupMembers := WrongUserGroupMembers + 1;
        UNTIL UserGroupMember.NEXT = 0;

      UserGroupPermissionSet.RESET;
      IF UserGroupPermissionSet.FIND('-') THEN
        REPEAT
          IF NOT PermissionSet.GET(UserGroupPermissionSet."Role ID") THEN BEGIN
            TenantPermissionSet.RESET;
            TenantPermissionSet.SETRANGE("Role ID",UserGroupPermissionSet."Role ID");
            IF NOT TenantPermissionSet.FIND('-') THEN
              WrongUserGroupPermissionSets := WrongUserGroupPermissionSets + 1;
          END;
        UNTIL UserGroupPermissionSet.NEXT = 0;

      IF (WrongUserGroupMembers = 0) AND (WrongUserGroupPermissionSets = 0) THEN BEGIN
        MESSAGE(Text001);
        EXIT;
      END ELSE
        IF NOT CONFIRM(
                 Text002,FALSE,
                 WrongUserGroupMembers,UserGroupMember.TABLECAPTION,
                 WrongUserGroupPermissionSets,UserGroupPermissionSet.TABLECAPTION)
        THEN
          EXIT;

      WrongUserGroupMembers := 0;

      UserGroupMember.RESET;
      UserGroupMember.SETFILTER("Company Name",'<>%1','');
      IF UserGroupMember.FIND('-') THEN
        REPEAT
          IF NOT Company.GET(UserGroupMember."Company Name") THEN BEGIN
            UserGroupMember2 := UserGroupMember;
            UserGroupMember2.DELETE;

            WrongUserGroupMembers := WrongUserGroupMembers + 1;
          END;
        UNTIL UserGroupMember.NEXT = 0;

      UserGroupPermissionSet.RESET;
      IF UserGroupPermissionSet.FIND('-') THEN
        REPEAT
          IF NOT PermissionSet.GET(UserGroupPermissionSet."Role ID") THEN BEGIN
            TenantPermissionSet.RESET;
            TenantPermissionSet.SETRANGE("Role ID",UserGroupPermissionSet."Role ID");
            IF NOT TenantPermissionSet.FIND('-') THEN BEGIN
              UserGroupPermissionSet2 := UserGroupPermissionSet;
              UserGroupPermissionSet2.DELETE;

              WrongUserGroupPermissionSets := WrongUserGroupPermissionSets + 1;
            END;
          END;
        UNTIL UserGroupPermissionSet.NEXT = 0;

      IF ShowStatus THEN
        MESSAGE(
          Text003,WrongUserGroupMembers,UserGroupMember.TABLECAPTION,
          WrongUserGroupPermissionSets,UserGroupPermissionSet);
    END;

    PROCEDURE CopyNAVUserGroups@1240520003(RemoveDuplicates@1014 : Boolean;ShowStatus@1002 : Boolean);
    VAR
      ESSecuritySetup@1006 : Record 14123521;
      UserGroup@1000 : Record 9000;
      UserGroupPermissionSet@1001 : Record 9003;
      ESRoleGroup@1003 : Record 14123525;
      ESRoleGroupLine@1004 : Record 14123526;
      ESLogin@1010 : Record 14123527;
      ESLoginAccessControl@1009 : Record 14123528;
      ESCompanyGroup@1012 : Record 14123530;
      ESCompanyGroupLine@1013 : Record 14123531;
      UserGroupMember@1005 : Record 9001;
      NewRoleGroups@1007 : Integer;
      NewLoginAccessControls@1008 : Integer;
      DeletedAccessControls@1015 : Integer;
      TempText@1016 : Text[1000];
    BEGIN
      UserGroupMember.RESET;
      IF NOT UserGroupMember.FIND('-') THEN
        ERROR(Text004,UserGroupMember.TABLECAPTION);

      ESSecuritySetup.GET;
      ESSecuritySetup.TestPublishCompany;

      UserGroup.RESET;
      IF UserGroup.FIND('-') THEN BEGIN
        REPEAT
          ESRoleGroup.INIT;
          ESRoleGroup.VALIDATE("Group ID",UserGroup.Code);
          ESRoleGroup.VALIDATE(Name,UserGroup.Name);
          ESRoleGroup.INSERT(TRUE);

          UserGroupPermissionSet.RESET;
          UserGroupPermissionSet.SETRANGE("User Group Code",UserGroup.Code);
          IF UserGroupPermissionSet.FIND('-') THEN BEGIN
            REPEAT
              ESRoleGroupLine.INIT;
              ESRoleGroupLine.VALIDATE("Role Group ID",ESRoleGroup."Group ID");
              ESRoleGroupLine.VALIDATE("Role Type",ESRoleGroupLine."Role Type"::Role);
              ESRoleGroupLine.VALIDATE("Role ID",UserGroupPermissionSet."Role ID");
              ESRoleGroupLine.INSERT(TRUE);
            UNTIL UserGroupPermissionSet.NEXT = 0;
          END;

          NewRoleGroups := NewRoleGroups + 1;
        UNTIL UserGroup.NEXT = 0;
      END;

      UserGroupMember.RESET;
      IF UserGroupMember.FIND('-') THEN
        REPEAT
          ESLogin.RESET;
          ESLogin.SETRANGE("Login SID",UserGroupMember."User Security ID");
          ESLogin.FIND('-');

          ESLoginAccessControl.INIT;
          ESLoginAccessControl.VALIDATE("Login Type",ESLogin.Type);
          ESLoginAccessControl.VALIDATE("Login User ID",ESLogin."User ID");
          ESLoginAccessControl.VALIDATE("Role Type",ESLoginAccessControl."Role Type"::Group);
          ESLoginAccessControl.VALIDATE("Role ID",UserGroupMember."User Group Code");
          IF UserGroupMember."Company Name" <> '' THEN BEGIN
            ESCompanyGroupLine.RESET;
            ESCompanyGroupLine.SETRANGE("Company Name",UserGroupMember."Company Name");
            ESCompanyGroupLine.FIND('-');
            IF ESCompanyGroupLine.NEXT > 0 THEN
              ERROR(Text201,ESCompanyGroup.TABLECAPTION,UserGroupMember."Company Name");
            ESLoginAccessControl.VALIDATE("Company Group ID",ESCompanyGroupLine."Company Group ID");
          END ELSE
            ESLoginAccessControl."Company Group ID" := '';
          ESLoginAccessControl.SetAllowBlankNoQuestion(TRUE,FALSE);
          ESLoginAccessControl.INSERT(TRUE);

          NewLoginAccessControls := NewLoginAccessControls + 1;
        UNTIL UserGroupMember.NEXT = 0;

      TempText := Text202;

      IF RemoveDuplicates THEN BEGIN
        ESLogin.RESET;
        IF ESLogin.FIND('-') THEN
          REPEAT
            DeletedAccessControls := DeletedAccessControls + ESLogin.RemoveDuplicateRoles(FALSE);
          UNTIL ESLogin.NEXT = 0;

        TempText :=
          TempText + ' ' +
          STRSUBSTNO(Text203,DeletedAccessControls,ESLoginAccessControl.TABLECAPTION);
      END;

      IF ShowStatus THEN
        MESSAGE(
          TempText,NewRoleGroups,ESRoleGroup.TABLECAPTION,
          NewLoginAccessControls,ESLoginAccessControl.TABLECAPTION);
      ESSecuritySetup.Comment(
        1,
        STRSUBSTNO(
          TempText,NewRoleGroups,ESRoleGroup.TABLECAPTION,
          NewLoginAccessControls,ESLoginAccessControl.TABLECAPTION));

      MESSAGE(Text005);
    END;

    PROCEDURE DeleteNAVUserGroupAccessCtrls@1(ShowStatus@1000 : Boolean);
    VAR
      ESSecuritySetup@1001 : Record 14123521;
      UserGroupMember@1002 : Record 9001;
      UserGroupAccessControl@1006 : Record 9002;
      UserPersonalization@1003 : Record 2000000073;
      UserPersonalizationTmp@1004 : TEMPORARY Record 2000000073;
      ESMgt@1005 : Codeunit 14123501;
    BEGIN
      UserGroupMember.RESET;
      IF NOT UserGroupMember.FIND('-') THEN
        ERROR(Text004,UserGroupMember.TABLECAPTION);

      ESSecuritySetup.GET;
      ESSecuritySetup.TestPublishCompany;

      CLEAR(ESMgt);
      ESMgt.PublishSecurityTestRequirement(TRUE,TRUE,FALSE);

      UserPersonalization.RESET;
      IF UserPersonalization.FIND('-') THEN
        REPEAT
          UserPersonalizationTmp := UserPersonalization;
          UserPersonalizationTmp.INSERT;
        UNTIL UserPersonalization.NEXT = 0;

      UserGroupMember.DELETEALL;
      UserGroupAccessControl.DELETEALL;

      UserPersonalization.RESET;
      IF UserPersonalization.FIND('-') THEN
        REPEAT
          UserPersonalizationTmp.GET(UserPersonalization."User SID");
          IF UserPersonalization."Profile ID" <> UserPersonalizationTmp."Profile ID" THEN BEGIN
            UserPersonalization."Profile ID" := UserPersonalizationTmp."Profile ID";
            UserPersonalization.MODIFY(TRUE);
          END;
        UNTIL UserPersonalization.NEXT = 0;

      CLEAR(ESMgt);
      ESMgt.PublishSecurity(
        Text006,TRUE,TRUE,TRUE,TRUE,TRUE,TRUE,TRUE,FALSE);

      MESSAGE(
        Text007,UserGroupMember.TABLECAPTION,UserGroupAccessControl.TABLECAPTION);
    END;

    PROCEDURE DeleteNAVUserGroups@2(ShowStatus@1000 : Boolean);
    VAR
      ESSecuritySetup@1001 : Record 14123521;
      UserGroupMember@1002 : Record 9001;
      UserGroupAccessControl@1003 : Record 9002;
      UserGroup@1004 : Record 9000;
      UserGroupPermissionSet@1005 : Record 9003;
    BEGIN
      UserGroup.RESET;
      IF NOT UserGroup.FIND('-') THEN
        ERROR(Text004,UserGroup.TABLECAPTION);

      ESSecuritySetup.GET;
      ESSecuritySetup.TestPublishCompany;

      UserGroupMember.RESET;
      IF UserGroupMember.FIND('-') THEN
        ERROR(Text008,UserGroupMember.TABLECAPTION);

      UserGroupAccessControl.RESET;
      IF UserGroupAccessControl.FIND('-') THEN
        ERROR(Text008,UserGroupAccessControl.TABLECAPTION);

      UserGroup.RESET;
      UserGroup.DELETEALL(TRUE);

      UserGroupPermissionSet.RESET;
      UserGroupPermissionSet.DELETEALL;

      MESSAGE(
        Text007,UserGroup.TABLECAPTION,UserGroupPermissionSet.TABLECAPTION);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

