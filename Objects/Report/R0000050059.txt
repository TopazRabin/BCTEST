OBJECT Report 50059 DG2 Upload Cust. Profit Entry
{
  OBJECT-PROPERTIES
  {
    Date=07/01/20;
    Time=12:00:00 AM;
    Modified=Yes;
    Version List=TPZ000.00.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=DG2 Upload Cust. Profit Entry;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  ExcelBuf.DELETEALL;
                  ExcelBuf.OpenBook(ServerFileName,SheetName);
                  ExcelBuf.ReadSheet;
                  AnalyzeData;
                END;

    OnPostReport=BEGIN
                   MESSAGE('Customer Profit Entries Uploa Finished');
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1000000000;;Container;
                  Name=ContentArea;
                  ContainerType=ContentArea }

      { 1000000003;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 1000000004;2;Group  ;
                  CaptionML=[ENU=Import from;
                             ESM=Importar de;
                             FRC=Importer de;
                             ENC=Import from] }

      { 1000000001;3;Field  ;
                  CaptionML=[ENU=Workbook File Name;
                             ESM=Nombre fichero libro;
                             FRC=Nom du classeur;
                             ENC=Workbook File Name];
                  SourceExpr=FileName;
                  Editable=FALSE;
                  OnValidate=BEGIN
                               FileNameOnAfterValidate;
                             END;

                  OnAssistEdit=BEGIN
                                 RequestFile;
                                 SheetName := ExcelBuf.SelectSheetsName(ServerFileName);
                               END;
                                }

      { 1000000002;3;Field  ;
                  CaptionML=[ENU=Worksheet Name;
                             ESM=Nombre hoja;
                             FRC=Nom de la feuille de travail;
                             ENC=Worksheet Name];
                  SourceExpr=SheetName;
                  Editable=FALSE;
                  OnAssistEdit=BEGIN
                                 IF ServerFileName = '' THEN
                                   RequestFile;

                                 SheetName := ExcelBuf.SelectSheetsName(ServerFileName);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ExcelBuf@1000000009 : TEMPORARY Record 370;
      cuFileMgt@1000000007 : Codeunit 419;
      FileName@1000000006 : Text[250];
      ServerFileName@1000000005 : Text[250];
      SheetName@1000000004 : Text[250];
      TotalRecNo@1000000003 : Integer;
      RecNo@1000000002 : Integer;
      EntryNo@1000000001 : Integer;
      Window@1000000000 : Dialog;
      Text006@1000000011 : TextConst 'ENU=Import Excel File;ESM=Importar fich. Excel;FRC=Importer fichier Excel;ENC=Import Excel File';
      Text007@1000000010 : TextConst 'ENU=Analyzing Data...\\;ESM=Analizar Datos...\\;FRC=Analyse des donn‚es...\\;ENC=Analysing Data...\\';
      Text029@1000000008 : TextConst 'ENU=You must enter a file name.;ESM=Debe introd. nombre fichero.;FRC=Vous devez entrer un nom de fichier.;ENC=You must enter a file name.';
      UpdateCount@1000000012 : Integer;

    PROCEDURE RequestFile@6();
    BEGIN
      IF FileName <> '' THEN
        ServerFileName := cuFileMgt.UploadFile(Text006,FileName)
      ELSE
        ServerFileName := cuFileMgt.UploadFile(Text006,'.xlsx');

      ValidateServerFileName;
      FileName := cuFileMgt.GetFileName(ServerFileName);
    END;

    LOCAL PROCEDURE FileNameOnAfterValidate@19040090();
    BEGIN
      RequestFile;
    END;

    LOCAL PROCEDURE ValidateServerFileName@9();
    BEGIN
      IF ServerFileName = '' THEN
      BEGIN
        FileName := '';
        SheetName := '';
        ERROR(Text029);
      END;
    END;

    PROCEDURE AnalyzeData@1000000000();
    VAR
      YearTxt@1000000000 : Text;
      MonthTxt@1000000001 : Text;
      CustNo@1000000002 : Code[20];
      DivisionCode@1000000003 : Code[10];
      GrossSalesTxt@1000000004 : Text;
      CashDiscTxt@1000000005 : Text;
      IMARKTxt@1000000006 : Text;
      RebatesTxt@1000000007 : Text;
      PromotionsTxt@1000000008 : Text;
      OtherDeductionTxt@1000000009 : Text;
      LandedCostTxt@1000000010 : Text;
      Freight3rdPartyTxt@1000000011 : Text;
      FreightLocalFleetTxt@1000000012 : Text;
      FreightToCustTxt@1000000013 : Text;
      WarehouseTxt@1000000014 : Text;
      MfrRepComTxt@1000000015 : Text;
      StockingRepComTxt@1000000016 : Text;
      CustProfitEntry@1000000017 : Record 50011;
      NewEntryNo@1000000018 : Integer;
    BEGIN
      UpdateCount := 0;

      Window.OPEN(
        Text007 +
        '@1@@@@@@@@@@@@@@@@@@@@@@@@@\');
      Window.UPDATE(1,0);
      TotalRecNo := ExcelBuf.COUNT;
      RecNo := 0;

      NewEntryNo := 1;
      IF CustProfitEntry.FINDLAST THEN
        NewEntryNo := CustProfitEntry."Entry No." + 1;


      ExcelBuf.SETFILTER(ExcelBuf."Row No.",'2..');
      IF ExcelBuf.FINDSET THEN
      BEGIN
        REPEAT
          RecNo := RecNo + 1;
          Window.UPDATE(1,ROUND(RecNo / TotalRecNo * 10000,1));

          CASE ExcelBuf.xlColID OF
            'A': YearTxt := ExcelBuf."Cell Value as Text";
            'B': MonthTxt := ExcelBuf."Cell Value as Text";
            'C': CustNo := ExcelBuf."Cell Value as Text";
            'D': DivisionCode := ExcelBuf."Cell Value as Text";
            'E': GrossSalesTxt := ExcelBuf."Cell Value as Text";
            'F': CashDiscTxt := ExcelBuf."Cell Value as Text";
            'G': IMARKTxt := ExcelBuf."Cell Value as Text";
            'H': RebatesTxt := ExcelBuf."Cell Value as Text";
            'I': PromotionsTxt := ExcelBuf."Cell Value as Text";
            'J': OtherDeductionTxt := ExcelBuf."Cell Value as Text";
            'K': LandedCostTxt := ExcelBuf."Cell Value as Text";
            'L': Freight3rdPartyTxt := ExcelBuf."Cell Value as Text";
            'M': FreightLocalFleetTxt := ExcelBuf."Cell Value as Text";
            'N': FreightToCustTxt := ExcelBuf."Cell Value as Text";
            'O': WarehouseTxt := ExcelBuf."Cell Value as Text";
            'P': MfrRepComTxt := ExcelBuf."Cell Value as Text";
            'Q': BEGIN
               StockingRepComTxt := ExcelBuf."Cell Value as Text";

               //Insert record in customer profit entry table.
               CustProfitEntry.INIT;
               WITH CustProfitEntry DO BEGIN
                "Entry No." := NewEntryNo;
                EVALUATE(Year, YearTxt);
                EVALUATE(Month, MonthTxt);
                "Customer No." := CustNo;
                "Division Code" := DivisionCode;
                EVALUATE("Gross Sales", GrossSalesTxt);
                EVALUATE("Cash Discounts", CashDiscTxt);
                EVALUATE(IMARK, IMARKTxt);
                EVALUATE(Rebates, RebatesTxt);
                EVALUATE(Promotions, PromotionsTxt);
                EVALUATE("Other Deductions",OtherDeductionTxt);
                EVALUATE("Landed Cost", LandedCostTxt);
                EVALUATE("Freight - 3rd Party", Freight3rdPartyTxt);
                EVALUATE("Freight - Local Fleet", FreightLocalFleetTxt);
                EVALUATE("Freight Charged to Customer", FreightToCustTxt);
                EVALUATE(Warehouse, WarehouseTxt);
                EVALUATE("Mfr. Rep Commissions", MfrRepComTxt);
                EVALUATE("Stocking Rep Commisions", StockingRepComTxt);
                "User ID" := USERID;
                "Creation Date Time" := CURRENTDATETIME;
                INSERT;
                NewEntryNo += 1;
               END;
              //UpdateCount += 1;
            END;
          END;

        UNTIL ExcelBuf.NEXT = 0;
      END;
      Window.CLOSE;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

