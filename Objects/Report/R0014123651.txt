OBJECT Report 14123651 ES Compare Recording
{
  OBJECT-PROPERTIES
  {
    Date=04/30/18;
    Time=12:00:00 PM;
    Version List=ES1.41.51;
  }
  PROPERTIES
  {
    CaptionML=ENU=Compare Recording;
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   IF FullPrimaryKey THEN
                     Factor := 0
                   ELSE
                     Factor := 1;

                   ESTextLineTmp.RESET;
                   ESTextLineTmp.DELETEALL;

                   IF Compare THEN BEGIN
                     IF ESRecording.GETFILTERS = '' THEN
                       ERROR(Text003,ESRecording.TABLECAPTION);
                     IF ESRecording.FIND('-') THEN BEGIN
                       IF ESRecording.NEXT <> 0 THEN
                         ERROR(Text004,ESRecording.TABLECAPTION);
                     END ELSE
                       ERROR(Text004,ESRecording.TABLECAPTION);

                     IF ESRecordingCompare.GETFILTERS = '' THEN
                       ERROR(Text005,ESRecordingCompare.TABLECAPTION);
                     IF ESRecordingCompare.FIND('-') THEN BEGIN
                       IF ESRecordingCompare.NEXT <> 0 THEN
                         ERROR(Text006,ESRecordingCompare.TABLECAPTION);
                     END ELSE
                       ERROR(Text006,ESRecordingCompare.TABLECAPTION);

                     ESTextLineOldTmp.RESET;
                     ESTextLineOldTmp.DELETEALL;

                     ESTextLineNewTmp.RESET;
                     ESTextLineNewTmp.DELETEALL;
                   END;

                   CASE ESReportFormat OF
                     ESReportFormat::Basic:
                       IF Compare THEN BEGIN
                         BasicFormat(ESTextLineOldTmp,ESRecordingCompare);
                         BasicFormat(ESTextLineNewTmp,ESRecording);
                         ESTextLineMgt.Compare(ESTextLineOldTmp,ESTextLineNewTmp,ESTextLineTmp);
                       END ELSE
                         BasicFormat(ESTextLineTmp,ESRecording);
                   END;

                   IF ISSERVICETIER THEN BEGIN
                     CLEAR(ESTextLinesRTC);
                     ESTextLinesRTC.InitVersion(
                       ESTextLineTmp,STRSUBSTNO('%1 %2',ESRecording.TABLECAPTION,FORMAT(ESReportFormat)),Compare);
                     ESTextLinesRTC.RUNMODAL;
                   END ELSE BEGIN
                     CLEAR(ESTextLines);
                     ESTextLines.InitVersion(
                       ESTextLineTmp,STRSUBSTNO('%1 %2',ESRecording.TABLECAPTION,FORMAT(ESReportFormat)),Compare);
                     ESTextLines.RUNMODAL;
                   END;
                 END;

  }
  DATASET
  {
    { 1443;    ;DataItem;ESRecording         ;
               DataItemTable=Table14123651;
               DataItemTableView=SORTING(Code);
               OnPreDataItem=BEGIN
                               CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  CurrReport.BREAK;
                                END;

               ReqFilterFields=Code }

    { 1146;    ;DataItem;ESRecordingCompare  ;
               DataItemTable=Table14123651;
               DataItemTableView=SORTING(Code);
               ReqFilterHeadingML=ENU=Compare to Recording;
               OnPreDataItem=BEGIN
                               CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  CurrReport.BREAK;
                                END;

               ReqFilterFields=Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1240520008;2;Field  ;
                  CaptionML=ENU=Report Format;
                  OptionCaptionML=ENU=Basic;
                  SourceExpr=ESReportFormat }

      { 1240520012;2;Field  ;
                  CaptionML=ENU=Full Primary Key;
                  SourceExpr=FullPrimaryKey }

      { 1240520018;2;Field  ;
                  CaptionML=ENU=Origin Details;
                  SourceExpr=OriginDetails }

      { 1240520010;2;Field  ;
                  CaptionML=ENU=User Date Time Details;
                  SourceExpr=UserDateTimeDetails }

      { 1240520016;2;Field  ;
                  CaptionML=ENU=Compare;
                  SourceExpr=Compare }

      { 1240520015;2;Field  ;
                  CaptionML=ENU=Recording Permissions;
                  SourceExpr=RecordingPermissions }

      { 1240520021;2;Field  ;
                  CaptionML=ENU=Summary Permissions;
                  SourceExpr=SummaryPermissions }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ESTextLineTmp@1240520009 : TEMPORARY Record 14123520;
      ESTextLineOldTmp@1240520020 : TEMPORARY Record 14123520;
      ESTextLineNewTmp@1240520019 : TEMPORARY Record 14123520;
      ESSecuritySetup@1240520001 : Record 14123521;
      ESTextLines@1240520011 : Page 14123524;
      ESTextLinesRTC@1240520010 : Page 14123555;
      ESTextLineMgt@1240520024 : Codeunit 14123503;
      Window@1240520014 : Dialog;
      ESReportFormat@1240520008 : 'Basic';
      Text001@1240520013 : TextConst 'ENU=Creating Text Lines\';
      Text002@1240520012 : TextConst 'ENU=Process #1################################################';
      RecordingPermissions@1240520016 : Boolean;
      SummaryPermissions@1240520028 : Boolean;
      UserDateTimeDetails@1240520015 : Boolean;
      OriginDetails@1240520027 : Boolean;
      FullPrimaryKey@1240520017 : Boolean;
      Compare@1240520021 : Boolean;
      Factor@1240520018 : Integer;
      Text003@1240520022 : TextConst 'ENU=%1 must be filtered.';
      Text004@1240520023 : TextConst 'ENU=Filter on %1 must be a simple filter.';
      Text005@1240520026 : TextConst 'ENU=Compare to %1 must be filtered.';
      Text006@1240520025 : TextConst 'ENU=Filter on Compare to %1 must be a simple filter.';

    PROCEDURE BasicFormat@1240520008(VAR ESTextLine@1240520011 : TEMPORARY Record 14123520;VAR CurrentESRecording@1240520012 : Record 14123651);
    VAR
      ESRecording@1240520008 : Record 14123651;
      ESRecordingPerm@1240520009 : Record 14123652;
      ESSummaryPerm@1240520010 : Record 14123537;
    BEGIN
      IF GUIALLOWED THEN
        Window.OPEN(ESSecuritySetup.ExtendProgressText(Text001) + Text002);

      IF GUIALLOWED THEN
        Window.UPDATE(1,ESRecording.TABLECAPTION);
      ESRecording.COPYFILTERS(CurrentESRecording);
      IF ESRecording.FIND('-') THEN
        REPEAT
          ESRecording.FormatAsTextLine(ESTextLine,0,TRUE,UserDateTimeDetails);
          ESTextLine.Level := 0;
          ESTextLine.INSERT;

          IF RecordingPermissions THEN BEGIN
            ESRecordingPerm.RESET;
            ESRecordingPerm.SETRANGE("Recording Code",ESRecording.Code);
            IF ESRecordingPerm.FIND('-') THEN
              REPEAT
                ESRecordingPerm.FormatAsTextLine(
                  TRUE,ESTextLine,Factor * 1,TRUE,UserDateTimeDetails,OriginDetails);
                ESTextLine.Level := 1;
                ESTextLine.INSERT;
              UNTIL ESRecordingPerm.NEXT = 0;
          END;

          IF SummaryPermissions THEN BEGIN
            ESSummaryPerm.RESET;
            ESSummaryPerm.SETRANGE(Type,ESSummaryPerm.Type::Recording);
            ESSummaryPerm.SETRANGE(Code,ESRecording.Code);
            IF ESSummaryPerm.FIND('-') THEN
              REPEAT
                ESSummaryPerm.FormatAsTextLine(TRUE,ESTextLine,Factor * 2,TRUE);
                ESTextLine.Level := 1;
                ESTextLine.INSERT;
              UNTIL ESSummaryPerm.NEXT = 0;
          END;
        UNTIL ESRecording.NEXT = 0;

      IF GUIALLOWED THEN
        Window.CLOSE;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

