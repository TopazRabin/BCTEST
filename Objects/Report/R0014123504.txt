OBJECT Report 14123504 ES Compare Restore Point
{
  OBJECT-PROPERTIES
  {
    Date=04/30/18;
    Time=12:00:00 PM;
    Version List=ES1.41.51;
  }
  PROPERTIES
  {
    CaptionML=ENU=Compare Restore Point;
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   IF FullPrimaryKey THEN
                     Factor := 0
                   ELSE
                     Factor := 1;

                   ESTextLineTmp.RESET;
                   ESTextLineTmp.DELETEALL;

                   IF Compare THEN BEGIN
                     IF ESRestorePoint.GETFILTERS = '' THEN
                       ERROR(Text003,ESRestorePoint.TABLECAPTION);
                     IF ESRestorePoint.FIND('-') THEN BEGIN
                       IF ESRestorePoint.NEXT <> 0 THEN
                         ERROR(Text004,ESRestorePoint.TABLECAPTION);
                     END ELSE
                       ERROR(Text004,ESRestorePoint.TABLECAPTION);

                     IF ESRestorePointCompare.GETFILTERS = '' THEN
                       ERROR(Text005,ESRestorePointCompare.TABLECAPTION);
                     IF ESRestorePointCompare.FIND('-') THEN BEGIN
                       IF ESRestorePointCompare.NEXT <> 0 THEN
                         ERROR(Text006,ESRestorePointCompare.TABLECAPTION);
                     END ELSE
                       ERROR(Text006,ESRestorePointCompare.TABLECAPTION);

                     ESTextLineOldTmp.RESET;
                     ESTextLineOldTmp.DELETEALL;

                     ESTextLineNewTmp.RESET;
                     ESTextLineNewTmp.DELETEALL;
                   END;

                   CASE ESReportFormat OF
                     ESReportFormat::"Simple List":
                       IF Compare THEN BEGIN
                         ShowTextLines(ESTextLineOldTmp,ESRestorePointCompare,FALSE);
                         ShowTextLines(ESTextLineNewTmp,ESRestorePoint,FALSE);
                         ESTextLineMgt.Compare(ESTextLineOldTmp,ESTextLineNewTmp,ESTextLineTmp);
                       END ELSE
                         ShowTextLines(ESTextLineTmp,ESRestorePoint,FALSE);
                     ESReportFormat::Indented:
                       IF Compare THEN BEGIN
                         ShowTextLines(ESTextLineOldTmp,ESRestorePointCompare,TRUE);
                         ShowTextLines(ESTextLineNewTmp,ESRestorePoint,TRUE);
                         ESTextLineMgt.Compare(ESTextLineOldTmp,ESTextLineNewTmp,ESTextLineTmp);
                       END ELSE
                         ShowTextLines(ESTextLineTmp,ESRestorePoint,TRUE);
                   END;

                   IF ISSERVICETIER THEN BEGIN
                     CLEAR(ESTextLinesRTC);
                     ESTextLinesRTC.InitVersion(
                       ESTextLineTmp,STRSUBSTNO('%1 %2',ESRestorePoint.TABLECAPTION,FORMAT(ESReportFormat)),Compare);
                     ESTextLinesRTC.RUNMODAL;
                   END ELSE BEGIN
                     CLEAR(ESTextLines);
                     ESTextLines.InitVersion(
                       ESTextLineTmp,STRSUBSTNO('%1 %2',ESRestorePoint.TABLECAPTION,FORMAT(ESReportFormat)),Compare);
                     ESTextLines.RUNMODAL;
                   END;
                 END;

  }
  DATASET
  {
    { 3359;    ;DataItem;ESRestorePoint      ;
               DataItemTable=Table14123523;
               DataItemTableView=SORTING(Code);
               OnPreDataItem=BEGIN
                               CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  CurrReport.BREAK;
                                END;

               ReqFilterFields=Code }

    { 1117;    ;DataItem;ESRestorePointCompare;
               DataItemTable=Table14123523;
               DataItemTableView=SORTING(Code);
               ReqFilterHeadingML=ENU=Compare to Restore Point;
               OnPreDataItem=BEGIN
                               CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  CurrReport.BREAK;
                                END;

               ReqFilterFields=Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=ENU=Options }

      { 1240520008;2;Field  ;
                  CaptionML=ENU=Report Format;
                  OptionCaptionML=ENU=Indented,Simple List;
                  SourceExpr=ESReportFormat }

      { 1240520030;2;Field  ;
                  CaptionML=ENU=Full Primary Key;
                  SourceExpr=FullPrimaryKey }

      { 1240520028;2;Field  ;
                  CaptionML=ENU=User Date Time Details;
                  SourceExpr=UserDateTimeDetails }

      { 1240520032;2;Field  ;
                  CaptionML=ENU=Compare;
                  SourceExpr=Compare }

      { 1240520015;2;Field  ;
                  CaptionML=ENU=Users;
                  SourceExpr=WindowsLogins }

      { 1240520017;2;Field  ;
                  CaptionML=ENU=User Access Controls;
                  SourceExpr=WindowsAccessControls }

      { 1240520019;2;Field  ;
                  CaptionML=ENU=Permission Sets;
                  SourceExpr=Roles }

      { 1240520021;2;Field  ;
                  CaptionML=ENU=Permissions;
                  SourceExpr=RolePermissions }

      { 1240520023;2;Field  ;
                  CaptionML=ENU=Companies;
                  SourceExpr=Companies }

      { 1240520025;2;Field  ;
                  CaptionML=ENU=Objects;
                  SourceExpr=Objects }

      { 1240520027;2;Field  ;
                  CaptionML=ENU=Object Informations;
                  SourceExpr=ObjectInfos }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ESTextLineTmp@1240520009 : TEMPORARY Record 14123520;
      ESTextLineOldTmp@1240520031 : TEMPORARY Record 14123520;
      ESTextLineNewTmp@1240520030 : TEMPORARY Record 14123520;
      ESTextLines@1240520011 : Page 14123524;
      ESTextLinesRTC@1240520010 : Page 14123555;
      ESTextLineMgt@1240520033 : Codeunit 14123503;
      Window@1240520022 : Dialog;
      ESReportFormat@1240520008 : 'Indented,Simple List';
      Text001@1240520012 : TextConst 'ENU=Creating Text Lines\';
      Text002@1240520052 : TextConst 'ENU=Process #1################################################';
      WindowsLogins@1240520019 : Boolean;
      WindowsAccessControls@1240520018 : Boolean;
      Roles@1240520017 : Boolean;
      RolePermissions@1240520016 : Boolean;
      Companies@1240520015 : Boolean;
      Objects@1240520014 : Boolean;
      ObjectInfos@1240520013 : Boolean;
      UserDateTimeDetails@1240520024 : Boolean;
      FullPrimaryKey@1240520023 : Boolean;
      Compare@1240520032 : Boolean;
      Factor@1240520025 : Integer;
      Text003@1240520029 : TextConst 'ENU=%1 must be filtered.';
      Text004@1240520028 : TextConst 'ENU=Filter on %1 must be a simple filter.';
      Text005@1240520027 : TextConst 'ENU=Compare to %1 must be filtered.';
      Text006@1240520026 : TextConst 'ENU=Filter on Compare to %1 must be a simple filter.';

    PROCEDURE CreateTextLines@1240520018(VAR ESTextLine@1240520008 : Record 14123520;ESRestorePoint@1240520018 : Record 14123523;Indented@1240520030 : Boolean);
    VAR
      ESSecuritySetup@1240520001 : Record 14123521;
      ESRestPntWindowsLogin@1240520015 : Record 14123513;
      ESRestPntWindowsAccCtrl@1240520014 : Record 14123514;
      ESRestPntRole@1240520013 : Record 14123515;
      ESRestPntRolePerm@1240520012 : Record 14123516;
      ESRestPntCompany@1240520011 : Record 14123517;
      ESRestPntObject@1240520010 : Record 14123518;
      ESRestPntObjectInfo@1240520009 : Record 14123519;
    BEGIN
      IF GUIALLOWED THEN
        Window.OPEN(ESSecuritySetup.ExtendProgressText(Text001) + Text002);

      ESSecuritySetup.GET;

      ESRestorePoint.FormatAsTextLine(ESTextLine,0,TRUE,UserDateTimeDetails);
      ESTextLine.INSERT;

      IF WindowsLogins THEN BEGIN
        IF GUIALLOWED THEN
          Window.UPDATE(1,ESRestPntWindowsLogin.TABLECAPTION);
        ESRestPntWindowsLogin.RESET;
        ESRestPntWindowsLogin.SETRANGE("Version Code",ESRestorePoint.Code);
        IF ESRestPntWindowsLogin.FIND('-') THEN
          REPEAT
            ESRestPntWindowsLogin.FormatAsTextLine(ESTextLine,Factor * 1,TRUE);
            ESTextLine.Level := 1;
            ESTextLine.INSERT;

            IF WindowsAccessControls AND Indented THEN BEGIN
              ESRestPntWindowsAccCtrl.RESET;
              ESRestPntWindowsAccCtrl.SETRANGE("Version Code",ESRestorePoint.Code);
              ESRestPntWindowsAccCtrl.SETRANGE("Login SID",ESRestPntWindowsLogin.SID);
              IF ESRestPntWindowsAccCtrl.FIND('-') THEN
                REPEAT
                  ESRestPntWindowsAccCtrl.FormatAsTextLine(
                    ESTextLine,Factor * 1,FALSE,ESSecuritySetup."Use User Name instead of SID");
                  ESTextLine.Level := 2;
                  ESTextLine.INSERT;
                UNTIL ESRestPntWindowsAccCtrl.NEXT = 0;
            END;
          UNTIL ESRestPntWindowsLogin.NEXT = 0;
      END;

      IF WindowsAccessControls AND NOT Indented THEN BEGIN
        IF GUIALLOWED THEN
          Window.UPDATE(1,ESRestPntWindowsAccCtrl.TABLECAPTION);
        ESRestPntWindowsAccCtrl.RESET;
        ESRestPntWindowsAccCtrl.SETRANGE("Version Code",ESRestorePoint.Code);
        IF ESRestPntWindowsAccCtrl.FIND('-') THEN
          REPEAT
            ESRestPntWindowsAccCtrl.FormatAsTextLine(
              ESTextLine,Factor * 1,TRUE,ESSecuritySetup."Use User Name instead of SID");
            ESTextLine.Level := 1;
            ESTextLine.INSERT;
          UNTIL ESRestPntWindowsAccCtrl.NEXT = 0;
      END;

      IF Roles THEN BEGIN
        IF GUIALLOWED THEN
          Window.UPDATE(1,ESRestPntRole.TABLECAPTION);
        ESRestPntRole.RESET;
        ESRestPntRole.SETRANGE("Version Code",ESRestorePoint.Code);
        IF ESRestPntRole.FIND('-') THEN
          REPEAT
            ESRestPntRole.FormatAsTextLine(ESTextLine,Factor * 1,TRUE);
            ESTextLine.Level := 1;
            ESTextLine.INSERT;

            IF RolePermissions AND Indented THEN BEGIN
              ESRestPntRolePerm.RESET;
              ESRestPntRolePerm.SETRANGE("Version Code",ESRestorePoint.Code);
              ESRestPntRolePerm.SETRANGE("Role ID",ESRestPntRole."Role ID");
              IF ESRestPntRolePerm.FIND('-') THEN
                REPEAT
                  ESRestPntRolePerm.FormatAsTextLine(TRUE,ESTextLine,Factor * 1,FALSE);
                  ESTextLine.Level := 2;
                  ESTextLine.INSERT;
                UNTIL ESRestPntRolePerm.NEXT = 0;
            END;
          UNTIL ESRestPntRole.NEXT = 0;
      END;

      IF RolePermissions AND NOT Indented THEN BEGIN
        IF GUIALLOWED THEN
          Window.UPDATE(1,ESRestPntRolePerm.TABLECAPTION);
        ESRestPntRolePerm.RESET;
        ESRestPntRolePerm.SETRANGE("Version Code",ESRestorePoint.Code);
        IF ESRestPntRolePerm.FIND('-') THEN
          REPEAT
            ESRestPntRolePerm.FormatAsTextLine(TRUE,ESTextLine,Factor * 1,TRUE);
            ESTextLine.Level := 1;
            ESTextLine.INSERT;
          UNTIL ESRestPntRolePerm.NEXT = 0;
      END;

      IF Companies THEN BEGIN
        IF GUIALLOWED THEN
          Window.UPDATE(1,ESRestPntCompany.TABLECAPTION);
        ESRestPntCompany.RESET;
        ESRestPntCompany.SETRANGE("Version Code",ESRestorePoint.Code);
        IF ESRestPntCompany.FIND('-') THEN
          REPEAT
            ESRestPntCompany.FormatAsTextLine(ESTextLine,Factor * 1,TRUE);
            ESTextLine.Level := 1;
            ESTextLine.INSERT;
          UNTIL ESRestPntCompany.NEXT = 0;
      END;

      IF Objects THEN BEGIN
        IF GUIALLOWED THEN
          Window.UPDATE(1,ESRestPntObject.TABLECAPTION);
        ESRestPntObject.RESET;
        ESRestPntObject.SETRANGE("Version Code",ESRestorePoint.Code);
        IF ESRestPntObject.FIND('-') THEN
          REPEAT
            ESRestPntObject.FormatAsTextLine(ESTextLine,Factor * 1,TRUE);
            ESTextLine.Level := 1;
            ESTextLine.INSERT;
          UNTIL ESRestPntObject.NEXT = 0;
      END;

      IF ObjectInfos THEN BEGIN
        IF GUIALLOWED THEN
          Window.UPDATE(1,ESRestPntObjectInfo.TABLECAPTION);
        ESRestPntObjectInfo.RESET;
        ESRestPntObjectInfo.SETRANGE("Version Code",ESRestorePoint.Code);
        IF ESRestPntObjectInfo.FIND('-') THEN
          REPEAT
            ESRestPntObjectInfo.FormatAsTextLine(ESTextLine,Factor * 1,TRUE);
            ESTextLine.Level := 1;
            ESTextLine.INSERT;
          UNTIL ESRestPntObjectInfo.NEXT = 0;
      END;

      IF GUIALLOWED THEN
        Window.CLOSE;
    END;

    PROCEDURE ShowTextLines@1240520022(VAR ESTextLine@1240520009 : Record 14123520;VAR CurrentESRestorePoint@1240520011 : Record 14123523;Indented@1240520010 : Boolean);
    VAR
      ESRestorePoint@1240520008 : Record 14123523;
    BEGIN
      ESRestorePoint.COPYFILTERS(CurrentESRestorePoint);
      IF ESRestorePoint.FIND('-') THEN
        REPEAT
          CreateTextLines(ESTextLine,ESRestorePoint,Indented);
        UNTIL ESRestorePoint.NEXT = 0;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

