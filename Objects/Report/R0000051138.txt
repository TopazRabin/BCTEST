OBJECT Report 51138 TO Consolidation Report
{
  OBJECT-PROPERTIES
  {
    Date=07/01/20;
    Time=12:00:00 AM;
    Modified=Yes;
    Version List=TPZ000.00.00;
  }
  PROPERTIES
  {
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   MESSAGE(Text50001,RecordCount);
                 END;

  }
  DATASET
  {
    { 1000000000;;DataItem;                  ;
               DataItemTable=Table5740;
               ReqFilterHeadingML=ENU=Filter for Transfer Orders;
               OnPreDataItem=BEGIN
                               IF "Transfer Header".GETFILTERS='' THEN
                                 ERROR(Text50002);
                               IF (ReceiptDate=0D) AND (DeliveryStatus=DeliveryStatus::" ") AND (ShipmentDate=0D) AND (ShippingAgentCode='')
                                   AND (PackageTrackingNo='') AND (EShipAgentServiceCode='') THEN
                                 ERROR(Text50003);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF (ReceiptDate<>0D) AND (ReceiptDate<>"Transfer Header"."Receipt Date") THEN
                                    "Transfer Header"."Receipt Date" := ReceiptDate;
                                  IF (DeliveryStatus<>DeliveryStatus::" ") AND (DeliveryStatus<>"Transfer Header"."TO Delivery Status") THEN
                                    "Transfer Header"."TO Delivery Status" := DeliveryStatus;
                                  IF (ShipmentDate<>0D) AND (ShipmentDate<>"Transfer Header"."Shipment Date") THEN
                                    "Transfer Header"."Shipment Date" := ShipmentDate;
                                  IF (ShippingAgentCode<>'') AND (ShippingAgentCode<>"Transfer Header"."Shipping Agent Code") THEN
                                    "Transfer Header".VALIDATE("Shipping Agent Code",ShippingAgentCode);
                                  IF (PackageTrackingNo<>'') AND (PackageTrackingNo<>"Transfer Header"."Package Tracking No.") THEN
                                    "Transfer Header".VALIDATE("Package Tracking No.",PackageTrackingNo);
                                  IF (EShipAgentServiceCode<>'') AND (EShipAgentServiceCode<>"Transfer Header"."E-Ship Agent Service") THEN
                                    "Transfer Header".VALIDATE("E-Ship Agent Service",EShipAgentServiceCode);
                                  "Transfer Header".MODIFY;
                                  RecordCount +=1;
                                END;

               ReqFilterFields=Transfer-from Code,Transfer-to Code,No.,Shipment Date,Receipt Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1000000000;;Container;
                  Name=[ ];
                  ContainerType=ContentArea }

      { 1000000001;1;Group  ;
                  CaptionML=ENU=Values to be updated;
                  GroupType=Group }

      { 1000000002;2;Field  ;
                  Name=ReceiptDate;
                  CaptionML=ENU=Receipt Date;
                  SourceExpr=ReceiptDate }

      { 1000000003;2;Field  ;
                  Name=DeliveryStatus;
                  CaptionML=ENU=TO Delivery Status;
                  SourceExpr=DeliveryStatus }

      { 1000000004;2;Field  ;
                  Name=ShipmentDate;
                  CaptionML=ENU=Shipment Date;
                  SourceExpr=ShipmentDate }

      { 1000000008;2;Field  ;
                  Name=PackageTrackingNo;
                  CaptionML=ENU=Package Tracking No.;
                  SourceExpr=PackageTrackingNo }

      { 1000000005;2;Field  ;
                  Name=ShippingAgentCode;
                  CaptionML=ENU=Shipping Agent Code;
                  SourceExpr=ShippingAgentCode;
                  TableRelation="Shipping Agent" }

      { 1000000007;2;Field  ;
                  Name=EShipAgentServiceCode;
                  CaptionML=ENU=E-Ship Agent Service;
                  SourceExpr=EShipAgentServiceCode;
                  TableRelation="E-Ship Agent Service".Code;
                  OnValidate=BEGIN
                               IF EShipAgentServiceCode<>'' THEN
                                 IF ShippingAgentCode='' THEN
                                   ERROR(Text50004)
                             END;

                  OnLookup=BEGIN
                             IF ShippingAgentCode='' THEN
                              ERROR(Text50004);
                             EShipAgentService.RESET;
                             EShipAgentService.SETRANGE("Shipping Agent Code",ShippingAgentCode);
                             CLEAR(EShipAgentServicesList);
                             EShipAgentServicesList.SETRECORD(EShipAgentService);
                             EShipAgentServicesList.SETTABLEVIEW(EShipAgentService);
                             EShipAgentServicesList.LOOKUPMODE(TRUE);
                             IF EShipAgentServicesList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               EShipAgentServicesList.GETRECORD(EShipAgentService);
                               EShipAgentServiceCode := EShipAgentService.Code;
                             END;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ReceiptDate@1000000000 : Date;
      DeliveryStatus@1000000001 : ' ,On Water,Arrival Confirmation,Appointment Confirmation,Custom Hold';
      ShipmentDate@1000000002 : Date;
      ShippingAgentCode@1000000003 : Code[10];
      PackageTrackingNo@1000000004 : Code[50];
      Text50001@1000000005 : TextConst 'ENU=Total %1 transfer orders updated successfully.';
      RecordCount@1000000006 : Integer;
      Text50002@1000000007 : TextConst 'ENU=Please specify filters for the Transfer Orders to be updated.';
      Text50003@1000000008 : TextConst 'ENU=Please specify values to be updated.';
      EShipAgentServiceCode@1000000009 : Code[30];
      Text50004@1000000010 : TextConst 'ENU=Shipping Agent Code must have a value.';
      WorldWideService@1000000012 : Boolean;
      EShipAgentService@1000000011 : Record 14000708;
      EShipAgentServicesList@1000000013 : Page 14000720;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

